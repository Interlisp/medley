(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "11-Feb-2026 16:27:51" {DSK}<home>frank>il>medley>doctools>NEWIRM.;28 13899  

      :EDIT-BY "FGH"

      :CHANGES-TO (FNS SCRATCH IM.CHAP.CHAPTER IM.CHAP.BEFOREHARDCOPYFN IM.INDEX.UPDATE.TOC.NUMBERING
                       IM.CHAP.WHENINSERTEDFN TEDIT.SET.FOLIO.TEXT IM.INDEX.TOC.TEXT IRM-IMPTR 
                       IRMANALYZE IRMHC IRMTOC)
                  (VARS NEWIRMCOMS IRMFILES)

      :PREVIOUS-DATE "24-Jun-2025 00:36:10" {DSK}<home>frank>il>medley>doctools>NEWIRM.;14)


(PRETTYCOMPRINT NEWIRMCOMS)

(RPAQQ NEWIRMCOMS
       ((FILES PSEUDOHOSTS)
        (P (PSEUDOHOST 'IRM (CONCAT MEDLEYDIR "docs/medley-irm")))
        (INITVARS (DOCTOOLSDIR (CONCAT (MEDLEYDIR)
                                      "doctools>")))
        (FILES (FROM VALUEOF DOCTOOLSDIR)
               IMTOOLS)
        (FNS IM.CHAP.CHAPTER IRMTOC IRM-IMPTR IRMANALYZE IRMHC)
        (FNS IM.INDEX.UPDATE.TOC.NUMBERING IM.INDEX.TOC.TEXT IM.CHAP.BEFOREHARDCOPYFN 
             IM.CHAP.WHENINSERTEDFN TEDIT.SET.FOLIO.TEXT)
        (VARS IRMFILES)
        (FNS SCRATCH)))

(FILESLOAD PSEUDOHOSTS)

(PSEUDOHOST 'IRM (CONCAT MEDLEYDIR "docs/medley-irm"))

(RPAQ? DOCTOOLSDIR (CONCAT (MEDLEYDIR)
                          "doctools>"))

(FILESLOAD (FROM VALUEOF DOCTOOLSDIR)
       IMTOOLS)
(DEFINEQ

(IM.CHAP.CHAPTER
  [LAMBDA (TEXTSTREAM NEWVALUE)                              (* ; "Edited  5-Feb-2026 21:11 by FGH")

    (* ;; "Find the IM.CHAP object in this TextStream and return the chapter value.")

    (* ;; " If NEWVALUE is given then set the chapter value of the IM.CHAP object accordingly")

    (SETQ TEXTSTREAM (TEXTSTREAM TEXTSTREAM))
    (LET* [[CHAPOBJ (CAR (TEDIT.MAP.OBJECTS TEXTSTREAM [FUNCTION (LAMBDA (CH# OBJ FNARG)
                                                                   (EQ 'IM.CHAP.OBJECT
                                                                       (IMAGEOBJPROP OBJ 
                                                                              'IMAGECLASSNAME]
                                NIL
                                'OBJECT]
           (CHAPTER (AND CHAPOBJ (NUMBERP (MKATOM (IMAGEOBJPROP CHAPOBJ 'OBJECTDATUM]
          (AND (NUMBERP NEWVALUE)
               (IMAGEOBJPROP CHAPOBJ 'OBJECTDATUM NEWVALUE))
          CHAPTER])

(IRMTOC
  [LAMBDA NIL                                                (* ; "Edited 18-Jan-2026 22:40 by FGH")
    (TEDIT (MAKE.IM.TOC T NIL (for F in IRMFILES collect (CONCAT F ".IMPTR"])

(IRM-IMPTR
  [LAMBDA NIL                                                (* ; "Edited 18-Jan-2026 22:57 by FGH")
    (SETQ IMPTR.TOC.LIST NIL)
    (bind FF for F in IRMFILES collect (SETQ FF (CONCAT F ".IMPTR"))
                                     (GRAB.IMPTR FF))
    IMPTR.TOC.LIST])

(IRMANALYZE
  [LAMBDA NIL                                                (* ; "Edited 18-Jan-2026 23:01 by FGH")
    (LET* ((TS (TEXTOBJ (WHICHW)))
           (OBJS (TEDIT.MAP.OBJECTS TS (FUNCTION TRUE)
                        NIL T)))
          (for OBJ in OBJS collect (fetch IMAGECLASSNAME of (fetch IMAGEOBJFNS of (CADR OBJ])

(IRMHC
  [LAMBDA NIL                                                (* ; "Edited 18-Jan-2026 23:00 by FGH")
    (bind FF for F in IRMFILES do (SETQ FF (CONCAT F ".TEDIT"))
                                  (TEDIT.FORMAT.HARDCOPY FF (PACKFILENAME.STRING 'VERSION NIL
                                                                   'EXTENSION NIL 'BODY FF)
                                         T NIL NIL NIL 'PDF])
)
(DEFINEQ

(IM.INDEX.UPDATE.TOC.NUMBERING
  [LAMBDA (TEXTSTREAM CHAPTER)                               (* ; "Edited  5-Feb-2026 19:46 by FGH")
                                                             (* ; "Edited  3-Feb-2026 23:48 by FGH")
                                                             (* ; "Edited  3-Feb-2026 21:24 by FGH")
                                                             (* ; "Edited  1-Feb-2026 22:49 by FGH")
    (SETQ TEXTSTREAM (TEXTSTREAM TEXTSTREAM))
    (OR (NUMBERP CHAPTER)
        (SETQ CHAPTER (OR (IM.CHAP.CHAPTER TEXTSTREAM)
                          1)))
    (LET ((IM.TOC.OBJECTS (TEDIT.MAP.OBJECTS
                           TEXTSTREAM
                           [FUNCTION (LAMBDA (CH# OBJ)
                                       (if [AND (EQ 'IM.INDEX.OBJECT (IMAGEOBJPROP OBJ 
                                                                            'IMAGECLASSNAME))
                                                (EQ 'TOC (CAR (fetch (IM.INDEX.DATA TYPE)
                                                                 of (fetch (IMAGEOBJ OBJECTDATUM)
                                                                       of OBJ]
                                           then OBJ
                                         else NIL]
                           NIL T))
          (LEVEL.CTRS (LIST 0)))
         [for OBJ.DESC in IM.TOC.OBJECTS
            do (LET* ((OBJ (CADR OBJ.DESC))
                      (OBJ.CH# (CAR OBJ.DESC))
                      (OBJ.DATA (fetch (IMAGEOBJ OBJECTDATUM) of OBJ))
                      (OBJ.LEVEL (fetch (IM.INDEX.DATA LEVEL) of OBJ.DATA)))
                     (if (IGREATERP OBJ.LEVEL (LENGTH LEVEL.CTRS))
                         then (for I from (ADD1 (LENGTH LEVEL.CTRS)) to OBJ.LEVEL
                                 do (NCONC1 LEVEL.CTRS 0)))
                     [for CTRS on LEVEL.CTRS as LVL from 1
                        do (if (EQ LVL 1)
                               then (RPLACA CTRS CHAPTER)
                             elseif (IGREATERP LVL OBJ.LEVEL)
                               then (RPLACA CTRS 0)
                             elseif (EQ LVL OBJ.LEVEL)
                               then (RPLACA CTRS (ADD1 (CAR CTRS]
                     (replace (IM.INDEX.DATA SUBSEC) of OBJ.DATA
                        with (for CTR in LEVEL.CTRS as I from 1 to OBJ.LEVEL
                                do (PUSH $$VAL CTR)))
                     [replace (IM.INDEX.DATA NAME) of OBJ.DATA
                        with (CONCATLIST (for CTR in LEVEL.CTRS as LVL from 1 to OBJ.LEVEL
                                            do (PUSH $$VAL CTR)
                                               (if (ILESSP LVL OBJ.LEVEL)
                                                   then (PUSH $$VAL "."))
                                            finally (RETURN (DREVERSE $$VAL]
                     (replace (IM.INDEX.DATA TEXT) of OBJ.DATA with (IM.INDEX.GET.LINE TEXTSTREAM 
                                                                           OBJ.CH#]
         (for OBJ.DESC in IM.TOC.OBJECTS collect (CADR OBJ.DESC])

(IM.INDEX.TOC.TEXT
  [LAMBDA (TEXTSTREAM CH#)                                   (* ; "Edited  3-Feb-2026 21:29 by FGH")
    (LET ((CTR CH#))
         (SETFILEPTR TEXTSTREAM CTR)
         (CONCATLIST (bind CHX for I from 1 to 1000 collect (SETQ CHX (BIN TEXTSTREAM))
                                                          (if (AND (NUMBERP CHX)
                                                                   (ILEQ CHX 13))
                                                              then (GO $$OUT)
                                                            elseif (NOT (NUMBERP CHX))
                                                              then 'X
                                                            else (CHARACTER CHX])

(IM.CHAP.BEFOREHARDCOPYFN
  [LAMBDA (TEXTSTREAM)                                       (* ; "Edited  5-Feb-2026 19:46 by FGH")

    (* ;; "To be called as the BEFOREHARDCOPYFN on an IRM Textstream")

    (* ;; "Does a couple of things:")

    (* ;; "  1.  Find the IM.CHAP imageobject; extract the chapter number from it; set the chapter number as the page number pre-text on")

    (* ;; "        all page types.  There should be only one IM.CHAP object per document.  It there are more, it will use the first.")

    (* ;; "  2.  Hang the chapter number on the TEXTSTREAM's INDEXING-CHAPTER textprop")

    (* ;; "  3. Update the %"numbering%" on all of the IM.TOC image objects in the document")

    (SETQ TEXTSTREAM (TEXTSTREAM TEXTSTREAM))
    (LET ((CHAPTER (IM.CHAP.CHAPTER TEXTSTREAM)))
         (if CHAPTER
             then (TEDIT.SET.FOLIO.TEXT TEXTSTREAM (CONCAT CHAPTER "-")
                         "")
                  (TEXTPROP TEXTSTREAM 'INDEXING-CHAPTER CHAPTER)
                  (IM.INDEX.UPDATE.TOC.NUMBERING TEXTSTREAM CHAPTER])

(IM.CHAP.WHENINSERTEDFN
  [LAMBDA (OBJ FROM.TEXTSTREAM TO.TEXTSTREAM)                (* ; "Edited  5-Feb-2026 18:50 by FGH")
    (\TEDIT.TEXTPROP (TEXTOBJ TO.TEXTSTREAM)
           'BEFOREHARDCOPYFN T (FUNCTION IM.CHAP.BEFOREHARDCOPYFN])

(TEDIT.SET.FOLIO.TEXT
  [LAMBDA (TEXTSTREAM PRETEXT POSTTEXT)                      (* ; "Edited  5-Feb-2026 14:41 by FGH")
    (if (NOT POSTTEXT)
        then (SETQ POSTTEXT ""))
    (SETQ TEXTSTREAM (TEXTSTREAM TEXTSTREAM))
    (LET [(TOP.PAGEREGION (TEXTPROP TEXTSTREAM 'PAGEFORMAT))
          (PROCESS.PAGEREGION (FUNCTION (LAMBDA (PAGEREGION PROCESS.PAGEREGION)
                                          (LET* ((REGIONLOCALINFO (fetch (PAGEREGION REGIONLOCALINFO)
                                                                     of PAGEREGION))
                                                 (FOLIOINFO (LISTGET REGIONLOCALINFO 'FOLIOINFO))
                                                 (FORMATINFO (LISTGET REGIONLOCALINFO 'FORMATINFO))
                                                 (REGIONSUBBOXES (fetch (PAGEREGION REGIONSUBBOXES)
                                                                    of PAGEREGION)))
                                                (BLOCK)
                                                (if FOLIOINFO
                                                    then (RPLACA (CDR FOLIOINFO)
                                                                PRETEXT)
                                                         (RPLACA (CDDR FOLIOINFO)
                                                                POSTTEXT))
                                                (if FORMATINFO
                                                    then (RPLACA (CDR FORMATINFO)
                                                                PRETEXT)
                                                         (RPLACA (CDDR FORMATINFO)
                                                                POSTTEXT))
                                                (for SUB.PAGEREGION in REGIONSUBBOXES
                                                   do (APPLY* PROCESS.PAGEREGION SUB.PAGEREGION 
                                                             PROCESS.PAGEREGION]
         (APPLY* PROCESS.PAGEREGION TOP.PAGEREGION PROCESS.PAGEREGION])
)

(RPAQQ IRMFILES ("{IRM}01-INTRO" "{IRM}02-LITATOM" "{IRM}03-LISTS" "{IRM}04-STRINGS" "{IRM}05-ARRAY"
                       "{IRM}06-HASHARRAYS" "{IRM}07-NUMBERS" "{IRM}08-RECORDPACKAGE" 
                       "{IRM}09-CONDITIONALS" "{IRM}10-FUNC-DEF" "{IRM}11-VAR-BINDINGS" 
                       "{IRM}12-MISC" "{IRM}13-EXECUTIVE" "{IRM}14-ERRORS" "{IRM}15-BREAKING" 
                       "{IRM}16-SEDIT" "{IRM}17-FILEPACKAGE" "{IRM}18-COMPILER" "{IRM}19-DWIM" 
                       "{IRM}20-CLISP" "{IRM}21-PERFORMANCE" "{IRM}22-PROCESSES" "{IRM}23-STREAMS" 
                       "{IRM}24-IO" "{IRM}25-USERIO-PACKAGES" "{IRM}26-GRAPHICS" "{IRM}27-WINDOWS" 
                       "{IRM}28-HARDCOPY" "{IRM}29-TERMINAL"))
(DEFINEQ

(SCRATCH
  [LAMBDA NIL                                                (* ; "Edited 11-Feb-2026 16:27 by FGH")
                                                             (* ; "Edited  1-Feb-2026 22:33 by FGH")
    (IM.INDEX.CREATEOBJ (create IM.INDEX.DATA
                               TYPE _ (LIST 'TOC)
                               NAME _ "TBD"
                               LEVEL _ 1))
    (IM.INDEX.CREATEOBJ (create IM.INDEX.DATA
                               TYPE _ (LIST 'TOC)
                               NAME _ "TBD"
                               LEVEL _ 2))
    (IM.INDEX.CREATEOBJ (create IM.INDEX.DATA
                               TYPE _ (LIST 'TOC)
                               NAME _ "TBD"
                               LEVEL _ 3))
    (TEDIT.MAP.OBJECTS (TEXTSTREAM (WHICHW))
           [FUNCTION (LAMBDA (CH# OBJ)
                       (if [AND (EQ 'IM.INDEX.OBJECT (fetch (IMAGEFNS IMAGECLASSNAME)
                                                        of (fetch (IMAGEOBJ IMAGEOBJFNS) of OBJ)))
                                (EQ 'TOC (CAR (fetch (IM.INDEX.DATA TYPE) of (fetch (IMAGEOBJ 
                                                                                          OBJECTDATUM
                                                                                           )
                                                                                of OBJ]
                           then OBJ
                         else NIL]
           NIL
           'OBJECT)
    (IM.INDEX.UPDATE.TOC.NUMBERING (TEXTSTREAM (WHICHW))
           7])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1376 3742 (IM.CHAP.CHAPTER 1386 . 2393) (IRMTOC 2395 . 2605) (IRM-IMPTR 2607 . 2920) (
IRMANALYZE 2922 . 3291) (IRMHC 3293 . 3740)) (3743 11463 (IM.INDEX.UPDATE.TOC.NUMBERING 3753 . 7158) (
IM.INDEX.TOC.TEXT 7160 . 7957) (IM.CHAP.BEFOREHARDCOPYFN 7959 . 9068) (IM.CHAP.WHENINSERTEDFN 9070 . 
9319) (TEDIT.SET.FOLIO.TEXT 9321 . 11461)) (12200 13876 (SCRATCH 12210 . 13874)))))
STOP
