(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "29-Oct-2025 23:56:02" {DSK}<home>frank>il>medley>greetfiles>ONLINE-INIT.;2 11034  

      :EDIT-BY "FGH"

      :CHANGES-TO (ADVICE (SAVEVM :IN \IDLER))
                  (FNS Online.SftpInitInfo Online.SftpUpdateInfo Online.SetUpNOTECARDSDIRECTORIES 
                       Online.DoInit ONLINEP Online.FileButton)
                  (VARS ONLINE-INITCOMS)

      :PREVIOUS-DATE "18-Mar-2025 22:45:51" {DSK}<home>frank>il>medley>greetfiles>ONLINE-INIT.;1)


(PRETTYCOMPRINT ONLINE-INITCOMS)

(RPAQQ ONLINE-INITCOMS
       [(FILES (SYSLOAD)
               APPS-INIT)
        (FILES (SYSLOAD)
               VTCHAT)
        (GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay 
               IDLE.PROFILE IDLE.BOUNCING.BOX Online.SftpDisplayMenu ONLINEP ShellBrowser ShellOpener
               CLHS.OPENER MEDLEYDIR)
        (INITVARS (Online.LogoutTimeout 30)
               (ONLINEP NIL))
        (ADVISE (SAVEVM :IN \IDLER))
        (FNS Online.SftpInitInfo Online.SftpUpdateInfo Online.SetUpNOTECARDSDIRECTORIES 
             Online.FileButton Online.DoInit ONLINEP)
        (DECLARE%: DONTEVAL@LOAD DOCOPY (P (Online.DoInit)))
        (DECLARE%: FIRST DONTEVAL@LOAD DOCOPY (P (BKSYSBUF " "])

(FILESLOAD (SYSLOAD)
       APPS-INIT)

(FILESLOAD (SYSLOAD)
       VTCHAT)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay IDLE.PROFILE
       IDLE.BOUNCING.BOX Online.SftpDisplayMenu ONLINEP ShellBrowser ShellOpener CLHS.OPENER 
       MEDLEYDIR)
)

(RPAQ? Online.LogoutTimeout 30)

(RPAQ? ONLINEP NIL)

[XCL:REINSTALL-ADVICE '(SAVEVM :IN \IDLER)
       :AROUND
       '((:LAST (COND
                   ((ONLINEP)
                    (LOGOUT))
                   (T *]

(READVISE (SAVEVM :IN \IDLER))
(DEFINEQ

(Online.SftpInitInfo
  [LAMBDA NIL                                                (* ; "Edited 17-Dec-2021 01:25 by abc")
    (SETQ Online.SftpPort (UNIX-GETENV "SFTP_PORT"))
    (SETQ Online.SftpPassword (UNIX-GETENV "SFTP_PWD"))
    (SETQ Online.SftpDisplay (CONCAT Online.SftpPort " / " (L-CASE USERNAME)
                                    " / " Online.SftpPassword))
    (SETQ Online.SftpDisplayMenu (create MENU
                                        TITLE _ "SFTP Info"
                                        ITEMS _ (LIST (MKATOM (CONCAT "Port: " Online.SftpPort))
                                                      (MKATOM (CONCAT "User: " (L-CASE USERNAME)))
                                                      (MKATOM (CONCAT "Pwd: " Online.SftpPassword)))
                                        MENUFONT _ (FONTCREATE 'CLASSIC 14 'BOLD])

(Online.SftpUpdateInfo
  [LAMBDA (WHEN)                                          (* ; "Edited 23-Nov-2021 22:44 by medley")
    (COND
       ((OR (EQ WHEN 'AFTERLOGOUT)
            (EQ WHEN 'AFTERSAVEVM))
        (Online.SftpInitInfo])

(Online.SetUpNOTECARDSDIRECTORIES
  [LAMBDA NIL                                                (* ; "Edited 25-Feb-2024 11:20 by fgh")

    (* ;; "Find the location of the Notecards directory and call NC.SetUpNOTECARDSDIRECTORIES.")

    (* ;; " This is needed to make sure that lazy loading of Notecard types works.")

    (* ;; " This function should actually be defined and called  in APPS-INIT.  But until it is")

    (* ;; " we'll include and call it here in ONLIONE-INIT.  No harm in calling it twice when")

    (* ;; " it eventually gets integrated into APPS-INIT.")
                                                             (* ; "Edited 25-Feb-2024 11:15 by fgh")
    (LET* [(LOC1 (CONCAT MEDLEYDIR "notecards>"))
           (LOC2 (CONCAT MEDLEYDIR "..>notecards>"))
           (LOC3 (CONCAT MEDLEYDIR "..>..>notecards>"))
           (NCDIR (for LOC in (LIST LOC1 LOC2 LOC3) thereis (OR (INFILEP (CONCAT LOC 
                                                                                "system>NOTECARDS"))
                                                                (INFILEP (CONCAT LOC 
                                                                              "system>NOTECARDS.LCOM"
                                                                                ]
          (if NCDIR
              then (NC.SetUpNOTECARDSDIRECTORIES NCDIR)
                   T
            else (PRIN1 "Warning: Notecards directory could not be found." T)
                 (PRIN1 "Hence, NOTECARDSDIRECTORIES is probably not set correctly" T)
                 (PRIN1 "and Notecards will not work properly." T)
                 NIL])

(Online.FileButton
  [LAMBDA NIL                                              (* ; "Edited 18-Mar-2025 22:31 by guest")
                                                             (* ; "Edited 17-Mar-2025 16:51 by fgh")
                                                           (* ; "Edited 16-Mar-2025 23:40 by guest")

    (* ;; " Add the FILES IMPORT/EXPORT button to the bottom of the button stack on the right side of the screen")

    (LET* ((RIGHTMARGINISH 140)
           (Apps.BUTTONS (for B in *ALL-BUTTONS* when (WINDOWPROP B 'Apps.BUTTON) collect B))
           (LowestButton (CAR Apps.BUTTONS))
           P B L)
          (if LowestButton
              then 
                   (* ;; " Find the lowest existing button on the right side of the screen")

                   (for B in Apps.BUTTONS when [ILESSP (fetch BOTTOM of (WINDOWPROP B 'REGION))
                                                      (fetch BOTTOM of (WINDOWPROP LowestButton
                                                                              'REGION]
                      do (SETQ LowestButton B)) 

                   (* ;; 
               " Set the y-position for the label  based on the lowest button, then create the label")

                   (SETQ L (Apps.CreateLabel "FILES" (IDIFFERENCE SCREENWIDTH (IDIFFERENCE 
                                                                                     RIGHTMARGINISH 
                                                                                     50))
                                  (IDIFFERENCE (fetch BOTTOM of (WINDOWPROP LowestButton 'REGION))
                                         35)))
                   (WINDOWPROP L 'RIGHTBUTTONFN 'NILL) 

                   (* ;; 
                   " Set the position of the files button based on the label, then create the button")

                   (SETQ P (create POSITION
                                  XCOORD _ (IDIFFERENCE SCREENWIDTH RIGHTMARGINISH)
                                  YCOORD _ (IDIFFERENCE (fetch BOTTOM of (WINDOWPROP LowestButton
                                                                                'REGION))
                                                  85)))
                   (SETQ B (CREATE-BUTTON '(ShellBrowse (UNIX-GETENV "OIO_FB_URL"))
                                  "IMPORT/EXPORT" P))
                   (WINDOWPROP B 'RIGHTBUTTONFN 'NILL)
                   [WINDOWPROP B 'BUTTONEVENTFN (FUNCTION (LAMBDA (BUTTON)
                                                            (if (LASTMOUSESTATE (ONLY LEFT))
                                                                then (EXECUTE-BUTTON BUTTON]
                   T])

(Online.DoInit
  [LAMBDA NIL

    (* ;; "Edited 29-Oct-2025 23:55 by FGH")

    (* ;; "Edited 16-Mar-2025 23:26 by guest")

    (* ;; "Edited 25-Feb-2024 11:37 by fgh")

    (* ;; "Edited 24-Feb-2024 00:26 by runner")

    (* ;; "Edited  7-Dec-2022 11:14 by FGH")

    (* ;; "Edited 12-Nov-2022 13:57 by FGH")

    (* ;; "Edited 12-Oct-2022 20:23 by fgh")

    (* ;; "Edited  6-Sep-2022 17:22 by fgh")

    (* ;; "Edited  4-Sep-2022 16:44 by larry")

    (* ;; "Edited 18-Mar-2022 18:53 by fgh")

    (* ;; "Edited 17-Dec-2021 22:05 by fgh")

    (PROGN 
           (* ;; "Set up SFTP Info widget in WHO-LINE window")

           (Online.SftpInitInfo)
           (NCONC1 AROUNDEXITFNS 'Online.SftpUpdateInfo)
           [COND
              ([NOTANY *WHO-LINE-ENTRY-REGISTRY* (FUNCTION (LAMBDA (ENTRY)
                                                             (STRING-EQUAL (CAR ENTRY)
                                                                    "SFTP"]
               (PROGN (NCONC1 *WHO-LINE-ENTRIES* (LIST "SFTP" 'Online.SftpDisplay 30
                                                       [FUNCTION (LAMBDA NIL
                                                                   (MENU Online.SftpDisplayMenu]
                                                       NIL 
                                       "Port/Username/Password thru which to access files using SFTP"
                                                       ))
                      (NCONC *WHO-LINE-ENTRY-REGISTRY* (LAST *WHO-LINE-ENTRIES*))
                      (INSTALL-WHO-LINE-OPTIONS]

           (* ;; "  Set the IDLE timeout, set to SAVEVM 1 min after idle, set idle bouncing box")

           (LISTPUT IDLE.PROFILE 'SAVEVM 1)
           (LISTPUT IDLE.PROFILE 'TIMEOUT Online.LogoutTimeout)
           (SETQ IDLE.BOUNCING.BOX "Press any key to continue")

           (* ;; "set ONLINEP as a definitive flag that this is an online session")

           (SETQ ONLINEP (COND
                            ((STRING-EQUAL (UNIX-GETENV 'MEDLEY_ONLINE)
                                    "true")
                             T)
                            (T NIL)))

           (* ;; 
     "set the opener for accesssing the Common Lisp Hyperspec - for use in HELPSYS LispUsers package")

           (SETQ CLHS.OPENER "/usr/local/interlisp/online/bin/request_new_tab")
           (SETQ ShellOpener "/usr/local/interlisp/online/bin/request_new_tab")
           (SETQ ShellBrowser "/usr/local/interlisp/online/bin/request_new_tab")

           (* ;; " get rid of Notecards logout prevention")

           (ADVISE 'NC.LogoutAdvice 'BEFORE 'FIRST '(RETURN T))

           (* ;; "Reset Chat displaytype to enable vt100")

           (RPLACA (CAR CHAT.DISPLAYTYPES)
                  NIL)

           (* ;; "Create File Import/Export Button")

           (Online.FileButton)

           (* ;; "If there is a start-script file, load it ")

           (LET [(START-SCRIPT (UNIX-GETENV 'START_SCRIPT]
                (IF START-SCRIPT
                    THEN (LOAD START-SCRIPT])

(ONLINEP
  [LAMBDA NIL                                                (* ; "Edited 24-Feb-2024 22:31 by fgh")
    ONLINEP])
)
(DECLARE%: DONTEVAL@LOAD DOCOPY 

(Online.DoInit)
)
(DECLARE%: FIRST DONTEVAL@LOAD DOCOPY 

(BKSYSBUF " ")
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1924 10898 (Online.SftpInitInfo 1934 . 2807) (Online.SftpUpdateInfo 2809 . 3064) (
Online.SetUpNOTECARDSDIRECTORIES 3066 . 4774) (Online.FileButton 4776 . 7604) (Online.DoInit 7606 . 
10759) (ONLINEP 10761 . 10896)))))
STOP
