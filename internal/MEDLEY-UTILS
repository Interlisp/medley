(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "XCL" BASE 10)

(FILECREATED "25-Jun-2022 08:30:35" |{DSK}<home>larry>medley>internal>MEDLEY-UTILS.;2| 15885  

      :CHANGES-TO (VARS MEDLEY-UTILSCOMS)

      :PREVIOUS-DATE "22-Jun-2022 09:40:10" |{DSK}<home>larry>medley>internal>MEDLEY-UTILS.;1|)


(PRETTYCOMPRINT MEDLEY-UTILSCOMS)

(RPAQQ MEDLEY-UTILSCOMS
       ((FNS GATHER-INFO MAKE-FULLER-DB MEDLEY-FIX-LINKS MEDLEY-FIX-DATES)
        (VARS MEDLEY-FIX-DIRS OKSOURCES OKLIBRARY OKLISPUSERS OKINTERNAL)
        (FNS MAKE-EXPORTS-ALL MAKE-WHEREIS-HASH)
        
        (* |;;| "hardcopy files")

        (FNS HCFILES BADFILE)
        (INITVARS (HCFILES)
               (BADFILES))
        (FUNCTIONS PICK)
        (COMMANDS "pick")))
(DEFINEQ

(GATHER-INFO
  (LAMBDA (PHASE)                                         (* \; "Edited 26-Dec-2021 18:56 by larry")
                                                          (* \; "Edited 24-Oct-2021 09:43 by larry")
    (SELECTQ PHASE
        (ALL (|for| I |from| 0 |to| 4 |do| (GATHER-INFO I)))
        (0 (SETQ SYSFILES (UNION SYSFILES FILELST))
           (SETQ FILELST NIL)
           (FILESLOAD (SOURCE)
                  SYSEDIT))
        (1 (SETQ LOADEDFILES (|for| X |in| LOADEDFILELST |collect| (FILENAMEFIELD X 'NAME)))
           (FILESLOAD FILESETS)
           (SETQ ALLFILESETSFILES (|for| X |in| FILESETS |join| (APPEND (EVAL X))))
           (SETQ SOURCES (|for| X |in| (DIRECTORY (MEDLEYDIR "sources" "*.*;" T))
                            |when| (NOT (MEMB (FILENAMEFIELD X 'EXTENSION)
                                              '(LCOM DFASL TEDIT TXT)))
                            |collect| (FILENAMEFIELD X 'NAME))))
        (-1 (PRINTOUT T " loaded files not in SYSFILES or FILELST: "
                   (|for| X |in| LOADEDFILES |when| (NOT (OR (FMEMB X SYSFILES)
                                                             (FMEMB X FILELST))) |collect| X)
                   T)
            (PRINTOUT T "Sources not loaded: " (CL:SET-DIFFERENCE SOURCES (APPEND ALLFILESETSFILES 
                                                                                 LOADEDFILES))
                   T)
            (PRINTOUT T "Files in FILESETS not loaded " (CL:SET-DIFFERENCE ALLFILESETSFILES 
                                                               LOADEDFILES)
                   T))
        (2 (SETQ DEFINEDFNS (LET ((DEFD NIL))
                                 (MAPATOMS (FUNCTION (CL:LAMBDA (X)
                                                            (CL:WHEN (GETD X)
                                                                (CL:SETQ DEFD (CONS X DEFD))))))
                                 DEFD))
           (|for| X |in| DEFINEDFNS |when| (CCODEP X)
              |do| (LET ((Y (PUTPROP X 'CCC (CALLSCCODE X))))
                        (|for| REV |in| '(BLOCK-CALLED-BY CALLED-BY SPECIAL-BY GLOBAL-BY)
                           |as| VAL |in| Y |do| (|for| S |in| VAL
                                                   |do| (PUTPROP S REV (CONS X (GETPROP S REV)))))))
           (SETQ CALLEDFNS NIL)
           (MAPATOMS (FUNCTION (LAMBDA (X)
                                 (|if| (AND (NOT (GETD X))
                                            (GETPROP X 'CALLED-BY))
                                     |then| (CL:PUSH X CALLEDFNS))))))
        (-2 (PRINTOUT T "Functions called and not defined" CALLEDFNS T))
        (3 (|for| X |in| SYSFILES
              |do| (LOAD X 'PROP)
                   (PUTPROP X 'CONTENT (READFILE X))
                   (|for| EXR |in| (GETPROP X 'CONTENT)
                      |do| (SELECTQ (CAR EXR)
                               (DEFINEQ (|for| DFN |in| (CDR EXR)
                                           |do| (|if| (EQUAL (CADR DFN)
                                                             (GETPROP (CAR DFN)
                                                                    'EXPR))
                                                    |then| (PRINTOUT T (CAR DFN)
                                                                  " ")
                                                          (PUTPROP (CAR DFN)
                                                                 'EXPR
                                                                 (CADR DFN))
                                                  |else| (PRINTOUT T (CAR DFN)
                                                                "* "))))
                               NIL)))
           (SETQ ALLCONTENT (|for| X |in| SYSFILES |collect| (CONS X (GETPROP X 'CONTENT))))
                                                             (* \; " don't edit with SEDIT")
           (LET (DUPS)
                (|for| X |in| SYSFILES
                   |do| (|for| FN |in| (FILEFNSLST X)
                           |do| (|if| (GETPROP FN 'WHEREIS)
                                    |then| (NCONC1 (GETPROP FN 'WHEREIS)
                                                  X)
                                          (OR (FMEMB FN DUPS)
                                              (SETQ DUPS (CONS FN DUPS)))
                                  |else| (PUTPROP FN 'WHEREIS (LIST X)))))
                (SETQ DUPFNS DUPS))
           (SETQ NO-SOURCE (|for| X |in| DEFINEDFNS |when| (NOT (GETPROP X 'EXPR)) |collect| X)))
        (-3 (PRINTOUT T "Functions compiled but no expr" NO-SOURCE T)
            (PRINTOUT T "Functions on more than one file: " DUPFNS T))
        (4 (PRINTOUT T T "STARTING MASTERSCOPE PHASE ON " (DATE)
                  T)
           (FILESLOAD (SOURCE)
                  SYSEDIT)
           (|for| X |in| SYSFILES |do| (MSNOTICEFILE X))
           (|for| X |in| SYSFILES |do| (PRINTOUT T T "Analyzing " X T)
                                       (MASTERSCOPE `(ANALYZE ON ,(KWOTE X)))))
        (-4 "No queries yet")
        (HELP))))

(MAKE-FULLER-DB
  (LAMBDA NIL                                             (* \; "Edited 20-Jun-2022 17:23 by larry")
    (FILESLOAD (SOURCE)
           FILESETS)
    (DRIBBLE (MEDLEYDIR "tmp" "fuller.dribble" T T))
    (DOFILESLOAD (APPEND OKSOURCES OKLIBRARY OKLISPUSERS OKINTERNAL))
    (GATHER-INFO 'ALL)
    (MASTERSCOPE '(WHO CALLS XYZZY))
    (DUMPDATABASE NIL (MKATOM (MEDLEYDIR "tmp" "fuller.database" T T)))
    (DRIBBLE)
    (MAKESYS (MEDLEYDIR "tmp" "fuller.sysout" T T)
           "Welcome to Fuller sysout")))

(MEDLEY-FIX-LINKS
  (LAMBDA (UNIXPATH)                                      (* \; "Edited 18-Jan-2021 12:01 by larry")
    (OR UNIXPATH (SETQ UNIXPATH (UNIX-GETENV "MEDLEYDIR"))
        (ERROR "No Directory"))                           (* \; "Edited 18-Jan-2021 11:45 by larry")
    (|ShellCommand| (CONCAT "cd " UNIXPATH " && /bin/sh scripts/fixlinks && /bin/sh /tmp/doit"))))

(MEDLEY-FIX-DATES
  (LAMBDA (DIRS)                                          (* \; "Edited 28-Jan-2021 12:15 by larry")
    (|for| X |in| (OR DIRS MEDLEY-FIX-DIRS) |join| (FIX-DIRECTORY-DATES (MEDLEYDIR (PRINT X T))))))
)

(RPAQQ MEDLEY-FIX-DIRS ("sources" "library" "lispusers" "internal" "greetfiles" "doctools"))

(RPAQQ OKSOURCES (RENAMEFNS VMEM READSYS CASH-FILE HASH-FILE MEDLEYDIR MAKEINIT))

(RPAQQ OKLIBRARY
       (POSTSCRIPTSTREAM CHATTERMINAL DMCHAT CHAT PRESS TEDITDCL PCTREE TEXTOFD TEDITCOMMAND 
              TEDITSCREEN TEDITABBREV TEDITLOOKS TEDITFIND TEDITHISTORY TEDITFILE TEDITWINDOW 
              TEDITSELECTION READNUMBER EDITBITMAP IMAGEOBJ TFBRAVO TEDITHCPY TEDITPAGE TEDITMENU 
              TEDITFNKEYS TEDIT HRULE TEDITCHAT TABLEBROWSER FILEBROWSER GRAPHER SPY WHERE-IS 
              COPYFILES MSANALYZE MSPARSE MSCOMMON MASTERSCOPE UNIXCOMM UNIXPRINT UNICODE HASH 
              CLIPBOARD UNIXCHAT VT100KP VTCHAT SKETCH SKETCHBMELT SCALEBITMAP SKETCHOBJ SKETCHEDIT 
              SKETCHELEMENTS SKETCHOPS MATMULT SAMEDIR))

(RPAQQ OKLISPUSERS (THINFILES ISO8859IO DINFO HELPSYS MODERNIZE WHEELSCROLL PRETTYFILEINDEX WHO-LINE
                          BACKGROUND-YIELD OBJECTWINDOW REGIONMANAGER COMPARETEXT EXAMINEDEFS 
                          COMPARESOURCES COMPAREDIRECTORIES PSEUDOHOSTS DATEFORMAT-EDITOR DOC-OBJECTS
                          EQUATIONS BICLOCK FILEWATCH LIFE IDLEHAX GITFNS TMAX IMTOOLS))

(RPAQQ OKINTERNAL (MEDLEY-UTILS))
(DEFINEQ

(MAKE-EXPORTS-ALL
  (LAMBDA NIL                                             (* \; "Edited  9-Mar-2021 16:11 by larry")
                                                             (* "Edited May 3, 2018 by Ron Kaplan--relative to MEDLEYDIR/lispcore/.  Don't know why it does the CORE/RENAME")
                                                             (* 
          "Edited Aug 17 94 by Sybalsky -- point it to /king/export/lispcore as the truth directory.")
                                                             (* 
                        "Edited July 5, 1990 by Sybalsky -- point it to Pele as the truth directory.")
                                                             (* 
                                                             "Edited September 29, 1986 by van Melle")
    (CNDIR (MEDLEYDIR "sources"))
    (LOAD 'FILESETS)
    (GATHEREXPORTS EXPORTFILES (MEDLEYDIR "tmp" "exports.all" T))))

(MAKE-WHEREIS-HASH
  (LAMBDA NIL                                               (* \; "Edited 12-Mar-2022 12:46 by rmk")
                                                          (* \; "Edited 24-Mar-2021 13:26 by larry")
    (LET ((FILING.ENUMERATION.DEPTH 2)
          HASHFILE)
         (DRIBBLE (MEDLEYDIR "tmp" "whereis.dribble" T))
         (SETQ HASHFILE (XCL::WHERE-IS-NOTICE (MEDLEYDIR "tmp" "whereis.hash-tmp" T)
                               :FILES
                               (|for| X |in| MEDLEY-FIX-DIRS |collect| (CONCAT (MEDLEYDIR X)
                                                                              "*.;"))
                               :HASH-FILE-SIZE 60000 :NEW T))
         (RENAMEFILE HASHFILE (MEDLEYDIR "tmp" "whereis.hash" T))
         (DRIBBLE))))
)



(* |;;| "hardcopy files")

(DEFINEQ

(HCFILES
  (LAMBDA (TFILE DEST REDOFLG TOPDIRLEN)                  (* \; "Edited 21-Jun-2022 22:59 by larry")
                                                          (* \; "Edited 31-May-2022 09:31 by larry")
                                                          (* \; "Edited 20-Feb-2022 12:16 by larry")
                                                          (* \; "Edited 21-Aug-2021 20:56 by larry")
    (DECLARE (SPECVARS TFILE))
    (|if| (NULL TFILE)
        |then| (SETQ TFILE MEDLEYDIR))
    (COND
       ((DIRECTORYNAMEP TFILE)

        (* |;;| "canonicalize")

        (SETQ TFILE (DIRECTORYNAME TFILE))
        (OR TOPDIRLEN (SETQ TOPDIRLEN (CL:LENGTH (FILENAMEFIELD.STRING TFILE 'DIRECTORY))))
        (CL:UNLESS DEST
            (|ShellCommand| (CONCAT "mkdir -p " (UNIX-GETENV "MEDLEYDIR")
                                   "/tmp/psfiles/"))
            (SETQ DEST (MEDLEYDIR "tmp/psfiles" NIL T T)))

        (* |;;| "first deal with files in this directory")

        (|for| X |in| (DIRECTORY (CONCAT TFILE "*.TED*;")) |do| (HCFILES X DEST REDOFLG TOPDIRLEN))

        (* |;;| " then deal with subdirs ")

        (|for| X |in| (DIRECTORY (CONCAT TFILE "*"))
           |when| (|for| SKIP |in| '(">." ">dinfo>") |always| (NOT (STRPOS SKIP (L-CASE X))))
           |when| (DIRECTORYNAMEP X) |do| (HCFILES X DEST REDOFLG TOPDIRLEN)))
       ((SETQ TFILE (INFILEP TFILE))
        (LET* ((TF (UNPACKFILENAME.STRING TFILE))
               (NAME (LISTGET TF 'NAME))
               (DIR (LISTGET TF 'DIRECTORY))
               (PSFILE (PACKFILENAME.STRING
                        'EXTENSION
                        (|if| (EQ REDOFLG 'IP)
                            |then| "IP"
                          |else| "PS")
                        'NAME
                        (|if| (EQ DEST T)
                            |then|                           (* \; "with the tedit file")
                                  NAME
                          |else| (CONCAT (PACK (SUBST '- '> (UNPACK (SUBSTRING DIR (IPLUS 2 TOPDIRLEN
                                                                                          )
                                                                           -1))))
                                        "-" NAME))
                        'HOST
                        (LISTGET TF 'HOST)
                        'DIRECTORY
                        (|if| (EQ DEST T)
                            |then| DIR
                          |else| DEST)))
               (TEXTSTREAM))
              (|if| (AND (NOT REDOFLG)
                         (INFILEP PSFILE))
                  |then|                                     (* \; " do nothing")
                        (PRINTOUT T PSFILE " already there" T)
                |elseif| (EQ REDOFLG 'TEST)
                  |then| (PRINTOUT T TFILE "-> " PSFILE T)
                        (CLOSEF (OPENTEXTSTREAM TFILE))
                ELSEIF (MEMBER TFILE BADFILES)
                  THEN (PRINTOUT T "Skipping " TFILE " on BADFILES")
                |else| (PRINTOUT T "Converting " TFILE " to " PSFILE "...")
                      (TEDIT.FORMAT.HARDCOPY (SETQ TEXTSTREAM (OPENTEXTSTREAM TFILE))
                             PSFILE T NIL NIL NIL (|if| (EQ REDOFLG 'IP)
                                                      |then| 'INTERPRESS
                                                    |else| 'POSTSCRIPT))
                      (|printout| T " DONE" T)
                      (CLOSEF? TEXTSTREAM))))
       (T (PRINTOUT T "no such file " T)))))

(BADFILE
  (LAMBDA NIL                                             (* \; "Edited 22-Jun-2022 09:40 by larry")
    (PUSHNEW BADFILES TFILE)
    (LET ((STR (OPENSTREAM "BADFILES.TXT" 'APPEND)))
         (SETFILEPTR STR -1)
         (PRINT TFILE STR)
         (CLOSEF STR))
    (RETFROM 'HCFILES)))
)

(RPAQ? HCFILES )

(RPAQ? BADFILES )

(CL:DEFUN PICK (&OPTIONAL TYPE CHOICES)                   (* \; "Edited 24-Jun-2022 23:07 by larry")
                                                          (* \; "Edited 22-Jun-2022 23:22 by larry")
   (SELECTQ TYPE
       (NIL (PICK (PICK 'ONEOF '(FILE ISSUE PFOJECT))))
       (ISSUE (LET ((ISSUE (PICK 'ONEOF (OR CHOICES (GIT-COMMAND 
                            "gh issue list -L 5000 -R interlisp/medley | sed 's/\\([0-9]*\\).*/\\1/'"
                                                           ))))
                    (STR (OPENTEXTSTREAM)))
                   (FOR S IN (GIT-COMMAND (CL:FORMAT NIL "gh issue view ~a --comments" ISSUE))
                      DO (CL:FORMAT STR "~a~&" S)
                      FINALLY (TEDIT STR NIL T `(READONLY T TITLE ,(CL:FORMAT NIL "Issue #~a" ISSUE))
                                     ))))
       (DIR (PICK 'ONEOF '(LISPUSERS LIBRARY DOCTOOLS SOURCES INTERNAL)))
       (FILE (PICK 'ONEOF (DIRECTORY (MEDLEYDIR (PICK 'DIR)))))
       (PROJECT (PICK 'ONEOF '(CLOS ROOMS LOOPS NOTECARDS ONLINE TEST GITBOOK COMMUNITY RNVOD)))
       (ONEOF (CAR (NTH CHOICES (RAND 1 (LENGTH CHOICES)))))
       (HELP)))

(DEFCOMMAND "pick" (FIRST . REST)
   (PICK FIRST REST))
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (757 7362 (GATHER-INFO 767 . 6177) (MAKE-FULLER-DB 6179 . 6717) (MEDLEY-FIX-LINKS 6719
 . 7116) (MEDLEY-FIX-DATES 7118 . 7360)) (8638 10431 (MAKE-EXPORTS-ALL 8648 . 9607) (MAKE-WHEREIS-HASH
 9609 . 10429)) (10466 14531 (HCFILES 10476 . 14216) (BADFILE 14218 . 14529)) (14578 15793 (PICK 14578
 . 15793)))))
STOP
