(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "XCL" BASE 10)

(FILECREATED "20-Feb-2022 12:59:27" |{DSK}<home>larry>medley>internal>MEDLEY-UTILS.;4| 12701  

      :CHANGES-TO (FNS HCFILES)

      :PREVIOUS-DATE "17-Feb-2022 21:44:44" |{DSK}<home>larry>medley>internal>MEDLEY-UTILS.;1|)


(PRETTYCOMPRINT MEDLEY-UTILSCOMS)

(RPAQQ MEDLEY-UTILSCOMS ((FNS GATHER-INFO MEDLEY-FIX-LINKS MEDLEY-FIX-DATES)
                         (VARS MEDLEY-FIX-DIRS)
                         (FNS MAKE-EXPORTS-ALL MAKE-WHEREIS-HASH)
                         
                         (* |;;| "hardcopy files")

                         (FNS HCFILES)
                         (INITVARS (HCFILES))))
(DEFINEQ

(GATHER-INFO
  (LAMBDA (PHASE)                                            (* \; 
                                                             "Edited 26-Dec-2021 18:56 by larry")
                                                             (* \; 
                                                             "Edited 24-Oct-2021 09:43 by larry")
    (SELECTQ PHASE
        (ALL (|for| I |from| 0 |to| 4 |do| (GATHER-INFO I)))
        (0 (SETQ SYSFILES (UNION SYSFILES FILELST))
           (SETQ FILELST NIL)
           (FILESLOAD (SOURCE)
                  SYSEDIT))
        (1 (SETQ LOADEDFILES (|for| X |in| LOADEDFILELST |collect| (FILENAMEFIELD X 'NAME)))
           (FILESLOAD FILESETS)
           (SETQ ALLFILESETSFILES (|for| X |in| FILESETS |join| (APPEND (EVAL X))))
           (SETQ SOURCES (|for| X |in| (DIRECTORY (MEDLEYDIR "sources" "*.*;" T))
                            |when| (NOT (MEMB (FILENAMEFIELD X 'EXTENSION)
                                              '(LCOM DFASL TEDIT TXT)))
                            |collect| (FILENAMEFIELD X 'NAME))))
        (-1 (PRINTOUT T " loaded files not in SYSFILES or FILELST: "
                   (|for| X |in| LOADEDFILES |when| (NOT (OR (FMEMB X SYSFILES)
                                                             (FMEMB X FILELST))) |collect| X)
                   T)
            (PRINTOUT T "Sources not loaded: " (CL:SET-DIFFERENCE SOURCES (APPEND ALLFILESETSFILES 
                                                                                 LOADEDFILES))
                   T)
            (PRINTOUT T "Files in FILESETS not loaded " (CL:SET-DIFFERENCE ALLFILESETSFILES 
                                                               LOADEDFILES)
                   T))
        (2 (SETQ DEFINEDFNS (LET ((DEFD NIL))
                                 (MAPATOMS (FUNCTION (CL:LAMBDA (X)
                                                            (CL:WHEN (GETD X)
                                                                (CL:SETQ DEFD (CONS X DEFD))))))
                                 DEFD))
           (|for| X |in| DEFINEDFNS |when| (CCODEP X)
              |do| (LET ((Y (PUTPROP X 'CCC (CALLSCCODE X))))
                        (|for| REV |in| '(BLOCK-CALLED-BY CALLED-BY SPECIAL-BY GLOBAL-BY)
                           |as| VAL |in| Y |do| (|for| S |in| VAL
                                                   |do| (PUTPROP S REV (CONS X (GETPROP S REV)))))))
           (SETQ CALLEDFNS NIL)
           (MAPATOMS (FUNCTION (LAMBDA (X)
                                 (|if| (AND (NOT (GETD X))
                                            (GETPROP X 'CALLED-BY))
                                     |then| (CL:PUSH X CALLEDFNS))))))
        (-2 (PRINTOUT T "Functions called and not defined" CALLEDFNS T))
        (3 (|for| X |in| SYSFILES
              |do| (LOAD X 'PROP)
                   (PUTPROP X 'CONTENT (READFILE X))
                   (|for| EXR |in| (GETPROP X 'CONTENT)
                      |do| (SELECTQ (CAR EXR)
                               (DEFINEQ (|for| DFN |in| (CDR EXR)
                                           |do| (|if| (EQUAL (CADR DFN)
                                                             (GETPROP (CAR DFN)
                                                                    'EXPR))
                                                    |then| (PRINTOUT T (CAR DFN)
                                                                  " ")
                                                          (PUTPROP (CAR DFN)
                                                                 'EXPR
                                                                 (CADR DFN))
                                                  |else| (PRINTOUT T (CAR DFN)
                                                                "* "))))
                               NIL)))
           (SETQ ALLCONTENT (|for| X |in| SYSFILES |collect| (CONS X (GETPROP X 'CONTENT))))
                                                             (* \; " don't edit with SEDIT")
           (LET (DUPS)
                (|for| X |in| SYSFILES
                   |do| (|for| FN |in| (FILEFNSLST X)
                           |do| (|if| (GETPROP FN 'WHEREIS)
                                    |then| (NCONC1 (GETPROP FN 'WHEREIS)
                                                  X)
                                          (OR (FMEMB FN DUPS)
                                              (SETQ DUPS (CONS FN DUPS)))
                                  |else| (PUTPROP FN 'WHEREIS (LIST X)))))
                (SETQ DUPFNS DUPS))
           (SETQ NO-SOURCE (|for| X |in| DEFINEDFNS |when| (NOT (GETPROP X 'EXPR)) |collect| X)))
        (-3 (PRINTOUT T "Functions compiled but no expr" NO-SOURCE T)
            (PRINTOUT T "Functions on more than one file: " DUPFNS T))
        (4 (PRINTOUT T T "STARTING MASTERSCOPE PHASE ON " (DATE)
                  T)
           (FILESLOAD (SOURCE)
                  SYSEDIT)
           (|for| X |in| SYSFILES |do| (MSNOTICEFILE X))
           (|for| X |in| SYSFILES |do| (PRINTOUT T T "Analyzing " X T)
                                       (MASTERSCOPE `(ANALYZE ON ,(KWOTE X)))))
        (-4 "No queries yet")
        (HELP))))

(MEDLEY-FIX-LINKS
  (LAMBDA (UNIXPATH)                                         (* \; 
                                                           "Edited 18-Jan-2021 12:01 by larry")
    (OR UNIXPATH (SETQ UNIXPATH (UNIX-GETENV "MEDLEYDIR"))
        (ERROR "No Directory"))                              (* \; 
                                                           "Edited 18-Jan-2021 11:45 by larry")
    (|ShellCommand| (CONCAT "cd " UNIXPATH " && /bin/sh scripts/fixlinks && /bin/sh /tmp/doit"))))

(MEDLEY-FIX-DATES
  (LAMBDA (DIRS)                                             (* \; 
                                                           "Edited 28-Jan-2021 12:15 by larry")
    (|for| X |in| (OR DIRS MEDLEY-FIX-DIRS) |join| (FIX-DIRECTORY-DATES
                                                                (MEDLEYDIR (PRINT X T))))))
)

(RPAQQ MEDLEY-FIX-DIRS ("sources" "library" "lispusers" "internal" "greetfiles" "doctools"))
(DEFINEQ

(MAKE-EXPORTS-ALL
  (LAMBDA NIL                                                (* \; 
                                                           "Edited  9-Mar-2021 16:11 by larry")
                                                             (* "Edited May 3, 2018 by Ron Kaplan--relative to MEDLEYDIR/lispcore/.  Don't know why it does the CORE/RENAME")
                                                             (* 
        "Edited Aug 17 94 by Sybalsky -- point it to /king/export/lispcore as the truth directory.")
                                                             (* 
                      "Edited July 5, 1990 by Sybalsky -- point it to Pele as the truth directory.")
                                                             (* 
                                                           "Edited September 29, 1986 by van Melle")
    (CNDIR (MEDLEYDIR "sources"))
    (LOAD 'FILESETS)
    (GATHEREXPORTS EXPORTFILES (MEDLEYDIR "tmp" "exports.all" T))))

(MAKE-WHEREIS-HASH
  (LAMBDA NIL                                                (* \; 
                                                           "Edited 24-Mar-2021 13:26 by larry")
    (LET ((FILING.ENUMERATION.DEPTH 1)
          HASHFILE)
         (DRIBBLE (MEDLEYDIR "tmp" "whereis.dribble" T))
         (SETQ HASHFILE (XCL::WHERE-IS-NOTICE (MEDLEYDIR "tmp" "whereis.hash-tmp" T)
                               :FILES
                               (|for| X |in| MEDLEY-FIX-DIRS |collect|
                                                                     (CONCAT (MEDLEYDIR X)
                                                                            "*.;"))
                               :HASH-FILE-SIZE 60000 :NEW T))
         (RENAMEFILE HASHFILE (MEDLEYDIR "tmp" "whereis.hash" T))
         (DRIBBLE))))
)



(* |;;| "hardcopy files")

(DEFINEQ

(HCFILES
  (LAMBDA (TFILE PREFIX DEST REDOFLG TOPDIRLEN)           (* \; "Edited 20-Feb-2022 12:16 by larry")
                                                          (* \; "Edited 21-Aug-2021 20:56 by larry")
    (DECLARE (SPECVARS TFILE))
    (COND
       ((NULL TFILE)
        (HCFILES MEDLEYDIR))
       ((DIRECTORYNAMEP TFILE)
        (SETQ TFILE (DIRECTORYNAME TFILE))
        (OR TOPDIRLEN (SETQ TOPDIRLEN (IPLUS 1 (CL:LENGTH (MKSTRING (FILENAMEFIELD TFILE 'DIRECTORY))
                                                      ))))
        (OR DEST (SETQ DEST (MEDLEYDIR "tmp/psfiles" NIL T T)))

        (* |;;| "first deal with files in this directory")

        (|for| X |in| (|if| (EQ REDOFLG 'REV)
                          |then| (REVERSE (DIRECTORY (CONCAT TFILE "*.TED*;")))
                        |else| (DIRECTORY (CONCAT TFILE "*.TED*;")))
           |do| (HCFILES X PREFIX DEST REDOFLG TOPDIRLEN))

        (* |;;| " then deal with subdirs ")

        (|for| X |in| (|if| (EQ REDOFLG 'REV)
                          |then| (REVERSE (DIRECTORY (CONCAT TFILE "*")))
                        |else| (DIRECTORY (CONCAT TFILE "*")))
           |when| (|for| SKIP |in| '(">." ">dinfo>") |always| (NOT (STRPOS SKIP (L-CASE X))))
           |when| (DIRECTORYNAMEP X) |do| (HCFILES X PREFIX DEST REDOFLG TOPDIRLEN)))
       ((SETQ TFILE (INFILEP TFILE))
        (PROG ((PSFILE (PACKFILENAME.STRING 'EXTENSION (|if| (EQ REDOFLG 'IP)
                                                           |then| 'IP
                                                         |else| "PS")
                              'NAME
                              (CONCAT (OR PREFIX "")
                                     (|if| PREFIX
                                         |then| "-"
                                       |else| "")
                                     (PACK (SUBST '- '> (UNPACK (SUBSTRING (FILENAMEFIELD
                                                                            TFILE
                                                                            'DIRECTORY)
                                                                       (IPLUS 1 TOPDIRLEN)
                                                                       -1))))
                                     "-"
                                     (FILENAMEFIELD TFILE 'NAME))
                              'DIRECTORY DEST))
               (TEXTSTREAM))
              (|if| (AND (NOT REDOFLG)
                         (INFILEP PSFILE))
                  |then|                                     (* \; " do nothing")
                        (PRINTOUT T PSFILE " already there" T)
                |elseif| (EQ REDOFLG 'TEST)
                  |then| (PRINTOUT T "TESTING " TFILE)
                        (CLOSEF (OPENTEXTSTREAM TFILE))
                |else| (PRINTOUT T "Converting " TFILE "...")
                      (TEDIT.FORMAT.HARDCOPY (SETQ TEXTSTREAM (OPENTEXTSTREAM TFILE))
                             PSFILE T NIL NIL NIL (|if| (EQ REDOFLG 'IP)
                                                      |then| 'INTERPRESS
                                                    |else| 'POSTSCRIPT))
                      (|printout| T " DONE" T)
                      (CLOSEF? TEXTSTREAM))))
       (T (PRINTOUT T "no such file " T)))))
)

(RPAQ? HCFILES )
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (699 7147 (GATHER-INFO 709 . 6249) (MEDLEY-FIX-LINKS 6251 . 6774) (MEDLEY-FIX-DATES 6776
 . 7145)) (7246 9117 (MAKE-EXPORTS-ALL 7256 . 8272) (MAKE-WHEREIS-HASH 8274 . 9115)) (9152 12656 (
HCFILES 9162 . 12654)))))
STOP
