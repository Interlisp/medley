(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "XCL" BASE 10)

(FILECREATED "12-Aug-2022 10:11:51" |{DSK}<home>larry>medley>internal>MEDLEY-UTILS.;3| 12120  

      :CHANGES-TO (FNS GATHER-INFO MAKE-FULLER-DB)
                  (VARS MEDLEY-UTILSCOMS)

      :PREVIOUS-DATE " 4-Aug-2022 09:50:04" |{DSK}<home>larry>medley>internal>MEDLEY-UTILS.;1|)


(PRETTYCOMPRINT MEDLEY-UTILSCOMS)

(RPAQQ MEDLEY-UTILSCOMS ((FNS GATHER-INFO MAKE-FULLER-DB MEDLEY-FIX-LINKS MEDLEY-FIX-DATES)
                         (VARS MEDLEY-FIX-DIRS CHECKEDFILES)
                         (FNS MAKE-EXPORTS-ALL MAKE-WHEREIS-HASH)))
(DEFINEQ

(GATHER-INFO
  (LAMBDA (PHASE)                                           (* \; "Edited 12-Aug-2022 10:09 by lmm")
    (SELECTQ PHASE
        (ALL (|for| I |from| 0 |to| 4 |do| (GATHER-INFO I)))
        (0 (SETQ SYSFILES (UNION SYSFILES FILELST))
           (SETQ FILELST NIL)
           (FILESLOAD (SOURCE)
                  SYSEDIT))
        (1 (SETQ FILESTATUS (COPY CHECKEDFILES))             (* \; 
                                                    " Start with manual cultivated lispusers/library")
                                                             (* \; " uaing FILESTATUS")
           (FOR X IN FILESETS
              DO (FOR Y IN (EVAL X) |do| (CL:PUSH (CDR (OR (ASSOC Y FILESTATUS)
                                                           (CAR (CL:PUSH (LIST Y)
                                                                       FILESTATUS))))
                                                X)))         (* \; "add the 0LISPSET etc")
           (|for| DIR |in| '(SOURCES LIBRARY LISPUSERS GREETFILES ROOMS DOCTOOLS)
              DO (LET (NAME (FILING.ENUMERATION.DEPTH T))
                      (|for| F |in| (DIRECTORY (MEDLEYDIR DIR "*.;" T T))
                         |do| (CL:PUSH DIR (CDR (OR (ASSOC (SETQ F (FILENAMEFIELD F 'NAME))
                                                           FILESTATUS)
                                                    (CAR (CL:PUSH (LIST F)
                                                                FILESTATUS))))))))
           (|for| X |in| LOADEDFILELST |do| (CL:PUSH 'LOADED
                                                   (CDR (OR (ASSOC (SETQ X (FILENAMEFIELD
                                                                            X
                                                                            'NAME))
                                                                   FILESTATUS)
                                                            (CAR (CL:PUSH (LIST X)
                                                                        FILESTATUS)))))))
        (-1 (FOR X IN FILESTATUS |do| (CL:WHEN (AND (MEMB 'LOADED X)
                                                    (NOT (OR (MEMB (CAR X)
                                                                   SYSFILES)
                                                             (MEMB (CAR X)
                                                                   FILELST))))
                                          (CL:FORMAT T "~S loaded not in SYSFILES or FILELST ~n"
                                                 (CAR X)))))
        (2 (SETQ DEFINEDFNS (LET ((DEFD NIL))
                                 (MAPATOMS (FUNCTION (CL:LAMBDA (X)
                                                            (CL:WHEN (GETD X)
                                                                (CL:SETQ DEFD (CONS X DEFD))))))
                                 DEFD))
           (|for| X |in| DEFINEDFNS |when| (CCODEP X)
              |do| (LET ((Y (PUTPROP X 'CCC (CALLSCCODE X))))
                        (|for| REV |in| '(BLOCK-CALLED-BY CALLED-BY SPECIAL-BY GLOBAL-BY)
                           |as| VAL |in| Y |do| (|for| S |in| VAL
                                                   |do| (PUTPROP S REV (CONS X (GETPROP S REV)))))))
           (SETQ CALLEDFNS NIL)
           (MAPATOMS (FUNCTION (LAMBDA (X)
                                 (|if| (AND (NOT (GETD X))
                                            (GETPROP X 'CALLED-BY))
                                     |then| (CL:PUSH X CALLEDFNS))))))
        (-2 (PRINTOUT T "Functions called and not defined" CALLEDFNS T))
        (3 (|for| X |in| SYSFILES WHEN (FINDFILE X)
              |do| (LOAD X 'PROP)
                   (PUTPROP X 'CONTENT (READFILE X))
                   (|for| EXR |in| (GETPROP X 'CONTENT)
                      |do| (SELECTQ (CAR EXR)
                               (DEFINEQ (|for| DFN |in| (CDR EXR)
                                           |do| (|if| (EQUAL (CADR DFN)
                                                             (GETPROP (CAR DFN)
                                                                    'EXPR))
                                                    |then| (PRINTOUT T (CAR DFN)
                                                                  " ")
                                                          (PUTPROP (CAR DFN)
                                                                 'EXPR
                                                                 (CADR DFN))
                                                  |else| (PRINTOUT T (CAR DFN)
                                                                "* "))))
                               NIL)))
           (SETQ ALLCONTENT (|for| X |in| SYSFILES |collect| (CONS X (GETPROP X 'CONTENT))))
                                                             (* \; " don't edit with SEDIT")
           (LET (DUPS)
                (|for| X |in| SYSFILES
                   |do| (|for| FN |in| (FILEFNSLST X)
                           |do| (|if| (GETPROP FN 'WHEREIS)
                                    |then| (NCONC1 (GETPROP FN 'WHEREIS)
                                                  X)
                                          (OR (FMEMB FN DUPS)
                                              (SETQ DUPS (CONS FN DUPS)))
                                  |else| (PUTPROP FN 'WHEREIS (LIST X)))))
                (SETQ DUPFNS DUPS))
           (SETQ NO-SOURCE (|for| X |in| DEFINEDFNS |when| (NOT (GETPROP X 'EXPR)) |collect| X)))
        (-3 (PRINTOUT T "Functions compiled but no expr" NO-SOURCE T)
            (PRINTOUT T "Functions on more than one file: " DUPFNS T))
        (4 (PRINTOUT T T "STARTING MASTERSCOPE PHASE ON " (DATE)
                  T)
           (FILESLOAD (SOURCE)
                  SYSEDIT)
           (|for| X |in| SYSFILES |do| (MSNOTICEFILE X))
           (|for| X |in| SYSFILES |do| (PRINTOUT T T "Analyzing " X T)
                                       (MASTERSCOPE `(ANALYZE ON ,(KWOTE X)))))
        (-4 "No queries yet")
        (HELP))))

(MAKE-FULLER-DB
  (LAMBDA NIL                                               (* \; "Edited 12-Aug-2022 10:02 by lmm")
                                                          (* \; "Edited 16-Jul-2022 22:07 by larry")
                                                          (* \; "Edited 20-Jun-2022 17:23 by larry")
    (FILESLOAD (SOURCE)
           FILESETS)
    (DRIBBLE (MEDLEYDIR "tmp" "fuller.dribble" T T))
    (DOFILESLOAD (FOR X IN CHECKEDFILES WHEN (MEMB 'OK (CDR X)) WHEN (FINDFILE (CAR X))
                    COLLECT (CAR X)))
    (GATHER-INFO 'ALL)
    (MASTERSCOPE '(WHO CALLS XYZZY))
    (DUMPDATABASE NIL (MKATOM (MEDLEYDIR "tmp" "fuller.database" T T)))
    (DRIBBLE)
    (MAKESYS (MEDLEYDIR "tmp" "fuller.sysout" T T)
           "Welcome to Fuller sysout")))

(MEDLEY-FIX-LINKS
  (LAMBDA (UNIXPATH)                                      (* \; "Edited 18-Jan-2021 12:01 by larry")
    (OR UNIXPATH (SETQ UNIXPATH (UNIX-GETENV "MEDLEYDIR"))
        (ERROR "No Directory"))                           (* \; "Edited 18-Jan-2021 11:45 by larry")
    (|ShellCommand| (CONCAT "cd " UNIXPATH " && /bin/sh scripts/fixlinks && /bin/sh /tmp/doit"))))

(MEDLEY-FIX-DATES
  (LAMBDA (DIRS)                                          (* \; "Edited 28-Jan-2021 12:15 by larry")
    (|for| X |in| (OR DIRS MEDLEY-FIX-DIRS) |join| (FIX-DIRECTORY-DATES (MEDLEYDIR (PRINT X T))))))
)

(RPAQQ MEDLEY-FIX-DIRS ("sources" "library" "lispusers" "internal" "greetfiles" "doctools"))

(RPAQQ CHECKEDFILES
       ((POSTSCRIPTSTREAM OK)
        (CHATTERMINAL OK)
        (DMCHAT OK)
        (CHAT OK)
        (PRESS OK)
        (READNUMBER OK)
        (EDITBITMAP OK)
        (IMAGEOBJ OK)
        (TEDIT OK)
        (HRULE OK)
        (TABLEBROWSER OK)
        (FILEBROWSER OK)
        (GRAPHER OK)
        (SPY OK)
        (WHERE-IS OK)
        (COPYFILES OK)
        (MSANALYZE OK)
        (MSPARSE OK)
        (MSCOMMON OK)
        (MASTERSCOPE OK)
        (UNIXCOMM OK)
        (UNIXPRINT OK)
        (UNICODE OK)
        (HASH OK)
        (CLIPBOARD OK)
        (UNIXCHAT OK)
        (VT100KP OK)
        (VTCHAT OK)
        (SKETCH OK)
        (SKETCHBMELT OK)
        (SCALEBITMAP OK)
        (SKETCHOBJ OK)
        (SKETCHEDIT OK)
        (SKETCHELEMENTS OK)
        (SKETCHOPS OK)
        (MATMULT OK)
        (SAMEDIR OK)
        (THINFILES OK)
        (ISO8859IO OK)
        (DINFO OK)
        (HELPSYS OK)
        (MODERNIZE OK)
        (WHEELSCROLL OK)
        (PRETTYFILEINDEX OK)
        (WHO-LINE OK)
        (BACKGROUND-YIELD OK)
        (OBJECTWINDOW OK)
        (REGIONMANAGER OK)
        (COMPARETEXT OK)
        (EXAMINEDEFS OK)
        (COMPARESOURCES OK)
        (COMPAREDIRECTORIES OK)
        (PSEUDOHOSTS OK)
        (DATEFORMAT-EDITOR OK)
        (DOC-OBJECTS OK)
        (EQUATIONS OK)
        (BICLOCK OK)
        (FILEWATCH OK)
        (LIFE OK)
        (IDLEHAX OK)
        (GITFNS OK)
        (TMAX OK)
        (IMTOOLS OK)
        (RENAMEFNS OK)
        (VMEM OK)
        (READSYS OK)
        (CASH-FILE OK)
        (HASH-FILE OK)
        (MEDLEYDIR OK)
        (MAKEINIT OK)
        (MEDLEY-UTILS OK)))
(DEFINEQ

(MAKE-EXPORTS-ALL
  (LAMBDA NIL                                             (* \; "Edited  9-Mar-2021 16:11 by larry")
                                                             (* "Edited May 3, 2018 by Ron Kaplan--relative to MEDLEYDIR/lispcore/.  Don't know why it does the CORE/RENAME")
                                                             (* 
          "Edited Aug 17 94 by Sybalsky -- point it to /king/export/lispcore as the truth directory.")
                                                             (* 
                        "Edited July 5, 1990 by Sybalsky -- point it to Pele as the truth directory.")
                                                             (* 
                                                             "Edited September 29, 1986 by van Melle")
    (CNDIR (MEDLEYDIR "sources"))
    (LOAD 'FILESETS)
    (GATHEREXPORTS EXPORTFILES (MEDLEYDIR "tmp" "exports.all" T))))

(MAKE-WHEREIS-HASH
  (LAMBDA NIL                                               (* \; "Edited 12-Mar-2022 12:46 by rmk")
                                                          (* \; "Edited 24-Mar-2021 13:26 by larry")
    (LET ((FILING.ENUMERATION.DEPTH 2)
          HASHFILE)
         (DRIBBLE (MEDLEYDIR "tmp" "whereis.dribble" T))
         (SETQ HASHFILE (XCL::WHERE-IS-NOTICE (MEDLEYDIR "tmp" "whereis.hash-tmp" T)
                               :FILES
                               (|for| X |in| MEDLEY-FIX-DIRS |collect| (CONCAT (MEDLEYDIR X)
                                                                              "*.;"))
                               :HASH-FILE-SIZE 60000 :NEW T))
         (RENAMEFILE HASHFILE (MEDLEYDIR "tmp" "whereis.hash" T))
         (DRIBBLE))))
)
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (621 8549 (GATHER-INFO 631 . 7070) (MAKE-FULLER-DB 7072 . 7904) (MEDLEY-FIX-LINKS 7906
 . 8303) (MEDLEY-FIX-DATES 8305 . 8547)) (10304 12097 (MAKE-EXPORTS-ALL 10314 . 11273) (
MAKE-WHEREIS-HASH 11275 . 12095)))))
STOP
