(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "XCL" BASE 10)

(FILECREATED "17-Feb-2024 15:40:05" |{DSK}<home>larry>il>medley>internal>MEDLEY-UTILS.;5| 22288  

      :EDIT-BY "lmm"

      :CHANGES-TO (FNS RECMPL COMPILE-SETUP RECOMPILE-ONE RECOMPL REMAKEFILES)
                  (VARS MEDLEY-UTILSCOMS)

      :PREVIOUS-DATE "17-Feb-2024 08:24:45" |{DSK}<home>larry>il>medley>internal>MEDLEY-UTILS.;3|)


(PRETTYCOMPRINT MEDLEY-UTILSCOMS)

(RPAQQ MEDLEY-UTILSCOMS
       ((FNS GATHER-INFO MAKE-FULLER-DB MEDLEY-FIX-LINKS MEDLEY-FIX-DATES RECOMPILE-ONE RECMPL 
             COMPILE-SETUP REMAKEFILES)
        (VARS MEDLEY-FIX-DIRS OKSOURCES OKLIBRARY OKLISPUSERS OKINTERNAL)
        (FNS MAKE-EXPORTS-ALL MAKE-WHEREIS-HASH MAKE-WHEREIS-LOOPS)
        (FNS BADFILE HCFILES MAPFILES PRETTYFILES)
        (PROP FILETYPE MEDLEY-UTILS)
        (INITVARS (HCFILES)
               (BADFILES))
        COMPA))
(DEFINEQ

(GATHER-INFO
  (LAMBDA (PHASE)                                           (* \; "Edited 22-May-2023 23:59 by lmm")
                                                          (* \; "Edited 26-Dec-2021 18:56 by larry")
                                                          (* \; "Edited 24-Oct-2021 09:43 by larry")
    (SELECTQ PHASE
        (ALL (|for| I |from| 0 |to| 4 |do| (GATHER-INFO I)))
        (0 (SETQ SYSFILES (UNION SYSFILES FILELST))
           (SETQ FILELST NIL)
           (FILESLOAD (SOURCE)
                  SYSEDIT))
        (1 (SETQ LOADEDFILES (|for| X |in| LOADEDFILELST |collect| (FILENAMEFIELD X 'NAME)))
           (FILESLOAD FILESETS)
           (SETQ ALLFILESETSFILES (|for| X |in| FILESETS |join| (APPEND (EVAL X))))
           (SETQ SOURCES (|for| X |in| (DIRECTORY (MEDLEYDIR "sources" "*.*;" T))
                            |when| (NOT (MEMB (FILENAMEFIELD X 'EXTENSION)
                                              '(LCOM DFASL TEDIT TXT)))
                            |collect| (FILENAMEFIELD X 'NAME))))
        (-1 (PRINTOUT T " loaded files not in SYSFILES or FILELST: "
                   (|for| X |in| LOADEDFILES |when| (NOT (OR (FMEMB X SYSFILES)
                                                             (FMEMB X FILELST))) |collect| X)
                   T)
            (PRINTOUT T "Sources not loaded: " (CL:SET-DIFFERENCE SOURCES (APPEND ALLFILESETSFILES 
                                                                                 LOADEDFILES))
                   T)
            (PRINTOUT T "Files in FILESETS not loaded " (CL:SET-DIFFERENCE ALLFILESETSFILES 
                                                               LOADEDFILES)
                   T))
        (2 (SETQ DEFINEDFNS (LET ((DEFD NIL))
                                 (MAPATOMS (FUNCTION (CL:LAMBDA (X)
                                                            (CL:WHEN (GETD X)
                                                                (CL:SETQ DEFD (CONS X DEFD))))))
                                 DEFD))
           (|for| X |in| DEFINEDFNS |when| (CCODEP X)
              |do| (LET ((Y (PUTPROP X 'CCC (CALLSCCODE X))))
                        (|for| REV |in| '(BLOCK-CALLED-BY CALLED-BY BOUND-BY SPECIAL-BY GLOBAL-BY)
                           |as| VAL |in| Y |do| (|for| S |in| VAL
                                                   |do| (PUTPROP S REV (CONS X (GETPROP S REV)))))))
           (SETQ CALLEDFNS NIL)
           (MAPATOMS (FUNCTION (LAMBDA (X)
                                 (|if| (AND (NOT (GETD X))
                                            (GETPROP X 'CALLED-BY))
                                     |then| (CL:PUSH X CALLEDFNS))))))
        (-2 (PRINTOUT T "Functions called and not defined" CALLEDFNS T))
        (3 (|for| X |in| SYSFILES
              |do| (LOAD X 'PROP)
                   (PUTPROP X 'CONTENT (READFILE X))
                   (|for| EXR |in| (GETPROP X 'CONTENT)
                      |do| (SELECTQ (CAR EXR)
                               (DEFINEQ (|for| DFN |in| (CDR EXR)
                                           |do| (|if| (EQUAL (CADR DFN)
                                                             (GETPROP (CAR DFN)
                                                                    'EXPR))
                                                    |then| (PRINTOUT T (CAR DFN)
                                                                  " ")
                                                          (PUTPROP (CAR DFN)
                                                                 'EXPR
                                                                 (CADR DFN))
                                                  |else| (PRINTOUT T (CAR DFN)
                                                                "* "))))
                               NIL)))
           (SETQ ALLCONTENT (|for| X |in| SYSFILES |collect| (CONS X (GETPROP X 'CONTENT))))
                                                             (* \; " don't edit with SEDIT")
           (LET (DUPS)
                (|for| X |in| SYSFILES
                   |do| (|for| FN |in| (FILEFNSLST X)
                           |do| (|if| (GETPROP FN 'WHEREIS)
                                    |then| (NCONC1 (GETPROP FN 'WHEREIS)
                                                  X)
                                          (OR (FMEMB FN DUPS)
                                              (SETQ DUPS (CONS FN DUPS)))
                                  |else| (PUTPROP FN 'WHEREIS (LIST X)))))
                (SETQ DUPFNS DUPS))
           (SETQ NO-SOURCE (|for| X |in| DEFINEDFNS |when| (NOT (GETPROP X 'EXPR)) |collect| X)))
        (-3 (PRINTOUT T "Functions compiled but no expr" NO-SOURCE T)
            (PRINTOUT T "Functions on more than one file: " DUPFNS T))
        (4 (PRINTOUT T T "STARTING MASTERSCOPE PHASE ON " (DATE)
                  T)
           (FILESLOAD (SOURCE)
                  SYSEDIT)
           (|for| X |in| SYSFILES |do| (MSNOTICEFILE X))
           (|for| X |in| SYSFILES |do| (PRINTOUT T T "Analyzing " X T)
                                       (MASTERSCOPE `(ANALYZE ON ,(KWOTE X)))))
        (-4 "No queries yet")
        (HELP))))

(MAKE-FULLER-DB
  (LAMBDA (DRIBBLEFILE DBFILE SYSOUTFILE)                 (* \; "Edited  3-Aug-2023 18:12 by frank")
                                                          (* \; "Edited 16-Jul-2022 22:07 by larry")
                                                          (* \; "Edited 20-Jun-2022 17:23 by larry")
    (FILESLOAD (SOURCE)
           FILESETS)
    (DRIBBLE (OR DRIBBLEFILE "fuller.dribble"))
    (DOFILESLOAD (SUBSET (APPEND OKSOURCES OKLIBRARY OKLISPUSERS OKINTERNAL)
                        'FINDFILE))
    (GATHER-INFO 'ALL)
    (MASTERSCOPE '(WHO CALLS XYZZY))
    (DUMPDATABASE NIL (MKATOM (OR DBFILE "fuller.database")))
    (DRIBBLE)
    (MAKESYS (OR SYSOUTFILE "fuller.sysout")
           "Welcome to Fuller sysout")))

(MEDLEY-FIX-LINKS
  (LAMBDA (UNIXPATH)                                      (* \; "Edited 18-Jan-2021 12:01 by larry")
    (OR UNIXPATH (SETQ UNIXPATH (UNIX-GETENV "MEDLEYDIR"))
        (ERROR "No Directory"))                           (* \; "Edited 18-Jan-2021 11:45 by larry")
    (|ShellCommand| (CONCAT "cd " UNIXPATH " && /bin/sh scripts/fixlinks && /bin/sh /tmp/doit"))))

(MEDLEY-FIX-DATES
  (LAMBDA (DIRS)                                          (* \; "Edited 28-Jan-2021 12:15 by larry")
    (|for| X |in| (OR DIRS MEDLEY-FIX-DIRS) |join| (FIX-DIRECTORY-DATES (MEDLEYDIR (PRINT X T))))))

(RECOMPILE-ONE
  (LAMBDA (FILES)                                         (* \; "Edited 10-Feb-2024 13:31 by larry")
    (CL:WITH-OPEN-STREAM (S (OPENSTREAM (OR (INFILEP "COMPILE.DRIBBLE")
                                            "COMPILE.DRIBBLE")
                                   'BOTH))
           (DRIBBLE S)
           (DRIBBLE (INFILEP "COMPILE.DRIBBLE"))
           (BKSYSBUF " ")
           (PRINTOUT T "------------------" T "SEARCHING...")
           (|for| X |in| (OR FILES SYSFILES) |when| (EQ (GET X 'FILETYPE)
                                                        :COMPILE-FILE)
              |when| (NOT (INFILEP (CONCAT X '.DFASL))) |do| (PRINTOUT T "Compiling " X T "")
                                                             (DOFILESLOAD (LIST X))
                                                             (LOAD X 'PROP)
                                                             (COPYFILE (FINDFILE X)
                                                                    X)
                                                             (FOR V IN (CL:VALUES-LIST (
                                                                                      CL:COMPILE-FILE
                                                                                        X))
                                                                DO (PRINT V))
                                                             (CL:FORCE-OUTPUT (DRIBBLEFILE)
                                                                    T)
                                                             (DRIBBLE)
                                                             (RETURN) FINALLY (HELP "NO MORE")))))

(RECMPL
  (LAMBDA (FILES)                                           (* \; "Edited 17-Feb-2024 15:39 by lmm")
                                                            (* \; "Edited  8-Feb-2024 19:24 by lmm")
    (LET ((*PRINT-CASE* :DOWNCASE)
          SRC DESTPREV (PRETTYFLG T)
          (*PRINT-BASE* 10))
         (CNDIR)
         (|for| X |in| (OR FILES SYSFILES) |do| (IF (SETQ SRC (INFILEP (CONCAT SRCDIR X ".ilsp")))
                                                    THEN (APPLY* (COMPILE-FILE? SRC)
                                                                SRC))))))

(COMPILE-SETUP
  (LAMBDA NIL                                               (* \; "Edited 17-Feb-2024 08:23 by lmm")
                                                          (* \; "Edited  9-Feb-2024 16:15 by larry")

    (* |;;| "first set up compile environment")

    (FILESLOAD SYSEDIT)

    (* |;;| " load in necessary packages")

    (FILESLOAD MEDLEY-UTILS)
    (CLRHASH CLISPARRAY)                                     (* \; 
                                                       "clear out cache of file package translations")
    (FILESLOAD WHERE-IS MEDLEY-UTILS GITFNS FILEBROWSER)))

(REMAKEFILES
  (LAMBDA (FILES)                                           (* \; "Edited  8-Feb-2024 07:47 by lmm")
    (LET ((*PRINT-CASE* :DOWNCASE)
          WIN DIFF (PRETTYFLG T)
          (*PRINT-BASE* 10))
         (|for| X |in| (OR FILES SYSFILES)
            |do| (LOAD X 'PROP)
                 (PUTPROP X 'CONTENT (READFILE X))
                 (|for| EXR |in| (GETPROP X 'CONTENT)
                    |do| (SELECTQ (CAR EXR)
                             (DEFINEQ (|for| DFN |in| (CDR EXR)
                                         |do| (|if| (EQUAL (CADR DFN)
                                                           (GETPROP (CAR DFN)
                                                                  'EXPR))
                                                  |then| (PRINTOUT T (CAR DFN)
                                                                " ")
                                                        (PUTPROP (CAR DFN)
                                                               'EXPR
                                                               (CADR DFN))
                                                |else| (PRINTOUT T (CAR DFN)
                                                              "* "))))
                             NIL))
                 (MAKEFILE (MKATOM (SETQ DESTFILE (CONCAT (L-CASE X)
                                                         ".ilsp")))
                        '(NEW))
                 (SETQ DIFF (COMPARESOURCES X DESTFILE NIL))
                 (TERPRI)))))
)

(RPAQQ MEDLEY-FIX-DIRS ("sources" "library" "lispusers" "internal" "greetfiles" "doctools"))

(RPAQQ OKSOURCES (RENAMEFNS VMEM READSYS CASH-FILE HASH-FILE MEDLEYDIR MAKEINIT))

(RPAQQ OKLIBRARY
       (POSTSCRIPTSTREAM CHATTERMINAL DMCHAT CHAT PRESS READNUMBER EDITBITMAP IMAGEOBJ TEDIT HRULE 
              TABLEBROWSER FILEBROWSER GRAPHER SPY WHERE-IS COPYFILES MSANALYZE MSPARSE MSCOMMON 
              MASTERSCOPE UNIXCOMM UNIXPRINT UNICODE HASH CLIPBOARD UNIXCHAT VT100KP VTCHAT SKETCH 
              SKETCHBMELT SCALEBITMAP SKETCHOBJ SKETCHEDIT SKETCHELEMENTS SKETCHOPS MATMULT SAMEDIR 
              REMOTEVMEM ETHERRECORDS UNIXUTILS CHATDECLS BROWSER))

(RPAQQ OKLISPUSERS
       (THINFILES ISO8859IO DINFO HELPSYS MODERNIZE WHEELSCROLL PRETTYFILEINDEX WHO-LINE 
              BACKGROUND-YIELD OBJECTWINDOW REGIONMANAGER COMPARETEXT EXAMINEDEFS COMPARESOURCES 
              COMPAREDIRECTORIES PSEUDOHOSTS DATEFORMAT-EDITOR DOC-OBJECTS EQUATIONS BICLOCK 
              FILEWATCH LIFE IDLEHAX GITFNS TMAX IMTOOLS EQUATIONFORMS UNBOXEDOPS TILED-SEDIT 
              IDLEDEMO WDWHACKS BUTTONS PICK PAGEHOLD UNIXYCD))

(RPAQQ OKINTERNAL (MEDLEY-UTILS))
(DEFINEQ

(MAKE-EXPORTS-ALL
  (LAMBDA (OUTFILE)                                       (* \; "Edited  3-Aug-2023 18:34 by frank")
                                                          (* \; "Edited  9-Mar-2021 16:11 by larry")
                                                             (* "Edited May 3, 2018 by Ron Kaplan--relative to MEDLEYDIR/lispcore/.  Don't know why it does the CORE/RENAME")
                                                             (* 
          "Edited Aug 17 94 by Sybalsky -- point it to /king/export/lispcore as the truth directory.")
                                                             (* 
                        "Edited July 5, 1990 by Sybalsky -- point it to Pele as the truth directory.")
                                                             (* 
                                                             "Edited September 29, 1986 by van Melle")
    (CNDIR (MEDLEYDIR "sources"))
    (LOAD 'FILESETS)
    (GATHEREXPORTS EXPORTFILES (OR OUTFILE "exports.all"))))

(MAKE-WHEREIS-HASH
  (LAMBDA (DRIBBLEFILE TMPFILE WHEREISFILE DEPTH SUBDIRS)   (* \; "Edited  4-Feb-2024 21:57 by lmm")
                                                          (* \; "Edited  3-Aug-2023 18:37 by frank")
                                                            (* \; "Edited 12-Mar-2022 12:46 by rmk")
                                                          (* \; "Edited 24-Mar-2021 13:26 by larry")
    (LET ((FILING.ENUMERATION.DEPTH (OR DEPTH 2))
          HASHFILE)
         (DRIBBLE (OR DRIBBLEFILE "whereis.dribble"))
         (SETQ HASHFILE (XCL::WHERE-IS-NOTICE (OR TMPFILE "whereis.hash-tmp")
                               :FILES
                               (|for| X |in| (OR SUBDIRS MEDLEY-FIX-DIRS)
                                  |collect| (CONCAT (IF SUBDIRS
                                                        THEN X
                                                      ELSE (MEDLEYDIR X))
                                                   "*.;"))
                               :HASH-FILE-SIZE 60000 :NEW T))
         (RENAMEFILE HASHFILE (OR WHEREISFILE "whereis.hash"))
         (DRIBBLE))))

(MAKE-WHEREIS-LOOPS
  (LAMBDA NIL                                               (* \; "Edited  4-Feb-2024 22:29 by lmm")
    (MAKE-WHEREIS-HASH "whereis-loops.dribble" NIL "whereis-loops.hash" 4
           (LIST (NTH (ASSOC 'LOOPS GIT-PROJECTS)
                      8)))))
)
(DEFINEQ

(BADFILE
  (LAMBDA NIL                                               (* \; "Edited 20-Oct-2022 15:40 by lmm")
                                                          (* \; "Edited 22-Jun-2022 09:40 by larry")
    (|pushnew| BADFILES *FILE*)
    (LET ((STR (OPENSTREAM "BADFILES.TXT" 'APPEND)))
         (SETFILEPTR STR -1)
         (PRINT *FILE* STR)
         (CLOSEF STR))
    (RETFROM (OR (STKPOS 'PRETTYFILES)
                 'HCFILES))))

(HCFILES
  (LAMBDA (BASE DESTDIR REDOFLG EXTENSIONS)                 (* \; "Edited  5-Feb-2024 12:16 by lmm")
                                                            (* \; "Edited  4-Nov-2023 11:14 by lmm")
                                                            (* \; "Edited 20-Oct-2022 16:11 by lmm")
                                                            (* \; "Edited  9-Aug-2022 20:44 by lmm")
    (MAPFILES (OR BASE (SETQ BASE (MEDLEYDIR)))
           4
           (FUNCTION (LAMBDA (FILENAME)
                       (LET* ((*FILE* FILENAME)
                              (SRC (UNPACKFILENAME.STRING FILENAME))
                              (DIR (LISTGET SRC 'DIRECTORY))
                              (EXT (LISTGET SRC 'EXTENSION))
                              (DEST (PACKFILENAME.STRING
                                     'HOST
                                     (LISTGET SRC 'HOST)
                                     'DIRECTORY
                                     (IF (STRINGP DESTDIR)
                                         THEN (CONCAT DESTDIR (PACK (SUBST '- '> (UNPACK DIR))))
                                       ELSE DIR)
                                     'NAME
                                     (U-CASE (LISTGET SRC 'NAME))
                                     'EXTENSION
                                     'PDF
                                     'VERSION "")))
                             (DECLARE (SPECVARS *FILE*))
                             (IF (AND (NOT (EQUAL EXT "DINFO"))
                                      (SETQ FILENAME (INFILEP FILENAME))
                                      (OR (CAR (NLSETQ (TEDIT.FORMATTEDFILEP FILENAME)))
                                          (MEMBER EXT EXTENSIONS)))
                                 THEN (IF (AND (INFILEP DEST)
                                               (NULL REDOFLG))
                                          THEN (PRINTOUT T DEST " already there" T)
                                        ELSEIF (EQ REDOFLG 'TEST)
                                          THEN (PRINTOUT T "WOULD CONVERT " FILENAME " to " DEST 
                                                      "...")
                                               (CLOSEF? (OPENTEXTSTREAM FILENAME))
                                        |else| (PRINTOUT T "Converting " FILENAME " to " DEST "...")
                                              (TEDIT.FORMAT.HARDCOPY (SETQ TEXTSTREAM (OPENTEXTSTREAM
                                                                                       FILENAME))
                                                     DEST T NIL NIL NIL 'PDF)
                                              (|printout| T " DONE" T)
                                              (CLOSEF? TEXTSTREAM)))))))))

(MAPFILES
  (LAMBDA (BASE DEPTH EACHFN DEST)                          (* \; "Edited  4-Feb-2024 14:34 by lmm")
    (LET ((FILING.ENUMERATION.DEPTH (OR DEPTH T)))
         (DECLARE (SPECVARS EACHFN FILENAME))
         (DIRECTORY (OR BASE (MEDLEYDIR))
                '(@ (APPLY* EACHFN FILENAME))
                "*" ""))))

(PRETTYFILES
  (LAMBDA (BASE DESTDIR REDOFLG EXTENSIONS)                 (* \; "Edited  4-Feb-2024 14:58 by lmm")
                                                            (* \; "Edited 20-Oct-2022 16:12 by lmm")
                                                            (* \; "Edited  9-Aug-2022 20:44 by lmm")
    (MAPFILES (OR BASE (SETQ BASE (MEDLEYDIR)))
           4
           (FUNCTION (LAMBDA (FILENAME)
                       (LET* ((SRC (UNPACKFILENAME.STRING FILENAME))
                              (DIR (LISTGET SRC 'DIRECTORY))
                              (EXT (LISTGET SRC 'EXTENSION))
                              (FPR)
                              (DEST (PACKFILENAME.STRING
                                     'HOST
                                     (LISTGET SRC 'HOST)
                                     'DIRECTORY
                                     (IF (STRINGP DESTDIR)
                                         THEN (CONCAT DESTDIR (PACK (SUBST '- '> (UNPACK DIR))))
                                       ELSE DIR)
                                     'NAME
                                     (CONCAT (U-CASE (LISTGET SRC 'NAME))
                                            "P")
                                     'EXTENSION
                                     'PDF
                                     'VERSION "")))
                             (IF (AND (SETQ FILENAME (INFILEP FILENAME))
                                      (SETQ SFP (CAR (NLSETQ (LISPSOURCEFILEP FILENAME))))
                                      (NEQ SFP *COMMON-LISP-READER-ENVIRONMENT*))
                                 THEN (IF (EQ REDOFLG 'TEST)
                                          THEN (PRINTOUT T "WOULD CONVERT " FILENAME " to " DEST T)
                                        |else| (PRINTOUT T "Converting " FILENAME " to " DEST "...")
                                              (CL:WITH-OPEN-STREAM (STR (OPEN-PDF-STREAM DEST))
                                                     (PRETTYFILEINDEX FILENAME NIL STR))
                                              (CLOSEF? TEXTSTREAM)))))))))
)

(PUTPROPS MEDLEY-UTILS FILETYPE :COMPILE-FILE)

(RPAQ? HCFILES )

(RPAQ? BADFILES )

(RPAQQ COMPA NOBIND)
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (917 12482 (GATHER-INFO 927 . 6455) (MAKE-FULLER-DB 6457 . 7235) (MEDLEY-FIX-LINKS 7237
 . 7634) (MEDLEY-FIX-DATES 7636 . 7878) (RECOMPILE-ONE 7880 . 9629) (RECMPL 9631 . 10256) (
COMPILE-SETUP 10258 . 10888) (REMAKEFILES 10890 . 12480)) (13661 16230 (MAKE-EXPORTS-ALL 13671 . 14732
) (MAKE-WHEREIS-HASH 14734 . 15937) (MAKE-WHEREIS-LOOPS 15939 . 16228)) (16231 22138 (BADFILE 16241 . 
16709) (HCFILES 16711 . 19601) (MAPFILES 19603 . 19941) (PRETTYFILES 19943 . 22136)))))
STOP
