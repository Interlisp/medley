(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "24-Sep-2025 14:28:00" 
{DSK}<Users>kaplan>Local>medley3.5>working-medley>library>CLIPBOARD.;21 8150   

      :EDIT-BY rmk

      :CHANGES-TO (FNS CLIPBOARD-COPY-STREAM PUTCLIPBOARD)

      :PREVIOUS-DATE "21-Apr-2024 09:12:04" {WMEDLEY}<library>CLIPBOARD.;18)


(PRETTYCOMPRINT CLIPBOARDCOMS)

(RPAQQ CLIPBOARDCOMS
       [                                                     (* ; "Enable copy and paste")
        (FNS INSTALL-CLIPBOARD GETCLIPBOARD PUTCLIPBOARD PASTEFROMCLIPBOARD CLIPBOARD-COPY-STREAM 
             CLIPBOARD-PASTE-STREAM)
        (FNS SEDIT.COPYTOCLIPBOARD)
        (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DOCOPY (FILES (SYSLOAD)
                                                              UNIXCOMM UNICODE)
               (P (INSTALL-CLIPBOARD)))
        (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS (ADDVARS (NLAMA)
                                                                             (NLAML)
                                                                             (LAMA])



(* ; "Enable copy and paste")

(DEFINEQ

(INSTALL-CLIPBOARD
  [LAMBDA NIL                                                (* ; "Edited 21-Apr-2024 08:56 by rmk")
                                                             (* ; "Edited 30-Mar-2024 22:22 by lmm")
                                                            (* ; "Edited 24-Jun-2021 21:14 by rmk:")
                                                            (* ; "Edited 19-Apr-2020 12:15 by rmk:")
                                                            (* ; "Edited 18-Apr-2018 23:00 by rmk:")
    (INTERRUPTCHAR (CHARCODE "Meta,v")
           '(PASTEFROMCLIPBOARD))
    (INTERRUPTCHAR (CHARCODE "Meta,V")
           '(PASTEFROMCLIPBOARD))
    (/PUTASSOC 'PASTE [LIST (LIST (CHARCODE "1,v")
                                  '(PASTEFROMCLIPBOARD))
                            (LIST (CHARCODE "1,V")
                                  '(PASTEFROMCLIPBOARD]
           LISPINTERRUPTS)
    (CL:WHEN (GETP 'SEDIT 'FILEDATES)                        (* ; 
                                                             "SEDIT copy: INTERRUPTCHAR does paste")
        (SEDIT:ADD-COMMAND "Meta,c" 'SEDIT.COPYTOCLIPBOARD "M-c" "Copy to clipboard")
        (SEDIT:ADD-COMMAND "Meta,C" 'SEDIT.COPYTOCLIPBOARD)
        (SEDIT:RESET-COMMANDS))])

(GETCLIPBOARD
  [LAMBDA NIL                                           (* ; "Edited 23-Feb-2021 11:32 by rmk:")
                                                            (* ; "Edited 25-Apr-2018 16:56 by rmk:")
    (CL:WITH-OPEN-STREAM (s (CLIPBOARD-PASTE-STREAM))
           (CONCATCODES (BIND C WHILE (SETQ C (READCCODE s)) COLLECT C])

(PUTCLIPBOARD
  [LAMBDA (OBJECT PRINTFN)                                   (* ; "Edited 24-Sep-2025 14:23 by rmk")
                                                            (* ; "Edited 23-Feb-2021 11:32 by rmk:")
                                                            (* ; "Edited 25-Apr-2018 16:49 by rmk:")
    (if (STRPOS "darwin" (UNIX-GETENV "OSTYPE"))
        then 
             (* ;; "pbpaste doesn't seem to take a file argument.")

             (CL:WITH-OPEN-STREAM (S (CREATE-PROCESS-STREAM "pbcopy"))
                    (CL:IF PRINTFN
                        (APPLY* PRINTFN OBJECT S)
                        (PRIN3 OBJECT S)))
      else 
           (* ;; "Try to make xclip more reliable by passing a file that already contains the input.")

           (LET [(TEMPFILE (CONCAT "/tmp/xclip-copy " (DATE)
                                  "  "
                                  (RAND]
                (CL:WITH-OPEN-FILE (S TEMPFILE :EXTERNAL-FORMAT (SYSTEM-EXTERNALFORMAT))
                       (CL:IF PRINTFN
                           (APPLY* PRINTFN OBJECT S)
                           (PRIN3 OBJECT S)))
                (CREATE-PROCESS-STREAM (CONCAT "xclip -i -selection clipboard " TEMPFILE])

(PASTEFROMCLIPBOARD
  [LAMBDA NIL                                           (* ; "Edited 15-Feb-2021 23:43 by rmk:")
                                                            (* ; "Edited 18-Apr-2018 13:56 by rmk:")
                                                            (* ; "Edited 17-Apr-2018 23:11 by rmk:")

    (* ;; "If for some reason TTY process doesn't have a window (e.g. TEXEC), we can only do the character printing.  Presumably the right thing to do--no image objects in an exec.")

    (* ;; "Should be able to just call COPYINSERT, but the default BKSYSBUF puts in string quotes.")

    (LET [(STR (GETCLIPBOARD))
          (WINDOW (PROCESS.WINDOW (TTY.PROCESS]
         (IF (AND WINDOW (WINDOWPROP WINDOW 'COPYINSERTFN))
             THEN (COPYINSERT STR)
           ELSE (BIND C WHILE (SETQ C (GNCCODE STR)) DO (BKSYSCHARCODE C])

(CLIPBOARD-COPY-STREAM
  [LAMBDA NIL                                                (* ; "Edited 24-Sep-2025 14:27 by rmk")
                                                             (* ; "Edited  7-Jul-2022 23:51 by rmk")
                                                            (* ; "Edited 23-Feb-2021 22:11 by rmk:")
    (CREATE-PROCESS-STREAM (CL:IF (STRPOS "darwin" (UNIX-GETENV "OSTYPE"))
                               "pbcopy"
                               "xclip -i -selection clipboard")])

(CLIPBOARD-PASTE-STREAM
  [LAMBDA NIL                                                (* ; "Edited  7-Jul-2022 23:51 by rmk")
                                                            (* ; "Edited 23-Feb-2021 17:29 by rmk:")
    (LET (STRM (OST (UNIX-GETENV "OSTYPE")))
         (SETQ STRM (CREATE-PROCESS-STREAM (CL:IF (STRPOS "darwin" OST)
                                               "pbpaste"
                                               "xclip -o -selection clipboard")))
         [SETFILEINFO STRM 'ENDOFSTREAMOP #'(CL:LAMBDA (s)
                                                   (RETFROM (FUNCTION READCCODE)
                                                          NIL]
         STRM])
)
(DEFINEQ

(SEDIT.COPYTOCLIPBOARD
  [LAMBDA (CONTEXT)                                     (* ; "Edited  8-Aug-2020 15:25 by rmk:")
                                                            (* ; "Edited 24-Apr-2018 20:39 by rmk:")
                                                            (* ; "Edited 24-Apr-2018 20:33 by rmk:")
                                                            (* ; "Edited 23-Apr-2018 18:19 by rmk:")
    [CL:MULTIPLE-VALUE-BIND (SEL SELTYPE)
           (SEDIT:GET-SELECTION CONTEXT)

           (* ;; "SEL could be a list of several elements, or a structure, depending on SELTYPE.  ")

           (* ;; "SELTYPE=NIL means not a valid selection, and SEL is NIL.  Non-NIL values are :SUB-LIST, :CHARACTERS, and T")

           (CL:WHEN SELTYPE
               [PUTCLIPBOARD (CONS SEL (EQ SELTYPE :SUB-LIST))
                      (FUNCTION (LAMBDA (PAIR STREAM)
                                  (LET ((*PRINT-PRETTY* T)
                                        (PRETTYTABFLG NIL)
                                        (FONTCHANGEFLG NIL)
                                        (%#RPARS NIL))
                                       (DECLARE (SPECVARS *PRINT-PRETTY* %#RPARS PRETTYTABFLG 
                                                           FONTCHANGEFLG))
                                       (PRINTDEF (CAR PAIR)
                                              0 NIL (CDR PAIR)
                                              NIL STREAM])]
    T])
)
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DOCOPY 

(FILESLOAD (SYSLOAD)
       UNIXCOMM UNICODE)


(INSTALL-CLIPBOARD)
)
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 

(ADDTOVAR NLAMA )

(ADDTOVAR NLAML )

(ADDTOVAR LAMA )
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1167 6331 (INSTALL-CLIPBOARD 1177 . 2504) (GETCLIPBOARD 2506 . 2880) (PUTCLIPBOARD 2882
 . 4151) (PASTEFROMCLIPBOARD 4153 . 5071) (CLIPBOARD-COPY-STREAM 5073 . 5607) (CLIPBOARD-PASTE-STREAM 
5609 . 6329)) (6332 7871 (SEDIT.COPYTOCLIPBOARD 6342 . 7869)))))
STOP
