(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "15-Nov-2023 23:24:38" {WMEDLEY}<library>PDFSTREAM.;57 13507  

      :EDIT-BY rmk

      :CHANGES-TO (VARS PDFSTREAMCOMS)
                  (FNS SEE-PS OPEN-PDF-STREAM CLOSE-PDF-STREAM PS-TO-PDF)

      :PREVIOUS-DATE "11-Nov-2023 11:24:42" {WMEDLEY}<library>PDFSTREAM.;56)


(PRETTYCOMPRINT PDFSTREAMCOMS)

(RPAQQ PDFSTREAMCOMS
       ((FILES (SYSLOAD)
               POSTSCRIPTSTREAM)
        [COMS                                                (* ; "Hook into hardcopy interface")
              [ADDVARS [PRINTERTYPES ((PDF)
                                      (CANPRINT (PDF))
                                      (STATUS TRUE)
                                      (PROPERTIES NILL)
                                      (SEND POSTSCRIPTSEND)
                                      (BITMAPSCALE POSTSCRIPT.BITMAPSCALE)
                                      (BITMAPFILE (PDF.HARDCOPYW FILE BITMAP SCALEFACTOR REGION 
                                                         ROTATION TITLE]
                     [PRINTFILETYPES (PDF (TEST PDFFILEP)
                                          (EXTENSION (PDF))
                                          (CONVERSION (TEXT PDF.TEXT TEDIT PDF.TEDIT]
                     (IMAGESTREAMTYPES (PDF (OPENSTREAM OPEN-PDF-STREAM)
                                            (FONTCREATE POSTSCRIPT.FONTCREATE)
                                            (FONTSAVAILABLE POSTSCRIPT.FONTSAVAILABLE)
                                            (CREATECHARSET \CREATECHARSET.PSC]
              (VARS (DEFAULTPRINTERTYPE 'PDF))
              (FNS PDFFILEP PDF.HARDCOPYW PDF.TEXT PDF.TEDIT)
              (P (FONTPROFILE.ADDDEVICE 'PDF 'POSTSCRIPT]
        
        (* ;; "")

        
        (* ;; "Implementation of PDF streams")

        (INITVARS (PDFCONVERTER NIL))
                                                             (* ; "Mac with ghostscript?")
        (ALISTS (PDF-CONVERTER-TEMPLATES ps2pdf pstopdf))
        (GLOBALVARS PDFCONVERTER PDF-CONVERTER-TEMPLATES)
        (FNS OPEN-PDF-STREAM CLOSE-PDF-STREAM PS-TO-PDF)
        (FNS SEE-PDF SEE-PS)
        (ADDVARS (FB.SEE.METHODS (PDFFILEP SEE-PDF)
                        (POSTSCRIPTFILEP SEE-PS)))
        (FNS PDFCONVERTER)))

(FILESLOAD (SYSLOAD)
       POSTSCRIPTSTREAM)



(* ; "Hook into hardcopy interface")


(ADDTOVAR PRINTERTYPES ((PDF)
                        (CANPRINT (PDF))
                        (STATUS TRUE)
                        (PROPERTIES NILL)
                        (SEND POSTSCRIPTSEND)
                        (BITMAPSCALE POSTSCRIPT.BITMAPSCALE)
                        (BITMAPFILE (PDF.HARDCOPYW FILE BITMAP SCALEFACTOR REGION ROTATION TITLE))))

(ADDTOVAR PRINTFILETYPES (PDF (TEST PDFFILEP)
                              (EXTENSION (PDF))
                              (CONVERSION (TEXT PDF.TEXT TEDIT PDF.TEDIT))))

(ADDTOVAR IMAGESTREAMTYPES (PDF (OPENSTREAM OPEN-PDF-STREAM)
                                (FONTCREATE POSTSCRIPT.FONTCREATE)
                                (FONTSAVAILABLE POSTSCRIPT.FONTSAVAILABLE)
                                (CREATECHARSET \CREATECHARSET.PSC)))

(RPAQQ DEFAULTPRINTERTYPE PDF)
(DEFINEQ

(PDFFILEP
  [LAMBDA (FILE)                                             (* ; "Edited 23-Jun-2023 14:43 by rmk")
                                                             (* ; "Edited  5-Mar-93 21:40 by rmk:")
                                                             (* ; "Edited 14-Jan-93 10:56 by jds")
    (OR (CL:MEMBER (UNPACKFILENAME.STRING FILE 'EXTENSION)
               '("PDF")
               :TEST
               (FUNCTION STRING-EQUAL))
        (CL:WHEN (STREAMP FILE)
            (SETFILEPTR FILE 0)
            (PROG1 (AND (EQ (BIN FILE)
                            (CHARCODE %%))
                        (EQ (BIN FILE)
                            (CHARCODE P))
                        (EQ (BIN FILE)
                            (CHARCODE D))
                        (EQ (BIN FILE)
                            (CHARCODE F)))
                   (SETFILEPTR FILE 0)))])

(PDF.HARDCOPYW
  [LAMBDA (PDFFILE BITMAP SCALEFACTOR REGION Landscape? TITLE)
                                                             (* ; "Edited 24-Jul-2023 10:37 by rmk")
                                                             (* ; "Edited 23-Jun-2023 13:28 by rmk")
                                                             (* ; "Edited  6-Mar-2023 22:43 by rmk")
    (LET ((PSTTMP (PACKFILENAME 'EXTENSION 'TMPPS 'BODY PDFFILE)))
         (PS-TO-PDF (POSTSCRIPT.HARDCOPYW PSTTMP BITMAP SCALEFACTOR REGION Landscape? TITLE)
                PDFFILE])

(PDF.TEXT
  [LAMBDA (FILE PDFFILE FONTS HEADING TABS)                  (* ; "Edited  1-Oct-2023 15:24 by rmk")
                                                             (* ; "Edited 23-Jun-2023 13:23 by rmk")
                                                             (* ; "Edited  7-Mar-2023 08:39 by rmk")
    (TEXTTOIMAGEFILE FILE PDFFILE 'PDF FONTS HEADING TABS `(REGION ,POSTSCRIPT.DEFAULT.PAGEREGION 
                                                                  ROTATION ,(NOT (NOT 
                                                                        POSTSCRIPT.TEXTFILE.LANDSCAPE
                                                                                      ])

(PDF.TEDIT
  [LAMBDA (FILE PDFFILE)                                     (* ; "Edited 23-Jun-2023 13:22 by rmk")
                                                             (* ; "Edited  7-Mar-2023 08:39 by rmk")
    (LET ((TSTREAM (OPENTEXTSTREAM FILE)))
         (TEDIT.FORMAT.HARDCOPY FILE PDFFILE T NIL NIL NIL 'PDF)
         (CLOSEF TSTREAM])
)

(FONTPROFILE.ADDDEVICE 'PDF 'POSTSCRIPT)



(* ;; "")




(* ;; "Implementation of PDF streams")


(RPAQ? PDFCONVERTER NIL)



(* ; "Mac with ghostscript?")


(ADDTOVAR PDF-CONVERTER-TEMPLATES (ps2pdf " " PSFILE " " PDFFILE " 2> " ERRORFILE)
                                  (pstopdf " " PSFILE " -o " PDFFILE " 2> " ERRORFILE))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS PDFCONVERTER PDF-CONVERTER-TEMPLATES)
)
(DEFINEQ

(OPEN-PDF-STREAM
  [LAMBDA (FILE OPTIONS)                                     (* ; "Edited 15-Nov-2023 18:37 by rmk")
                                                             (* ; "Edited 23-Sep-2023 15:38 by rmk")
                                                             (* ; "Edited 22-Sep-2023 11:04 by rmk")
                                                             (* ; "Edited 24-Jun-2023 14:49 by rmk")

    (* ;; "Open a temporary PS file, but set it up so that at closing it gets converted to PDF using an operating-system utility (if available), and then gets renamed to the original intended filename.")

    (* ;; "We have to stash the original filename someplace.  We could put it in the tmp filename and then parse it out, but then we would have to worry about how unix filenames might parse against our {, },  etc.  ")

    (* ;; 
    "Simplest thing for now is to just add an extra field at the end of the \POSTSCRIPTDATA record.")

    (* ;; "")

    (CL:UNLESS (ASSOC (PDFCONVERTER)
                      PDF-CONVERTER-TEMPLATES)
           (ERROR "A specified POSTSCRIPT-to-PDF converter cannot be found"))
    (SETQ FILE (OR (AND (NEQ FILE T)
                        (OUTFILEP FILE))
                   (ERROR "PDF target file not found" FILE)))
    (LET ((PSSTREAM (OPENPOSTSCRIPTSTREAM (CONCAT "{UNIX}/tmp/medley-pdf-" (IDATE)
                                                 "-"
                                                 (RAND)
                                                 ".ps")
                           OPTIONS)))
         (STREAMPROP PSSTREAM 'AFTERCLOSE (CONS (FUNCTION CLOSE-PDF-STREAM)))
         (STREAMPROP PSSTREAM 'PDFTARGETINFO FILE)
         PSSTREAM])

(CLOSE-PDF-STREAM
  [LAMBDA (PSSTREAM)                                         (* ; "Edited 15-Nov-2023 18:39 by rmk")
                                                             (* ; "Edited 22-Sep-2023 11:18 by rmk")

    (* ;; "PSSTREAM is a postscript (maybe in tmp) rendition of what is intended to end up as a pdf.  If we are going directly to a printer, we can probably just pass it along without worrying about conversion.  In fact, in that case we probably should not have bothered even setting up the PDF stream.")

    (* ;; "But for a file we execute the PDFCONVERTER as a shell command to make a pdf")

    (STREAMPROP PSSTREAM 'AFTERCLOSE NIL)                    (* ; 
                                             "Maybe just remove  only CLOSE-PDF-STREAMfrom the list?")
    (PS-TO-PDF PSSTREAM (STREAMPROP PSSTREAM 'PDFTARGETINFO NIL])

(PS-TO-PDF
  [LAMBDA (PSFILE PDFFILE DONTDELETE)                        (* ; "Edited 15-Nov-2023 23:10 by rmk")
                                                             (* ; "Edited  1-Oct-2023 15:18 by rmk")
                                                            (* ; "Edited  7-Oct-2021 11:15 by rmk:")

    (* ;; "We have to map the filenames down to Unix conventions:  (not pseudohost or host, slashes, etc. to hand off to the shell.  If PDFFILE is not already on Unix, we create a Unix tmp file that then gets renamed at the end.")

    (* ;; "PSFILE is the name of a closed PS file.  This function uses the PDFCONVERTER Unix utility to convert that to pdf.  ")

    (* ;; "DONTDELETE is just for debugging, keeps the /tmp/ files")

    (CL:UNLESS (ASSOC (PDFCONVERTER)
                      PDF-CONVERTER-TEMPLATES)
           (ERROR "The specified POSTSCRIPT-to-PDF converter cannot be found"))

    (* ;; "Try to make new tmp files, as needed, the same as either of the /tmp/ inputs")

    [SETQ PDFFILE (OUTFILEP (PACKFILENAME 'EXTENSION 'pdf 'BODY (OR PDFFILE PSFILE]
    (LET* ((UNIX-PSFILE (UNIX-FILE PSFILE))
           (UNIX-PDFFILE (UNIX-FILE-NAME PDFFILE T))
           (PREFIX (CONCAT "/tmp/" (OR (AND (STRPOS "/tmp/" UNIX-PSFILE 1 NIL T)
                                            (FIXP (SUBATOM UNIX-PSFILE 6 9)))
                                       (AND (STRPOS "/tmp/" UNIX-PDFFILE 1 NIL T)
                                            (FIXP (SUBATOM UNIX-PDFFILE 6 9)))
                                       (RAND 1000 9999))
                          "-"))
           (ERRORFILE (CONCAT PREFIX (FILENAMEFIELD UNIX-PSFILE 'NAME)
                             ".error"))
           COMPLETIONCODE NEEDSRENAMING)
          (CL:UNLESS UNIX-PDFFILE                            (* ; "PDFFILE is not shell-accessible")
              (SETQ NEEDSRENAMING T)
              (SETQ UNIX-PDFFILE (CONCAT PREFIX (FILENAMEFIELD UNIX-PSFILE 'NAME)
                                        ".pdf")))
          [SETQ COMPLETIONCODE (PROCESS-COMMAND (CONCATLIST (SUBLIS `((PSFILE \, UNIX-PSFILE)
                                                                      (PDFFILE \, UNIX-PDFFILE)
                                                                      (ERRORFILE \, ERRORFILE))
                                                                   (ASSOC (PDFCONVERTER)
                                                                          PDF-CONVERTER-TEMPLATES]

          (* ;; "Now use Medley names")

          (CLOSEF? UNIX-PSFILE)
          (CL:UNLESS DONTDELETE (DELFILE UNIX-PSFILE))
          (CLOSEF? ERRORFILE)
          (CL:WHEN (INFILEP ERRORFILE)
              (CL:WHEN (IGREATERP (PROG1 (GETFILEINFO ERRORFILE 'LENGTH)
                                      (CL:UNLESS DONTDELETE (DELFILE ERRORFILE)))
                              0)
                     (ERROR "Cannot create PDF file for " PDFFILE)))
          (CL:WHEN (IGREATERP COMPLETIONCODE 0)
                 (ERROR "Cannot create PDF file for " PDFFILE))
          (CL:WHEN NEEDSRENAMING (RENAMEFILE UNIX-PDFFILE PDFFILE))
          PDFFILE])
)
(DEFINEQ

(SEE-PDF
  [LAMBDA (PDFFILE)                                          (* ; "Edited  1-Oct-2023 20:47 by rmk")
                                                             (* ; "Edited 26-Sep-2023 16:52 by rmk")

    (* ;; "Use the ShellOpener for this machine to open the PDF file outside of Medley")

    (ShellOpen (PACKFILENAME 'BODY PDFFILE 'EXTENSION 'PDF])

(SEE-PS
  [LAMBDA (PSFILE)                                           (* ; "Edited 15-Nov-2023 22:44 by rmk")
                                                             (* ; "Edited 14-Nov-2023 21:00 by rmk")
    (SEE-PDF (PS-TO-PDF PSFILE (CONCAT "{UNIX}/tmp/" (RAND 1000 9999)
                                      "-"
                                      (FILENAMEFIELD.STRING PSFILE 'NAME)
                                      ".PDF"])
)

(ADDTOVAR FB.SEE.METHODS (PDFFILEP SEE-PDF)
                         (POSTSCRIPTFILEP SEE-PS))
(DEFINEQ

(PDFCONVERTER
  [LAMBDA NIL
    (SETQ PDFCONVERTER (OR PDFCONVERTER (MKATOM (UNIX-GETENV "MEDLEY-PDFCONVERTER"))
                           (CAR (for TEMPLATE in PDF-CONVERTER-TEMPLATES
                                   thereis (ShellWhich (CAR TEMPLATE])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (3292 5906 (PDFFILEP 3302 . 4216) (PDF.HARDCOPYW 4218 . 4816) (PDF.TEXT 4818 . 5535) (
PDF.TEDIT 5537 . 5904)) (6346 12228 (OPEN-PDF-STREAM 6356 . 8127) (CLOSE-PDF-STREAM 8129 . 9021) (
PS-TO-PDF 9023 . 12226)) (12229 13099 (SEE-PDF 12239 . 12625) (SEE-PS 12627 . 13097)) (13200 13484 (
PDFCONVERTER 13210 . 13482)))))
STOP
