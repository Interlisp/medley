(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "12-Dec-2025 19:16:43" {WMEDLEY}<library>PDFSTREAM.;92 17210  

      :EDIT-BY rmk

      :CHANGES-TO (VARS PDFSTREAMCOMS)

      :PREVIOUS-DATE "26-Nov-2025 11:37:30" {WMEDLEY}<library>PDFSTREAM.;90)


(PRETTYCOMPRINT PDFSTREAMCOMS)

(RPAQQ PDFSTREAMCOMS
       ((FILES (SYSLOAD)
               POSTSCRIPTSTREAM)
        (INITVARS (PDFFONTCOERCIONS POSTSCRIPTFONTCOERCIONS)
               (PDFCHARCOERCIONS POSTSCRIPTCHARCOERCIONS))
        [COMS                                                (* ; "Hook into hardcopy interface")
              (ALISTS (PRINTFILETYPES PDF)
                     (IMAGESTREAMTYPES PDF)
                     (DEFAULTFILETYPELIST PDF))
              (FNS PDFFILEP PDF.HARDCOPYW PDF.TEXT PDF.TEDIT PDF.FONTSAVAILABLE)
              (P (FONTPROFILE.ADDDEVICE 'PDF 'POSTSCRIPT]
        
        (* ;; "")

        
        (* ;; "Implementation of PDF streams")

        (INITVARS (PDFCONVERTER NIL))
                                                             (* ; "Mac with ghostscript?")
        (ALISTS (PDF-CONVERTER-TEMPLATES ps2pdf pstopdf))
        (GLOBALVARS PDFCONVERTER PDF-CONVERTER-TEMPLATES)
        (FNS OPEN-PDF-STREAM CLOSE-PDF-STREAM PS-TO-PDF)
        (FNS PDF.POSTSCRIPT)
        (FNS SEE-PDF)
        (ADDVARS (FB.SEE.METHODS (PDFFILEP SEE-PDF)))
        (FNS PDFCONVERTER)
        (FNS \PDFINIT)
        (P (\PDFINIT))))

(FILESLOAD (SYSLOAD)
       POSTSCRIPTSTREAM)

(RPAQ? PDFFONTCOERCIONS POSTSCRIPTFONTCOERCIONS)

(RPAQ? PDFCHARCOERCIONS POSTSCRIPTCHARCOERCIONS)



(* ; "Hook into hardcopy interface")


(ADDTOVAR PRINTFILETYPES
          (PDF (TEST PDFFILEP)
               (EXTENSION (PDF))
               (CONVERSION (TEXT POSTSCRIPT.TEXT TEDIT TEDIT.TO.IMAGEFILE POSTSCRIPT PDF.POSTSCRIPT))
               (BITMAPSCALE POSTSCRIPT.BITMAPSCALE)
               (BITMAPFILE (PDF.HARDCOPYW FILE BITMAP SCALEFACTOR REGION ROTATION TITLE))))

(ADDTOVAR IMAGESTREAMTYPES (PDF (OPENSTREAM OPEN-PDF-STREAM)
                                (FONTCREATE POSTSCRIPT.FONTCREATE)
                                (FONTSAVAILABLE PDF.FONTSAVAILABLE)
                                (CREATECHARSET \CREATECHARSET.PSC)
                                (FONTEXISTS? POSTSCRIPT.FONTEXISTS?)))

(ADDTOVAR DEFAULTFILETYPELIST (PDF . BINARY))
(DEFINEQ

(PDFFILEP
  [LAMBDA (FILE)                                             (* ; "Edited 13-Sep-2025 23:24 by rmk")
                                                             (* ; "Edited 23-Jun-2023 14:43 by rmk")
                                                             (* ; "Edited  5-Mar-93 21:40 by rmk:")
                                                             (* ; "Edited 14-Jan-93 10:56 by jds")
    (OR (CL:MEMBER (UNPACKFILENAME.STRING FILE 'EXTENSION)
               '("PDF")
               :TEST
               (FUNCTION STRING-EQUAL))
        (RESETLST
            [LET (STRM)
                 [if (SETQ STRM (\GETSTREAM FILE 'INPUT T))
                     then [RESETSAVE NIL `(PROGN (SETFILEPTR ,STRM ,(GETFILEPTR STRM]
                          (SETFILEPTR STRM 0)
                   else (RESETSAVE (SETQ STRM (OPENSTREAM FILE 'INPUT))
                               `(PROGN (CLOSEF? OLDVALUE]
                 (AND (EQ (BIN STRM)
                          (CHARCODE %%))
                      (EQ (BIN STRM)
                          (CHARCODE P))
                      (EQ (BIN STRM)
                          (CHARCODE D))
                      (EQ (BIN STRM)
                          (CHARCODE F])])

(PDF.HARDCOPYW
  [LAMBDA (PDFFILE BITMAP SCALEFACTOR REGION Landscape? TITLE)
                                                             (* ; "Edited 19-Sep-2025 17:36 by rmk")
                                                             (* ; "Edited 24-Jul-2023 10:37 by rmk")
                                                             (* ; "Edited 23-Jun-2023 13:28 by rmk")
    (PS-TO-PDF (POSTSCRIPT.HARDCOPYW (OPENSTREAM '{NODIRCORE} 'OUTPUT)
                      BITMAP SCALEFACTOR REGION Landscape? TITLE)
           PDFFILE])

(PDF.TEXT
  [LAMBDA (FILE PDFFILE FONTS HEADING TABS)                  (* ; "Edited  1-Oct-2023 15:24 by rmk")
                                                             (* ; "Edited 23-Jun-2023 13:23 by rmk")
                                                             (* ; "Edited  7-Mar-2023 08:39 by rmk")
    (TEXTTOIMAGEFILE FILE PDFFILE 'PDF FONTS HEADING TABS `(REGION ,POSTSCRIPT.DEFAULT.PAGEREGION 
                                                                  ROTATION ,(NOT (NOT 
                                                                        POSTSCRIPT.TEXTFILE.LANDSCAPE
                                                                                      ])

(PDF.TEDIT
  [LAMBDA (FILE IMAGESTREAM IMAGETYPE OPTIONS)               (* ; "Edited 26-Sep-2025 23:02 by rmk")
                                                             (* ; "Edited 19-Sep-2025 07:33 by rmk")

    (* ;; "Make a scratch postscript stream, then convert it to a PDF that is stored in the caller's IMAGESTREAM (which may have been opened with some postscript preamble, which we discard)")

    (PS-TO-PDF (TEDIT.TO.IMAGEFILE FILE (OPENSTREAM '{NODIRCORE} 'OUTPUT)
                      'POSTSCRIPT OPTIONS)
           IMAGESTREAM])

(PDF.FONTSAVAILABLE
  [LAMBDA (FONTSPEC)                                         (* ; "Edited 23-Aug-2025 10:53 by rmk")
                                                             (* ; "Edited 16-Jun-2025 00:46 by rmk")
    (LET ((FA (FONTSAVAILABLE FONTSPEC NIL NIL NIL 'POSTSCRIPT T)))
         (for FS in FA do (replace (FONTSPEC FSDEVICE) of FS with 'PDF))
         FA])
)

(FONTPROFILE.ADDDEVICE 'PDF 'POSTSCRIPT)



(* ;; "")




(* ;; "Implementation of PDF streams")


(RPAQ? PDFCONVERTER NIL)



(* ; "Mac with ghostscript?")


(ADDTOVAR PDF-CONVERTER-TEMPLATES (ps2pdf " " PSFILE " " PDFFILE " 2> " ERRORFILE)
                                  (pstopdf " " PSFILE " -o " PDFFILE " 2> " ERRORFILE))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS PDFCONVERTER PDF-CONVERTER-TEMPLATES)
)
(DEFINEQ

(OPEN-PDF-STREAM
  [LAMBDA (FILE OPTIONS)                                     (* ; "Edited 14-Sep-2025 11:15 by rmk")
                                                             (* ; "Edited  5-Jun-2025 08:41 by rmk")
                                                             (* ; "Edited 23-Feb-2025 12:18 by rmk")
                                                             (* ; "Edited 23-Sep-2023 15:38 by rmk")
                                                             (* ; "Edited 22-Sep-2023 11:04 by rmk")
                                                             (* ; "Edited 24-Jun-2023 14:49 by rmk")

    (* ;; "Open FILE as a postscript file, but with IMAGETYPE=PDF and a closefn that calls PS-TO-PDF after the PS file closefn.")

    (DECLARE (GLOBALVARS \PDFIMAGEOPS))
    (CL:UNLESS (ASSOC (PDFCONVERTER)
                      PDF-CONVERTER-TEMPLATES)
           (ERROR "Can't find a POSTSCRIPT-to-PDF converter"))
    (LET ((STRM (OPENPOSTSCRIPTSTREAM FILE OPTIONS)))
         (replace (STREAM IMAGEOPS) of STRM with \PDFIMAGEOPS)
         STRM])

(CLOSE-PDF-STREAM
  [LAMBDA (PSSTREAM)                                         (* ; "Edited 27-Sep-2025 14:02 by rmk")
                                                             (* ; "Edited 19-Sep-2025 14:16 by rmk")
                                                             (* ; "Edited 14-Sep-2025 12:16 by rmk")
                                                             (* ; "Edited 22-Sep-2023 11:18 by rmk")
                                                             (* ; "Edited 24-Jul-2023 10:37 by rmk")
                                                             (* ; "Edited 17-Jul-2023 22:32 by rmk")
                                                             (* ; "Edited 24-Jun-2023 13:57 by rmk")
                                                             (* ; 
                                                             "Don't run again  for internal closing")
    (CL:WHEN (IMAGESTREAMTYPE PSSTREAM 'PDF)                 (* ; 
                                               "If it's still a PDF stream, it hasn't been converted")
        (CLOSEPOSTSCRIPTSTREAM PSSTREAM)
        (replace (STREAM IMAGEOPS) of PSSTREAM with \NOIMAGEOPS)
                                                             (* ; 
                                                             "Don't run again  for internal closing")
        (CLOSEF? PSSTREAM)                                   (* ; "PS-TO-PDF wants it closed?")
        (RESETLST
            (LET (PDFSTREAM)                                 (* ; 
                   "PS-TO-PDF returns a /tmp file if not given a PDFFILE, we copy it into our stream")
                 [RESETSAVE (SETQ PDFSTREAM (OPENSTREAM (PS-TO-PDF (FULLNAME PSSTREAM)
                                                               (OPENSTREAM '{NODIRCORE} 'OUTPUT))
                                                   'INPUT))
                        `(PROGN (DELFILE (CLOSEF? OLDVALUE]
                 [RESETSAVE (SETQ PSSTREAM (OPENSTREAM PSSTREAM 'OUTPUT))
                        `(PROGN (CLOSEF? OLDVALUE]
                 (SETFILEPTR PSSTREAM 0)
                 (SETFILEINFO PSSTREAM 'LENGTH 0)
                 (COPYBYTES PDFSTREAM PSSTREAM 0 -1)
                 (CLOSEF PSSTREAM))))])

(PS-TO-PDF
  [LAMBDA (PSFILE PDFFILE MAKEERRORFILE)                     (* ; "Edited 27-Sep-2025 16:51 by rmk")
                                                             (* ; "Edited 19-Sep-2025 14:14 by rmk")
                                                             (* ; "Edited 14-Sep-2025 09:44 by rmk")
                                                             (* ; "Edited  1-Oct-2023 15:18 by rmk")
                                                             (* ; "Edited 23-Jul-2023 22:30 by rmk")
                                                             (* ; "Edited 16-Jul-2022 13:06 by rmk")
                                                             (* ; "Edited  7-May-2022 22:40 by rmk")
                                                            (* ; "Edited  7-Oct-2021 11:15 by rmk:")

    (* ;; "PSFILE is a postscript file or stream whose contents are to be converted to a PDF-formatted file PDFFILE by means of a Shell PDFCONVERTER utility.")

    (* ;; "")

    (* ;; "PSFILE may be a Medley filename or a stream that is recognized as a PS formatted file.  If its contents do not reside in the Unix file system, it will be copied to a /tmp/ file to be given to the Shell. The /tmp/ file may be deleted at the end.")

    (* ;; "")

    (* ;; "PDFFILE is NIL, a file name, or perhaps a stream to receive the pdf.")

    (* ;; "     If NIL, a name is made by attaching PDF to PSFILE; a stream without a name goes to a scratch stream.")

    (* ;; "")

    (CL:UNLESS (ASSOC (PDFCONVERTER)
                      PDF-CONVERTER-TEMPLATES)
           (ERROR "A POSTSCRIPT-to-PDF converter cannot be found for this system"))
    (CL:UNLESS (POSTSCRIPTFILEP PSFILE)
           (ERROR "NOT A POSTSCRIPT FILE" PSFILE))
    (CL:UNLESS PDFFILE
        (SETQ PDFFILE (if (STREAMP PSFILE)
                          then (OPENSTREAM '{NODIRCORE} 'OUTPUT)
                        else (PACKFILENAME 'VERSION NIL 'EXTENSION (CAR (EXTENSIONS.FOR.IMAGEFILETYPE
                                                                         'PDF))
                                    'BODY PSFILE))))
    (CL:UNLESS (STREAMP PDFFILE)                             (* ; 
                            "NOTE: if it doesn't eventually end up with PDF, the Shell won't open it")
        [SETQ PDFFILE (PACKFILENAME 'BODY PDFFILE 'EXTENSION (CAR (EXTENSIONS.FOR.IMAGEFILETYPE
                                                                   'PDF])
    (RESETLST
        [SETQ PDFFILE (CLOSEF (OPENSTREAM PDFFILE 'OUTPUT]   (* ; "Get the version etc")

        (* ;; "UNIX-FILE-NAME returns NIL if output doesn't reside in Unix, will copy an input file to /tmp if necessary.")

        (LET* ((PSNAMEU (UNIX-FILE-NAME PSFILE 'INPUT 'pdf))
               (PDFNAMEU (UNIX-FILE-NAME PDFFILE 'OUTPUT))
               (ERRORFILE (CL:IF MAKEERRORFILE
                              (UNIX-FILE-NAME (OUTFILEP (PACKFILENAME 'HOST 'UNIX 'EXTENSION
                                                               'error
                                                               'BODY PDFNAMEU))
                                     'OUTPUT)
                              "/dev/null"))
               COMPLETIONCODE)

              (* ;; "PROCESS-COMMAND is currently from GITFNS.  Not sure whether ShellCommand in UNIXUTILS is appropriate.")

              [SETQ COMPLETIONCODE (PROCESS-COMMAND (CONCATLIST (SUBLIS `((PSFILE \, PSNAMEU)
                                                                          (PDFFILE \, PDFNAMEU)
                                                                          (ERRORFILE \, ERRORFILE))
                                                                       (ASSOC (PDFCONVERTER)
                                                                              PDF-CONVERTER-TEMPLATES
                                                                              ]

              (* ;; "Now use Medley names")

              (CL:WHEN (IGREATERP COMPLETIONCODE 0)
                  (CL:WHEN (AND MAKEERRORFILE (INFILEP ERRORFILE))
                      (CLOSEF? ERRORFILE)
                      (CL:WHEN (IGREATERP (GETFILEINFO ERRORFILE 'LENGTH)
                                      0)
                          (PRINTOUT T "See error file at " '%" ERRORFILE '%" T)))
                  (ERROR "Cannot create PDF file for " PSFILE))
              (if (STREAMP PDFFILE)
                  then 
                       (* ;; "Perhaps a scratch stream (NODIRCORE?)  to retrieve result. For sure it isn't in the unix file system")

                       (OPENSTREAM PDFFILE 'OUTPUT)
                       (SETFILEPTR PDFFILE 0)
                       (SETFILEINFO PDFFILE 'LENGTH 0)
                       (CLOSEF PDFFILE)
                       (SETQ PDFNAMEU (PACKFILENAME 'HOST 'UNIX 'BODY PDFNAMEU))
                       (PROG1 (COPYFILE PDFNAMEU PDFFILE)
                              (DELFILE PDFNAMEU))
                elseif (STRPOS "/tmp/" PDFNAMEU 1 NIL T)
                  then 
                       (* ;; "PDFILE must not have been in the Unix file system--a server or {CORE}?")

                       (COPYFILE PDFNAMEU PDFFILE)
                else 
                     (* ;; 
                     "PDFFILE presumably mapped to a Unix filesystem name, the Medley name is good.")

                     (FULLNAME PDFFILE))))])
)
(DEFINEQ

(PDF.POSTSCRIPT
  [LAMBDA (PSFILE IMAGEFILE IMAGETYPE OPTIONS)               (* ; "Edited 18-Sep-2025 23:49 by rmk")

    (* ;; "Can't pass OPTIONS, until the MAKEERROFILE flag goes away.")

    (PS-TO-PDF PSFILE IMAGEFILE])
)
(DEFINEQ

(SEE-PDF
  [LAMBDA (PDFFILE)                                          (* ; "Edited 30-Jul-2025 18:00 by rmk")
                                                             (* ; "Edited 25-Dec-2024 14:25 by rmk")
                                                             (* ; "Edited  1-Oct-2023 20:47 by rmk")
                                                             (* ; "Edited 26-Sep-2023 16:52 by rmk")

    (* ;; "Use the ShellOpener for this machine to open the PDF file outside of Medley")

    (LET [(FOUND (FINDFILE-WITH-EXTENSIONS PDFFILE NIL '(PDF]
         (if FOUND
             then (ShellOpen FOUND)
                  FOUND
           else (ERROR "FILE NOT FOUND" PDFFILE])
)

(ADDTOVAR FB.SEE.METHODS (PDFFILEP SEE-PDF))
(DEFINEQ

(PDFCONVERTER
  [LAMBDA NIL
    (SETQ PDFCONVERTER (OR PDFCONVERTER (MKATOM (UNIX-GETENV "MEDLEY-PDFCONVERTER"))
                           (CAR (for TEMPLATE in PDF-CONVERTER-TEMPLATES
                                   thereis (ShellWhich (CAR TEMPLATE])
)
(DEFINEQ

(\PDFINIT
  [LAMBDA NIL                                                (* ; "Edited 14-Sep-2025 01:15 by rmk")
    (SETQ \PDFIMAGEOPS (create IMAGEOPS using \POSTSCRIPTIMAGEOPS IMAGETYPE _ 'PDF IMCLOSEFN _
                                              (FUNCTION CLOSE-PDF-STREAM])
)

(\PDFINIT)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (2415 5995 (PDFFILEP 2425 . 3702) (PDF.HARDCOPYW 3704 . 4274) (PDF.TEXT 4276 . 4993) (
PDF.TEDIT 4995 . 5571) (PDF.FONTSAVAILABLE 5573 . 5993)) (6435 15507 (OPEN-PDF-STREAM 6445 . 7595) (
CLOSE-PDF-STREAM 7597 . 9945) (PS-TO-PDF 9947 . 15505)) (15508 15764 (PDF.POSTSCRIPT 15518 . 15762)) (
15765 16523 (SEE-PDF 15775 . 16521)) (16574 16858 (PDFCONVERTER 16584 . 16856)) (16859 17171 (\PDFINIT
 16869 . 17169)))))
STOP
