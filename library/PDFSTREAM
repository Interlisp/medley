(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "24-Jul-2023 10:37:31" {WMEDLEY}<library>PDFSTREAM.;44 13139  

      :EDIT-BY rmk

      :CHANGES-TO (VARS PDFSTREAMCOMS)
                  (FNS PDF.HARDCOPYW CLOSE-PDF-STREAM PS-TO-PDF PDF-CONVERT)

      :PREVIOUS-DATE "19-Jul-2023 09:28:33" {WMEDLEY}<library>PDFSTREAM.;40)


(PRETTYCOMPRINT PDFSTREAMCOMS)

(RPAQQ PDFSTREAMCOMS
       ([ADDVARS [PRINTERTYPES ((PDF)
                                (CANPRINT (PDF))
                                (STATUS TRUE)
                                (PROPERTIES NILL)
                                (SEND POSTSCRIPTSEND)
                                (BITMAPSCALE POSTSCRIPT.BITMAPSCALE)
                                (BITMAPFILE (PDF.HARDCOPYW FILE BITMAP SCALEFACTOR REGION ROTATION 
                                                   TITLE]
               [PRINTFILETYPES (PDF (TEST PDFFILEP)
                                    (EXTENSION (PDF))
                                    (CONVERSION (TEXT PDF.TEXT TEDIT PDF.TEDIT]
               (IMAGESTREAMTYPES (PDF (OPENSTREAM OPEN-PDF-STREAM)
                                      (FONTCREATE POSTSCRIPT.FONTCREATE)
                                      (FONTSAVAILABLE POSTSCRIPT.FONTSAVAILABLE)
                                      (CREATECHARSET \CREATECHARSET.PSC]
        (DECLARE%: EVAL@COMPILE DONTCOPY (FILES (FROM LOADUPS)
                                                EXPORTS.ALL)
               (FILES (LOADCOMP)
                      POSTSCRIPTSTREAM))
        (FNS PDFFILEP PDF.HARDCOPYW PDF.TEXT PDF.TEDIT)
        (INITVARS (PDFCONVERTER 'ps2pdf))
        (ALISTS (PDF-CONVERTER-TEMPLATES ps2pdf pstopdf))
        (VARS (DEFAULTPRINTERTYPE 'PDF))
        (GLOBALVARS PDFCONVERTER \PDFIMAGEOPS PDF-CONVERTER-TEMPLATES)
        (FNS PDF-INIT OPEN-PDF-STREAM CLOSE-PDF-STREAM PS-TO-PDF)
        (P (MOVD? 'CL:IDENTITY 'TRUEFILENAME)
           (PDF-INIT))))

(ADDTOVAR PRINTERTYPES ((PDF)
                        (CANPRINT (PDF))
                        (STATUS TRUE)
                        (PROPERTIES NILL)
                        (SEND POSTSCRIPTSEND)
                        (BITMAPSCALE POSTSCRIPT.BITMAPSCALE)
                        (BITMAPFILE (PDF.HARDCOPYW FILE BITMAP SCALEFACTOR REGION ROTATION TITLE))))

(ADDTOVAR PRINTFILETYPES (PDF (TEST PDFFILEP)
                              (EXTENSION (PDF))
                              (CONVERSION (TEXT PDF.TEXT TEDIT PDF.TEDIT))))

(ADDTOVAR IMAGESTREAMTYPES (PDF (OPENSTREAM OPEN-PDF-STREAM)
                                (FONTCREATE POSTSCRIPT.FONTCREATE)
                                (FONTSAVAILABLE POSTSCRIPT.FONTSAVAILABLE)
                                (CREATECHARSET \CREATECHARSET.PSC)))
(DECLARE%: EVAL@COMPILE DONTCOPY 

(FILESLOAD (FROM LOADUPS)
       EXPORTS.ALL)


(FILESLOAD (LOADCOMP)
       POSTSCRIPTSTREAM)
)
(DEFINEQ

(PDFFILEP
  [LAMBDA (FILE)                                             (* ; "Edited 23-Jun-2023 14:43 by rmk")
                                                             (* ; "Edited  5-Mar-93 21:40 by rmk:")
                                                             (* ; "Edited 14-Jan-93 10:56 by jds")
    (OR (CL:MEMBER (UNPACKFILENAME.STRING FILE 'EXTENSION)
               '("PDF")
               :TEST
               (FUNCTION STRING-EQUAL))
        (CL:WHEN (STREAMP FILE)
            (SETFILEPTR FILE 0)
            (PROG1 (AND (EQ (BIN FILE)
                            (CHARCODE %%))
                        (EQ (BIN FILE)
                            (CHARCODE P))
                        (EQ (BIN FILE)
                            (CHARCODE D))
                        (EQ (BIN FILE)
                            (CHARCODE F)))
                   (SETFILEPTR FILE 0)))])

(PDF.HARDCOPYW
  [LAMBDA (FILE BITMAP SCALEFACTOR REGION Landscape? TITLE)  (* ; "Edited 24-Jul-2023 10:37 by rmk")
                                                             (* ; "Edited 23-Jun-2023 13:28 by rmk")
                                                             (* ; "Edited  6-Mar-2023 22:43 by rmk")
    (LET ((PSTTMP (PACKFILENAME 'EXTENSION 'TMPPS 'BODY FILE)))
         (PS-TO-PDF (POSTSCRIPT.HARDCOPYW PSTTMP FILE BITMAP SCALEFACTOR REGION Landscape? TITLE)
                FILE])

(PDF.TEXT
  [LAMBDA (FILE PDFFILE FONTS HEADING TABS)                  (* ; "Edited 23-Jun-2023 13:23 by rmk")
                                                             (* ; "Edited  7-Mar-2023 08:39 by rmk")
    (TEXTTOIMAGEFILE FILE PDFFILE 'PDF FONTS HEADING TABS `(REGION ,POSTSCRIPT.DEFAULT.PAGEREGION 
                                                                  ROTATION ,(NOT (NOT 
                                                                        POSTSCRIPT.TEXTFILE.LANDSCAPE
                                                                                      ])

(PDF.TEDIT
  [LAMBDA (FILE PDFFILE)                                     (* ; "Edited 23-Jun-2023 13:22 by rmk")
                                                             (* ; "Edited  7-Mar-2023 08:39 by rmk")
    (LET ((TSTREAM (OPENTEXTSTREAM FILE)))
         (TEDIT.FORMAT.HARDCOPY FILE PDFFILE T NIL NIL NIL 'PDF)
         (CLOSEF TSTREAM])
)

(RPAQ? PDFCONVERTER 'ps2pdf)

(ADDTOVAR PDF-CONVERTER-TEMPLATES (ps2pdf " " PSFILE " " PDFFILE " 2> " ERRORFILE)
                                  (pstopdf " " PSFILE " -o " PDFFILE " 2> " ERRORFILE))

(RPAQQ DEFAULTPRINTERTYPE PDF)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS PDFCONVERTER \PDFIMAGEOPS PDF-CONVERTER-TEMPLATES)
)
(DEFINEQ

(PDF-INIT
  [LAMBDA NIL                                                (* ; "Edited 25-Jun-2023 16:41 by rmk")
                                                             (* ; "Edited 23-Jun-2023 11:23 by rmk")

    (* ;; "Seems OK to make callers see this as PDF, even though the implementation is postscript.  The pdf stream is opened as a temporary postscript stream, and the closefn then uses an operating-system utility to convert it to the original target file-name.")

    (SETQ \PDFIMAGEOPS (create IMAGEOPS using \POSTSCRIPTIMAGEOPS IMAGETYPE _ 'PDF IMCLOSEFN _
                                              (FUNCTION CLOSE-PDF-STREAM)))
    (FONTPROFILE.ADDDEVICE 'PDF 'POSTSCRIPT])

(OPEN-PDF-STREAM
  [LAMBDA (FILE OPTIONS)                                     (* ; "Edited 24-Jun-2023 14:49 by rmk")

    (* ;; "Open a temporary PS file, but set it up so that at closing it gets converted to PDF using an operating-system utility (if available), and then gets renamed to the original intended filename.")

    (* ;; "We have to stash the original filename someplace.  We could put it in the tmp filename and then parse it out, but then we would have to worry about how unix filenames might parse against our {, },  etc.  ")

    (* ;; 
    "Simplest thing for now is to just add an extra field at the end of the \POSTSCRIPTDATA record.")

    (* ;; "")

    (if [AND NIL (EQ 'LPT (FILENAMEFIELD FILE 'HOST]
        then 
             (* ;; "If FILE is on the LPT device, we could just ssume that it can be printed directly, no point in converting.  But then we would alo have to lie that and give it a PDF extension so it thinks that we are heading to a PDF printer.")

             (OPENPOSTSCRIPTSTREAM FILE OPTIONS)
      else (CL:UNLESS (OR (ASSOC (OR PDFCONVERTER (MKATOM (UNIX-GETENV "MEDLEY-PDFCONVERTER")))
                                 PDF-CONVERTER-TEMPLATES))
                  (ERROR "POSTSCRIPT-to-PDF converter is not specified"))
           (LET ((PSSTREAM (OPENPOSTSCRIPTSTREAM (CONCAT "{UNIX}/tmp/medley-pdf-" (IDATE)
                                                        "-"
                                                        (RAND)
                                                        ".ps")
                                  OPTIONS)))
                (replace (STREAM IMAGEOPS) of PSSTREAM with \PDFIMAGEOPS)

                (* ;; "Hopefully the postscript implementation functions won't notice that we did a shift to get the IMAGETYPE and IMCLOSEFN")

                (replace (\POSTSCRIPTDATA POSTSCRIPTTARGETINFO) of (fetch (STREAM IMAGEDATA)
                                                                      of PSSTREAM) with FILE)
                PSSTREAM])

(CLOSE-PDF-STREAM
  [LAMBDA (PSSTREAM)                                         (* ; "Edited 24-Jul-2023 10:37 by rmk")
                                                             (* ; "Edited 17-Jul-2023 22:32 by rmk")
                                                             (* ; "Edited 24-Jun-2023 13:57 by rmk")

    (* ;; "PSSTREAM is a tmp/ postscript rendition of what is intended to end up as a pdf.  If we are going directly to a printer, we can probably just pass it along without worrying about conversion.  In fact, in that case we probably should not have bothered even setting up the PDF stream.")

    (* ;; "But for a file we execute the PDFCONVERTER as a shell command to make a pdf, and then we rename it to the intended filename")

    (* ;; "We have to back up to the ordinary POSTSCRIPT close, so that we don't loop through here")

    (CL:WHEN (EQ \PDFIMAGEOPS (fetch (STREAM IMAGEOPS) of PSSTREAM))
        (replace (STREAM IMAGEOPS) of PSSTREAM with \POSTSCRIPTIMAGEOPS)
        (RENAMEFILE (PS-TO-PDF (CLOSEF PSSTREAM))
               (fetch (\POSTSCRIPTDATA POSTSCRIPTTARGETINFO) of (fetch (STREAM IMAGEDATA)
                                                                   of PSSTREAM))))])

(PS-TO-PDF
  [LAMBDA (PSFILE PDFFILE DONTDELETE)                        (* ; "Edited 23-Jul-2023 22:30 by rmk")
                                                             (* ; "Edited 24-Jun-2023 15:01 by rmk")
                                                             (* ; "Edited 16-Jul-2022 13:06 by rmk")
                                                             (* ; "Edited  8-Jul-2022 10:20 by rmk")
                                                             (* ; "Edited  7-May-2022 22:40 by rmk")
                                                            (* ; "Edited  7-Oct-2021 11:15 by rmk:")

    (* ;; "PSFILE is the name of a closed PS file on a DSK/UNIX device.  This function uses the PDFCONVERTER utility to convert that to a parallel pdf file, which is then renamed to PDFFILE.  ")

    (* ;; "DONTDELETE is just for debugging, keeps the /tmp/ files")

    (SETQ PSFILE (TRUEFILENAME PSFILE))
    (SETQ PDFFILE (if PDFFILE
                      then (TRUEFILENAME PDFFILE)
                    else (PACKFILENAME 'EXTENSION 'pdf 'BODY PSFILE)))
    (CL:UNLESS (INFILEP PSFILE)
           (ERROR "NO PS FILE TO CONVERT"))
    (LET ((ERRORFILE (PACKFILENAME 'EXTENSION 'error 'BODY PSFILE))
          COMPLETIONCODE)

         (* ;; "PROCESS-COMMAND is currently from GITFNS.  Not sure whether ShellCommand in UNIXUTILS is appropriate.")

         (* ;; "We have to map the filenames down to Unix conventions:  (not host, slashes, etc.")

         [SETQ COMPLETIONCODE (PROCESS-COMMAND
                               (CONCATLIST (SUBLIS `[(PSFILE \, (SLASHIT (PACKFILENAME 'HOST NIL
                                                                                'BODY PSFILE)))
                                                     (PDFFILE \, (SLASHIT (PACKFILENAME 'HOST NIL
                                                                                 'BODY PDFFILE)))
                                                     (ERRORFILE \, (SLASHIT (PACKFILENAME
                                                                             'HOST NIL 'BODY 
                                                                             ERRORFILE]
                                                  (ASSOC (OR PDFCONVERTER (MKATOM (UNIX-GETENV 
                                                                                "MEDLEY-PDFCONVERTER"
                                                                                         )))
                                                         PDF-CONVERTER-TEMPLATES]

         (* ;; "Now use Medley names")

         (CLOSEF? PSFILE)
         (CL:UNLESS DONTDELETE (DELFILE PSFILE))
         (CLOSEF? ERRORFILE)
         (CL:WHEN (INFILEP ERRORFILE)
             (CL:WHEN (IGREATERP (PROG1 (GETFILEINFO ERRORFILE 'LENGTH)
                                     (CL:UNLESS DONTDELETE (DELFILE ERRORFILE)))
                             0)
                    (ERROR "Cannot create PDF file for " PDFFILE)))
         (CL:WHEN (IGREATERP COMPLETIONCODE 0)
                (ERROR "Cannot create PDF file for " PDFFILE))
         PDFFILE])
)

(MOVD? 'CL:IDENTITY 'TRUEFILENAME)

(PDF-INIT)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (2929 5370 (PDFFILEP 2939 . 3853) (PDF.HARDCOPYW 3855 . 4389) (PDF.TEXT 4391 . 4999) (
PDF.TEDIT 5001 . 5368)) (5718 13064 (PDF-INIT 5728 . 6453) (OPEN-PDF-STREAM 6455 . 8558) (
CLOSE-PDF-STREAM 8560 . 9864) (PS-TO-PDF 9866 . 13062)))))
STOP
