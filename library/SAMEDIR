(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED " 1-Sep-2020 11:40:26" {DSK}<Users>kaplan>Local>medley3.5>lispcore>library>SAMEDIR.;9 5511   

      changes to%:  (FNS CHECKSAMEDIR)

      previous date%: "25-Aug-2020 07:42:08" 
{DSK}<Users>kaplan>Local>medley3.5>lispcore>library>SAMEDIR.;6)


(* ; "
Copyright (c) 1982, 1984, 1985, 1986, 1987, 1990, 2018, 2020 by Venue & Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT SAMEDIRCOMS)

(RPAQQ SAMEDIRCOMS
       ((FNS CHECKSAMEDIR HOST&DIRECTORYFIELD)
        (INITVARS (SAMEDIRWAIT 10)
               (SAMEDIRDEFAULT 'O))
        (ADDVARS [MAKEFILEFORMS (OR (NLSETQ (CHECKSAMEDIR FILE))
                                    (RETFROM 'MAKEFILE]
               (MIGRATIONS))
        (GLOBALVARS MIGRATIONS)))
(DEFINEQ

(CHECKSAMEDIR
  [LAMBDA (FILE)                                        (* ; "Edited  1-Sep-2020 11:40 by rmk:")

    (* ;; "Check (a) that we are writing FILE to the same directory we last read/wrote it and (b) that a version newer than the current one has not since appeared.")

    (* ;; " OKHOST/DIRS is a list of places it's OK for the file to be winding up, so if your'e migrating code from one place ot another, you can do it gracefully.")

    [RESETSAVE (DIRECTORYNAME T)
           '(PROGN (CNDIR OLDVALUE]                          (* ; 
                                                           "Assumes that MAKEFILE has  RESETLST")
    (PROG ((*UPPER-CASE-FILE-NAMES* NIL)
           (DATES (GET (SETQ FILE (MKATOM (U-CASE FILE)))
                       'FILEDATES))
           HOST/DIR HOST DIR NEWV OKHOST/DIRS)
      AGAIN
          (OR (LISTP DATES)
              (RETURN))                                      (* ; 
                   "RMK: Use HOST&DIRECTORYFIELD to canonicalize both file and connected directory")
          [SETQ OKHOST/DIRS (CONS (SETQ HOST/DIR (HOST&DIRECTORYFIELD (DIRECTORYNAME T)))
                                  (MKLIST (CDR (ASSOC HOST/DIR MIGRATIONS :TEST 'STRING-EQUAL]
          (COND
             ((for OLDFILE in DATES bind HOST DIR never (CL:MEMBER
                                                                         (HOST&DIRECTORYFIELD
                                                                          (CDR OLDFILE))
                                                                         OKHOST/DIRS :TEST
                                                                         'STRING-EQUAL))

              (* ;; "The file is going somewhere it has never been before.  ")

              (* ;; "Check that that is really what the user wants.")

              (SELECTQ (ASKUSER SAMEDIRWAIT SAMEDIRDEFAULT (LIST "You haven't loaded or written" FILE
                                                                 "in your connected directory" 
                                                                 HOST/DIR "-- write it out anyway")
                              `[[O ,(CONCAT "Oops!  Make file on " (SETQ HOST/DIR (
                                                                                HOST&DIRECTORYFIELD
                                                                                   (CDAR DATES]
                                (C "Make file on other directory: ")
                                (Y ,(CONCAT "Yes, write it here")
                                   (CHARACTER (CHARCODE EOL)))
                                (N ,(CONCAT "No, abort MAKEFILE")
                                   (CHARACTER (CHARCODE EOL]
                              NIL NIL '(NOECHOFLG T))
                  (Y (RETURN))
                  (N (ERROR!))
                  (C (SETQ HOST/DIR))
                  (O (TERPRI T))
                  (SHOULDNT))
              [NLSETQ (CNDIR (OR HOST/DIR (READ T T]
              (GO AGAIN))
             ([AND [SETQ NEWV (INFILEP (PACKFILENAME.STRING 'VERSION NIL 'BODY (CDAR DATES]
                   (NOT (STRING-EQUAL NEWV (CDAR DATES]

              (* ;; "A newer version appeared while the user was editing this file.")

              (* ;; "Ask if he should over-write it.")

              (SELECTQ (ASKUSER 15 'Y (LIST (CDAR DATES)
                                            "is not the most recent version (version"
                                            (MKSTRING (FILENAMEFIELD NEWV 'VERSION))
                                            "has since appeared)." 
                                            "Do you want to make the file anyway"))
                  (Y)
                  (N (ERROR!))
                  (SHOULDNT])

(HOST&DIRECTORYFIELD
  [LAMBDA (FILENAME)                                    (* ; "Edited 15-Apr-2018 19:05 by rmk:")

    (* ;; "Returns the host&dir fields packed together.  HOST and device are upper cased")

    (PACKFILENAME.STRING 'DEVICE (U-CASE (FILENAMEFIELD FILENAME 'DEVICE))
           'HOST
           (U-CASE (FILENAMEFIELD FILENAME 'HOST))
           'DIRECTORY
           (FILENAMEFIELD FILENAME 'DIRECTORY])
)

(RPAQ? SAMEDIRWAIT 10)

(RPAQ? SAMEDIRDEFAULT 'O)

(ADDTOVAR MAKEFILEFORMS (OR (NLSETQ (CHECKSAMEDIR FILE))
                                (RETFROM 'MAKEFILE)))

(ADDTOVAR MIGRATIONS )
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS MIGRATIONS)
)
(PUTPROPS SAMEDIR COPYRIGHT ("Venue & Xerox Corporation" 1982 1984 1985 1986 1987 1990 2018 2020))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (817 5124 (CHECKSAMEDIR 827 . 4681) (HOST&DIRECTORYFIELD 4683 . 5122)))))
STOP
