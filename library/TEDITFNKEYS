(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED " 6-May-2018 17:15:13" 
{DSK}<Users>kaplan>Local>medley3.5>lispcore>library>TEDITFNKEYS.;10 30302  

      changes to%:  (VARS TEDITFNKEYSCOMS)
                    (FNS \TEDIT.FIND \TEDIT.FINDAGAIN)

      previous date%: " 6-May-2018 17:07:57" 
{DSK}<Users>kaplan>Local>medley3.5>lispcore>library>TEDITFNKEYS.;9)


(* ; "
Copyright (c) 1985, 1986, 1987, 1990, 1991, 1994, 2018 by Venue & Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT TEDITFNKEYSCOMS)

(RPAQQ TEDITFNKEYSCOMS
       ((FILES TEDITDCL)
        (DECLARE%: EVAL@COMPILE DONTCOPY (CONSTANTS (\SCRATCHLEN 64))
               (FILES (LOADCOMP)
                      TEDITDCL))
        (COMS 
              (* ;; "Functions that actually implement the commands for the function keys:")

              (FNS \TEDIT.BOLD.SEL.OFF \TEDIT.BOLD.SEL.ON \TEDIT.CENTER.SEL \TEDIT.CENTER.SEL.REV 
                   \TEDIT.DEFAULTS.CARET \TEDIT.DEFAULTSSEL \TEDIT.SETDEFAULT.FROM.SEL \TEDIT.FIND 
                   \TEDIT.FINDAGAIN \TEDIT.ITALIC.SEL.OFF \TEDIT.ITALIC.SEL.ON \TEDIT.LARGERSEL 
                   \TEDIT.LCASE.SEL \TEDIT.SHOWCARETLOOKS \TEDIT.SMALLERSEL \TEDIT.SUBSCRIPTSEL 
                   \TEDIT.SUPERSCRIPTSEL \TEDIT.UCASE.SEL \TEDIT.UNDERLINE.SEL.OFF 
                   \TEDIT.UNDERLINE.SEL.ON \TEDIT.STRIKEOUT.SEL.ON \TEDIT.STRIKEOUT.SEL.OFF 
                   \TEDIT.SELECT.ALL))
        (COMS 
              (* ;; "Auxiliary functions used in the above main functions:")

              (FNS \TEDIT.BOLD.CARET.OFF \TEDIT.BOLD.CARET.ON \TEDIT.ITALIC.CARET.OFF 
                   \TEDIT.ITALIC.CARET.ON \TEDIT.LARGER.CARET \TEDIT.SMALLER.CARET 
                   \TEDIT.SUBSCRIPT.CARET \TEDIT.SUPERSCRIPT.CARET \TEDIT.UNDERLINE.CARET.OFF 
                   \TEDIT.UNDERLINE.CARET.ON \TEDIT.STRIKEOUT.CARET.OFF \TEDIT.STRIKEOUT.CARET.ON))
        (COMS                                                (* ; 
                                              "little selection utilities etc., for building hacks")
              (FNS \SEL.LIMIT \SEL.LINEDESC \TK.DESCRIBEFONT \PARAS.IN.SEL))
        [VARS (TEDIT.FNKEY.VERBOSE T)
              (\TEDIT.KEYS '(("2,4" UNDO)
                             ("2,44" UNDO)
                             ("2,3" FN \TEDIT.FIND)
                             ("2,43" FN \TEDIT.FIND)
                             ("2,10" REDO)
                             ("2,50" REDO)
                             ("2,22" NEXT)
                             ("2,62" NEXT)
                             (ESC EXPAND)
                             ("2,24" EXPAND)
                             ("2,101" FN \TEDIT.CENTER.SEL)
                             ("2,141" FN \TEDIT.CENTER.SEL.REV)
                             ("2,102" FN \TEDIT.BOLD.SEL.ON)
                             ("2,142" FN \TEDIT.BOLD.SEL.OFF)
                             ("2,103" FN \TEDIT.ITALIC.SEL.ON)
                             ("2,143" FN \TEDIT.ITALIC.SEL.OFF)
                             ("2,104" FN \TEDIT.UCASE.SEL)
                             ("2,144" FN \TEDIT.LCASE.SEL)
                             ("2,105" FN \TEDIT.STRIKEOUT.SEL.ON)
                             ("2,145" FN \TEDIT.STRIKEOUT.SEL.OFF)
                             ("2,106" FN \TEDIT.UNDERLINE.SEL.ON)
                             ("2,146" FN \TEDIT.UNDERLINE.SEL.OFF)
                             ("2,107" FN \TEDIT.SUBSCRIPTSEL)
                             ("2,147" FN \TEDIT.SUPERSCRIPTSEL)
                             ("2,110" FN \TEDIT.SMALLERSEL)
                             ("2,150" FN \TEDIT.LARGERSEL)
                             ("2,113" FN \TEDIT.SUPERSCRIPTSEL)
                             ("2,153" FN \TEDIT.SUBSCRIPTSEL)
                             ("2,114" FN \TEDIT.SUBSCRIPTSEL)
                             ("2,154" FN \TEDIT.SUPERSCRIPTSEL)
                             ("2,115" FN \TEDIT.DEFAULTSSEL)
                             ("2,155" FN \TEDIT.SETDEFAULT.FROM.SEL)
                             ("2,1" FN \TEDIT.SHOWCARETLOOKS)
                             ("1,U" UNDO)
                             ("1,u" UNDO)
                             ("1,z" UNDO)
                             ("1,Z" UNDO)
                             ("1,F" FN \TEDIT.FIND)
                             ("1,f" FN \TEDIT.FIND)
                             ("1,a" FN \TEDIT.SELECT.ALL)
                             ("1,A" FN \TEDIT.SELECT.ALL)
                             ("1,g" FN \TEDIT.FINDAGAIN]
        [P (MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                                              (SELECTQ (CADR ENTRY)
                                                     (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                                                (CADDR ENTRY)))
                                                     (TEDIT.SETSYNTAX (CAR ENTRY)
                                                            (CADR ENTRY]
                                                             (* ; "Original was %"(FOR ENTRY IN \TEDIT.KEYS DO (SELECTQ (CADR ENTRY) (FN (TEDIT.SETFUNCTION (CAR ENTRY) (CADDR ENTRY))) (TEDIT.SETSYNTAX (CAR ENTRY) (CADR ENTRY))))%".")
                                                             (* ; 
                                                  "Changed by yabu.fx, for SUNLOADUP without DWIM.")
        ))

(FILESLOAD TEDITDCL)
(DECLARE%: EVAL@COMPILE DONTCOPY 
(DECLARE%: EVAL@COMPILE 

(RPAQQ \SCRATCHLEN 64)


(CONSTANTS (\SCRATCHLEN 64))
)


(FILESLOAD (LOADCOMP)
       TEDITDCL)
)



(* ;; "Functions that actually implement the commands for the function keys:")

(DEFINEQ

(\TEDIT.BOLD.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)              (* ; "Edited  6-Nov-87 11:00 by jds")
          
          (* ;; "Turn boldness off for the selected characters, and for future type-in.")

    (\TEDIT.BOLD.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT MEDIUM) SEL])

(\TEDIT.BOLD.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                       (* ; "Edited  6-Nov-87 11:00 by jds")
          
          (* ;; "Turn boldness on for selected characters and for future type-in.")

    (\TEDIT.BOLD.CARET.ON TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT BOLD) SEL])

(\TEDIT.CENTER.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                       (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "makes the current paragraph centered")

    (PROG (LOOKS OLDQUAD NEWQUAD NEWQUADS PARASEL (SAVECH# (fetch (SELECTION CH#) of SEL))
                 (SAVEDCH (fetch (SELECTION DCH) of SEL)))
          (for PARA in (\PARAS.IN.SEL SEL TEXTOBJ)
             do (SETQ LOOKS (TEDIT.GET.PARALOOKS TEXTSTREAM PARA))
                   (SETQ OLDQUAD (LISTGET LOOKS 'QUAD))
                   [SETQ NEWQUAD (CADR (MEMB OLDQUAD (CONSTANT '(LEFT JUSTIFIED CENTERED LEFT]
                   (LISTPUT LOOKS 'QUAD NEWQUAD)
                   (SETQ PARASEL (TEDIT.SETSEL TEXTSTREAM PARA 1))
                   (TEDIT.PARALOOKS TEXTSTREAM LOOKS PARASEL)
                   (push NEWQUADS NEWQUAD))
          (TEDIT.SETSEL TEXTSTREAM SAVECH# SAVEDCH)
          (COND
             (TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM (DREVERSE NEWQUADS)
                                         T])

(\TEDIT.CENTER.SEL.REV
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                       (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "acts like center.sel but cycles in the opposite direction")

    (PROG (LOOKS OLDQUAD NEWQUAD NEWQUADS PARASEL (SAVECH# (fetch (SELECTION CH#) of SEL))
                 (SAVEDCH (fetch (SELECTION DCH) of SEL)))
          (for PARA in (\PARAS.IN.SEL SEL TEXTOBJ)
             do (SETQ LOOKS (TEDIT.GET.PARALOOKS TEXTSTREAM PARA))
                   (SETQ OLDQUAD (LISTGET LOOKS 'QUAD))
                   [SETQ NEWQUAD (CADR (MEMB OLDQUAD (CONSTANT '(LEFT CENTERED JUSTIFIED LEFT]
                   (LISTPUT LOOKS 'QUAD NEWQUAD)
                   (SETQ PARASEL (TEDIT.SETSEL TEXTSTREAM PARA 1))
                   (TEDIT.PARALOOKS TEXTSTREAM LOOKS PARASEL)
                   (push NEWQUADS NEWQUAD))
          (TEDIT.SETSEL TEXTSTREAM SAVECH# SAVEDCH)
          (COND
             (TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM (DREVERSE NEWQUADS)
                                         T])

(\TEDIT.DEFAULTS.CARET
  (LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 11:24")
    (PROGN (TEDIT.CARETLOOKS TEXTSTREAM (create CHARLOOKS using TEDIT.DEFAULT.CHARLOOKS))
           (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))))

(\TEDIT.DEFAULTSSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
                                                             (* acts on the selection)
    (TEDIT.LOOKS TEXTSTREAM (create CHARLOOKS using TEDIT.DEFAULT.CHARLOOKS)
           SEL])

(\TEDIT.SETDEFAULT.FROM.SEL
  (LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds " 8-Nov-85 15:22")
                                                             (* Set the defaults from the current 
                                                             selection.)
    (PROG ((LOOKS (TEDIT.GET.LOOKS TEXTSTREAM SEL)))
          (SETQ TEDIT.DEFAULT.CHARLOOKS (\TEDIT.PARSE.CHARLOOKS.LIST LOOKS)))))

(\TEDIT.FIND
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN)                (* ; "Edited  6-May-2018 17:14 by rmk:")
                                                             (* ; "Edited 30-May-91 21:05 by jds")
                                                             (* just calls the normal tedit.find 
                                                           starting at the right of the current 
                                                           selection)
    (PROG (TARGET (TEXTOBJ (TEXTOBJ TEXTSTREAM))
                 SEL CH W)                                   (* Case sensitive search, with * and 
                                                           %# wildcards)
          [SETQ W (CAR (MKLIST (fetch (TEXTOBJ \WINDOW) of TEXTOBJ]
          (CL:WHEN AGAIN
              (SETQ TARGET (WINDOWPROP W 'TEDIT.LAST.FIND.STRING)))
          (CL:UNLESS TARGET
              [SETQ TARGET (TEDIT.GETINPUT TEXTOBJ "Text to find: " (WINDOWPROP W 
                                                                           'TEDIT.LAST.FIND.STRING)
                                  (CHARCODE (EOL LF ESC])
          [COND
             (TARGET (SETQ SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
                    (\SHOWSEL SEL NIL NIL)
                    (TEDIT.PROMPTPRINT TEXTOBJ "Searching..." T)
                    (SETQ CH (TEDIT.FIND TEXTOBJ (MKSTRING TARGET)
                                    NIL NIL T))
                    (COND
                       (CH                                   (* We found the target text.)
                           (TEDIT.PROMPTPRINT TEXTOBJ "Done.")
                           (replace (SELECTION CH#) of SEL with (CAR CH))
                                                             (* Set up SELECTION to be the found 
                                                           text)
                           (replace (SELECTION CHLIM) of SEL with (ADD1 (CADR CH)))
                           [replace (SELECTION DCH) of SEL with
                                                                   (ADD1 (IDIFFERENCE (CADR CH)
                                                                                (CAR CH]
                           (replace (SELECTION POINT) of SEL with 'RIGHT)
                           (replace (TEXTOBJ CARETLOOKS) of TEXTOBJ with (
                                                                          \TEDIT.GET.INSERT.CHARLOOKS
                                                                                      TEXTOBJ SEL))
                           (TEDIT.RESET.EXTEND.PENDING.DELETE SEL)
                                                             (* And never pending a deletion.)
                           (\FIXSEL SEL TEXTOBJ)
                           (TEDIT.NORMALIZECARET TEXTOBJ)
                           (\SHOWSEL SEL NIL T)
                           (WINDOWPROP W 'TEDIT.LAST.FIND.STRING TARGET)
                                                             (* And get it into the window)
                           )
                       (T (TEDIT.PROMPTPRINT TEXTOBJ "(not found)")
                          (\SHOWSEL SEL NIL T]
          (replace (TEXTOBJ \INSERTNEXTCH) of TEXTOBJ with -1])

(\TEDIT.FINDAGAIN
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.FIND TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.ITALIC.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited 20-Oct-87 10:43 by jds")

    (\TEDIT.ITALIC.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE REGULAR) SEL])

(\TEDIT.ITALIC.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 10:43 by jds")

    (TEDIT.LOOKS TEXTSTREAM '(SLOPE ITALIC) SEL])

(\TEDIT.LARGERSEL
  (LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.LARGER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT 2)
                 SEL)))))

(\TEDIT.LCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                           (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "LOWER-CASEs the selection")

    (PROG ((STR (TEDIT.SEL.AS.STRING STREAM SEL))
           (POS (fetch (SELECTION CH#) of SEL))
           (LEN (fetch (SELECTION DCH) of SEL))
           (POINT (fetch (SELECTION POINT) of SEL)))
          (TEDIT.DELETE STREAM SEL)
          (TEDIT.INSERT STREAM (L-CASE STR))
          (TEDIT.SETSEL STREAM POS LEN POINT)
          (TEDIT.NORMALIZECARET TEXTOBJ)
          (replace (TEDITHISTORYEVENT THACTION) of (fetch (TEXTOBJ TXTHISTORY)
                                                              of TEXTOBJ) with 'LowerCase])

(\TEDIT.SHOWCARETLOOKS
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                       (* ; "Edited 30-May-91 21:09 by jds")

         (* * comment)

    (PROG ((LOOKS (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)))
          (TEDIT.PROMPTPRINT TEXTSTREAM (CONCAT (\TK.DESCRIBEFONT (fetch (CHARLOOKS CLFONT)
                                                                         of LOOKS))
                                               (COND
                                                  ((AND (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                        (NEQ (fetch (CHARLOOKS CLOFFSET)
                                                                of LOOKS)
                                                             0))
                                                   (CONCAT " offset " (fetch (CHARLOOKS CLOFFSET)
                                                                         of LOOKS)))
                                                  (T ""))
                                               (COND
                                                  ((fetch (CHARLOOKS CLOLINE) of LOOKS)
                                                   " overlined")
                                                  (T ""))
                                               (COND
                                                  ((fetch (CHARLOOKS CLULINE) of LOOKS)
                                                   " underlined")
                                                  (T "")))
                 T)
          (RETURN])

(\TEDIT.SMALLERSEL
  (LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.SMALLER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT -2)
                 SEL)))))

(\TEDIT.SUBSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")

    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT -2)
           SEL])

(\TEDIT.SUPERSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:13 by jds")

    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT 2)
           SEL])

(\TEDIT.UCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                           (* ; "Edited 30-May-91 21:05 by jds")
                                                             (* ; "uppercasifies the selection")
    (PROG ((STR (TEDIT.SEL.AS.STRING STREAM SEL))
           (POS (fetch (SELECTION CH#) of SEL))
           (LEN (fetch (SELECTION DCH) of SEL))
           (POINT (fetch (SELECTION POINT) of SEL)))
          (TEDIT.DELETE STREAM SEL)
          (TEDIT.INSERT STREAM (U-CASE STR))
          (TEDIT.SETSEL STREAM POS LEN POINT)
          (TEDIT.NORMALIZECARET TEXTOBJ)
          (replace (TEDITHISTORYEVENT THACTION) of (fetch (TEXTOBJ TXTHISTORY)
                                                              of TEXTOBJ) with 'UpperCase])

(\TEDIT.UNDERLINE.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:26 by jds")

    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE OFF) SEL])

(\TEDIT.UNDERLINE.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")

    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE ON) SEL])

(\TEDIT.STRIKEOUT.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")

    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT ON) SEL])

(\TEDIT.STRIKEOUT.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")

    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT OFF) SEL])

(\TEDIT.SELECT.ALL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited  6-May-2018 12:41 by rmk:")
    (TEDIT.SETSEL TEXTSTREAM 0 (ADD1 (fetch TEXTLEN of TEXTOBJ))
           'LEFT])
)



(* ;; "Auxiliary functions used in the above main functions:")

(DEFINEQ

(\TEDIT.BOLD.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT MEDIUM)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.BOLD.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT BOLD)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.ITALIC.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE REGULAR)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.ITALIC.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE ITALIC)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.LARGER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT 2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SMALLER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT -2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SUBSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT -2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SUPERSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT 2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.UNDERLINE.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE OFF)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.UNDERLINE.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.STRIKEOUT.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT OFF)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.STRIKEOUT.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                      (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])
)



(* ; "little selection utilities etc., for building hacks")

(DEFINEQ

(\SEL.LIMIT
  [LAMBDA (SEL)                                          (* ; "Edited 30-May-91 21:06 by jds")

         (* returns the character that delimits this selection.
       The first char if the point is left else the last)

    (COND
       ((EQ (fetch (SELECTION POINT) of SEL)
            'LEFT)
        (fetch (SELECTION CH#) of SEL))
       (T (SUB1 (fetch (SELECTION CHLIM) of SEL])

(\SEL.LINEDESC
  [LAMBDA (SEL)                                          (* ; "Edited 30-May-91 21:06 by jds")
                                                             (* returns the first line descriptor 
                                                           if the point is left, otherwise the 
                                                           last)
    (COND
       [(EQ (fetch (SELECTION POINT) of SEL)
            'LEFT)
        (CAR (MKLIST (fetch (SELECTION L1) of SEL]
       (T (CAR (MKLIST (fetch (SELECTION LN) of SEL])

(\TK.DESCRIBEFONT
  (LAMBDA (FONT)                                             (* gbn "15-Dec-84 17:54")
          
          (* * returns a string which describes a font
          (in short. If it's not italic then no mention is made of slope, etc.))

    (CONCAT (L-CASE (FONTPROP FONT 'FAMILY))
           " "
           (FONTPROP FONT 'SIZE)
           (COND
              ((NEQ (FONTPROP FONT 'WEIGHT)
                    'MEDIUM)
               (CONCAT " " (L-CASE (FONTPROP FONT 'WEIGHT))))
              (T ""))
           (COND
              ((NEQ (FONTPROP FONT 'SLOPE)
                    'REGULAR)
               (CONCAT " " (L-CASE (FONTPROP FONT 'SLOPE))))
              (T "")))))

(\PARAS.IN.SEL
  [LAMBDA (SEL TEXTOBJ)                                  (* ; "Edited 30-May-91 21:06 by jds")

         (* returns a list which contains one character number for each paragraph 
       included in the selection)

    (PROG ((PARAS)
           PARAENDED PCS (POS (fetch (SELECTION CH#) of SEL)))
          (COND
             ((ZEROP (fetch (SELECTION DCH) of SEL))

         (* there are not really any pieces in this selection, however, effect the 
       change to the para containing this selection by starting the selection one 
       character earlier. This is not the right soln, but TEdit has no looks on the 
       empty last para as yet.)

              (replace (SELECTION CH#) of SEL with (IDIFFERENCE (fetch (SELECTION
                                                                                        CH#)
                                                                               of SEL)
                                                                      1))
              (replace (SELECTION DCH) of SEL with 1)
              (\FIXSEL SEL TEXTOBJ)))
          (SETQ PCS (TEDIT.SELECTED.PIECES TEXTOBJ SEL))     (* to include the first char)
          (SETQ PARAENDED T)
          (for PC in PCS do (COND
                                           (PARAENDED        (* the last piece ended a paragraph, 
                                                           so include this character in the 
                                                           list)
                                                  (SETQ PARAENDED NIL)
                                                  (push PARAS POS)))
                                       (SETQ PARAENDED (fetch (PIECE PPARALAST) of PC))
                                       (add POS (fetch (PIECE PLEN) of PC)))
          (RETURN (DREVERSE PARAS])
)

(RPAQQ TEDIT.FNKEY.VERBOSE T)

(RPAQQ \TEDIT.KEYS
       (("2,4" UNDO)
        ("2,44" UNDO)
        ("2,3" FN \TEDIT.FIND)
        ("2,43" FN \TEDIT.FIND)
        ("2,10" REDO)
        ("2,50" REDO)
        ("2,22" NEXT)
        ("2,62" NEXT)
        (ESC EXPAND)
        ("2,24" EXPAND)
        ("2,101" FN \TEDIT.CENTER.SEL)
        ("2,141" FN \TEDIT.CENTER.SEL.REV)
        ("2,102" FN \TEDIT.BOLD.SEL.ON)
        ("2,142" FN \TEDIT.BOLD.SEL.OFF)
        ("2,103" FN \TEDIT.ITALIC.SEL.ON)
        ("2,143" FN \TEDIT.ITALIC.SEL.OFF)
        ("2,104" FN \TEDIT.UCASE.SEL)
        ("2,144" FN \TEDIT.LCASE.SEL)
        ("2,105" FN \TEDIT.STRIKEOUT.SEL.ON)
        ("2,145" FN \TEDIT.STRIKEOUT.SEL.OFF)
        ("2,106" FN \TEDIT.UNDERLINE.SEL.ON)
        ("2,146" FN \TEDIT.UNDERLINE.SEL.OFF)
        ("2,107" FN \TEDIT.SUBSCRIPTSEL)
        ("2,147" FN \TEDIT.SUPERSCRIPTSEL)
        ("2,110" FN \TEDIT.SMALLERSEL)
        ("2,150" FN \TEDIT.LARGERSEL)
        ("2,113" FN \TEDIT.SUPERSCRIPTSEL)
        ("2,153" FN \TEDIT.SUBSCRIPTSEL)
        ("2,114" FN \TEDIT.SUBSCRIPTSEL)
        ("2,154" FN \TEDIT.SUPERSCRIPTSEL)
        ("2,115" FN \TEDIT.DEFAULTSSEL)
        ("2,155" FN \TEDIT.SETDEFAULT.FROM.SEL)
        ("2,1" FN \TEDIT.SHOWCARETLOOKS)
        ("1,U" UNDO)
        ("1,u" UNDO)
        ("1,z" UNDO)
        ("1,Z" UNDO)
        ("1,F" FN \TEDIT.FIND)
        ("1,f" FN \TEDIT.FIND)
        ("1,a" FN \TEDIT.SELECT.ALL)
        ("1,A" FN \TEDIT.SELECT.ALL)
        ("1,g" FN \TEDIT.FINDAGAIN)))

[MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                              (SELECTQ (CADR ENTRY)
                                  (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                             (CADDR ENTRY)))
                                  (TEDIT.SETSYNTAX (CAR ENTRY)
                                         (CADR ENTRY]



(* ; 
"Original was %"(FOR ENTRY IN \TEDIT.KEYS DO (SELECTQ (CADR ENTRY) (FN (TEDIT.SETFUNCTION (CAR ENTRY) (CADDR ENTRY))) (TEDIT.SETSYNTAX (CAR ENTRY) (CADR ENTRY))))%"."
)




(* ; "Changed by yabu.fx, for SUNLOADUP without DWIM.")

(PUTPROPS TEDITFNKEYS COPYRIGHT ("Venue & Xerox Corporation" 1985 1986 1987 1990 1991 1994 2018))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5707 18741 (\TEDIT.BOLD.SEL.OFF 5717 . 6056) (\TEDIT.BOLD.SEL.ON 6058 . 6387) (
\TEDIT.CENTER.SEL 6389 . 7444) (\TEDIT.CENTER.SEL.REV 7446 . 8526) (\TEDIT.DEFAULTS.CARET 8528 . 8812)
 (\TEDIT.DEFAULTSSEL 8814 . 9137) (\TEDIT.SETDEFAULT.FROM.SEL 9139 . 9578) (\TEDIT.FIND 9580 . 12943) 
(\TEDIT.FINDAGAIN 12945 . 13119) (\TEDIT.ITALIC.SEL.OFF 13121 . 13363) (\TEDIT.ITALIC.SEL.ON 13365 . 
13548) (\TEDIT.LARGERSEL 13550 . 13845) (\TEDIT.LCASE.SEL 13847 . 14598) (\TEDIT.SHOWCARETLOOKS 14600
 . 16240) (\TEDIT.SMALLERSEL 16242 . 16540) (\TEDIT.SUBSCRIPTSEL 16542 . 16746) (\TEDIT.SUPERSCRIPTSEL
 16748 . 16953) (\TEDIT.UCASE.SEL 16955 . 17762) (\TEDIT.UNDERLINE.SEL.OFF 17764 . 17952) (
\TEDIT.UNDERLINE.SEL.ON 17954 . 18140) (\TEDIT.STRIKEOUT.SEL.ON 18142 . 18328) (
\TEDIT.STRIKEOUT.SEL.OFF 18330 . 18518) (\TEDIT.SELECT.ALL 18520 . 18739)) (18813 24253 (
\TEDIT.BOLD.CARET.OFF 18823 . 19271) (\TEDIT.BOLD.CARET.ON 19273 . 19718) (\TEDIT.ITALIC.CARET.OFF 
19720 . 20170) (\TEDIT.ITALIC.CARET.ON 20172 . 20620) (\TEDIT.LARGER.CARET 20622 . 21070) (
\TEDIT.SMALLER.CARET 21072 . 21522) (\TEDIT.SUBSCRIPT.CARET 21524 . 21978) (\TEDIT.SUPERSCRIPT.CARET 
21980 . 22435) (\TEDIT.UNDERLINE.CARET.OFF 22437 . 22890) (\TEDIT.UNDERLINE.CARET.ON 22892 . 23343) (
\TEDIT.STRIKEOUT.CARET.OFF 23345 . 23798) (\TEDIT.STRIKEOUT.CARET.ON 23800 . 24251)) (24322 28070 (
\SEL.LIMIT 24332 . 24770) (\SEL.LINEDESC 24772 . 25368) (\TK.DESCRIBEFONT 25370 . 26085) (
\PARAS.IN.SEL 26087 . 28068)))))
STOP
