(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 7-Feb-2022 12:04:09" 
{DSK}<Users>kaplan>Local>medley3.5>my-medley>library>lafite>LAFITESEND.;2 100778 

      :CHANGES-TO (FILES LAFITEDECLS)
                  (FNS \SENDMESSAGE.RESTARTABLE \SENDMESSAGE LAFITE.SENDMESSAGE MAKEXXXSUPPORTFORM 
                       MAKENEWMESSAGEFORM MAKEANSWERFORM LAFITE.FILL.IN.ANSWER.FORM MAKEFORWARDFORM)

      :PREVIOUS-DATE "30-Sep-2021 22:58:58" 
{DSK}<Users>kaplan>Local>medley3.5>my-medley>library>lafite>LAFITESEND.;1)


(* ; "
Copyright (c) 1984-1990, 1993, 1999-2000, 2021 by Xerox Corporation.
")

(PRETTYCOMPRINT LAFITESENDCOMS)

(RPAQQ LAFITESENDCOMS
       ((COMS                                                (* ; "Sending mail")
              (FNS DOLAFITESENDINGCOMMAND \SENDMESSAGE.INITIATE \SENDMSG.DELIVER \SENDMSG.EXIT.TEDIT
                   \SENDMSG.SAVE.FORM \LAFITE.HEADER.EOF \LAFITE.INSERT.REPLYTO \SENDMSG.REPLYTO 
                   \SENDMSG.CHANGE.MODE \SENDMSG.FIND.FIELD \SENDMESSAGE.PARSE \LAFITE.PREPARE.SEND 
                   \LAFITE.PREPARE.ERROR \LAFITE.CHOOSE.MSG.FORMAT LAFITE.MAKE.PLAIN.TEXTSTREAM 
                   \SENDMESSAGE.MENUPROMPT \SENDMESSAGE.PROMPT \SENDMESSAGEFAIL)
              (FNS \SENDMESSAGE \SENDMESSAGE.RESTARTABLE \SENDMESSAGE.CLEANUP \SENDMESSAGE.MAKEWINDOW
                   MAKELAFITEDELIVERMENU \LAFITE.CLOSEMSG? \LAFITE.AFTER.DELIVER \LAFITE.UNSENT.ICON
                   \LAFITE.FETCH.SUBJECT LAFITE.SENDMESSAGE \SENDMESSAGE0 LA.ASSURE.PROMPT.WINDOW 
                   \LAFITE.SEND.FAIL \LAFITE.INVALID.RECIPIENTS \SENDMESSAGE.ABORT))
        (COMS                                                (* ; "Outbox hacking")
              (FNS \OUTBOX.CREATE \OUTBOX.RESET \OUTBOX.CLOSEFN \OUTBOX.REPAINTFN \OUTBOX.RESHAPEFN 
                   \OUTBOX.SHADEITEM \OUTBOX.BUTTONFN \OUTBOX.DISPLAYLINE \OUTBOX.ADD.ITEM)
              (INITVARS (LAFITEOUTBOXSIZE 2)
                     (\LAFITE.OUTBOX))
              (DECLARE%: EVAL@COMPILE DONTCOPY (RECORDS OUTBOXITEM)
                     (GLOBALVARS LAFITEOUTBOXSIZE)))
        (COMS                                                (* ; "Built-in message forms")
              (FNS \LAFITE.MESSAGEFORM MAKELAFITESUPPORTFORM MAKELISPSUPPORTFORM MAKEXXXSUPPORTFORM 
                   MAKENEWMESSAGEFORM MAKELAFITEPRIVATEFORMSITEMS \LAFITE.UNCACHE.MESSAGEFORM 
                   \LAFITE.DELETE.MESSAGEFORM \LAFITE.SELECT.FORM \LAFITE.DELETE.FORM.INTERNAL 
                   \LAFITE.READ.FORM \LAFITE.FIND.TEMPLATE))
        (COMS                                                (* ; "ANSWER")
              (FNS \LAFITE.ANSWER \LAFITE.ANSWER.PROC MAKEANSWERFORM LA.PRINT.COMMA.LIST 
                   LAFITE.FILL.IN.ANSWER.FORM))
        (COMS                                                (* ; "FORWARD")
              (FNS \LAFITE.FORWARD \LAFITE.FORWARD.PROC MAKEFORWARDFORM))
        [COMS (VARS LAFITESENDINGMENUITEMS LAFITEFORMSMENUITEMS LAFITEFORMATMENUITEMS 
                    LAFITEFORWARDSTRINGS)
              (ADDVARS (\SYSTEMCACHEVARS \LAFITE.REPORT.MACHINE)
                     (LAFITESPECIALFORMS ("Lisp Report" (FUNCTION MAKELISPSUPPORTFORM)
                                                "A form to report a Lisp bug or suggestion")
                            ("Lafite Report" (FUNCTION MAKELAFITESUPPORTFORM)
                                   "A form to report a Lafite bug or suggestion"))
                     (LAFITEMENUVARS LAFITEFORMSMENU LAFITEFORMATMENU))
              (INITVARS (\LAFITE.REPORT.MACHINE)
                     (LAFITECURRENTEDITORWINDOWS)
                     (LAFITEFORMFILES)
                     (LAFITEFORMSMENU)
                     (LAFITEFORMATMENU))
              (INITVARS (LAFITEEDITORFONT LAFITEDISPLAYFONT)
                     (LAFITEFORM.EXT "Lafite-form")
                     (LAFITEFORMDIRECTORIES NIL)
                     (LAFITE.EDITOR.SIZE '(470 . 300))
                     (LAFITE.EDITOR.LAYOUTS NIL)
                     (LAFITEFORWARDSUBJECTSTR NIL)
                     (LAFITESUPPORT NIL)
                     (LISPSUPPORT NIL)
                     (MESSAGESTR ">>Message<<")
                     (RECIPIENTSSTR ">>Recipients<<")
                     (SUBJECTSTR ">>Subject<<")
                     (LAFITE.SEND.FORMATTED '((NSCHARS :ASK)
                                              (CHARLOOKS :ASK)
                                              (PARALOOKS :ASK)
                                              (IMAGEOBJ :ASK]
        (COMS                                                (* ; "Obsolete")
              (INITVARS (LAFITEEDITORREGION NIL)))
        (COMS                                                (* ; "ICON stuff")
              (VARS LAFITE.MSG.ICON))
        (DECLARE%: EVAL@COMPILE DONTCOPY (RECORDS SENDINGCOMMAND)
               (GLOBALVARS \LAFITE.REPORT.MACHINE LAFITECURRENTEDITORWINDOWS LAFITEEDITORFONT 
                      LAFITEEDITORREGION LAFITEFORMATMENU LAFITEFORMSMENUITEMS LAFITEFORMATMENUITEMS
                      LAFITEFORWARDSTRINGS LAFITEFORWARDSUBJECTSTR LAFITESENDINGMENUITEMS 
                      LAFITESPECIALFORMS LAFITESUPPORT LISPSUPPORT MAKESYSDATE MESSAGESTR 
                      RECIPIENTSSTR SUBJECTSTR LAFITE.MSG.ICON LAFITEFORMDIRECTORIES 
                      LAFITE.SEND.FORMATTED)
               (FILES (SOURCE)
                      LAFITEDECLS)
               (LOCALVARS . T))))



(* ; "Sending mail")

(DEFINEQ

(DOLAFITESENDINGCOMMAND
  [LAMBDA (ITEM MENU KEY)                                (* bvm%: "31-Jul-84 15:03")

(* ;;; "this function is invoked by buttoning the menu on top of the 'sending' window")

    (PROG ((WINDOW (WINDOWPROP (WFROMMENU MENU)
                          'MAINWINDOW))
           PROC)
          (AND (SETQ PROC (WINDOWPROP WINDOW 'PROCESS))
               (PROCESS.APPLY PROC (FUNCTION \SENDMESSAGE.INITIATE)
                      (LIST WINDOW MENU ITEM])

(\SENDMESSAGE.INITIATE
  [LAMBDA (WINDOW MENU ITEM)                             (* ; "Edited 31-Jan-89 16:59 by bvm")

    (* ;; "Called by selecting a menu command from a message composition window")

    (ERSETQ (RESETLST
                (LET ((COMMAND (EXTRACTMENUCOMMAND ITEM)))
                     (RESETSAVE NIL (LIST [FUNCTION (LAMBDA (ITEM MENU)
                                                      (COND
                                                         (RESETSTATE 
                                                             (* ; 
                                           "In case of error/abort, set menu & proc back to normal")
                                                                (SHADEITEM ITEM MENU WHITESHADE)
                                                                (replace (MENU WHENSELECTEDFN)
                                                                   of MENU
                                                                   with (FUNCTION 
                                                                             DOLAFITESENDINGCOMMAND))
                                                                (PROCESSPROP (THIS.PROCESS)
                                                                       'BEFOREEXIT NIL]
                                          ITEM MENU))
                     (SHADEITEM ITEM MENU LAFITEITEMBUSYSHADE)
                                                             (* ; "Now disable the menu")
                     (replace (MENU WHENSELECTEDFN) of MENU with (FUNCTION NILL))
                     (PROCESSPROP (THIS.PROCESS)
                            'BEFOREEXIT
                            'DON'T)                          (* ; "Don't let anyone logout now!")
                     (CL:FUNCALL COMMAND WINDOW (WINDOWPROP WINDOW 'TEXTSTREAM)
                            MENU ITEM)))])

(\SENDMSG.DELIVER
  [LAMBDA (WINDOW TEXTSTREAM MENU ITEM)                  (* ; "Edited 31-Jan-89 16:41 by bvm")
    (LET (PARSE)
         (printout (GETPROMPTWINDOW WINDOW)
                T "Parsing...")
         (OR (SETQ PARSE (\SENDMESSAGE.PARSE TEXTSTREAM WINDOW))
             (ERROR!))
         (\SENDMSG.EXIT.TEDIT WINDOW TEXTSTREAM
                (create SENDINGCOMMAND
                       COMMAND _ '%##SEND##
                       ITEM _ ITEM
                       MENU _ MENU
                       MESSAGE _ TEXTSTREAM
                       MESSAGEPARSE _ PARSE])

(\SENDMSG.EXIT.TEDIT
  [LAMBDA (WINDOW TEXTSTREAM VALUE)                      (* ; "Edited 31-Jan-89 16:39 by bvm")
    (WINDOWADDPROP WINDOW 'CLOSEFN 'DON'T)                   (* ; 
                                                          "Keep TEDIT.QUIT from closing the window")
    (TEDIT.QUIT TEXTSTREAM VALUE)
    (LA.DETACH.TEDIT TEXTSTREAM])

(\SENDMSG.SAVE.FORM
  [LAMBDA (WINDOW TEXTSTREAM MENU ITEM)                  (* ; "Edited  3-Nov-89 15:33 by bvm")

    (* ;; "Shortcut to TEdit Put that saves on mail directory and remembers it as a %"Saved Form%"")

    (LET ((*UPPER-CASE-FILE-NAMES* NIL)
          (PROMPT "Save under name: ")
          (FORMNAME (WINDOWPROP WINDOW 'LAFITEFORM))
          PWINDOW FORMFILE)
         [COND
            (FORMNAME (SETQ FORMNAME (LA.SHORTFILENAME FORMNAME LAFITEFORM.EXT]
         (SETQ PWINDOW (LA.ASSURE.PROMPT.WINDOW WINDOW PROMPT (OR FORMNAME "XXX")))
                                                             (* ; "Kludge to keep it small")
         (CLEARW PWINDOW)
         [COND
            ((SETQ FORMFILE (PROMPTFORFILENAME PWINDOW FORMNAME PROMPT))
             (SETQ FORMNAME (LA.SHORTFILENAME (TEDIT.PUT TEXTSTREAM (LA.LONGFILENAME FORMFILE 
                                                                           LAFITEFORM.EXT)
                                                     NIL
                                                     (if (EQ (TEDIT.FORMATTEDFILEP TEXTSTREAM)
                                                                 'NSCHARS)
                                                         then 
                                                             (* ; 
                      "Force no formatting--TEdit defaultly saves formatting even if only ns chars")
                                                               T))
                                   LAFITEFORM.EXT))
             (WINDOWPROP WINDOW 'LAFITEFORM FORMNAME)
             (COND
                ((NOT (CL:MEMBER FORMNAME LAFITEFORMFILES :TEST 'STRING-EQUAL))
                 (SETQ LAFITEFORMFILES (APPEND LAFITEFORMFILES (LIST FORMNAME)))
                 (SETQ \LAFITEPROFILECHANGED T)
                 (SETQ LAFITEFORMSMENU]

         (* ;; "Exit with error to restore window state (what a kludge)")

         (ERROR!])

(\LAFITE.HEADER.EOF
  [LAMBDA (TEXTSTREAM)                                   (* ; "Edited  3-Nov-89 14:29 by bvm")

    (* ;; "Return the character number in TEXTSTREAM of the blank line following the header")

    (ADD1 (CADAR (LAFITE.PARSE.HEADER TEXTSTREAM NIL 0 NIL NIL T])

(\LAFITE.INSERT.REPLYTO
  [LAMBDA (TEXTSTREAM NAME HIGHLIGHT HEADEREOF)          (* ; "Edited  3-Nov-89 12:57 by bvm")

    (* ;; "Insert a %"Reply-to: name%" field in this message.  If HIGHLIGHT, leave the name pending-delete selected for potential replacement.")

    [TEDIT.INSERT TEXTSTREAM (CONCAT "Reply-to: " NAME LAFITEEOL)
           (OR HEADEREOF (SETQ HEADEREOF (\LAFITE.HEADER.EOF TEXTSTREAM]
    (if HIGHLIGHT
        then (TEDIT.SETSEL TEXTSTREAM (+ HEADEREOF (CONSTANT (NCHARS "Reply-to: ")))
                        (NCHARS NAME)
                        'RIGHT T])

(\SENDMSG.REPLYTO
  [LAMBDA (WINDOW TEXTSTREAM MENU ITEM)                  (* ; "Edited  3-Nov-89 14:03 by bvm")

    (* ;; "Add a Reply-to field to the message")

    (\LAFITE.INSERT.REPLYTO TEXTSTREAM [fetch (LAFITEMODEDATA FULLUSERNAME)
                                              of (\LAFITE.GET.USER.DATA (TEXTPROP TEXTSTREAM
                                                                                   'LAFITEMODE]
           T)

    (* ;; "Exit with error to restore window state (what a kludge)")

    (ERROR!])

(\SENDMSG.CHANGE.MODE
  [LAMBDA (WINDOW TEXTSTREAM MENU ITEM)                  (* ; "Edited  5-Jan-90 18:06 by bvm")
    (LET*
     [(OLDMODE (TEXTPROP TEXTSTREAM 'LAFITEMODE))
      (OTHERMODES (for MODE in LAFITEMODELST unless (OR (EQ (fetch (LAFITEOPS 
                                                                                          LAFITEMODE)
                                                                           of MODE)
                                                                        OLDMODE)
                                                                    (NLISTP (CDR MODE)))
                     collect (fetch (LAFITEOPS LAFITEMODE) of MODE)))
      (NEWMODE (if (NULL OTHERMODES)
                   then (\SENDMESSAGE.PROMPT WINDOW "There are no other modes")
                 elseif (CDR OTHERMODES)
                   then (MENU (\LAFITE.CREATE.MENU OTHERMODES "New mode"))
                 else (CAR OTHERMODES]
     [if NEWMODE
         then
         (LET* ((TITLE (WINDOWPROP WINDOW 'TITLE))
                (OLDMODEDATA (\LAFITE.GET.USER.DATA OLDMODE))
                (NEWMODEDATA (\LAFITE.GET.USER.DATA NEWMODE))
                N N2)
               (if (NULL NEWMODEDATA)
                   then (\SENDMESSAGE.PROMPT WINDOW (CL:FORMAT NIL 
                                                                 "Can't authenticate user in ~A mode"
                                                                   NEWMODE))
                 else (LET ((OLDNAME (fetch (LAFITEMODEDATA FULLUSERNAME) of OLDMODEDATA)
                                       )
                                (END (TEDIT.FIND TEXTSTREAM "

" 1))
                                START N LEN NEW OLDSEL)
                               (if END
                                   then (add END 1)) (* ; 
                                   "Don't search past end of header.  END now points at second cr.")
                               [for FIELD in '("cc" "Reply-to")
                                  when [AND (SETQ N (\SENDMSG.FIND.FIELD TEXTSTREAM FIELD END
                                                               ))
                                                (PROGN (SETQ LEN (CADR N))
                                                       (SETQ N (CAR N))
                                                       (SETQ START
                                                        (STRPOS OLDNAME
                                                               (SETQ OLDSEL
                                                                (TEDIT.SEL.AS.STRING TEXTSTREAM
                                                                       (create SELECTION
                                                                              CH# _ N
                                                                              DCH _ LEN)))
                                                               NIL NIL NIL NIL UPPERCASEARRAY]
                                  do                     (* ; "Change field containing old user name to new.  This is much more complicated than it needs to be because TEDIT.FIND is case sensitive.")
                                        (TEDIT.DELETE TEXTSTREAM N LEN)
                                        (TEDIT.INSERT TEXTSTREAM
                                               (SETQ NEW (CONCAT (OR (SUBSTRING OLDSEL 1 (SUB1 START)
                                                                            )
                                                                     "")
                                                                (fetch (LAFITEMODEDATA 
                                                                                  FULLUSERNAME)
                                                                   of NEWMODEDATA)
                                                                (OR (SUBSTRING OLDSEL
                                                                           (+ START (NCHARS OLDNAME))
                                                                           )
                                                                    "")))
                                               N)
                                        (AND END (add END (- (NCHARS NEW)
                                                                 LEN]
                               (if (SETQ N (\SENDMSG.FIND.FIELD TEXTSTREAM "To" END))
                                   then                  (* ; 
                                             "Leave the To field selected for address modification")
                                         (TEDIT.SETSEL TEXTSTREAM (CAR N)
                                                (CADR N)
                                                'RIGHT T))
                               (TEXTPROP TEXTSTREAM 'LAFITEMODE NEWMODE)
                               (if (SETQ N (STRPOS (CONCAT "(" OLDMODE ")")
                                                      TITLE))
                                   then (WINDOWPROP WINDOW 'TITLE (CONCAT (SUBSTRING TITLE 1 N)
                                                                             NEWMODE ")")))
                               (\SENDMESSAGE.PROMPT WINDOW "Message mode is now " NEWMODE]

     (* ;; "Exit with error so that the window is restored to previous state")

     (ERROR!])

(\SENDMSG.FIND.FIELD
  [LAMBDA (TEXTSTREAM FIELD END)                         (* ; "Edited  5-Jan-90 17:54 by bvm")

    (* ;; "Find and select the header field beginning with %"FIELD:%".  Return starting index.")

    (LET* ((STR (CONCAT "
" FIELD ": "))
           (N (TEDIT.FIND TEXTSTREAM STR 1 END))
           N2)
          (if (AND N (SETQ N2 (TEDIT.FIND TEXTSTREAM "
" (add N (NCHARS STR))
                                         END)))
              then (LIST N (- N2 N])

(\SENDMESSAGE.PARSE
  [LAMBDA (MSG EDITORWINDOW)                             (* ; "Edited 10-Aug-89 17:25 by bvm")

    (* ;; "Parse MSG in the current mode, returning a parse structure that the corresponding sender will be happy with")

    (LET* ((MODE (TEXTPROP MSG 'LAFITEMODE))
           (*LAFITE-MODE-DATA* (\LAFITE.GET.USER.DATA MODE)))
          (if *LAFITE-MODE-DATA*
              then (CL:FUNCALL (fetch (LAFITEMODEDATA SENDPARSER) of *LAFITE-MODE-DATA*)
                              MSG EDITORWINDOW)
            else (\SENDMESSAGE.PROMPT EDITORWINDOW (CL:FORMAT NIL 
                                                                 "Can't authenticate user in ~A mode"
                                                                  MODE])

(\LAFITE.PREPARE.SEND
  [LAMBDA (MSG EDITORWINDOW PARSETABLE)                  (* bvm%: "13-Nov-84 12:50")

    (* ;; "Does generic things to MSG, a textstream about to be sent as a message: makes sure it ends in a CR, has no leading CRs, and parses it according to PARSETABLE which defaults to \LAPARSE.FULL -- returns a parse, whose first element tries to be (EOF end-of-header-position)")

    (PROG (MSGEOF HEADEREOF MSGFIELDS EOFINFO)
          [COND
             ((NOT (TYPENAMEP MSG 'STREAM))
              (RETURN (LISPERROR "ILLEGAL ARG" MSG]
          [COND
             (EDITORWINDOW                                   (* ; 
                                                   "Scroll so that beginning of message is visible")
                    (TEDIT.SETSEL MSG 1 0 'LEFT)
                    (TEDIT.NORMALIZECARET MSG)
                    (first (SETFILEPTR MSG 0) until (NEQ (BIN MSG)
                                                                 (CHARCODE EOL))
                       do                                (* ; "hack to get rid of leading CRs")
                             (TEDIT.DELETE MSG 1 1))
                    [SETFILEPTR MSG (SUB1 (SETQ MSGEOF (GETEOFPTR MSG]
                    (COND
                       ((NEQ (BIN MSG)
                             (CHARCODE EOL))                 (* ; "Make sure message ends in eol")
                        (TEDIT.INSERT MSG LAFITEEOL (ADD1 MSGEOF)
                               NIL T]
          (SETFILEINFO MSG 'ENDOFSTREAMOP (FUNCTION \LAFITE.EOF))
                                                             (* ; 
                                                     "Avoid parsing failure if header-only message")
          (SETQ MSGFIELDS (LAFITE.PARSE.HEADER MSG (OR PARSETABLE \LAPARSE.FULL)
                                 0
                                 (SETQ MSGEOF (GETEOFPTR MSG))
                                 NIL T))
          (COND
             ((EQ (CAR (SETQ EOFINFO (CAR MSGFIELDS)))
                  'EOF)
              (SETQ HEADEREOF (CADR EOFINFO))
              [COND
                 ((CADDR EOFINFO)                            (* ; "Error")
                  (RETURN (\LAFITE.PREPARE.ERROR MSG EDITORWINDOW HEADEREOF]
              [COND
                 ((= HEADEREOF MSGEOF)                       (* ; 
                          "Parse ended at eof, so message does not end in double CR -- add another")
                  (SETFILEPTR MSG MSGEOF)
                  (BOUT MSG (CHARCODE CR]
              (RPLACA (CDR EOFINFO)
                     (SETQ HEADEREOF (ADD1 HEADEREOF)))      (* ; 
                                                     "Add one for tedit fileptr one-based nonsense")
              ))
          (RETURN MSGFIELDS])

(\LAFITE.PREPARE.ERROR
  [LAMBDA (MSG EDITORWINDOW HEADEREOF)                   (* bvm%: "13-Nov-84 12:53")

(* ;;; "Called when header of MSG contained a line not conforming to spec.  Most likely cause is user deleted the blank line between header and message.  Print a suitable error message")

    (PROG (LINE)
          (SETFILEPTR MSG HEADEREOF)
          (SETQ LINE (LAFITE.READ.TO.EOL MSG))
          (SETFILEPTR MSG HEADEREOF)
          (BOUT MSG (CHARCODE CR))
          (\SENDMESSAGEFAIL EDITORWINDOW (CONCAT "Header not understood: %""
                                                    (COND
                                                       ((> (NCHARS LINE)
                                                           30)
                                                        (CONCAT (SUBSTRING LINE 1 30)
                                                               '|...|))
                                                       (T LINE)))
                 "%".   Assumed this was not part of header, and inserted blank line before it.  If this is correct, press 'Deliver' again, else edit the message appropriately."
                 ])

(\LAFITE.CHOOSE.MSG.FORMAT
  [LAMBDA (TEXTSTREAM HEADEREOF EDITORWINDOW)            (* ; "Edited  3-Feb-89 18:36 by bvm")

    (* ;; "Ask if user intends to retain formatting info, and if so, send formatted")

    (LET ((FORMATTING (TEDIT.FORMATTEDFILEP TEXTSTREAM))
          TMP)
         (COND
            ((NULL FORMATTING)                               (* ; "It's just plain text")
             'TEXT)
            [(AND (TEXTSTREAMP TEXTSTREAM)
                  (TEXTPROP TEXTSTREAM 'LAFITEFORMAT]
            ((NULL EDITORWINDOW)                             (* ; "Nobody to interact with")
             'TEDIT)
            (T (SELECTQ (COND
                           ((NLISTP LAFITE.SEND.FORMATTED)
                            LAFITE.SEND.FORMATTED)
                           ((SETQ TMP (ASSOC FORMATTING LAFITE.SEND.FORMATTED))
                            (CADR TMP))
                           (T :ASK))
                   (T                                        (* ; "Send formatted")
                      'TEDIT)
                   (NIL                                      (* ; "Send unformatted")
                        'TEXT)
                   (SELECTQ (SETQ TMP (\SENDMESSAGE.MENUPROMPT EDITORWINDOW
                                             (OR LAFITEFORMATMENU (SETQ LAFITEFORMATMENU
                                                                   (\LAFITE.CREATE.MENU 
                                                                          LAFITEFORMATMENUITEMS 
                                                                     "Retain formatting information?"
                                                                          T)))
                                             (CONCAT "Message " (SELECTQ FORMATTING
                                                                    (CHARLOOKS "has font information")
                                                                    (PARALOOKS 
                                                                           "has paragraph formatting")
                                                                    (NSCHARS 
                                                                        "uses extended character set")
                                                                    (IMAGEOBJ "contains images")
                                                                    "has unknown formatting")
                                                    ".")
                                             'LAFITEFORMATMENU))
                       (ABORT NIL)
                       TMP])

(LAFITE.MAKE.PLAIN.TEXTSTREAM
  [LAMBDA (TEXTSTREAM START)                             (* ; "Edited 24-Sep-87 16:48 by bvm:")

    (* ;; "Coerces TEXTSTREAM to a %"plain text%" stream, returning the new stream.  If START is specified, only copies from that file pointer onward.")

    (LET [(PLAIN (OPENSTREAM '{NODIRCORE} 'BOTH]
         (SETFILEPTR TEXTSTREAM (OR START (SETQ START 0)))

         (* ;; "TEXT streams return character codes on BIN, so we have to translate to bytes on output side to handle fat chars correctly and avoid image objects")

         [to (- (GETEOFPTR TEXTSTREAM)
                    START) do (\OUTCHAR PLAIN (OR (FIXP (BIN TEXTSTREAM))
                                                      (CHARCODE *]
                                                             (* ; "Reopen to avoid core bug")
         (OPENSTREAM (CLOSEF PLAIN)
                'INPUT])

(\SENDMESSAGE.MENUPROMPT
  [LAMBDA (EDITWINDOW MENU PROMPT MENUVAR)               (* ; "Edited 20-Apr-89 19:37 by bvm")

    (* ;; "Prompt with MENU at the upper left corner of EDITWINDOW, printing PROMPT in the prompt window.  If MENUVAR is specified, it is the global variable that holds this menu, which we smash to NIL while inside MENU, lest someone else try to use it")

    (LET ((PWINDOW (GETPROMPTWINDOW EDITWINDOW))
          RESULT)
         (CLEARW PWINDOW)
         (printout PWINDOW PROMPT)
         (if MENUVAR
             then (SET MENUVAR NIL))
         (SETQ RESULT (MENU MENU (LA.POSITION.FROM.REGION (WINDOWPROP PWINDOW 'REGION)
                                        NIL T)
                            T))
         (CLEARW PWINDOW)
         (if MENUVAR
             then (SET MENUVAR MENU))
         RESULT])

(\SENDMESSAGE.PROMPT
  [LAMBDA (EDITORWINDOW MESS1 MESS2)                     (* ; "Edited 31-Jan-89 17:03 by bvm")

    (* ;; 
"Display message MESS1 & optionally MESS2 in the prompt window of EDITORWINDOW.  Returns NIL always")

    (LET [(PWINDOW (COND
                      (EDITORWINDOW (LA.ASSURE.PROMPT.WINDOW EDITORWINDOW MESS1 MESS2))
                      (T PROMPTWINDOW]
         (CLEARW PWINDOW)
         (PRIN3 MESS1 PWINDOW)
         (COND
            (MESS2 (PRIN3 MESS2 PWINDOW)))
         NIL])

(\SENDMESSAGEFAIL
  [LAMBDA (EDITORWINDOW MESS1 MESS2)                     (* ; "Edited 31-Jan-89 17:02 by bvm")
    (\SENDMESSAGE.PROMPT EDITORWINDOW MESS1 MESS2)
    (RETFROM '\SENDMESSAGE.PARSE])
)
(DEFINEQ

(\SENDMESSAGE
  [LAMBDA (FORM TEDITPROPS FORMNAME)                         (* ; "Edited  7-Feb-2022 11:54 by rmk")
                                                             (* ; "Edited 10-Feb-89 12:22 by bvm")

(* ;;; "FORM can be a string, file, or stream --- The value of \SENDMESSAGE is T only if the message was actually sent")

    (OR (TEXTSTREAMP FORM)
        (SETQ FORM (OPENTEXTSTREAM (OPENSTRINGSTREAM FORM)
                          NIL NIL NIL TEDITPROPS)))
    (TEDIT.STREAMCHANGEDP FORM T)                            (* ; "Clear the changed bit")
    (if (NOT (LISTGET TEDITPROPS 'LEAVETTY))
        then                                                 (* ; "Take control of the keyboard")
             (TTY.PROCESS (THIS.PROCESS)))
    (PROG [(MODE (LISTGET TEDITPROPS 'LAFITEMODE]            (* ; "Old way of specifying mode")
          (if MODE
              then (TEXTPROP FORM 'LAFITEMODE MODE)
            elseif (TEXTPROP FORM 'LAFITEMODE)
            elseif (SETQ MODE (fetch LAFITEMODE of \LAFITEMODE))
              then (TEXTPROP FORM 'LAFITEMODE MODE)
            else (PRINTOUT PROMPTWINDOW T "Can't send mail without a Lafite mode.")
                 (RETURN NIL))
          (RETURN (\SENDMESSAGE.RESTARTABLE FORM TEDITPROPS NIL FORMNAME])

(\SENDMESSAGE.RESTARTABLE
  [LAMBDA (FORM TEDITPROPS EDITORWINDOW FORMNAME)            (* ; "Edited  7-Feb-2022 11:50 by rmk")
                                                             (* ; "Edited  3-Nov-89 15:06 by bvm")
    (bind (CURRENTMESSAGE _ FORM)
          (FIRSTTIME _ T)
          EDITORRESULT DONE SENTOK PARSE
       do (PROCESSPROP (THIS.PROCESS)
                 'BEFOREEXIT NIL)                            (* ; 
                   "Allow LOGOUT until delivery is attempted.  Need to do this if we loop or restart")
          (COND
             ([NULL (PROG1 EDITORWINDOW
                        [SETQ EDITORWINDOW (\SENDMESSAGE.MAKEWINDOW CURRENTMESSAGE NIL EDITORWINDOW
                                                  (TEXTPROP FORM 'LAFITEMODE])]
                                                             (* ; 
                                         "First time thru.  Fix it so that we can restart if aborted")
              (PROCESSPROP (THIS.PROCESS)
                     'RESTARTFORM
                     (LIST (FUNCTION \SENDMESSAGE.RESTARTABLE)
                           (KWOTE FORM)
                           (KWOTE TEDITPROPS)
                           (KWOTE EDITORWINDOW)))            (* ; 
                                           "If process is reset or aborted, this is how to resurrect")
              (PROCESSPROP (THIS.PROCESS)
                     'RESTARTABLE T)
              (WINDOWPROP EDITORWINDOW 'LAFITEFORM FORMNAME)))
          (COND
             (FIRSTTIME (RESETSAVE NIL (LIST (FUNCTION \SENDMESSAGE.CLEANUP)
                                             EDITORWINDOW))
                    (push LAFITECURRENTEDITORWINDOWS EDITORWINDOW)
                    (SETQ FIRSTTIME)))
          [SETQ EDITORRESULT (TEDIT (OPENSTRINGSTREAM FORM)
                                    EDITORWINDOW T (APPEND TEDITPROPS (LIST 'FONT LAFITEEDITORFONT]
          (COND
             ((TTY.PROCESSP)                                 (* ; "give back the keyboard")
              (TTY.PROCESS T)))
          (WINDOWDELPROP EDITORWINDOW 'CLOSEFN 'DON'T)       (* ; "let the window close")
          (COND
             ((NOT (type? SENDINGCOMMAND EDITORRESULT))      (* ; 
 "get out anyway since the user used the TEDIT `quit' command instead of one of the sending commands")
              (SETQ DONE T))
             (T                                              (* ; 
     "the user used the lafite menu to get out rather than the TEDIT menu so we have to do something")
                                                             (* ; 
                                                        "make sure CURRENTMESSAGE is always a string")
                (SETQ CURRENTMESSAGE (fetch (SENDINGCOMMAND MESSAGE) of EDITORRESULT))
                (SETQ DONE (SELECTQ (AND EDITORRESULT (fetch (SENDINGCOMMAND COMMAND) of EDITORRESULT
                                                             ))
                               (%##SEND## [SETQ SENTOK (\SENDMESSAGE0 CURRENTMESSAGE EDITORWINDOW
                                                              (SETQ PARSE (fetch (SENDINGCOMMAND
                                                                                  MESSAGEPARSE)
                                                                             of EDITORRESULT])
                               (SHOULDNT)))
                (SHADEITEM (fetch (SENDINGCOMMAND ITEM) of EDITORRESULT)
                       (fetch (SENDINGCOMMAND MENU) of EDITORRESULT)
                       WHITESHADE)                           (* ; 
                                   "Unshade command.  DOLAFITESENDINGCOMMAND shaded it to begin with")
                ))
          (COND
             (DONE                                           (* ; "Message successfully dispatched")
                   (PROCESSPROP (THIS.PROCESS)
                          'RESTARTABLE NIL)                  (* ; 
                                              "Don't try to restart if there's any sort of error now")
                   (COND
                      (CURRENTMESSAGE                        (* ; 
                                                "Mark text unchanged now, so no trouble closing icon")
                             (TEDIT.STREAMCHANGEDP CURRENTMESSAGE T)))
                   (COND
                      ((NULL SENTOK)
                       (CLOSEW EDITORWINDOW))
                      (T                                     (* ; "shrink the window")
                         (\LAFITE.AFTER.DELIVER EDITORWINDOW CURRENTMESSAGE PARSE)))
                   (RETURN SENTOK))
             (T                                              (* ; 
                                           "Loop if deliver failed or \LAFITE.SAVE.FORM was aborted.")
                ])

(\SENDMESSAGE.CLEANUP
  [LAMBDA (EDITORWINDOW)                                 (* ; "Edited  6-Oct-87 15:58 by bvm:")
    (SETQ LAFITECURRENTEDITORWINDOWS (REMOVE EDITORWINDOW LAFITECURRENTEDITORWINDOWS])

(\SENDMESSAGE.MAKEWINDOW
  [LAMBDA (MESSAGEFORM TITLE WINDOW MODE)                (* ; "Edited  3-Nov-89 16:16 by bvm")

(* ;;; "Editor for Mail system Lafite -- Handles the process mechanism right")

(* ;;; "Assumes that it's running in a separate process created above")

    (PROG ((MENU (MAKELAFITEDELIVERMENU))
           EDITWINDOW LAYOUT REGION)
          [COND
             ((NOT TITLE)
              (SETQ TITLE "Message Editor")
              (if (AND MODE (LAFITE.SHOW.MODE.P))
                  then (SETQ TITLE (CONCAT TITLE " (" MODE ")"]
          [COND
             ((WINDOWP (SETQ EDITWINDOW WINDOW))
              (WINDOWPROP EDITWINDOW 'TITLE TITLE)
              (for W in (ATTACHEDWINDOWS EDITWINDOW) when (WINDOWPROP W 'MENUWINDOW)
                 do                                      (* ; 
                "there's already an attached window menu, make sure we have a delivery menu in it.")
                       (LET [(OLDMENU (CAR (WINDOWPROP W 'MENU]
                            (if (if (NULL OLDMENU)
                                        then             (* ; "E.g., after ABORT got removed")
                                              T
                                      elseif (NOT (EQUAL (fetch (MENU ITEMS) of MENU)
                                                             (fetch (MENU ITEMS) of OLDMENU))
                                                      )
                                        then (DELETEMENU OLDMENU NIL W) 
                                                             (* ; "Get rid of different menu")
                                              T
                                      else (SETQ MENU OLDMENU) 
                                                             (* ; "They're the same, don't fuss")
                                            NIL)
                              else (ADDMENU MENU W '(0 . 0)) 
                                                             (* ; "Now make it fit")
                                    (MENUWRESHAPEFN W)))
                       (RETURN) finally                  (* ; "No attached menu yet")
                                      (ATTACHWINDOW (SETQ W (MENUWINDOW MENU))
                                             EDITWINDOW
                                             'TOP)
                                      (WINDOWPROP W 'MENUWINDOW T)))
             (T (SETQ REGION (if (for old LAYOUT in LAFITE.EDITOR.LAYOUTS
                                        unless (for WINDOW in LAFITECURRENTEDITORWINDOWS
                                                      thereis (EQ (WINDOWPROP WINDOW 
                                                                             'LAFITE.LAYOUT)
                                                                      LAYOUT))
                                        do               (* ; 
                                                           "Use first layout not already in use")
                                              (RETURN (CAR LAYOUT)))
                               elseif (AND (NULL LAFITECURRENTEDITORWINDOWS)
                                               (type? REGION LAFITEEDITORREGION))
                                 then                    (* ; 
                                                        "Old way of doing this for a single window")
                                       LAFITEEDITORREGION
                               elseif LAFITE.EDITOR.SIZE
                                 then                    (* ; "Get window of appropriate size")
                                       (GETBOXREGION (CAR LAFITE.EDITOR.SIZE)
                                              (CDR LAFITE.EDITOR.SIZE))
                               else (GETREGION)))
                [SETQ EDITWINDOW (CREATEMENUEDWINDOW MENU TITLE 'TOP
                                        (create REGION using
                                                           REGION HEIGHT _
                                                           (- (fetch (REGION HEIGHT) of
                                                                                         REGION)
                                                              (HEIGHTIFWINDOW (FONTPROP 
                                                                                     LAFITEEDITORFONT
                                                                                     'HEIGHT]
                (WINDOWPROP (CAR (ATTACHEDWINDOWS EDITWINDOW))
                       'MENUWINDOW T)
                (if LAYOUT
                    then (WINDOWPROP EDITWINDOW 'LAFITE.LAYOUT LAYOUT)
                          (WINDOWPROP EDITWINDOW 'ICONPOSITION (CADR LAYOUT]
          (GETPROMPTWINDOW EDITWINDOW 1 LAFITEEDITORFONT)
          [COND
             (NIL                                            (* ; 
                                                           "don't let TEDIT close the window")
                  (WINDOWADDPROP EDITWINDOW 'CLOSEFN 'DON'T]
          (PROGN (WINDOWDELPROP EDITWINDOW 'CLOSEFN (FUNCTION CLOSEATTACHEDWINDOWS))
                                                             (* ; 
                                        "On closing, get rid of attachments, don't just close them")
                 (WINDOWADDPROP EDITWINDOW 'CLOSEFN (FUNCTION DETACHALLWINDOWS))
                 (WINDOWADDPROP EDITWINDOW 'CLOSEFN (FUNCTION \LAFITE.CLOSEMSG?)
                        T))
          (WINDOWPROP EDITWINDOW 'ICONFN (FUNCTION \LAFITE.UNSENT.ICON))
          (WINDOWPROP EDITWINDOW 'PROCESS (THIS.PROCESS))    (* ; 
                                                      "Associate this process with the edit window")
          (replace (MENU WHENSELECTEDFN) of MENU with (FUNCTION DOLAFITESENDINGCOMMAND))
                                                             (* ; "Enable the menu")
          (RETURN EDITWINDOW])

(MAKELAFITEDELIVERMENU
  [LAMBDA NIL                                            (* bvm%: "28-Mar-84 12:47")
    (create MENU
           ITEMS _ LAFITESENDINGMENUITEMS
           CENTERFLG _ T
           MENUFONT _ LAFITEMENUFONT
           WHENSELECTEDFN _ (FUNCTION DOLAFITESENDINGCOMMAND])

(\LAFITE.CLOSEMSG?
  [LAMBDA (WINDOW)                                       (* ; "Edited  3-Sep-87 17:21 by bvm:")

    (* ;; 
"This is the first CLOSEFN on a message sending window.  If contents have changed, get confirmation")

    (LET [(TEXTSTREAM (WINDOWPROP WINDOW 'TEXTSTREAM]
         (COND
            ((OR (NULL TEXTSTREAM)
                 (NOT (TEDIT.STREAMCHANGEDP TEXTSTREAM)))    (* ; 
                                             "TEXTSTREAM is null once TEdit's gotten thru with it.")
             NIL)
            ((MOUSECONFIRM "Message has been edited -- LEFT to flush anyway" T (GETPROMPTWINDOW
                                                                                WINDOW))
             (TEDIT.STREAMCHANGEDP TEXTSTREAM T)             (* ; 
                                            "Reset bit so question doesn't get asked a second time")
             NIL)
            (T 'DON'T])

(\LAFITE.AFTER.DELIVER
  [LAMBDA (EDITORWINDOW TEXTSTREAM PARSE)                (* ; "Edited 30-May-90 16:25 by bvm")
    (TEDIT.ASSURE.NO.BACKING.FILE TEXTSTREAM)                (* ; 
                                                           "In case the backing file gets deleted")
    (\OUTBOX.ADD.ITEM TEXTSTREAM (OR (CAR PARSE)
                                         UNSUPPLIEDFIELDSTR))
    [LET ((FORMNAME (WINDOWPROP EDITORWINDOW 'LAFITEFORM NIL)))
         (if (AND FORMNAME (EQ (CAR (UNPACKFILENAME.STRING FORMNAME))
                                   'NAME))
             then 

                   (* ;; "See if user wants to keep the form, or if it was saved just as a checkpoint.  Do this only for files saved in primary directory")

                   (LET* ((PWINDOW (GETPROMPTWINDOW EDITORWINDOW))
                          [MENUW (find W in (ATTACHEDWINDOWS EDITORWINDOW)
                                    suchthat (WINDOWPROP W 'MENUWINDOW]
                          (MENU (create MENU
                                       ITEMS _ '(("Delete File" T 
                                        "Delete the file(s) in which this message was earlier saved."
                                                        )
                                                 ("Retain Saved Form" NIL 
                                               "Don't delete the saved form, I want to use it again."
                                                        ))
                                       WHENSELECTEDFN _ [FUNCTION (LAMBDA (ITEM MENU KEY)
                                                                    (LET ((W (WFROMMENU MENU)))
                                                                         (WINDOWPROP W 'RESULT ITEM)
                                                                         (SHADEITEM ITEM MENU 
                                                                                LAFITEITEMBUSYSHADE]
                                       MENUFONT _ LAFITEMENUFONT
                                       CENTERFLG _ T
                                       ITEMWIDTH _ (IQUOTIENT (WINDOWPROP PWINDOW 'WIDTH)
                                                          2)
                                       MENUROWS _ 1))
                          RESULT
                          (MSG (CONCAT 
                         "Delivery complete.  Do you want to delete the saved form of this message ("
                                      FORMNAME ")?")))
                         (LA.ASSURE.PROMPT.WINDOW EDITORWINDOW MSG)
                         (TERPRI PWINDOW)
                         (PRIN3 MSG PWINDOW)
                         (ADDMENU MENU MENUW '(0 . 0))
                         (until (SETQ RESULT (WINDOWPROP MENUW 'RESULT))
                            do (BLOCK 500))
                         (if (CADR RESULT)
                             then (PRINTOUT PWINDOW T "Deleting file(s)... "
                                             (if (\LAFITE.DELETE.FORM.INTERNAL FORMNAME)
                                                 then "done."
                                               else "failed."]
    (DETACHALLWINDOWS EDITORWINDOW)
    (CLOSEW EDITORWINDOW])

(\LAFITE.UNSENT.ICON
  [LAMBDA (WINDOW OLDICON)                               (* ; "Edited 24-Sep-87 16:58 by bvm:")
    (TITLEDICONW LAFITE.MSG.ICON (\LAFITE.FETCH.SUBJECT (WINDOWPROP WINDOW 'TEXTSTREAM))
           LAFITEMSGICONFONT
           (WINDOWPROP WINDOW 'ICONPOSITION)
           T])

(\LAFITE.FETCH.SUBJECT
  [LAMBDA (TEXTSTREAM)                                   (* bvm%: " 2-Mar-86 16:27")
    (COND
       (TEXTSTREAM (RESETLST
                       [RESETSAVE NIL (LIST (FUNCTION SETFILEINFO)
                                            TEXTSTREAM
                                            'ENDOFSTREAMOP
                                            (GETFILEINFO TEXTSTREAM 'ENDOFSTREAMOP]
                       (SETFILEINFO TEXTSTREAM 'ENDOFSTREAMOP (FUNCTION \LAFITE.EOF))
                       (LET ((STR (LAFITE.PARSE.HEADER TEXTSTREAM \LAPARSE.SUBJECTFIELD 0 NIL T)))
                            (COND
                               ((STRING-EQUAL STR SUBJECTSTR)
                                UNSUPPLIEDFIELDSTR)
                               (T STR))))])

(LAFITE.SENDMESSAGE
  [LAMBDA (MESSAGEFORM)                                      (* ; "Edited  7-Feb-2022 11:55 by rmk")
                                                             (* ; "Edited 12-Sep-88 14:07 by bvm")

(* ;;; "this is the external interface to sending a message")

    (SETQ MESSAGEFORM (OPENTEXTSTREAM (OPENSTRINGSTREAM MESSAGEFORM)))
    (LET* ((MODE (TEXTPROP MESSAGEFORM 'LAFITEMODE))
           (*LAFITE-MODE-DATA* (\LAFITE.GET.USER.DATA MODE))
           PARSE)
          (AND *LAFITE-MODE-DATA* (SETQ PARSE (CL:FUNCALL (fetch (LAFITEMODEDATA SENDPARSER)
                                                             of *LAFITE-MODE-DATA*)
                                                     MESSAGEFORM))
               (CL:FUNCALL (fetch (LAFITEMODEDATA SENDER) of *LAFITE-MODE-DATA*)
                      MESSAGEFORM PARSE])

(\SENDMESSAGE0
  [LAMBDA (TEXTSTREAM WINDOW PARSE)                      (* ; "Edited 12-Sep-88 14:04 by bvm")
    (PROG ((PWINDOW (GETPROMPTWINDOW WINDOW))
           *LAFITE-MODE-DATA* MENUW OLDMENU ABORTMENU RESULT)
          (for W in (ATTACHEDWINDOWS WINDOW) when [SETQ OLDMENU
                                                               (CAR (WINDOWPROP W 'MENU]
             do (SETQ MENUW W)
                   (DELETEMENU OLDMENU NIL MENUW)            (* ; 
                                                           "Remove Deliver menu, add Abort menu")
                   (ADDMENU (SETQ ABORTMENU (create MENU
                                                   ITEMS _ '(("Abort" NIL 
                                                                    "Abort delivery of this message")
                                                             )
                                                   WHENSELECTEDFN _ (FUNCTION \SENDMESSAGE.ABORT)
                                                   MENUFONT _ LAFITEMENUFONT
                                                   CENTERFLG _ T
                                                   ITEMWIDTH _ (fetch ITEMWIDTH of OLDMENU)))
                          MENUW
                          '(0 . 0))
                   (RETURN))
          [if [NULL (SETQ *LAFITE-MODE-DATA* (\LAFITE.GET.USER.DATA (TEXTPROP TEXTSTREAM
                                                                               'LAFITEMODE]
              then (printout PWINDOW "Failed to authenticate user.")
            else [SETQ RESULT (ERSETQ (RESETLST
                                              (CL:FUNCALL (fetch (LAFITEMODEDATA SENDER)
                                                             of *LAFITE-MODE-DATA*)
                                                     TEXTSTREAM PARSE WINDOW MENUW))]
                  (COND
                     ((NULL RESULT)
                      (printout PWINDOW "aborted."))
                     ((SETQ RESULT (CAR RESULT))
                      (printout PWINDOW "done."]
          (RETURN (COND
                     (RESULT                                 (* ; "Success")
                            (CLOSEF TEXTSTREAM)              (* ; 
"Explicit Close here after successful delivery so that TEdit can close any files it might have open")
                            RESULT)
                     (T                                      (* ; "Restore Deliver menu")
                        (COND
                           ((WINDOWPROP MENUW 'MENU)
                            (DELETEMENU ABORTMENU NIL MENUW)))
                        (ADDMENU OLDMENU MENUW '(0 . 0)
                               NIL)
                        (WINDOWPROP MENUW 'ABORT NIL)
                        NIL])

(LA.ASSURE.PROMPT.WINDOW
  [LAMBDA (MAINWINDOW MESS1 MESS2)                       (* bvm%: "24-Feb-85 18:33")

(* ;;; 
"Returns prompt window for MAINWINDOW assuring that it is big enough to print MESS1 and MESS2")

    (LET ((PWINDOW (GETPROMPTWINDOW MAINWINDOW))
          %#LINES)
         (COND
            ((> [SETQ %#LINES (QUOTIENT (+ (STRINGWIDTH MESS1 PWINDOW)
                                           (COND
                                              (MESS2 (STRINGWIDTH MESS2 PWINDOW))
                                              (T 0)))
                                     (WINDOWPROP PWINDOW 'WIDTH]
                0)                                           (* ; 
                                                           "Make sure prompt window is big enough")
             (GETPROMPTWINDOW MAINWINDOW (ADD1 %#LINES)))
            (T PWINDOW])

(\LAFITE.SEND.FAIL
  [LAMBDA (EDITORWINDOW ERRMSG)                          (* bvm%: "24-Feb-85 18:38")

    (* ;; "Print a message explaining why delivery failed")

    (LET ((FULLMSG (CONCAT "Delivery failed -- " ERRMSG))
          PWINDOW)
         [COND
            [EDITORWINDOW (CLEARW (SETQ PWINDOW (LA.ASSURE.PROMPT.WINDOW EDITORWINDOW FULLMSG]
            (T (TERPRI (SETQ PWINDOW PROMPTWINDOW]
         (PRIN3 FULLMSG PWINDOW)
         NIL])

(\LAFITE.INVALID.RECIPIENTS
  [LAMBDA (NAMES)                                        (* bvm%: " 5-Nov-84 15:26")

(* ;;; "Returns an 'invalid recipients' error string")

    (PROG (NAME)
          (SETQ NAME (for RECIPIENT in NAMES join (LIST ", " RECIPIENT)))
          (RPLACA NAME ": ")
          (COND
             ((CDR NAMES)
              (push NAME "s")))
          (RETURN (CONCATLIST (CONS "Invalid recipient" NAME])

(\SENDMESSAGE.ABORT
  [LAMBDA (ITEM MENU KEY)                                (* bvm%: " 1-Jun-84 12:21")
                                                             (* ; 
                                                           "The WHENSELECTEDFN for the Abort menu")
    (PROG ((W (WFROMMENU MENU)))
          (WINDOWPROP W 'ABORT T)
          (SHADEITEM ITEM MENU LAFITEITEMBUSYSHADE])
)



(* ; "Outbox hacking")

(DEFINEQ

(\OUTBOX.CREATE
  [LAMBDA NIL                                            (* bvm%: "21-Dec-84 22:35")
    (PROG (FONT NLINES W FONTHEIGHT)
          (OR (AND LAFITESTATUSWINDOW (FIXP (SETQ NLINES LAFITEOUTBOXSIZE))
                   (IGREATERP NLINES 0))
              (RETURN))
          (SETQ FONTHEIGHT (FONTPROP (SETQ FONT LAFITEBROWSERFONT)
                                  'HEIGHT))
          (SETQ W (CREATEW (CREATEREGION 0 0 (WINDOWPROP LAFITESTATUSWINDOW 'WIDTH)
                                  (HEIGHTIFWINDOW (ITIMES NLINES FONTHEIGHT)
                                         T))
                         "Delivered Messages" NIL T))
          (ATTACHWINDOW W LAFITESTATUSWINDOW 'BOTTOM 'JUSTIFY 'LOCALCLOSE)
          (DSPFONT FONT W)
          (WINDOWADDPROP W 'CLOSEFN (FUNCTION \OUTBOX.CLOSEFN))
          (WINDOWPROP W 'REPAINTFN (FUNCTION \OUTBOX.REPAINTFN))
          (WINDOWPROP W 'BUTTONEVENTFN (FUNCTION \OUTBOX.BUTTONFN))
          (WINDOWPROP W 'RESHAPEFN (FUNCTION \OUTBOX.RESHAPEFN))
          (WINDOWPROP W 'MINSIZE (CONS 0 (HEIGHTIFWINDOW FONTHEIGHT T)))
          (RETURN (SETQ \LAFITE.OUTBOX
                   (\OUTBOX.RESET (create OUTBOX
                                             OBWINDOW _ W
                                             OBSIZE _ NLINES
                                             OBHEIGHT _ FONTHEIGHT
                                             OBDESCENT _ (FONTPROP FONT 'DESCENT])

(\OUTBOX.RESET
  [LAMBDA (OUTBOX)                                       (* bvm%: " 9-Nov-84 16:29")
    (PROG ((WINDOW (fetch OBWINDOW of OUTBOX)))
          (CLEARW WINDOW)
          (LINELENGTH MAX.SMALLP WINDOW)
          (DSPRIGHTMARGIN MAX.SMALLP WINDOW)
          (replace OBORIGIN of OUTBOX with (IPLUS (DSPYPOSITION NIL WINDOW)
                                                              (fetch OBHEIGHT of OUTBOX)))
          (RETURN OUTBOX])

(\OUTBOX.CLOSEFN
  [LAMBDA (WINDOW)                                       (* bvm%: " 8-Nov-84 16:02")
    (SETQ \LAFITE.OUTBOX])

(\OUTBOX.REPAINTFN
  [LAMBDA (WINDOW REGION)                                (* bvm%: "13-Nov-84 10:57")
    (PROG ((OUTBOX \LAFITE.OUTBOX))
          (OR (EQ WINDOW (fetch OBWINDOW of OUTBOX))
              (RETURN))
          (MOVETO 0 (IDIFFERENCE (fetch OBORIGIN of OUTBOX)
                           (fetch OBHEIGHT of OUTBOX))
                 WINDOW)
          (for ITEM in (fetch OBITEMS of OUTBOX) do (\OUTBOX.DISPLAYLINE
                                                                         OUTBOX ITEM)
                                                                       (TERPRI WINDOW])

(\OUTBOX.RESHAPEFN
  [LAMBDA (WINDOW OLDIMAGE IMAGEREGION OLDSCREENREGION)  (* bvm%: "13-Nov-84 10:57")
    (COND
       ((EQ WINDOW (fetch OBWINDOW of \LAFITE.OUTBOX))
        (PROG ((NLINES (IQUOTIENT (WINDOWPROP WINDOW 'HEIGHT)
                              (fetch OBHEIGHT of \LAFITE.OUTBOX)))
               (OLDSIZE (fetch OBSIZE of \LAFITE.OUTBOX))
               N ITEMS)
              [COND
                 ((NEQ NLINES OLDSIZE)
                  (replace OBSIZE of \LAFITE.OUTBOX with NLINES)
                  (COND
                     ((AND (ILESSP NLINES OLDSIZE)
                           (IGREATERP (SETQ N (IDIFFERENCE (LENGTH (SETQ ITEMS (fetch OBITEMS
                                                                                  of 
                                                                                       \LAFITE.OUTBOX
                                                                                      )))
                                                     NLINES))
                                  0))
                      (replace OBITEMS of \LAFITE.OUTBOX with (CDR (NTH ITEMS N]
              (\OUTBOX.RESET \LAFITE.OUTBOX)
              (REDISPLAYW WINDOW])

(\OUTBOX.SHADEITEM
  [LAMBDA (OUTBOX ITEM N SHADE OPERATION)                (* ; "Edited  3-Sep-87 17:24 by bvm:")

(* ;;; "Shade the indicated ITEM in OUTBOX using texture SHADE blted with OPERATION")

    (PROG ((W (fetch OBWINDOW of OUTBOX))
           HEIGHT)
          (BLTSHADE SHADE W 0 (- (fetch OBORIGIN of OUTBOX)
                                 (+ (ITIMES N (SETQ HEIGHT (fetch OBHEIGHT of OUTBOX)))
                                    (fetch OBDESCENT of OUTBOX)))
                 NIL HEIGHT OPERATION)
          (COND
             ((EQ OPERATION 'REPLACE)
              (\OUTBOX.DISPLAYLINE OUTBOX ITEM N])

(\OUTBOX.BUTTONFN
  [LAMBDA (WINDOW)                                       (* bvm%: "13-Nov-84 10:58")

(* ;;; "BUTTONEVENTFN for the outbox.  If a message is selected, edit it")

    (PROG ((SELECTIONREGION (DSPCLIPPINGREGION NIL WINDOW))
           (OUTBOX \LAFITE.OUTBOX)
           SELECTED SEL# NEWSEL# ITEMS HEIGHT ORIGIN DESCENT LASTX LASTY MAXITEM)
          (COND
             ((OR (NOT (SETQ ITEMS (fetch OBITEMS of OUTBOX)))
                  (NEQ WINDOW (fetch OBWINDOW of OUTBOX)))
                                                             (* ; "Nothing to select")
              (RETURN)))
          (SETQ MAXITEM (LENGTH ITEMS))
          (SETQ HEIGHT (fetch OBHEIGHT of OUTBOX))
          (SETQ DESCENT (fetch OBDESCENT of OUTBOX))
          (SETQ ORIGIN (fetch OBORIGIN of OUTBOX))

     (* ;; "keep looping until all mouse buttons are up")

          (do (GETMOUSESTATE)
                 (COND
                    [(OR [NOT (INSIDEP SELECTIONREGION (SETQ LASTX (LASTMOUSEX WINDOW))
                                     (SETQ LASTY (LASTMOUSEY WINDOW]
                         (> (SETQ NEWSEL# (ADD1 (QUOTIENT (- ORIGIN (+ LASTY DESCENT))
                                                       HEIGHT)))
                            MAXITEM))

                     (* ;; "I would like to just return here and let the next window take over, but current mouse arrangement means I'll never get control back unless user lets up on mouse")

                     [COND
                        (SELECTED (\OUTBOX.SHADEITEM OUTBOX SELECTED SEL# BLACKSHADE 'INVERT)
                               (SETQ SELECTED (SETQ SEL# NIL]
                     (COND
                        ((LASTMOUSESTATE UP)
                         (RETURN))
                        (T (BLOCK]
                    ((LASTMOUSESTATE UP)                     (* ; 
                                                      "Let mouse up while over a selection.  Do it")
                     [COND
                        (SELECTED (\LAFITE.PROCESS [LIST (FUNCTION \SENDMESSAGE)
                                                         (KWOTE (COPYTEXTSTREAM (fetch OBITEXT
                                                                                   of SELECTED]
                                         'MESSAGESENDER T 'NO)
                               (\OUTBOX.SHADEITEM OUTBOX SELECTED SEL# BLACKSHADE 'INVERT]
                     (RETURN))
                    ((NEQ NEWSEL# SEL#)
                     [COND
                        (SELECTED (\OUTBOX.SHADEITEM OUTBOX SELECTED SEL# BLACKSHADE 'INVERT]
                     (\OUTBOX.SHADEITEM OUTBOX [SETQ SELECTED (CAR (NTH ITEMS (SETQ SEL# NEWSEL#]
                            SEL# BLACKSHADE 'INVERT])

(\OUTBOX.DISPLAYLINE
  [LAMBDA (OUTBOX ITEM N)                                (* bvm%: " 8-Nov-84 21:35")
    (PROG ((W (fetch OBWINDOW of OUTBOX)))
          (COND
             (N (MOVETO 0 (IDIFFERENCE (fetch OBORIGIN of OUTBOX)
                                 (ITIMES N (fetch OBHEIGHT of OUTBOX)))
                       W)))
          (printout W (fetch OBIDATE of ITEM)
                 %,,
                 (fetch OBISUBJECT of ITEM])

(\OUTBOX.ADD.ITEM
  [LAMBDA (TEXTSTREAM SUBJECT)                           (* ; "Edited  3-Sep-87 18:08 by bvm:")
    (PROG ((OUTBOX (OR \LAFITE.OUTBOX (\OUTBOX.CREATE)))
           W N ITEM BOTTOM HEIGHT ITEMS)
          (OR OUTBOX (RETURN))
          [COND
             ((>= [SETQ N (LENGTH (SETQ ITEMS (fetch OBITEMS of OUTBOX]
                  (fetch OBSIZE of OUTBOX))
              (replace OBITEMS of OUTBOX with (SETQ ITEMS (CDR ITEMS)))
              (BITBLT (SETQ W (fetch OBWINDOW of OUTBOX))
                     0
                     [SETQ BOTTOM (- (fetch OBORIGIN of OUTBOX)
                                     (+ (ITIMES N (SETQ HEIGHT (fetch OBHEIGHT of OUTBOX)))
                                        (fetch OBDESCENT of OUTBOX]
                     W 0 (+ BOTTOM HEIGHT)
                     NIL
                     (ITIMES HEIGHT (SUB1 N))
                     'INPUT
                     'REPLACE)
              (BLTSHADE WHITESHADE W 0 BOTTOM NIL HEIGHT 'REPLACE))
             (T (SETQ N (ADD1 N]
          [replace OBITEMS of OUTBOX with (NCONC1 ITEMS
                                                             (SETQ ITEM
                                                              (create OUTBOXITEM
                                                                     OBITEXT _ TEXTSTREAM
                                                                     OBIDATE _
                                                                     (DATE (DATEFORMAT NO.DATE 
                                                                                  NO.SECONDS))
                                                                     OBISUBJECT _ SUBJECT]
          (\OUTBOX.DISPLAYLINE OUTBOX ITEM N])
)

(RPAQ? LAFITEOUTBOXSIZE 2)

(RPAQ? \LAFITE.OUTBOX )
(DECLARE%: EVAL@COMPILE DONTCOPY 
(DECLARE%: EVAL@COMPILE

(RECORD OUTBOXITEM (OBITEXT OBIDATE OBISUBJECT OBIWINDOW))
)

(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS LAFITEOUTBOXSIZE)
)
)



(* ; "Built-in message forms")

(DEFINEQ

(\LAFITE.MESSAGEFORM
  [LAMBDA (ITEM MENU BUTTON)                             (* ; "Edited 23-Feb-89 12:50 by bvm")
    (COND
       ((NULL (OR \LAFITEMODE (\LAFITE.INFER.MODE)))
        (printout PROMPTWINDOW T "Must set Lafite Mode before sending mail"))
       (T (RESETLST
              (AND ITEM (LA.RESETSHADE ITEM MENU))
              [PROG ((*UPPER-CASE-FILE-NAMES* NIL)
                     FORM FORMNAME FULLFORMNAME)
                    (COND
                       ((EQ BUTTON 'LEFT)
                        (SETQ FORM (MAKENEWMESSAGEFORM)))
                       ([NOT (SETQ FORM (MENU (.LAFITEMENU. LAFITEFORMSMENU (APPEND (
                                                                        MAKELAFITEPRIVATEFORMSITEMS
                                                                                     
                                                                 "Use the form defined in this file."
                                                                                     )
                                                                                   LAFITESPECIALFORMS
                                                                                   
                                                                                 LAFITEFORMSMENUITEMS
                                                                                   )
                                                     "Message Forms"]
                        (RETURN))
                       ((EQ FORM '%##ANOTHERFORM##)          (* ; "user buttoned 'Another Form'")
                        (OR (SETQ FORMNAME (PROMPTFORFILENAME))
                            (RETURN)))
                       ((DEFINEDP FORM)
                        (OR (SETQ FORM (CL:FUNCALL FORM))
                            (RETURN)))
                       [(BOUNDP FORM)
                        (SETQ FORM (OR (EVALV FORM)
                                       (MAKENEWMESSAGEFORM]
                       (T                                    (* ; "other private form")
                          (SETQ FORMNAME FORM)))
                    (COND
                       ((NULL FORMNAME)                      (* ; "Have form already")
                        )
                       ([OR (SETQ FULLFORMNAME (INFILEP (LA.LONGFILENAME FORMNAME LAFITEFORM.EXT)))
                            (AND LAFITEFORMDIRECTORIES (SETQ FULLFORMNAME (FINDFILE
                                                                           (PACKFILENAME.STRING
                                                                            'BODY FORMNAME
                                                                            'EXTENSION LAFITEFORM.EXT
                                                                            )
                                                                           T LAFITEFORMDIRECTORIES]
                                                             (* ; "read the form and return it")
                        (COND
                           ((NOT (CL:MEMBER (SETQ FORMNAME (LA.SHORTFILENAME FULLFORMNAME 
                                                                  LAFITEFORM.EXT))
                                        LAFITEFORMFILES :TEST 'STRING-EQUAL))
                            (push LAFITEFORMFILES FORMNAME)
                            (SETQ \LAFITEPROFILECHANGED T)
                            (SETQ LAFITEFORMSMENU)))
                        (SETQ FORM (\LAFITE.READ.FORM FULLFORMNAME)))
                       (T (printout PROMPTWINDOW T FORMNAME " not found.")
                          (RETURN)))
                    (ADD.PROCESS (LIST (FUNCTION \SENDMESSAGE)
                                       (KWOTE FORM)
                                       NIL
                                       (KWOTE FORMNAME))
                           'NAME
                           'MESSAGESENDER
                           'RESTARTABLE
                           'NO)                              (* ; 
                                                 "Finally, start authenticating if we haven't yet.")
                    (\LAFITE.GET.USER.DATA (AND (TEXTSTREAMP FORM)
                                                (TEXTPROP FORM 'LAFITEMODE])])

(MAKELAFITESUPPORTFORM
  [LAMBDA NIL                                            (* bvm%: "12-Mar-85 00:39")
    (MAKEXXXSUPPORTFORM "Lafite" LAFITESUPPORT LAFITESYSTEMDATE])

(MAKELISPSUPPORTFORM
  [LAMBDA NIL                                            (* bvm%: "12-Mar-85 00:39")
    (MAKEXXXSUPPORTFORM "Lisp" LISPSUPPORT])

(MAKEXXXSUPPORTFORM
  [LAMBDA (SYSTEMNAME ADDRESS SYSTEMDATE)                    (* ; "Edited  7-Feb-2022 11:56 by rmk")
                                                             (* ; "Edited  3-May-89 18:37 by bvm")
    (PROG ((SUBJFIELD ">>Terse summary of problem<<")
           (UCODEVERSION (MICROCODEVERSION))
           (SCRATCH (OPENSTREAM "{nodircore}" 'BOTH))
           TEXTSTREAM SELECTPOSITION MODE)
          [COND
             [(LISTP ADDRESS)                                (* ; 
               "Mode-dependent address.  Pick the first address that's in a mode we know how to send")
              (SETQ ADDRESS (for PAIR in ADDRESS when (\LAFITE.GET.USER.DATA (SETQ MODE (CAR PAIR)))
                               do (RETURN (CADR PAIR]
             (T                                              (* ; "Just send in current mode")
                (SETQ MODE (fetch LAFITEMODE of \LAFITEMODE]
          (COND
             ((NOT ADDRESS)
              (printout PROMPTWINDOW T "Can't -- no address known for " SYSTEMNAME " report.")
              (RETURN)))
          (SETQ TEXTSTREAM (OPENTEXTSTREAM (OPENSTRINGSTREAM (CONCAT "Subject: " SYSTEMNAME ": "))
                                  NIL NIL NIL (LIST 'FONT LAFITEEDITORFONT)))
          (SETQ SELECTPOSITION (ADD1 (GETEOFPTR TEXTSTREAM)))
          (PROGN                                             (* ; 
        "Now write the main stuff to a scratch stream. faster than bouting a byte at a time to tedit")
                 (printout SCRATCH SUBJFIELD T)
                 (printout SCRATCH "To: " ADDRESS T)
                 (printout SCRATCH "cc: " (FULLUSERNAME NIL MODE)
                        T T)
                 (COND
                    (SYSTEMDATE (printout SCRATCH SYSTEMNAME " System Date: " SYSTEMDATE T)))
                 (printout SCRATCH "Lisp System Date: " MAKESYSDATE " (" (L-CASE (MKSTRING 
                                                                                        MAKESYSNAME)
                                                                                T)
                        ")" T)
                 (printout SCRATCH "Machine: " (OR \LAFITE.REPORT.MACHINE
                                                   (PROGN (SETQ \LAFITE.REPORT.MACHINE (L-CASE (
                                                                                          MACHINETYPE
                                                                                                )
                                                                                              T))
                                                          [COND
                                                             ((EQ \PUP.READY T)
                                                              (SETQ \LAFITE.REPORT.MACHINE
                                                               (CONCAT \LAFITE.REPORT.MACHINE " ("
                                                                      (ETHERHOSTNAME NIL T)
                                                                      ")"]
                                                          \LAFITE.REPORT.MACHINE))
                        T)
                 (printout SCRATCH "Microcode version: " .I1.8 (fetch HIBYTE of UCODEVERSION)
                        "," .I1.8 (fetch LOBYTE of UCODEVERSION)
                        T)
                 (printout SCRATCH "Memory size: " .I4.8 (REALMEMORYSIZE)
                        T)
                 (printout SCRATCH "Frequency: >> Always, Intermittent, Once <<
Impact: >> Fatal, Serious, Moderate, Annoying, Minor <<" T T)
                 (printout SCRATCH ">>detailed problem description<<" T))
          (TEDIT.SETSEL TEXTSTREAM SELECTPOSITION 0 'RIGHT)
          (TEDIT.INCLUDE TEXTSTREAM SCRATCH)
          (TEDIT.SETSEL TEXTSTREAM SELECTPOSITION (NCHARS SUBJFIELD)
                 'RIGHT T)
          (TEXTPROP TEXTSTREAM 'LAFITEMODE MODE)
          (RETURN TEXTSTREAM])

(MAKENEWMESSAGEFORM
  [LAMBDA NIL                                                (* ; "Edited  7-Feb-2022 11:56 by rmk")
                                                             (* ; "Edited  6-Jun-88 12:22 by bvm")
    (LET ((OUTSTREAM (OPENTEXTSTREAM NIL NIL NIL NIL (LIST 'FONT LAFITEEDITORFONT)))
          SELECTPOSITION)
         (printout OUTSTREAM "Subject: ")
         (SETQ SELECTPOSITION (ADD1 (GETFILEPTR OUTSTREAM)))
         (printout OUTSTREAM SUBJECTSTR T)
         (printout OUTSTREAM "To: " RECIPIENTSSTR T)
         (printout OUTSTREAM "cc: " (FULLUSERNAME)
                T T)
         (printout OUTSTREAM MESSAGESTR T)
         (if LAFITE.SIGNATURE
             then                                            (* ; "Pre-sign it")
                  (PRIN3 LAFITE.SIGNATURE OUTSTREAM))
         (TEDIT.SETSEL OUTSTREAM SELECTPOSITION (NCHARS SUBJECTSTR)
                'RIGHT T)
         OUTSTREAM])

(MAKELAFITEPRIVATEFORMSITEMS
  [LAMBDA (HELPSTR)                                      (* ; "Edited 23-Feb-89 12:38 by bvm")
    (for FORMFILE in (SORT LAFITEFORMFILES) when FORMFILE
       collect `(,(if (U-CASEP FORMFILE)
                          then (CL:STRING-CAPITALIZE FORMFILE)
                        else FORMFILE)
                     ',FORMFILE
                     ,HELPSTR])

(\LAFITE.UNCACHE.MESSAGEFORM
  [LAMBDA (ITEM MENU)                                    (* ; "Edited  8-Nov-89 12:38 by bvm")
    (LET ((FORM (\LAFITE.SELECT.FORM "Forget about this message form")))
         (COND
            (FORM (SETQ LAFITEFORMFILES (DREMOVE FORM LAFITEFORMFILES))
                  (SETQ \LAFITEPROFILECHANGED T)
                  (SETQ LAFITEFORMSMENU)
                  (printout PROMPTWINDOW T FORM " forgotten."])

(\LAFITE.DELETE.MESSAGEFORM
  [LAMBDA (ITEM MENU)                                    (* ; "Edited  8-Nov-89 12:38 by bvm")
    (LET ((FORM (\LAFITE.SELECT.FORM "Delete this saved message")))
         (if (AND FORM (PROGN (CLRPROMPT)
                                  (MOUSECONFIRM (CL:FORMAT NIL 
                                                  "Click LEFT to confirm deleting saved message '~A'"
                                                       FORM)
                                         T PROMPTWINDOW)))
             then (\LAFITE.DELETE.FORM.INTERNAL FORM])

(\LAFITE.SELECT.FORM
  [LAMBDA (MSG)                                          (* ; "Edited  8-Nov-89 12:37 by bvm")
    (COND
       ((NULL LAFITEFORMFILES)
        (printout PROMPTWINDOW T "You have no private message forms"))
       (T (MENU (\LAFITE.CREATE.MENU (MAKELAFITEPRIVATEFORMSITEMS MSG)
                       "Private Forms"])

(\LAFITE.DELETE.FORM.INTERNAL
  [LAMBDA (FORMNAME)                                     (* ; "Edited  8-Nov-89 12:34 by bvm")
    (LET ((*UPPER-CASE-FILE-NAMES* NIL)
          (LONGNAME (LA.LONGFILENAME FORMNAME LAFITEFORM.EXT))
          FULL)
         (while (SETQ FULL (FULLNAME LONGNAME 'OLDEST))
            do (if (NOT (DELFILE FULL))
                       then (PRINTOUT PROMPTWINDOW T "Could not delete " FULL)
                             (RETURN NIL)) finally (SETQ LAFITEFORMFILES (CL:DELETE FORMNAME 
                                                                                    LAFITEFORMFILES 
                                                                                    :TEST
                                                                                    'STRING-EQUAL))
                                                 (SETQ \LAFITEPROFILECHANGED T)
                                                 (SETQ LAFITEFORMSMENU)
                                                 (PRINTOUT PROMPTWINDOW T FORMNAME " deleted.")
                                                 (RETURN T])

(\LAFITE.READ.FORM
  [LAMBDA (FILE)                                        (* ; "Edited 18-Jul-2000 03:09 by rmk:")
                                                            (* ; "Edited 18-Jul-2000 03:08 by rmk:")
                                                             (* ; "Edited  2-Nov-89 15:55 by bvm")

(* ;;; "copies the messaage form in the FILE into a text stream")

    (PROG ((TEXTSTREAM (OPENTEXTSTREAM [OPENSTREAM FILE 'INPUT NIL '((TYPE TEXT]
                              NIL NIL NIL (LIST 'FONT LAFITEEDITORFONT)))
           NAME CH)
          (SETFILEPTR TEXTSTREAM 0)
          (COND
             ([OR (EQ (SETQ CH (BIN TEXTSTREAM))
                      (CHARCODE %"))
                  (AND (EQ CH (CHARCODE CR))
                       (EQ (BIN TEXTSTREAM)
                           (CHARCODE %"]                     (* ; 
                                             "Old-style form, get rid of surrounding double quotes")
              (TEDIT.DELETE TEXTSTREAM 1 (ADD1 (GETFILEPTR TEXTSTREAM)))
              (TEDIT.DELETE TEXTSTREAM (GETEOFPTR TEXTSTREAM)
                     1)))
          [bind [OPENMARKER _ (CONSTANT (ALLOCSTRING 1 (CHARCODE ^A]
                 J
                 (I _ 1) while (SETQ I (TEDIT.FIND TEXTSTREAM OPENMARKER I))
             do                                          (* ; 
                                                           "Change Laurel forms into Lafite forms")
                   (COND
                      ((AND (SETQ J (TEDIT.FIND TEXTSTREAM (CONSTANT (ALLOCSTRING 1 (CHARCODE ^B)))
                                           (ADD1 I)
                                           (IPLUS I 70)))
                            (NOT (TEDIT.FIND TEXTSTREAM OPENMARKER (ADD1 I)
                                        J)))
                       (TEDIT.DELETE TEXTSTREAM J 1)
                       (TEDIT.INSERT TEXTSTREAM "<<" J)
                       (TEDIT.DELETE TEXTSTREAM I 1)
                       (TEDIT.INSERT TEXTSTREAM ">>" I)
                       (SETQ I J))
                      (T (RETURN]
          (bind (I _ 1) while (SETQ I (TEDIT.FIND TEXTSTREAM ">>Self<<" I))
             do                                          (* ; 
                                                           "Replace '>>Self<<' with user name")
                   (OR NAME (SETQ NAME (FULLUSERNAME)))
                   (TEDIT.DELETE TEXTSTREAM I 8)
                   (TEDIT.INSERT TEXTSTREAM NAME I)
                   (SETFILEPTR TEXTSTREAM I)                 (* ; "Patch around tedit bug..."))
          (\LAFITE.FIND.TEMPLATE TEXTSTREAM)
          (RETURN TEXTSTREAM])

(\LAFITE.FIND.TEMPLATE
  [LAMBDA (TEXTSTREAM)                                   (* bvm%: "22-Apr-84 23:59")
    (LET (SELECTSTART)
         (COND
            ((SETQ SELECTSTART (TEDIT.FIND TEXTSTREAM ">>*<<" 1 NIL T))
                                                             (* ; 
                                                           "Wait until TEDIT.FIND gets fixed")
                                                             (* ; 
                                                           "highlight the first 'blank' to fill in")
             [COND
                ((LISTP SELECTSTART)
                 (SETQ SELECTSTART (CAR SELECTSTART]
             (TEDIT.SETSEL TEXTSTREAM SELECTSTART (+ 2 (- (TEDIT.FIND TEXTSTREAM "<<" SELECTSTART)
                                                          SELECTSTART))
                    'RIGHT T)
             T)
            (T (TEDIT.SETSEL TEXTSTREAM 1 0 'LEFT])
)



(* ; "ANSWER")

(DEFINEQ

(\LAFITE.ANSWER
  [LAMBDA (WINDOW FOLDERDATA ITEM MENU)                  (* bvm%: " 1-Feb-84 15:08")
    (ADD.PROCESS (LIST (FUNCTION \LAFITE.ANSWER.PROC)
                       (KWOTE WINDOW)
                       (KWOTE FOLDERDATA)
                       (KWOTE ITEM)
                       (KWOTE MENU))
           'NAME
           'MESSAGEANSWERER
           'RESTARTABLE
           'NO])

(\LAFITE.ANSWER.PROC
  [LAMBDA (WINDOW MAILFOLDER ITEM MENU)                  (* bvm%: "29-May-84 15:59")
    (PROG (MSGDESCRIPTOR FORM)
          [SETQ FORM (RESETLST
                         (LA.RESETSHADE ITEM MENU)
                         (WITH.MONITOR (fetch (MAILFOLDER FOLDERLOCK) of MAILFOLDER)
                             (\LAFITE.MAYBE.CLEAR.PROMPT MAILFOLDER)
                             (COND
                                ((NOT (LAB.ASSURE.SELECTIONS MAILFOLDER))
                                 (MAKEANSWERFORM (SETQ MSGDESCRIPTOR
                                                      (find MSGDESCRIPTOR selectedin 
                                                                                    MAILFOLDER
                                                         suchthat T))
                                        MAILFOLDER)))))]
          (COND
             ((AND FORM (\SENDMESSAGE FORM))
              (WITH.MONITOR (fetch (MAILFOLDER FOLDERLOCK) of MAILFOLDER)
                  [PROG ((MESSAGES (fetch (MAILFOLDER MESSAGEDESCRIPTORS) of MAILFOLDER)))
                        (COND
                           ([AND MESSAGES (EQ MSGDESCRIPTOR (NTHMESSAGE MESSAGES (fetch
                                                                                  (LAFITEMSG %#)
                                                                                    of 
                                                                                        MSGDESCRIPTOR
                                                                                  ]
                                                             (* ; 
               "If message got expunged since we constructed the answer form, we can't do anything")
                            (MARKMESSAGE MSGDESCRIPTOR MAILFOLDER ANSWERMARK])])

(MAKEANSWERFORM
  [LAMBDA (MSGDESCRIPTORS MAILFOLDER)                        (* ; "Edited  7-Feb-2022 11:58 by rmk")
                                                             (* ; "Edited 10-Aug-89 17:28 by bvm")
    (LET* ((FIRSTMSG (if (LISTP MSGDESCRIPTORS)
                         then (CAR MSGDESCRIPTORS)
                       else MSGDESCRIPTORS))
           (MODEBITS (fetch (LAFITEMSG MODEBITS) of FIRSTMSG))
           (MODE (CL:NTH MODEBITS *LAFITE-WELL-KNOWN-MODES*)))
          (if (NULL MODE)
              then (if [OR (NEQ MODEBITS 0)
                           (NULL (SETQ MODE (\LAFITE.GUESS.MODE FIRSTMSG]
                       then (LAB.PROMPTPRINT MAILFOLDER (if (EQ MODEBITS 0)
                                                            then "Message of unknown protocol."
                                                          else 
                        "Warning: This message was retrieved under a protocol not currently enabled."
                                                            ))
                            (LAB.PROMPTPRINT MAILFOLDER "Will answer in " (SETQ MODE
                                                                           (fetch (LAFITEOPS 
                                                                                         LAFITEMODE)
                                                                              of \LAFITEMODE))
                                   " mode; this may not work.  ")))

          (* ;; "Currently we only pay attention to the first message.  If we ever do otherwise, we'll want to notice whether the other messages are in the same mode")

          (LET ((*LAFITE-MODE-DATA* (\LAFITE.GET.USER.DATA MODE))
                MSG)

               (* ;; "Before returning the form, tag it with a mail mode")

               (if (NULL *LAFITE-MODE-DATA*)
                   then (LAB.FORMAT MAILFOLDER "Failed: can't authenticate user in ~A mode" MODE)
                 elseif (SETQ MSG (CL:FUNCALL (fetch (LAFITEMODEDATA ANSWERER) of *LAFITE-MODE-DATA*)
                                         MSGDESCRIPTORS MAILFOLDER))
                   then (if (TEXTSTREAMP MSG)
                            then (TEXTPROP MSG 'LAFITEMODE MODE)
                                 MSG
                          else (OPENTEXTSTREAM (OPENSTRINGSTREAM MSG)
                                      NIL NIL NIL `(LAFITEMODE ,MODE])

(LA.PRINT.COMMA.LIST
  [LAMBDA (STRINGS STREAM)                               (* ; "Edited  6-Jun-88 12:50 by bvm")
    (for STR in STRINGS bind NTHTIME when STR do (COND
                                                                        (NTHTIME (PRIN3 ", " STREAM))
                                                                        (T (SETQ NTHTIME T)))
                                                                    (PRIN3 STR STREAM])

(LAFITE.FILL.IN.ANSWER.FORM
  [LAMBDA (SUBJECT FROM DATE TO CC ADDRESSPRINTFN)           (* ; "Edited  7-Feb-2022 11:58 by rmk")
                                                             (* ; "Edited 10-Jun-88 17:19 by bvm")

    (* ;; "Construct an answer form replying to a message from FROM on DATE with specified SUBJECT.  Reply should go to the lists of names TO and CC.  ADDRESSPRINTFN is a function that prints a list of names suitably for the protocol in question.")

    (LET ((OUTSTREAM (OPENTEXTSTREAM NIL NIL NIL NIL (LIST 'FONT LAFITEEDITORFONT)))
          SELECTPOSITION)
         (LINELENGTH MAX.SMALLP OUTSTREAM)                   (* ; 
                                                      "Sigh, apparently text streams have linelength")
         (PROGN (printout OUTSTREAM "Subject: ")
                (if SUBJECT
                    then (COND
                            ((NOT (STRING-EQUAL (SUBSTRING SUBJECT 1 3)
                                         "Re:"))
                             (printout OUTSTREAM "Re: ")))
                         (printout OUTSTREAM SUBJECT)
                  else (printout OUTSTREAM "(reply to message)")))
         (PROGN (printout OUTSTREAM T "In-reply-to: ")
                (if (NULL FROM)
                    then (printout OUTSTREAM "your")
                  else (printout OUTSTREAM FROM "'s"))
                (printout OUTSTREAM " message of " DATE T))
         (PROGN (printout OUTSTREAM "To: ")
                (if TO
                    then (CL:FUNCALL ADDRESSPRINTFN TO OUTSTREAM)
                  else                                       (* ; "No to, so ask to fill in")
                       (printout OUTSTREAM RECIPIENTSSTR T))
                (TERPRI OUTSTREAM))
         (COND
            (CC (printout OUTSTREAM "cc: ")
                (CL:FUNCALL ADDRESSPRINTFN CC OUTSTREAM)
                (TERPRI OUTSTREAM)))
         (TERPRI OUTSTREAM)
         (SETQ SELECTPOSITION (ADD1 (GETFILEPTR OUTSTREAM)))
         (printout OUTSTREAM MESSAGESTR T)
         (if LAFITE.SIGNATURE
             then                                            (* ; "Pre-sign it")
                  (PRIN3 LAFITE.SIGNATURE OUTSTREAM))
         (TEDIT.SETSEL OUTSTREAM SELECTPOSITION (NCHARS MESSAGESTR)
                'RIGHT T)
         OUTSTREAM])
)



(* ; "FORWARD")

(DEFINEQ

(\LAFITE.FORWARD
  [LAMBDA (WINDOW MAILFOLDER ITEM MENU)                  (* bvm%: " 1-Feb-84 15:05")
    (ADD.PROCESS (LIST (FUNCTION \LAFITE.FORWARD.PROC)
                       (KWOTE WINDOW)
                       (KWOTE MAILFOLDER)
                       (KWOTE ITEM)
                       (KWOTE MENU))
           'NAME
           'MESSAGEFORWARDER
           'RESTARTABLE
           'NO])

(\LAFITE.FORWARD.PROC
  [LAMBDA (WINDOW MAILFOLDER ITEM MENU)                  (* ; "Edited 14-Oct-87 16:20 by bvm:")
    (PROG (FORWARDEDMSGS FORM)

     (* ;; "the reason to get the MSG#S first is that they may have changed by the time \SENDMESSAGE finishes and then we would have marked the wrong ones")

          (RESETLST
              (OBTAIN.MONITORLOCK (fetch (MAILFOLDER FOLDERLOCK) of MAILFOLDER)
                     NIL T)
              (LA.RESETSHADE ITEM MENU)
              (\LAFITE.MAYBE.CLEAR.PROMPT MAILFOLDER)
              [COND
                 ((NOT (LAB.ASSURE.SELECTIONS MAILFOLDER))
                  (SETQ FORM (MAKEFORWARDFORM WINDOW MAILFOLDER (SETQ FORWARDEDMSGS (
                                                                                LAB.SELECTED.MESSAGES
                                                                                         MAILFOLDER])
          (COND
             ((AND FORM (\SENDMESSAGE FORM))
              (WITH.MONITOR (fetch (MAILFOLDER FOLDERLOCK) of MAILFOLDER)
                  [PROG ((MESSAGES (fetch (MAILFOLDER MESSAGEDESCRIPTORS) of MAILFOLDER)))
                        (COND
                           (MESSAGES                         (* ; 
                                                        "Make sure folder hasn't been closed since")
                                  (for MSG in FORWARDEDMSGS
                                     when (EQ MSG (NTHMESSAGE MESSAGES (fetch (LAFITEMSG
                                                                                       %#)
                                                                              of MSG)))
                                     do                  (* ; 
              "If message got expunged since we constructed the forward form, we can't do anything")
                                           (MARKMESSAGE MSG MAILFOLDER FORWARDMARK])])

(MAKEFORWARDFORM
  [LAMBDA (WINDOW FOLDER MESSAGELIST)                        (* ; "Edited  7-Feb-2022 11:59 by rmk")
                                                             (* ; "Edited  5-Jan-90 17:46 by bvm")

    (* ;; "Make a message form that forwards each of the messages in MESSAGELIST")

    (PROG ((FOLDERSTREAM (\LAFITE.OPEN.FOLDER FOLDER 'INPUT :ABORT))
           (TEXTSTREAM (OPENTEXTSTREAM NIL NIL NIL NIL (LIST 'FONT LAFITEEDITORFONT)))
           (CURMSG (CAR MESSAGELIST))
           SUBJECT SELECTPOSITION SELECTLEN)
          (OR (fetch (LAFITEMSG PARSED?) of CURMSG)
              (LAFITE.PARSE.MSG.FOR.TOC CURMSG FOLDER))
          (LINELENGTH MAX.SMALLP TEXTSTREAM)
          (PRIN3 "Subject: " TEXTSTREAM)
          (COND
             ([OR LAFITEFORWARDSUBJECTSTR (NULL (SETQ SUBJECT (fetch (LAFITEMSG SUBJECT) of CURMSG]
              (SETQ SELECTPOSITION (ADD1 (GETFILEPTR TEXTSTREAM)))
              [SETQ SELECTLEN (NCHARS (SETQ SUBJECT (OR LAFITEFORWARDSUBJECTSTR SUBJECTSTR]
              (PRIN3 SUBJECT TEXTSTREAM))
             (T (CL:FORMAT TEXTSTREAM "[~@[~A: ~]~A]" (fetch (LAFITEMSG FROM) of CURMSG)
                       SUBJECT)))
          (TERPRI TEXTSTREAM)
          (PRIN3 "To: " TEXTSTREAM)
          [COND
             ((NOT SELECTPOSITION)
              (SETQ SELECTPOSITION (ADD1 (GETFILEPTR TEXTSTREAM)))
              (SETQ SELECTLEN (NCHARS RECIPIENTSSTR]
          (CL:FORMAT TEXTSTREAM "~A
cc: ~A

~A
" RECIPIENTSSTR (FULLUSERNAME)
                 (CAR LAFITEFORWARDSTRINGS))
          (if LAFITE.SIGNATURE
              then                                           (* ; 
                                        "Sign it up here, after the user's inserted comments, if any")
                   (PRIN3 LAFITE.SIGNATURE TEXTSTREAM)
                   (TERPRI TEXTSTREAM))
          (for MSGDESCRIPTOR in MESSAGELIST bind NTHTIME do (PRIN3 (COND
                                                                      (NTHTIME 
                                                             (* ; "%"Next message%"")
                                                                             (CADDR 
                                                                                 LAFITEFORWARDSTRINGS
                                                                                    ))
                                                                      (T 
                                                             (* ; "%"Begin forwarded messages%"")
                                                                         (SETQ NTHTIME T)
                                                                         (CADR LAFITEFORWARDSTRINGS))
                                                                      )
                                                                   TEXTSTREAM)
                                                            (TERPRI TEXTSTREAM)
                                                            (\LAFITE.APPEND.MESSAGE.BODY TEXTSTREAM 
                                                                   FOLDERSTREAM MSGDESCRIPTOR 
                                                                   \LAPARSE.DONT.FORWARD.HEADERS)
                                                            (TERPRI TEXTSTREAM)
                                                            (TEDIT.CARETLOOKS TEXTSTREAM 
                                                                   LAFITEEDITORFONT))
          (PRIN3 (CADDDR LAFITEFORWARDSTRINGS)
                 TEXTSTREAM)
          (TERPRI TEXTSTREAM)
          (TEDIT.SETSEL TEXTSTREAM SELECTPOSITION SELECTLEN 'RIGHT T)
          (RETURN TEXTSTREAM])
)

(RPAQQ LAFITESENDINGMENUITEMS (("Deliver" '\SENDMSG.DELIVER "Send the message in the edit window")
                               ("Reply To" '\SENDMSG.REPLYTO 
                                      "Insert a Reply-to field in this message")
                               ("Change Mode" '\UNIXMAIL.CHANGE.MODE 
                                      "Change the mode (mail protocol) used to send this message.")
                               ("Save" '\SENDMSG.SAVE.FORM 
                    "Save the message in a file for later use (retrieve with middle-button SendMail)"
                                      )))

(RPAQQ LAFITEFORMSMENUITEMS (("Saved Form" '%##ANOTHERFORM## 
                                    "You will be asked to specify a filename for the form")
                             ("Standard Form" (FUNCTION MAKENEWMESSAGEFORM)
                                    "A clean message form")))

(RPAQQ LAFITEFORMATMENUITEMS (("Send Formatted Message" 'TEDIT)
                              ("Send Plain Text" 'TEXT)
                              ("Abort" 'ABORT)))

(RPAQQ LAFITEFORWARDSTRINGS (">>CoveringMessage<<" "
     ----- Begin Forwarded Messages -----
" "
     ----- Next Message -----
" "
     ----- End Forwarded Messages -----"))

(ADDTOVAR \SYSTEMCACHEVARS \LAFITE.REPORT.MACHINE)

(ADDTOVAR LAFITESPECIALFORMS ("Lisp Report" (FUNCTION MAKELISPSUPPORTFORM)
                                    "A form to report a Lisp bug or suggestion")
                             ("Lafite Report" (FUNCTION MAKELAFITESUPPORTFORM)
                                    "A form to report a Lafite bug or suggestion"))

(ADDTOVAR LAFITEMENUVARS LAFITEFORMSMENU LAFITEFORMATMENU)

(RPAQ? \LAFITE.REPORT.MACHINE )

(RPAQ? LAFITECURRENTEDITORWINDOWS )

(RPAQ? LAFITEFORMFILES )

(RPAQ? LAFITEFORMSMENU )

(RPAQ? LAFITEFORMATMENU )

(RPAQ? LAFITEEDITORFONT LAFITEDISPLAYFONT)

(RPAQ? LAFITEFORM.EXT "Lafite-form")

(RPAQ? LAFITEFORMDIRECTORIES NIL)

(RPAQ? LAFITE.EDITOR.SIZE '(470 . 300))

(RPAQ? LAFITE.EDITOR.LAYOUTS NIL)

(RPAQ? LAFITEFORWARDSUBJECTSTR NIL)

(RPAQ? LAFITESUPPORT NIL)

(RPAQ? LISPSUPPORT NIL)

(RPAQ? MESSAGESTR ">>Message<<")

(RPAQ? RECIPIENTSSTR ">>Recipients<<")

(RPAQ? SUBJECTSTR ">>Subject<<")

(RPAQ? LAFITE.SEND.FORMATTED '((NSCHARS :ASK)
                               (CHARLOOKS :ASK)
                               (PARALOOKS :ASK)
                               (IMAGEOBJ :ASK)))



(* ; "Obsolete")


(RPAQ? LAFITEEDITORREGION NIL)



(* ; "ICON stuff")


(RPAQQ LAFITE.MSG.ICON (#*(82 72)@@@@@@@@@GO@@@@@@@@@@@@@@@@@@@@@AOOL@@@@@@@@@@@@@@@@@@@@GH@O@@@@@@@@@@@@@@@@@@@CN@@CL@@@@@@@@@@@@@@@@@@OH@@@OH@@@@@@@@@@@@@@@@CL@@@@CN@@@@@@@@@@@@@@@@O@@@@@@GH@@@@@@@@@@@@@@CL@@@@@@AN@@@@@@@@@@@@@AO@@@@@@@@GL@@@@@@@@@@@@GL@@@@@@@@AO@@@@@@@@@@@AN@@@@@@@@@@CL@@@@@@@@@@GH@@@@@@@@@@@O@@@@@@@@@CN@@@@@@@@@@@@CL@@@@@@@@OH@@@@@@@@@@@@@OH@@@@@@CL@@@@@@@@@@@@@@CN@@@@@@O@@@@@@@@@@@@@@@@GH@@@@CL@@@@@@@@@@@@@@@@AN@@@@O@@@@@@@@@@@@@@@@@@GH@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@O@@@@@@@@@@@@@@@@@@GL@@@ML@@@@@@@@@@@@@@@@ALL@@@LN@@@@@@@@@@@@@@@@CHL@@@LCH@@@@@@@@@@@@@@@N@L@@@LAL@@@@@@@@@@@@@@CL@L@@@L@G@@@@@@@@@@@@@@G@@L@@@L@CL@@@@@@@@@@@@AL@@L@@@L@@N@@@@@@@@@@@@CH@@L@@@L@@CH@@@@@@@@@@@N@@@L@@@L@@AL@@@@@@@@@@AL@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@@@@@@@@@@@@@@@@@@L@@@L@@N@@@@@@@@@@@@CH@@L@@@L@CL@@@@@@@@@@@@AL@@L@@@L@G@@@@@@@@@@@@@@G@@L@@@LAL@@@@@@@@@@@@@@CL@L@@@LCH@@@@@@@@@@@@@@@N@L@@@LN@@@@@@@@@@@@@@@@CHL@@@ML@@@@@@@@@@@@@@@@ALL@@@O@@@@@@@@@@@@@@@@@@GL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@
                        #*(82 72)@@@@@@@@@GO@@@@@@@@@@@@@@@@@@@@@AOOL@@@@@@@@@@@@@@@@@@@@GOOO@@@@@@@@@@@@@@@@@@@COOOOL@@@@@@@@@@@@@@@@@@OOOOOOH@@@@@@@@@@@@@@@@COOOOOON@@@@@@@@@@@@@@@@OOOOOOOOH@@@@@@@@@@@@@@COOOOOOOON@@@@@@@@@@@@@AOOOOOOOOOOL@@@@@@@@@@@@GOOOOOOOOOOO@@@@@@@@@@@AOOOOOOOOOOOOL@@@@@@@@@@GOOOOOOOOOOOOO@@@@@@@@@COOOOOOOOOOOOOOL@@@@@@@@OOOOOOOOOOOOOOOOH@@@@@@COOOOOOOOOOOOOOOON@@@@@@OOOOOOOOOOOOOOOOOOH@@@@COOOOOOOOOOOOOOOOOON@@@@OOOOOOOOOOOOOOOOOOOOH@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@OOOOOOOOOOOOOOOOOOOOL@@@
                        (8 8 64 36)))
(DECLARE%: EVAL@COMPILE DONTCOPY 
(DECLARE%: EVAL@COMPILE

(RECORD SENDINGCOMMAND (COMMAND ITEM MENU MESSAGE MESSAGEPARSE)
                       [TYPE? (AND (LISTP DATUM)
                                   (FMEMB (fetch COMMAND of DATUM)
                                          '(%##SEND## %##SAVE## %##FORGETIT##])
)

(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS \LAFITE.REPORT.MACHINE LAFITECURRENTEDITORWINDOWS LAFITEEDITORFONT LAFITEEDITORREGION 
       LAFITEFORMATMENU LAFITEFORMSMENUITEMS LAFITEFORMATMENUITEMS LAFITEFORWARDSTRINGS 
       LAFITEFORWARDSUBJECTSTR LAFITESENDINGMENUITEMS LAFITESPECIALFORMS LAFITESUPPORT LISPSUPPORT 
       MAKESYSDATE MESSAGESTR RECIPIENTSSTR SUBJECTSTR LAFITE.MSG.ICON LAFITEFORMDIRECTORIES 
       LAFITE.SEND.FORMATTED)
)


(FILESLOAD (SOURCE)
       LAFITEDECLS)

(DECLARE%: DOEVAL@COMPILE DONTCOPY

(LOCALVARS . T)
)
)
(PUTPROPS LAFITESEND COPYRIGHT ("Xerox Corporation" 1984 1985 1986 1987 1988 1989 1990 1993 1999 2000 
2021))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5539 28516 (DOLAFITESENDINGCOMMAND 5549 . 6039) (\SENDMESSAGE.INITIATE 6041 . 7980) (
\SENDMSG.DELIVER 7982 . 8590) (\SENDMSG.EXIT.TEDIT 8592 . 8963) (\SENDMSG.SAVE.FORM 8965 . 10952) (
\LAFITE.HEADER.EOF 10954 . 11247) (\LAFITE.INSERT.REPLYTO 11249 . 11857) (\SENDMSG.REPLYTO 11859 . 
12418) (\SENDMSG.CHANGE.MODE 12420 . 17996) (\SENDMSG.FIND.FIELD 17998 . 18508) (\SENDMESSAGE.PARSE 
18510 . 19306) (\LAFITE.PREPARE.SEND 19308 . 22141) (\LAFITE.PREPARE.ERROR 22143 . 23325) (
\LAFITE.CHOOSE.MSG.FORMAT 23327 . 25968) (LAFITE.MAKE.PLAIN.TEXTSTREAM 25970 . 26895) (
\SENDMESSAGE.MENUPROMPT 26897 . 27760) (\SENDMESSAGE.PROMPT 27762 . 28298) (\SENDMESSAGEFAIL 28300 . 
28514)) (28517 52962 (\SENDMESSAGE 28527 . 29879) (\SENDMESSAGE.RESTARTABLE 29881 . 34865) (
\SENDMESSAGE.CLEANUP 34867 . 35083) (\SENDMESSAGE.MAKEWINDOW 35085 . 41258) (MAKELAFITEDELIVERMENU 
41260 . 41567) (\LAFITE.CLOSEMSG? 41569 . 42519) (\LAFITE.AFTER.DELIVER 42521 . 45840) (
\LAFITE.UNSENT.ICON 45842 . 46152) (\LAFITE.FETCH.SUBJECT 46154 . 46954) (LAFITE.SENDMESSAGE 46956 . 
47849) (\SENDMESSAGE0 47851 . 50715) (LA.ASSURE.PROMPT.WINDOW 50717 . 51614) (\LAFITE.SEND.FAIL 51616
 . 52087) (\LAFITE.INVALID.RECIPIENTS 52089 . 52547) (\SENDMESSAGE.ABORT 52549 . 52960)) (52994 62907 
(\OUTBOX.CREATE 53004 . 54467) (\OUTBOX.RESET 54469 . 54962) (\OUTBOX.CLOSEFN 54964 . 55104) (
\OUTBOX.REPAINTFN 55106 . 55769) (\OUTBOX.RESHAPEFN 55771 . 57054) (\OUTBOX.SHADEITEM 57056 . 57729) (
\OUTBOX.BUTTONFN 57731 . 60579) (\OUTBOX.DISPLAYLINE 60581 . 61075) (\OUTBOX.ADD.ITEM 61077 . 62905)) 
(63203 79611 (\LAFITE.MESSAGEFORM 63213 . 67556) (MAKELAFITESUPPORTFORM 67558 . 67747) (
MAKELISPSUPPORTFORM 67749 . 67915) (MAKEXXXSUPPORTFORM 67917 . 71966) (MAKENEWMESSAGEFORM 71968 . 
72924) (MAKELAFITEPRIVATEFORMSITEMS 72926 . 73354) (\LAFITE.UNCACHE.MESSAGEFORM 73356 . 73809) (
\LAFITE.DELETE.MESSAGEFORM 73811 . 74412) (\LAFITE.SELECT.FORM 74414 . 74769) (
\LAFITE.DELETE.FORM.INTERNAL 74771 . 75915) (\LAFITE.READ.FORM 75917 . 78654) (\LAFITE.FIND.TEMPLATE 
78656 . 79609)) (79635 87366 (\LAFITE.ANSWER 79645 . 80050) (\LAFITE.ANSWER.PROC 80052 . 81946) (
MAKEANSWERFORM 81948 . 84478) (LA.PRINT.COMMA.LIST 84480 . 84966) (LAFITE.FILL.IN.ANSWER.FORM 84968 . 
87364)) (87391 93587 (\LAFITE.FORWARD 87401 . 87809) (\LAFITE.FORWARD.PROC 87811 . 89800) (
MAKEFORWARDFORM 89802 . 93585)))))
STOP
