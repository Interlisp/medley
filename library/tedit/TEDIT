(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "20-Jun-2023 23:09:30" {WMEDLEY}<library>tedit>TEDIT.;296 113439 

      :EDIT-BY rmk

      :CHANGES-TO (FNS TEDIT.INSERT.OBJECT)

      :PREVIOUS-DATE "17-Jun-2023 17:16:35" {WMEDLEY}<library>tedit>TEDIT.;295)


(* ; "
Copyright (c) 1983-1993, 1995, 1999, 2018, 2021 by Venue & Xerox Corporation.
")

(PRETTYCOMPRINT TEDITCOMS)

(RPAQQ TEDITCOMS
       [(COMS                                                (* ; "Loadup stuff")
              (VARS TEDITFILES)
              (FNS MAKE-TEDIT-EXPORTS.ALL UPDATE-TEDIT EDIT-TEDIT)
              (DECLARE%: DONTEVAL@LOAD DONTCOPY DONTEVAL@COMPILE 

                     (* ;; "This gets EXPORTS.ALL in the TEDIT-EXPORTS.ALL")

                     (EXPORT (FILES EXPORTS.ALL)))
              (DECLARE%: DONTCOPY (FILES TEDIT-EXPORTS.ALL)))
        (DECLARE%: EVAL@COMPILE DONTCOPY 

               (* ;; "Assertions go to comments if not being checked, so we see value-warnings")

               (EXPORT (COMS (MACROS TEDIT-ASSERT)
                             (GLOBALVARS CHECK-TEDIT-ASSERTIONS)
                             (INITVARS (CHECK-TEDIT-ASSERTIONS T)))
                      (MACROS OBJECT.ALLOWS)))
        (FILES TEDIT-PCTREE TEDIT-STREAM TEDIT-COMMAND TEDIT-SCREEN TEDIT-ABBREV TEDIT-LOOKS)
        (FNS \CREATE.TEDIT.RESTART.MENU)
        (VARS (TEDIT.TERMSA.FONTS NIL)
              (TEDIT.TENTATIVE NIL)
              (TEDIT.DEFAULT.PROPS NIL)
              (TEDIT.STARTUP.MONITORLOCK (CREATE.MONITORLOCK 'TEDIT.STARTUP))
              (TEDIT.RESTART.MENU (\CREATE.TEDIT.RESTART.MENU)))
        (GLOBALVARS TEDIT.TENTATIVE TEDIT.DEFAULT.PROPS)
        
        (* ;; "Unslashed functions.  Public?")

        (FNS TEDIT COERCETEXTOBJ TEDITSTRING TEDIT-SEE TEDIT.CHARWIDTH TEDIT.COPY \TEDIT.MOVE.MSG 
             TEDIT.DELETE TEDIT.INSERT TEDIT.KILL TEDIT.MAPPIECES TEDIT.QUIT 
             TEDIT.DO.BLUEPENDINGDELETE TEDIT.MOVE TEDIT.STRINGWIDTH TEXTOBJ TEDITMENUP TEXTSTREAM)
        
        (* ;; "Slashed functions. Private?")

        (FNS \TEDIT2 \TEDIT.INSERT \TEDIT.MOVE.SELPIECES \TEDIT.REPLACE.SELPIECES 
             \TEDIT.INSERT.SELPIECES \TEDIT.OBJECT.SHOWSEL \TEDIT.RESTARTFN \TEDIT.CHARDELETE 
             \TEDIT.COPYPIECE \TEDIT.APPLY.OBJFN \TEDIT.DELETE \TEDIT.DIFFUSE.PARALOOKS 
             \TEDIT.FOREIGN.COPY? \TEDIT.QUIT \TEDIT.WORDDELETE \TEDIT1)
        (FNS \SELPIECES.COPY \SELPIECES.CONCAT \SELPIECES.CHARTRANSFORM \SELPIECES.FROM.STRING 
             \SELPIECES.TO.STRING)
        (P (MOVD? 'NILL 'OBJECTOUTOFTEDIT))
                                                             (* ; 
                                    "HOOK for looked-string copy, etc. Used in \TEDIT.FOREIGN.COPY?.")
        (COMS                                                (* ; "Object-oriented editing")
              (FNS TEDIT.INSERT.OBJECT TEDIT.EDIT.OBJECT TEDIT.FIND.OBJECT TEDIT.PUT.OBJECT 
                   TEDIT.GET.OBJECT TEDIT.OBJECT.CHANGED TEDIT.MAP.OBJECTS)
              (FILES IMAGEOBJ))
        
        (* ;; "Would be nice to just do (DOFILESLOAD (CDR TEDITFILES)).  But the order for exports.all and the order for loading have to be aligned.")

        (FILES TEDIT-FIND TEDIT-HISTORY TEDIT-FILE TEDIT-WINDOW TEDIT-SELECTION TEDIT-TFBRAVO 
               TEDIT-HCPY TEDIT-PAGE TEDIT-MENU TEDIT-FNKEYS)
        (COMS                                                (* ; "TEDIT Support information")
              (E (SETQ TEDITSYSTEMDATE (DATE)))
              (VARS TEDITSYSTEMDATE))
        (COMS                                                (* ; 
                           "LISTFILES Interface, so the system can decide if a file is a TEdit file.")
              (ADDVARS (PRINTFILETYPES (TEDIT (TEST \TEDIT.FORMATTEDP1)
                                              (EXTENSION (TEDIT])



(* ; "Loadup stuff")


(RPAQQ TEDITFILES (TEDIT TEDIT-PCTREE TEDIT-SELECTION TEDIT-SCREEN TEDIT-STREAM TEDIT-COMMAND 
                         TEDIT-FILE TEDIT-LOOKS TEDIT-WINDOW TEDIT-MENU TEDIT-FIND TEDIT-FNKEYS 
                         TEDIT-HCPY TEDIT-HISTORY TEDIT-PAGE TEDIT-ABBREV TEDIT-TFBRAVO))
(DEFINEQ

(MAKE-TEDIT-EXPORTS.ALL
  [LAMBDA NIL                                                (* ; "Edited 11-Sep-2022 23:43 by rmk")
    (CNDIR (MEDLEYDIR "library>tedit"))
    (GATHEREXPORTS TEDITFILES (MEDLEYDIR "library/tedit" "tedit-exports.all" T])

(UPDATE-TEDIT
  [LAMBDA (FILES)                                            (* ; "Edited 26-Oct-2022 21:10 by rmk")

    (* ;; "updates sysout with new versions of loaded files.  Keeps the extension")

    (FOR FILE DIRFILE LOADEDFILE INSIDE (OR FILES TEDITFILES)
       WHEN [AND (SETQ LOADEDFILE (FOR F IN LOADEDFILELST WHEN (EQ FILE (FILENAMEFIELD F 'NAME))
                                     DO (RETURN F)))
                 (SETQ DIRFILE (INFILEP (PACKFILENAME 'VERSION NIL 'BODY LOADEDFILE]
       UNLESS (EQ LOADEDFILE DIRFILE) COLLECT (LOAD DIRFILE T])

(EDIT-TEDIT
  [LAMBDA NIL                                                (* ; "Edited 17-Jun-2023 10:00 by rmk")
                                                             (* ; "Edited 25-Apr-2023 17:39 by rmk")
                                                             (* ; "Edited 26-Oct-2022 21:12 by rmk")
                                                             (* ; "Edited 14-Sep-2022 08:37 by rmk")
    (BKSYSBUF " ")
    (RESETLST
        (RESETSAVE LOADDBFLG 'YES)
        (FOR F IN TEDITFILES DO (LOADCOMP F)
                                (LOADFROM F)))
    (%. ANALYZE ON IN TEDITFILES])
)
(DECLARE%: DONTEVAL@LOAD DONTCOPY DONTEVAL@COMPILE 
(* "FOLLOWING DEFINITIONS EXPORTED")
(FILESLOAD EXPORTS.ALL)

(* "END EXPORTED DEFINITIONS")

)
(DECLARE%: DONTCOPY 

(FILESLOAD TEDIT-EXPORTS.ALL)
)
(DECLARE%: EVAL@COMPILE DONTCOPY 
(* "FOLLOWING DEFINITIONS EXPORTED")(DECLARE%: EVAL@COMPILE 

(PUTPROPS TEDIT-ASSERT MACRO [ARGS (COND
                                      [CHECK-TEDIT-ASSERTIONS
                                       `(CL:UNLESS ,(CAR ARGS)
                                            [HELP "TEDIT-ASSERT FAILURE" ,(KWOTE (CAR ARGS])]
                                      (T `                   (* (TEDIT-ASSERT (\,@ ARGS)))])
)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS CHECK-TEDIT-ASSERTIONS)
)

(RPAQ? CHECK-TEDIT-ASSERTIONS T)
(DECLARE%: EVAL@COMPILE 

(PUTPROPS OBJECT.ALLOWS MACRO ((PC OPERATION FROMTOBJ TOTOBJ)
                               (OR (NOT (EQ OBJECT.PTYPE (PTYPE PC)))
                                   (\TEDIT.APPLY.OBJFN (PCONTENTS PC)
                                          OPERATION FROMTOBJ TOTOBJ))))
)

(* "END EXPORTED DEFINITIONS")

)

(FILESLOAD TEDIT-PCTREE TEDIT-STREAM TEDIT-COMMAND TEDIT-SCREEN TEDIT-ABBREV TEDIT-LOOKS)
(DEFINEQ

(\CREATE.TEDIT.RESTART.MENU
  [LAMBDA NIL                                                (* ; "Edited 28-Aug-2022 22:45 by rmk")

    (* ;; "Separate dinky function to compile for SUNLOADUP without DWIM.")

    (CREATE MENU
           ITEMS _ '(NewEditProcess])
)

(RPAQQ TEDIT.TERMSA.FONTS NIL)

(RPAQQ TEDIT.TENTATIVE NIL)

(RPAQQ TEDIT.DEFAULT.PROPS NIL)

(RPAQ TEDIT.STARTUP.MONITORLOCK (CREATE.MONITORLOCK 'TEDIT.STARTUP))

(RPAQ TEDIT.RESTART.MENU (\CREATE.TEDIT.RESTART.MENU))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDIT.TENTATIVE TEDIT.DEFAULT.PROPS)
)



(* ;; "Unslashed functions.  Public?")

(DEFINEQ

(TEDIT
  [LAMBDA (TEXT WINDOW DONTSPAWN PROPS)

    (* ;; "Edited 10-Aug-2022 11:24 by rmk")

    (* ;; "Edited 22-Jun-2022 20:01 by rmk: Call to OPENSTREAM passes FORMAT from PROPS")

    (* ;; "Edited 22-Jun-2022 19:58 by rmk")

    (* ;; "Edited  4-Jun-2022 15:42 by rmk")

    (* ;; "Edited 31-Jan-2022 17:19 by rmk: String TEXT is a file name")

    (* ;; "Edited 24-Dec-2021 19:21 by rmk")

    (* ;; "Edited 11-Jun-99 14:14 by rmk:")

    (* ;; "Edited  3-Jun-88 14:27 by jds")

    (* ;; "User entry to the text editor.  Takes an optional window to be used for editing")

    (* ;; "DONTSPAWN => Don't try to create a new process for this edit.")

    (PROG (PROC TEDITCREATEDWINDOW)
          (CL:WHEN (AND TEXT (OR (LITATOM TEXT)
                                 (STRINGP TEXT)
                                 (CL:PATHNAMEP TEXT)))       (* ; 
                                        "Make sure the file exists before trying to open the window.")
              [SETQ TEXT (OPENSTREAM TEXT 'INPUT 'OLD `((TYPE TEXT)
                                                        (FORMAT ,(LISTGET PROPS 'FORMAT])
          (CL:WHEN (AND WINDOW (OR (LITATOM WINDOW)
                                   (REGIONP WINDOW)))        (* ; 
                                                  "Pass specified and typed regions to TEDIT.CREATEW")
              (PUSH PROPS 'REGION-TYPE WINDOW)
              (SETQ WINDOW NIL))
          (RESETLST
              [RESETSAVE NIL `(AND ,WINDOW (WINDOWPROP ,WINDOW 'TEXTOBJ NIL]
              (WITH.MONITOR TEDIT.STARTUP.MONITORLOCK
                  (CL:UNLESS WINDOW
                      (SETQ TEDITCREATEDWINDOW T)
                      (SETQ WINDOW (COND
                                      [(OR (LISTGET PROPS 'REGION-TYPE)
                                           (NOT TEDIT.DEFAULT.WINDOW)
                                           (\TEDIT.ACTIVE.WINDOWP TEDIT.DEFAULT.WINDOW))
                                       (TEDIT.CREATEW (COND
                                                         (TEXT (CONCAT 
                                                              "Please specify an editing window for "
                                                                      (FULLNAME TEXT)))
                                                         (T 
                                                    "Please specify a region for the editing window."
                                                            ))
                                              TEXT
                                              (APPEND PROPS (COPY TEDIT.DEFAULT.PROPS]
                                      (T (\TEDIT.CREATEW.FROM.REGION (WINDOWPROP TEDIT.DEFAULT.WINDOW
                                                                            'REGION)
                                                TEXT
                                                (APPEND PROPS (COPY TEDIT.DEFAULT.PROPS)))
                                                             (* ; "Replace the old title")
                                         TEDIT.DEFAULT.WINDOW)))

                      (* ;; 
                 "Mark the newly-created window reserved until the OPENTEXTSTREAM has done its work.")

                      (* ;; 
                      "mark that we created the window so that we know we can update the title, etc.")

                      (WINDOWPROP WINDOW 'TEXTOBJ T))))
          [SETQ TEXT (OPENTEXTSTREAM TEXT WINDOW NIL NIL (APPEND PROPS '(BEING-EDITED T]
                                                             (* ; "Connect the editor to the window")
          (replace (TEXTOBJ TXTEDITING) of (TEXTOBJ TEXT) with T)
                                                             (* ; "For the moment, mark the document as actively in edit. (so caret flashes when the window is first brought up.)")
          (CL:WHEN TEDITCREATEDWINDOW
              (TEXTPROP TEXT 'TEDITCREATEDWINDOW 'T))
          (COND
             (DONTSPAWN                                      (* ; 
                                     "Either no processes running, or specifically not to spawn one.")
                    (RETURN (\TEDIT2 TEXT WINDOW T)))
             (T                                              (* ; "Spawn a process to do the edit.")
                [SETQ PROC (ADD.PROCESS (LIST '\TEDIT2 (KWOTE TEXT)
                                              WINDOW NIL)
                                  'NAME
                                  'TEdit
                                  'RESTARTABLE
                                  'HARDRESET
                                  'RESTARTFORM
                                  (LIST '\TEDIT.RESTARTFN (KWOTE TEXT)
                                        WINDOW
                                        (KWOTE PROPS]
                (PROCESSPROP PROC 'WINDOW WINDOW)
                (COND
                   ((NOT (LISTGET (APPEND PROPS (COPY TEDIT.DEFAULT.PROPS))
                                'LEAVETTY))                  (* ; 
                              "Unless he asked us to leave the tty where it is, TEdit should get it.")
                    (TTY.PROCESS PROC)))
                (RETURN PROC])

(COERCETEXTOBJ
  [LAMBDA (TSTREAM TYPE OUTPUTSTREAM)                        (* ; "Edited  8-May-2023 13:25 by rmk")
                                                             (* ; "Edited  4-May-2023 12:13 by rmk")
                                                             (* ; "Edited 11-Jun-99 15:10 by rmk:")
                                                             (* ; "Edited 18-Apr-93 23:42 by jds")

    (* ;; "Coerce the contents of the TEXOTBJ to be of the given type.  This is for making a string from a textobj, e.g.")

    (* ;; "RMK: moved the string case up from the piece level to the stream-BIN level")

    (LET ((TEXTOBJ (TEXTOBJ TSTREAM))
          OFILE FMTFILE)
         (SETQ TSTREAM (TEXTSTREAM TEXTOBJ))
         (SELECTQ TYPE
             ((STRINGP STRING) 
                  (\TEXTSETFILEPTR TSTREAM 0)                (* ; 
                                      "This gets underneath the OPENP test of the generic SETFILEPTR")

                  (* ;; "Allocstring presumably errors if greater than max stringsize.")

                  [for CH# (STR _ (ALLOCSTRING (TEXTLEN TEXTOBJ))) from 1 to (TEXTLEN TEXTOBJ)
                     do (RPLCHARCODE STR CH# (BIN TSTREAM))
                     finally (RETURN (OR (SUBSTRING STR 1 (SUB1 CH#))
                                         (CONCAT ""])
             (STREAM                                         (* ; 
                         "Does this code make sense if pieces are on different files, e.g. INCLUDE ?")
                     (CL:WHEN (type? STREAM (fetch (TEXTOBJ TXTFILE) of TEXTOBJ))
                         [OPENFILE (fetch (STREAM FULLNAME) of (fetch (TEXTOBJ TXTFILE) of TEXTOBJ))
                                'INPUT NIL '((TYPE TEXT]
                         (replace (STREAM ACCESSBITS) of (fetch (TEXTOBJ TXTFILE) of TEXTOBJ)
                            with ReadBit))
                     (SETFILEPTR TSTREAM 0)
                     TSTREAM)
             (FILE [SETQ OFILE (OR (AND OUTPUTSTREAM (OPENP OUTPUTSTREAM 'OUTPUT))
                                   (OPENSTREAM '{NODIRCORE} 'BOTH 'NEW]
                   (TEDIT.PUT.PCTB TEXTOBJ OFILE)
                   (OR OUTPUTSTREAM (CLOSEF OFILE))
                   OFILE)
             (SPLIT 
                    (* ;; "I.e., Return 2 files, one with plain text, one with formatting info, such that concatenating them will do the right thing.")

                    (SETQ OFILE (OPENFILE '{NODIRCORE} 'BOTH 'NEW))
                    (SETQ FMTFILE (CAR (TEDIT.PUT.PCTB TEXTOBJ (\GETSTREAM OFILE 'BOTH)
                                              NIL T)))
                    (CLOSEF OFILE)
                    (CONS OFILE FMTFILE))
             NIL])

(TEDITSTRING
  [LAMBDA (TEXT WINDOW DONTSPAWN PROPS)

(* ;;; "Edited  9-May-2023 21:55 by rmk")

(* ;;; "Edited 23-May-2022 15:52 by rmk")

(* ;;; "Edited 19-May-2022 22:46 by rmk: An interface function to replace calls to TEDIT when the text argument may be the string to be edited rather than the name of a file.  This enables the transition that gets TEDIT aligned with the convention that strings, as well as litatoms, are file names")

    (TEDIT (IF (STRINGP TEXT)
               THEN (LET ((TSTR (OPENTEXTSTREAM)))
                         (TEDIT.INSERT TSTR TEXT 1 NIL T)
                         (TEDIT.SETSEL TSTR 1 0 'LEFT)
                         TSTR)
             ELSE TEXT)
           WINDOW DONTSPAWN PROPS])

(TEDIT-SEE
  [LAMBDA (FILE WINDOW FORMAT TITLE)

    (* ;; "Edited 13-Sep-2022 09:21 by rmk:  Changed the default format here to the current Medley default.  This shouldn't be special.")

    (* ;; "Edited  5-May-2022 15:18 by rmk")

    (* ;; "Edited 27-Feb-2021 20:07 by rmk:")

    (* ;; "Edited  1-Feb-88 19:00 by bvm:")

    (* ;; "See FILE in a scrollable READONLY TEDIT window.  If FILE is a LISP source file, copy first to a temporary NODIRCORE image file that interpretes the fontchange characters rather than showing black boxes.")

    (* ;; "FORMAT for text files defaults to to our *DEFAULT-EXTERNALFORMAT*")

    (CL:WITH-OPEN-FILE (STREAM FILE :DIRECTION :INPUT)
           (LET ((SEESTREAM STREAM)
                 TSTREAM)

                (* ;; "No need to fiddle with a TEDIT file")

                (IF (\TEDIT.FORMATTEDP1 STREAM)
                  ELSEIF (LISPSOURCEFILEP STREAM)
                    THEN 
                         (* ;; "Lisp source file")

                         (SETQ SEESTREAM (OPENTEXTSTREAM))
                         (APPLY* (FUNCTION SEE)
                                STREAM SEESTREAM)
                  ELSE 
                       (* ;; "Not a Lisp source file and not a Tedit file.  If it is not random access, we copy it so we can scroll around.")

                       (* ;; "Maybe there is a conventional way of finding out the external format of a plain-text stream (an EMACS header?), otherwise we use our default.")

                       (SETFILEINFO STREAM 'FORMAT (OR FORMAT :DEFAULT))
                       (CL:UNLESS (RANDACCESSP STREAM)
                           (SETQ SEESTREAM (OPENSTREAM '{NODIRCORE} 'BOTH 'NEW))
                           (COPYCHARS STREAM SEESTREAM)))
                [SETQ TSTREAM (TEXTSTREAM (TEDIT SEESTREAM WINDOW NIL
                                                 `(READONLY T LEAVETTY T FONT ,DEFAULTFONT]
                [WINDOWPROP (WFROMDS TSTREAM)
                       'TITLE
                       (OR TITLE (CONCAT "SEE window for " (FULLNAME STREAM]
                TSTREAM])

(TEDIT.CHARWIDTH
  [LAMBDA (CH FONT TERMSA)                                   (* jds "22-OCT-83 19:32")
          
          (* Returns the width of CH in FONT printed according to any special printing 
          instructions in CHARTABLE TERMSA)

    (COND
       (TERMSA                                               (* There IS a TERMTABLE to account for)
              (SELECTC (fetch CCECHO of (\SYNCODE TERMSA CH))
                  (INDICATE.CCE (IPLUS (COND
                                          ((IGREATERP CH 127)(* META character)
                                           (SETQ CH (LOGAND CH 127))
                                           (CHARWIDTH (CHARCODE %#)
                                                  FONT))
                                          (T 0))
                                       (COND
                                          ((ILESSP CH 32)    (* CONTROL character)
                                           (SETQ CH (LOGOR CH 64))
                                           (CHARWIDTH (CHARCODE ^)
                                                  FONT))
                                          (T 0))
                                       (CHARWIDTH CH FONT)))
                  (SIMULATE.CCE (SELCHARQ CH
                                     ((EOL CR LF) 
                                          (IMAX 6 (CHARWIDTH CH FONT)))
                                     (ESCAPE (CHARWIDTH (CHARCODE $)
                                                    FONT))
                                     (BELL 0)
                                     (TAB 36)
                                     (CHARWIDTH CH FONT)))
                  (REAL.CCE (CHARWIDTH CH FONT))
                  (IGNORE.CCE 0)
                  (SHOULDNT)))
       (T (* The usual case is to treat every character as a graphic.)
          (SELCHARQ CH
               (CR (IMAX 6 (CHARWIDTH CH FONT)))
               (TAB 36)
               (CHARWIDTH CH FONT])

(TEDIT.COPY
  [LAMBDA (FROM TO)                                          (* ; "Edited 20-May-2023 18:47 by rmk")
                                                             (* ; "Edited 15-May-2023 22:11 by rmk")
                                                             (* ; "Edited  4-Jun-92 11:11 by jds")

    (* ;; "Copy the FROM-selected pieces into the destination object and position as indicated by the TO selection.")

    (TEDIT.MOVE FROM TO 'copy])

(\TEDIT.MOVE.MSG
  [LAMBDA (FROM TO COPYFLG)                                  (* ; "Edited 22-May-2023 09:35 by rmk")
                                                             (* ; "Edited 20-May-2023 18:53 by rmk")
                                                             (* ; "Edited 15-May-2023 22:11 by rmk")
                                                             (* ; "Edited  4-Jun-92 11:11 by jds")

    (* ;; "Check whether it is possible to insert the FROM-selected pieces into the TO-selection.  Value is NON-NIL if the operation can't be performed.")

    (LET ((FROMOBJ (AND FROM (GETSEL FROM SELTEXTOBJ))
                 (AND FROM (GETSEL FROM SET)
                      (GETSEL FROM SELTEXTOBJ)))
          (TOOBJ (AND TO (GETSEL TO SET)
                      (GETSEL TO SELTEXTOBJ)))
          (TYPE (CL:IF COPYFLG
                    "copy"
                    "move"))
          MSG)
         (SETQ MSG (if (NOT (AND FROM (GETSEL FROM SET)))
                       then (CONCAT "Please select a source for the " TYPE)
                     elseif (NOT (AND TOOBJ (GETSEL TO SET)))
                       then (CONCAT "Please select a destination for the " TYPE)
                     elseif (fetch (TEXTOBJ TXTREADONLY) of TOOBJ)
                       then "Destination is read only--aborted"
                     elseif (AND (EQ TYPE 'move)
                                 (fetch (TEXTOBJ TXTREADONLY) of FROMOBJ))
                       then "Source is read only--aborted"))
         (TEDIT.PROMPTPRINT (OR FROMOBJ TOOBJ)
                (OR MSG "")
                T)                                           (* ; 
                                                             "empty string to clean out old garbage")
         MSG])

(TEDIT.DELETE
  [LAMBDA (STREAM SEL LEN LEAVECARETLOOKS)                   (* ; "Edited 23-May-2023 12:57 by rmk")
                                                             (* ; "Edited 22-May-2023 10:54 by rmk")
                                                             (* ; "Edited 10-Nov-2022 22:48 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:49 by mitani")

    (* ;; "Delete the specified characters from STREAM.")

    (* ;; "If LEAVECARETLOOKS is non-NIL, the selection will NOT be set up to do the right thing with type-in.  This can save time in inner loops.")

    (LET ((TEXTOBJ (TEXTOBJ STREAM)))
         (CL:WHEN (FIXP SEL)
             (TEDIT.SETSEL STREAM SEL LEN NIL NIL LEAVECARETLOOKS)
             (SETQ SEL (fetch (TEXTOBJ SEL) of TEXTOBJ)))
         (\TEDIT.DELETE TEXTOBJ (OR SEL (fetch (TEXTOBJ SEL) of TEXTOBJ])

(TEDIT.INSERT
  [LAMBDA (TSTREAM TEXT CH#ORSEL LOOKS DONTSCROLL)           (* ; "Edited 31-May-2023 23:25 by rmk")
                                                             (* ; "Edited 27-May-2023 10:47 by rmk")
                                                             (* ; "Edited  9-Nov-2022 10:36 by rmk")
                                                             (* ; "Edited 29-May-91 18:21 by jds")

    (* ;; "Insert TEXT (litatom or string) at the appropriate spot in the text..  No-op if given something else--should it error?")

    (CL:WHEN (LITATOM TEXT)
        (SETQ TEXT (MKSTRING TEXT)))

    (* ;; "Can't insert an empty string sensibly.")

    (PROG ((TEXTOBJ (TEXTOBJ TSTREAM))
           CH# NCHARSADDED)
          (CL:WHEN (OR (NOT (STRINGP TEXT))
                       (ZEROP (NCHARS TEXT))
                       (fetch (TEXTOBJ TXTREADONLY) of TEXTOBJ))
                 (RETURN))
          (if (FIXP CH#ORSEL)
              then (TEDIT.SETSEL TEXTOBJ CH#ORSEL 1 'LEFT)   (* ; "He gave us a ch# to insert before")
                   (SETQ CH#ORSEL (TEXTSEL TEXTOBJ))
            elseif (NOT CH#ORSEL)
              then (SETQ CH#ORSEL (TEXTSEL TEXTOBJ)))
          (CL:UNLESS (AND CH#ORSEL (GETSEL CH#ORSEL SET))
              (TEDIT.PROMPTPRINT TEXTOBJ "Please select a place for the insertion." T)
              (RETURN))
          (CL:WHEN LOOKS                                     (* ; "Set up any specified looks.")
              (TEDIT.CARETLOOKS TSTREAM LOOKS))
          (\TEDIT.INSERT TEXT CH#ORSEL TEXTOBJ DONTSCROLL T])

(TEDIT.KILL
  [LAMBDA (STREAM)                                      (* ; "Edited 12-Jun-90 17:49 by mitani")
                                                             (* Force the edit session supported 
                                                           by STREAM to terminate, and to 
                                                           return VALUE)
    (COND
       ((type? STREAM STREAM)                            (* If he gave us a textofd, get the 
                                                           textobj)
        (SETQ STREAM (fetch (TEXTSTREAM TEXTOBJ) of STREAM)))
       [(WINDOWP STREAM)                                     (* Take a window, and do the obvious 
                                                           with it.)
        (SETQ STREAM (WINDOWPROP STREAM 'TEXTOBJ]
       ((type? TEXTOBJ STREAM)                           (* A Textobj is just fine)
        )
       (T                                                    (* Anything else is ungood, 
                                                           double-plus)
          (\ILLEGAL.ARG STREAM)))
    (replace (TEXTOBJ EDITFINISHEDFLG) of STREAM with T)
    (PROG (TEDW TEDPROC)
          (AND (SETQ TEDW (CAR (fetch (TEXTOBJ \WINDOW) of STREAM)))
               [PROCESSP (SETQ TEDPROC (WINDOWPROP TEDW 'PROCESS]
               (NEQ TEDPROC (THIS.PROCESS))
               (DEL.PROCESS TEDPROC)
               (TEDIT.DEACTIVATE.WINDOW TEDW])

(TEDIT.MAPPIECES
  [LAMBDA (TEXTOBJ FN FNARG)                                 (* ; "Edited  6-Nov-2022 11:12 by rmk")
                                                             (* ; "Edited  4-Nov-2022 23:11 by rmk")
                                                             (* ; "Edited  9-Sep-2022 16:08 by rmk")
                                                             (* ; "Edited 22-Aug-2022 14:28 by rmk")
                                                             (* ; "Edited 22-Jul-2022 09:19 by rmk")
                                                             (* ; "Edited 22-Apr-93 16:02 by jds")

    (* ;; "Go thru all the pieces in a document, applying a function to them serially")

    (* ;; "FN is a function of 3 args (PIECE    CH#-of-1st-char-in-piece    FNARG)")

    (for CH# from 1 by (PLEN PC) as PC inpieces (\FIRSTPIECE (TEXTOBJ TEXTOBJ))
       when (EQ (APPLY* FN CH# PC FNARG)
                'STOP) do (RETURN (LIST CH# PC])

(TEDIT.QUIT
  [LAMBDA (STREAM VALUE)                                     (* ; "Edited 10-Apr-2023 10:19 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:49 by mitani")

    (* ;; "Force the edit session supported by STREAM to terminate, and to return VALUE")

    (LET (MAINW (TEXTOBJ (TEXTOBJ STREAM)))
         (replace (TEXTOBJ EDITFINISHEDFLG) of TEXTOBJ with (OR VALUE T))
                                                             (* ; 
                                                    "tell the command loop to stop next time through")
         (CL:WHEN [AND (fetch (TEXTOBJ \WINDOW) of TEXTOBJ)
                       (NEQ (SETQ MAINW (\TEDIT.PRIMARYW TEXTOBJ))
                            (PROCESSPROP (TTY.PROCESS)
                                   'WINDOW]

             (* ;; "there is a main window of the stream, and it is not the window of the tty process, so give it the tty")

             (TTY.PROCESS (WINDOWPROP MAINW 'PROCESS))
             (AND (NEQ (TTY.PROCESS)
                       (THIS.PROCESS))
                  (until [OR (NOT (WINDOWPROP MAINW 'PROCESS))
                             (PROCESS.FINISHEDP (WINDOWPROP MAINW 'PROCESS] do 
                                                             (* ; 
                    "Wait until the Edit process has had a chance to go away before continuing here.")
                                                                               (DISMISS))))])

(TEDIT.DO.BLUEPENDINGDELETE
  [LAMBDA (SEL TEXTOBJ)                                      (* ; "Edited  4-May-2023 00:05 by rmk")
                                                             (* ; "Edited 22-Apr-2023 18:31 by rmk")
                                                             (* ; "Edited 29-May-91 18:21 by jds")

    (* ;; "Check for blue-pending-delete, and do it if it's there.")

    (* ;; "Return T if the deletion was made.  For people who need to know")

    (CL:WHEN (fetch (TEXTOBJ BLUEPENDINGDELETE) of TEXTOBJ)
        (TEDIT.RESET.EXTEND.PENDING.DELETE SEL)              (* ; "Make it a normal selection again.")
        (\TEDIT.DELETE SEL TEXTOBJ NIL))])

(TEDIT.MOVE
  [LAMBDA (FROM TO COPYFLG)                                  (* ; "Edited 27-May-2023 18:35 by rmk")
                                                             (* ; "Edited 23-May-2023 00:34 by rmk")
                                                             (* ; "Edited 18-May-2023 18:49 by rmk")
                                                             (* ; "Edited 29-May-91 18:21 by jds")

    (* ;; "Insert the pieces at FROM into the location described by TO, possibly first deleting a TO-pending-delete and unless COPYFLG also removing the FROM pieces.  The FROM and TO SEL's are saved as pre-selections.")

    (* ;; "This results in a single history event if FROM and TO are in the same TEXTOBJ--undo will restore it.")

    (* ;; "If they are in separate texts, then the modifcations to TO go into TO's history (pending delete and insert), the deletion (if not COPYFLG) of FROM is an event in its object.  In that case it will require undos in both objects to get them both back to the original state.")

    (* ;; "If the FROM object is formatted and the TO object is not, the TO is converted.  And if FROM is not but TO is, then the copied FROM-pieces are upgraded after they are installed.")

    (SETQ TEDIT.COPY.PENDING NIL)                            (* ; 
                              "Turn off the global flag that got us here--we never want to come back")
    (CL:UNLESS (\TEDIT.MOVE.MSG TO FROM COPYFLG)
        (PROG ((TOBJ (GETSEL TO SELTEXTOBJ))
               (FOBJ (GETSEL FROM SELTEXTOBJ))
               FROMPIECES)
              (\SHOWSEL FROM NIL)
              (\SHOWSEL TO NIL)

         (* ;; "Install FROM pieces  at TO, first clearing out the blue pending delete.  The move-event may be a composite of both.  If we are doing a move and not a copy in the same textobje, we assume that the FROM and TO selections are adjusted in the subfunctions.")

         (* ;; "")

         (* ;; "Grab (a copy of) the source pieces, if image object allows")

              (CL:UNLESS (SETQ FROMPIECES (\SELPIECES.COPY (\SELPIECES FROM)
                                                 (CL:IF COPYFLG
                                                     'COPY
                                                     'MOVE)
                                                 TOBJ FOBJ))
                     (RETURN))

         (* ;; "")

              (CL:WHEN (AND (fetch (TEXTOBJ FORMATTEDP) of FOBJ)
                            (NOT (fetch (TEXTOBJ FORMATTEDP) of TOBJ)))
                                                             (* ; 
                                                    "The source is formatted and the target isn't.  ")
                  (\TEDIT.CONVERT.TO.FORMATTED TOBJ))

         (* ;; "")

              (CL:UNLESS COPYFLG                             (* ; 
                                                             "If this is a move, delete the source")
                  (\TEDIT.DELETE FOBJ FROM))

         (* ;; "")

         (* ;; "Either replace or insert, depending on BLUEPENDINGDELETE")

              (if (fetch (TEXTOBJ BLUEPENDINGDELETE) of TOBJ)
                  then (replace (TEXTOBJ BLUEPENDINGDELETE) of TOBJ with NIL)
                       (SETQ TEDIT.PENDINGDEL NIL)
                       (\TEDIT.REPLACE.SELPIECES FROMPIECES TOBJ TO)
                else (\TEDIT.INSERT.SELPIECES FROMPIECES TOBJ TO))

         (* ;; "")

              (CL:WHEN (AND (EQ FOBJ TOBJ)
                            (NOT COPYFLG))                   (* ; 
                                                    "A delete/add in same textobject, combine events")
                  (\TEDIT.HISTORYADD TOBJ (LIST (\TEDIT.POPEVENT TOBJ)
                                                (\TEDIT.POPEVENT TOBJ))))
              (TEDIT.SET.SEL.LOOKS TO 'NORMAL)

         (* ;; "")

         (* ;; "If TO is formatted but FROM is not, just the copied text has to be converted.  (Could this be done implicitly after the \SELPIECE.COPY but before the insert?)")

              (CL:WHEN (AND (fetch (TEXTOBJ FORMATTEDP) of TOBJ)
                            (NOT (fetch (TEXTOBJ FORMATTEDP) of FOBJ)))
                  (\TEDIT.CONVERT.TO.FORMATTED TOBJ (GETSEL TO CH#)
                         (IPLUS (GETSEL TO CH#)
                                (GETSEL FROM DCH))))

         (* ;; "")

              (\SHOWSEL (fetch (TEXTOBJ SEL) of TOBJ)
                     NIL)
              (\SHOWSEL (fetch (TEXTOBJ SEL) of TOBJ)
                     T)))])

(TEDIT.STRINGWIDTH
  [LAMBDA (STR FONT TERMSA)                                  (* jds "19-AUG-83 14:40")
    (COND
       (TERMSA 
          
          (* We have a terminal table to take account of.
          Do so.)

              (for CH instring STR sum (TEDIT.CHARWIDTH CH FONT TERMSA)))
       (T                                                    (* Just use the native character 
                                                             widths)
          (for CH instring STR sum (SELCHARQ CH
                                        (TAB 36)
                                        (CHARWIDTH CH FONT])

(TEXTOBJ
  [LAMBDA (STREAM NOERROR)                                   (* ; "Edited 16-Jun-2023 08:21 by rmk")
                                                             (* ; "Edited  6-Jun-2023 15:23 by rmk")
                                                             (* ; "Edited 10-Apr-2023 09:32 by rmk")
                                                             (* ; "Edited  5-Apr-2023 15:12 by rmk")
                                                             (* ; "Edited 24-Mar-2023 18:01 by rmk")
                                                             (* jds "11-Jul-85 12:06")

    (* ;; "Convert from any designator of a textobj to that  textobj")

    (LET (TOBJ)
         (if (type? TEXTOBJ STREAM)
             then STREAM
           else (SETQ TOBJ (if [AND (type? STREAM STREAM)
                                    (type? TEXTOBJ (SETQ TOBJ (fetch (TEXTSTREAM TEXTOBJ)
                                                                 of STREAM]
                               then 
                                    (* ;; 
                                "Text stream - test TEXTOBJ to distinguish displaystream case below.")

                                    TOBJ
                             else (WINDOWP STREAM)
                               then (WINDOWPROP STREAM 'TEXTOBJ)
                             elseif (AND (PROCESSP STREAM)
                                         (PROCESS.WINDOW STREAM))
                               then                          (* ; "It's an edit PROCESS")
                                    (WINDOWPROP (PROCESS.WINDOW STREAM)
                                           'TEXTOBJ)
                             elseif (AND (DISPLAYSTREAMP STREAM)
                                         (SETQ TOBJ (WFROMDS STREAM)))
                               then (WINDOWPROP TOBJ 'TEXTOBJ)
                             elseif (type? SELECTION STREAM)
                               then (GETSEL STREAM SELTEXTOBJ)))
                (CL:UNLESS (OR (type? TEXTOBJ TOBJ)
                               NOERROR)
                       (ERROR STREAM "is not a Tedit document"))
                TOBJ])

(TEDITMENUP
  [LAMBDA (WINDOW TITLE)                                     (* ; "Edited 10-Apr-2023 10:14 by rmk")
    [if (WINDOWP WINDOW)
      elseif (SETQ WINDOW (TEXTOBJ WINDOW T))
        then (SETQ WINDOW (CAR (fetch (TEXTOBJ \WINDOW)
                                      WINDOW]
    (CL:WHEN (AND WINDOW (WINDOWPROP WINDOW 'TEDITMENU)
                  (WINDOWPROP WINDOW 'TEXTOBJ)
                  (CL:IF TITLE
                      (STRING.EQUAL TITLE (WINDOWPROP WINDOW 'TITLE))
                      T))
        (WINDOWPROP WINDOW 'TITLE))])

(TEXTSTREAM
  [LAMBDA (STREAM)                                           (* ; "Edited 21-Aug-2022 08:14 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:50 by mitani")
                                                             (* ; 
                                                           "Force a textobj or stream to be a stream")
    (COND
       ((AND (type? STREAM STREAM)
             (fetch (TEXTSTREAM TEXTOBJ) of STREAM)
             (type? TEXTOBJ (fetch (TEXTSTREAM TEXTOBJ) of STREAM)))
                                                             (* ; 
                                       "It's a stream, and is really a TEXT stream.  Just return it.")
        STREAM)
       ((type? TEXTOBJ STREAM)                               (* ; 
                                     "It's a TEXTOBJ, so grab the stream hint field and return that.")
        (fetch (TEXTOBJ STREAMHINT) of STREAM))
       ((AND (PROCESSP STREAM)
             (PROCESS.WINDOW STREAM))                        (* ; 
                                "It's an edit process, so grab the text stream from the edit window.")
        (WINDOWPROP (PROCESS.WINDOW STREAM)
               'TEXTSTREAM))
       [(AND (WINDOWP STREAM)
             (WINDOWPROP STREAM 'TEXTSTREAM]
       [(AND (DISPLAYSTREAMP STREAM)
             (WINDOWPROP STREAM 'TEXTSTREAM]
       (T                                                    (* ; 
                                               "Not a reasonable coercion to the text stream.  Punt.")
          (\ILLEGAL.ARG STREAM])
)



(* ;; "Slashed functions. Private?")

(DEFINEQ

(\TEDIT2
  [LAMBDA (TEXT WINDOW UNSPAWNED)                       (* ; "Edited 12-Jun-90 17:51 by mitani")

    (* ;; "Does the actual editing work, once TEDIT has OPENTEXTSTREAMed the thing to be edited.")

    (\TEDIT.COMMAND.LOOP (fetch (TEXTSTREAM TEXTOBJ) of TEXT))
                                                             (* ; "Run the editing engine")
    (CLOSEW WINDOW)                                          (* ; "Close the edit window")
    (\TEXTCLOSEF TEXT)                                       (* ; "Close the underlying files")
    (replace (STREAM ACCESSBITS) of TEXT with BothBits)
                                                             (* ; 
                                                           "But leave the stream itself accessible")
    (AND (TEXTPROP (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                'AFTERQUITFN)
         (APPLY* (TEXTPROP (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                        'AFTERQUITFN)
                WINDOW TEXT))                                (* ; 
                                             "Apply any post-window-close (and post-QUIT) function")
    (COND
       (UNSPAWNED                                            (* ; 
                    "We're not a distinct process: Send back the edited text in some suitable form")
              (COND
                 ((NEQ (fetch (TEXTOBJ EDITFINISHEDFLG) of (fetch (TEXTSTREAM
                                                                                   TEXTOBJ)
                                                                          of TEXT))
                       T)
                  (PROG1 (fetch (TEXTOBJ EDITFINISHEDFLG) of (fetch (TEXTSTREAM
                                                                                     TEXTOBJ)
                                                                            of TEXT))
                      (replace (TEXTOBJ EDITFINISHEDFLG) of (fetch (TEXTSTREAM
                                                                                    TEXTOBJ)
                                                                           of TEXT) with
                                                                                        NIL)))
                 ((STRINGP (fetch (TEXTOBJ TXTFILE) of (fetch (TEXTSTREAM TEXTOBJ
                                                                                     ) of TEXT)))
                  (COERCETEXTOBJ (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                         'STRINGP))
                 (T TEXT])

(\TEDIT.INSERT
  [LAMBDA (INSERT SEL TEXTOBJ DONTSCROLL NOTINCREMENTAL)     (* ; "Edited  6-Jun-2023 15:23 by rmk")
                                                             (* ; "Edited  2-Jun-2023 00:12 by rmk")
                                                             (* ; "Edited 27-May-2023 10:45 by rmk")
                                                             (* ; "Edited 24-May-2023 14:23 by rmk")
                                                             (* ; "Edited 29-May-91 18:22 by jds")

    (* ;; "The selection runs from CH# to CHLIM-1. The insertion is before the selection if POINT=LEFT or after the selection (POINT=RIGHT).  This translates to before CH# or before CHLIM respectively.  ")

    (* ;; "Inserts INSERT at the location picked out by the selection, and then implements all the consequences for line and screen updates. Assumes that the caller got the selection and the text set up properly.")

    (* ;; "")

    (* ;; "Text can be a string or a single charcode (only on the call from \TEDIT.COMMAND.LOOP).  ")

    (* ;; 
   "TEDIT.INSERT passes DONTSCROLL and asserts NOTINCREMENTAL, other calls do incremental scrolling.")

    (* ;; "SELECTION-SET test may be unnecessary here, TEDIT.INSERT already checks, not sure about the 2 other calls.")

    (if (fetch (TEXTOBJ TXTREADONLY) of TEXTOBJ)
        then (TEDIT.PROMPTPRINT TEXTOBJ "Text is read only--aborted" T)
      elseif (NOT (AND SEL (GETSEL SEL SET)))
        then (TEDIT.PROMPTPRINT TEXTOBJ "Please select a place for the insertion." T)
      else 
           (* ;; "Check for blue-pending-delete, and do it if it's there.")

           (CL:WHEN (fetch (TEXTOBJ BLUEPENDINGDELETE) of TEXTOBJ)
               (TEDIT.RESET.EXTEND.PENDING.DELETE SEL)       (* ; "Make it a normal selection again.")
               (\TEDIT.DELETE TEXTOBJ SEL))
           (PROG ((CHNO (TEDIT.GETPOINT TEXTOBJ SEL))
                  (PANES (fetch (TEXTOBJ \WINDOW) of TEXTOBJ))
                  NCHARSADDED)
                 (CL:WHEN PANES
                     (TEDIT.NORMALIZECARET TEXTOBJ SEL)
                     (\SHOWSEL SEL NIL))                     (* ; "Turn off any old highlights")
                 (if (CHARCODEP INSERT)
                     then (SELCHARQ INSERT
                               ((EOL %#EOL Meta,EOL)         (* ; "Need to do para breaking if EOL.")
                                    (\INSERTEOL INSERT CHNO TEXTOBJ))
                               (\INSERTCH INSERT CHNO TEXTOBJ))
                          (SETQ NCHARSADDED 1)
                   else 
                        (* ;; 
                 "It's worth a scan here to avoid the heavier per-character complexity of \INSERTCH.")

                        (if [thereis CH instring INSERT suchthat (FMEMB CH (CHARCODE (EOL %#EOL 
                                                                                          Meta,EOL]
                            then (for CH instring INSERT as NCH# from CHNO
                                    do (SELCHARQ CH
                                            ((EOL %#EOL Meta,EOL) 
                                                 (\INSERTEOL CH NCH# TEXTOBJ))
                                            (\INSERTCH CH NCH# TEXTOBJ)))
                          else (\INSERTCH INSERT CHNO TEXTOBJ))
                        (SETQ NCHARSADDED (NCHARS INSERT)))
                 (replace (TEXTOBJ \DIRTY) of TEXTOBJ with T)

            (* ;; "")

            (* ;; "The model (piece table) is now correct:  NCHARSADDED new characters have been been added in front of CHNO.  ")

                 (\TEDIT.UPDATE.LINES TEXTOBJ 'INSERTION CHNO NCHARSADDED DONTSCROLL)

            (* ;; " The insertion happened before the original CHNO, which is now shifted back by NCHARSADDED. The next insertion should also come in front of that (now displaced) character.")

                 (TEDIT.MAKEPOINT SEL (SUB1 (IPLUS CHNO NCHARSADDED))
                        'RIGHT
                        'NORMAL)
                 (CL:UNLESS DONTSCROLL (TEDIT.NORMALIZECARET TEXTOBJ SEL))
                 (\SHOWSEL SEL T)
                 (\COPYSEL SEL TEDIT.SELECTION)
                 (for PANE inside PANES do (\TEDIT.SET.WINDOW.EXTENT TEXTOBJ PANE])

(\TEDIT.MOVE.SELPIECES
  [LAMBDA (FROMPIECES TOSEL DONTDISPLAY)                     (* ; "Edited 26-May-2023 23:10 by rmk")
                                                             (* ; "Edited 23-May-2023 16:00 by rmk")

    (* ;; "Copy the piece-range FROMPIECES pieces-into the TOSELdestination object and position.")

    (NOTCALLED)
    (CL:UNLESS (ZEROP (fetch (SELPIECES SPLEN) of FROMPIECES))
        (LET ((TOBJ (GETSEL TOSEL SELTEXTOBJ))
              (FROMLEN (fetch (SELPIECES SPLEN) of FROMPIECES))
              BPEVENT IEVENT TOCH#)

             (* ;; "Prepare the TOBJ to receive the new pieces.  Turn of its current selection, clear out its pending delete,  figure out where the insert goes, convert to formatted if needed")

             (\SHOWSEL TOSEL NIL)
             (CL:WHEN (fetch (TEXTOBJ BLUEPENDINGDELETE) of TOBJ)
                 (replace (TEXTOBJ BLUEPENDINGDELETE) of TOBJ with NIL)
                 (TEDIT.SET.SEL.LOOKS TOSEL 'NORMAL)
                 (CL:WHEN (\TEDIT.DELETE TOBJ TOSEL T)       (* ; "Don't display yet")

                     (* ;; 
                     "We assume that TOSEL is one of the selections  that the deletion has adjusted")

                     (SETQ BPEVENT (\TEDIT.POPEVENT TOBJ)))
                 (SETQ TEDIT.PENDINGDEL NIL))

             (* ;; "")

             (* ;; "Now for the insertion")

             (SETQ TOCH# (TEDIT.GETPOINT NIL TOSEL))
             (\INSERTPIECES FROMPIECES TOBJ TOCH#)
             (\TEDIT.HISTORYADD TOBJ (create TEDITHISTORYEVENT
                                            THACTION _ :Insert
                                            THLEN _ FROMLEN
                                            THCH# _ TOCH#))
             (CL:WHEN BPEVENT                                (* ; "Combine into a single event")
                 (\TEDIT.HISTORYADD TOBJ (CONS (\TEDIT.POPEVENT TOBJ)
                                               (MKLIST BPEVENT))))
             (\TEDIT.UPDATE.LINES TOBJ 'INSERTION TOCH# FROMLEN DONTDISPLAY)
             (\TEDIT.ADJUST.SELS TOBJ 'INSERTION TOCH# FROMLEN TOSEL)
             (\TEDIT.UPDATE.SEL TOSEL TOCH# FROMLEN 'RIGHT)))])

(\TEDIT.REPLACE.SELPIECES
  [LAMBDA (SELPIECES TEXTOBJ SEL DONTDISPLAY)                (* ; "Edited 27-May-2023 11:22 by rmk")
                                                             (* ; "Edited 24-May-2023 22:38 by rmk")

    (* ;; "Replaces the selection SEL of TEXTOBJ with SELPIECES, either a string or SELPIECES.  Produces a :Replace history event.  TEXTOBJ will remember the inserted SELPIECES for undoing, our history event only has to keep track of the pieces it replaced. (and where they were).")

    (LET ((POINT (GETSEL SEL POINT))
          DELEVENT)                                          (* ; "Keep the SEL point in case ")
         (\SHOWSEL SEL NIL)
         (CL:WHEN (\TEDIT.DELETE TEXTOBJ SEL T)
             (SETQ DELEVENT (\TEDIT.LASTEVENT TEXTOBJ))
             (\TEDIT.INSERT.SELPIECES SELPIECES TEXTOBJ SEL DONTDISPLAY)

             (* ;; "SELPIECES is now in the TEXTOBJ; we don't want the insert event")

             (\TEDIT.POPEVENT TEXTOBJ)
             (SETTH DELEVENT THACTION :Replace)
             (SETTH DELEVENT THLEN (fetch (SELPIECES SPLEN) of SELPIECES))
             (SETTH DELEVENT THPOINT POINT)
             (\TEDIT.UPDATE.SEL SEL NIL (fetch (SELPIECES SPLEN) of SELPIECES)
                    POINT
                    'NORMAL)                                 (* ; "Maintain the original point.")

             (* ;; "Make sure SEL is off to guarantee turning on")

             (\SHOWSEL SEL NIL))
         (\SHOWSEL SEL T])

(\TEDIT.INSERT.SELPIECES
  [LAMBDA (SELPIECES TEXTOBJ SEL DONTDISPLAY)                (* ; "Edited 17-Jun-2023 17:08 by rmk")
                                                             (* ; "Edited  2-Jun-2023 12:02 by rmk")
                                                             (* ; "Edited 31-May-2023 09:56 by rmk")
                                                             (* ; "Edited 21-May-2023 23:57 by rmk")

    (* ;; "Insert SELPIECES into TEXTOBJ at SEL's caret. ")

    (* ;; 
 "\INSERTCH.HISTORY uses the first piece to decide whether it is in a consecutive run of insertions.")

    (CL:WHEN (AND SELPIECES (fetch (SELPIECES SPFIRST) of SELPIECES))
        [LET ((INSCH# (TEDIT.GETPOINT NIL SEL))
              (SPLEN (fetch (SELPIECES SPLEN) of SELPIECES)))
             (\INSERTPIECES SELPIECES TEXTOBJ INSCH#)
             (\TEDIT.UPDATE.LINES TEXTOBJ 'INSERTION INSCH# SPLEN DONTDISPLAY)
             (\TEDIT.ADJUST.SELS TEXTOBJ 'INSERTION INSCH# SPLEN)
             (\TEDIT.UPDATE.SEL (TEXTSEL TEXTOBJ)
                    INSCH# SPLEN 'RIGHT)
             (\TEDIT.HISTORYADD TEXTOBJ
                    (create TEDITHISTORYEVENT
                           THACTION _ :Insert
                           THLEN _ SPLEN
                           THCH# _ INSCH#
                           THFIRSTPIECE _ (fetch (SELPIECES SPFIRST) of SELPIECES])])

(\TEDIT.OBJECT.SHOWSEL
  [LAMBDA (TEXTOBJ SEL ON SELPANE)                           (* ; "Edited  6-Jun-2023 15:28 by rmk")
                                                             (* ; "Edited  1-May-2023 14:36 by rmk")
                                                             (* ; "Edited  9-Apr-2023 15:37 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:50 by mitani")

    (* ;; "We are hilighting (or dehilighting) a selected object.  Let it know.")

    (CL:WHEN SELPANE
        [LET ((X0 (GETSEL SEL X0))
              (Y (GETSEL SEL Y0))
              (L1 (CAR (GETSEL SEL L1)))
              (OBJ (GETSEL SEL SELOBJ))
              (XOFFSET (DSPXOFFSET NIL SELPANE))
              (YOFFSET (DSPYOFFSET NIL SELPANE))
              (IMAGEFN (IMAGEOBJPROP (GETSEL SEL SELOBJ)
                              'WHENOPERATEDONFN))
              (WWIDTH (WINDOWPROP SELPANE 'WIDTH))
              (WHEIGHT (WINDOWPROP SELPANE 'HEIGHT))
              IMAGEBOX)
             (CL:WHEN (INSIDE? (CREATEREGION 0 0 WWIDTH WHEIGHT)
                             X0 Y)                           (* ; 
                                                        "Only do this if the selection is on-screen.")
                 (SETQ IMAGEBOX (OR (IMAGEOBJPROP OBJ 'BOUNDBOX)
                                    (APPLY* (IMAGEOBJPROP OBJ 'IMAGEBOXFN)
                                           OBJ SELPANE)))
                 (CL:WHEN L1

                     (* ;; "There's really a line this selection is being displayed on, so we need to use the YBASE of the line- the object's descent, rather than the YBOT, which is what Y0 is.")

                     (SETQ Y (- (GETLD L1 YBASE)
                                (fetch (IMAGEBOX YDESC) of IMAGEBOX))))
                 (RESETLST
                     [RESETSAVE (DSPXOFFSET (IDIFFERENCE (IPLUS X0 XOFFSET)
                                                   (fetch XKERN of IMAGEBOX))
                                       SELPANE)
                            (LIST (FUNCTION DSPXOFFSET)
                                  XOFFSET
                                  (WINDOWPROP SELPANE 'DSP]
                     (RESETSAVE (DSPYOFFSET (IPLUS Y YOFFSET)
                                       SELPANE)
                            (LIST (FUNCTION DSPYOFFSET)
                                  YOFFSET SELPANE))
                     (RESETSAVE (DSPCLIPPINGREGION (create REGION
                                                          LEFT _ 0
                                                          BOTTOM _ 0
                                                          WIDTH _ (IMIN (GETSEL SEL DX)
                                                                        (IDIFFERENCE
                                                                         (fetch (TEXTOBJ WRIGHT)
                                                                            of TEXTOBJ)
                                                                         X0))
                                                          HEIGHT _ (fetch YSIZE of IMAGEBOX))
                                       SELPANE)
                            (LIST (FUNCTION DSPCLIPPINGREGION)
                                  (DSPCLIPPINGREGION NIL SELPANE)
                                  SELPANE))
                     [AND IMAGEFN (ERSETQ (APPLY* IMAGEFN OBJ SELPANE (COND
                                                                         (ON 'HIGHLIGHTED)
                                                                         (T 'UNHIGHLIGHTED))
                                                 SEL
                                                 (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ]))])])

(\TEDIT.RESTARTFN
  [LAMBDA (TEXT WINDOW PROPS)                                (* ; "Edited 21-Aug-2022 08:13 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:51 by mitani")
                                                             (* ; "Restarts a TEdit session.")
    (replace (TEXTOBJ \WINDOW) of (fetch (TEXTSTREAM TEXTOBJ) of TEXT) with NIL)
                                                             (* ; 
                                                         "Unattach the window, so we do a redisplay.")
    (LET [(ODIRTY (fetch (TEXTOBJ \DIRTY) of (fetch (TEXTSTREAM TEXTOBJ) of TEXT]
         (SETQ TEXT (OPENTEXTSTREAM TEXT WINDOW NIL NIL PROPS))
         (replace (TEXTOBJ \DIRTY) of (fetch (TEXTSTREAM TEXTOBJ) of TEXT) with ODIRTY))
                                                             (* ; 
                                                             "Now reconnect the world together again")
    (\TEDIT.COMMAND.LOOP (fetch (TEXTSTREAM TEXTOBJ) of TEXT))
                                                             (* ; "Run the editing engine")
    (CLOSEW WINDOW)                                          (* ; "Close the edit window")
    (\TEXTCLOSEF TEXT)                                       (* ; "Close the underlying files")
    (replace (STREAM ACCESSBITS) of TEXT with BothBits)      (* ; 
                                                             "But leave the stream itself accessible")
                                                             (* ; 
                                               "Apply any post-window-close (and post-QUIT) function")
    (CL:WHEN (TEXTPROP (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                    'AFTERQUITFN)
        (APPLY* (TEXTPROP (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                       'AFTERQUITFN)
               WINDOW TEXT))])

(\TEDIT.CHARDELETE
  [LAMBDA (TEXTOBJ SEL)                                      (* ; "Edited 24-May-2023 22:50 by rmk")
                                                             (* ; "Edited 22-May-2023 23:24 by rmk")
                                                             (* ; "Edited 19-Apr-93 10:50 by jds")

    (* ;; "This creates a selection that specifies a deletion of the single character just before SEL's caret, and then uses that to delete the character.  It assumes that SEL can be smashed.")

    (SETQ TEXTOBJ (TEXTOBJ TEXTOBJ))
    (PROG [FIRSTPIECE (CH# (SUB1 (TEDIT.GETPOINT TEXTOBJ SEL]
          (CL:WHEN (ILEQ CH# 0)                              (* ; 
                                                             "Can't backspace past start of document")
              (RETURN))

     (* ;; "Back up to the first visible character--that's the target, unless it is protected")

          (for PC PLOOKS START-OF-PIECE backpieces (SETQ FIRSTPIECE (\CHTOPC CH# TEXTOBJ T))
             declare (SPECVARS START-OF-PIECE) eachtime (SETQ PLOOKS (\TEDIT.APPLY.STYLES
                                                                      (PLOOKS PC)
                                                                      PC TEXTOBJ)) 
                                                             (* ; 
                                                             "Can't backspace over a protected piece")
             until (fetch (CHARLOOKS CLPROTECTED) of PLOOKS)
             do (if (ffetch (CHARLOOKS CLINVISIBLE) of PLOOKS)
                    then (add START-OF-PIECE (IMINUS (PLEN PC)))
                  else 
                       (* ;; "Start at the last character of the most recent visible piece")

                       (\TEDIT.UPDATE.SEL SEL (CL:IF (EQ PC FIRSTPIECE)
                                                  CH#
                                                  (SUB1 START-OF-PIECE))
                              1
                              'RIGHT)
                       (\TEDIT.DELETE TEXTOBJ SEL)
                       (RETURN])

(\TEDIT.COPYPIECE
  [LAMBDA (PC FROMOBJ TOOBJ UNPROTECT OPERATION PROMPTTEXTOBJ)
                                                             (* ; "Edited 19-May-2023 21:39 by rmk")
                                                             (* ; "Edited  7-May-2023 11:46 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:50 by mitani")

    (* ;; "TEXTOBJ's prompt gets the message that a copy is not allowed, the FROMOBJ and TOOBJ provide the streams for the object's copy function.  The copy is disconnected from PC's original connections.")

    (* ;; "If UNPROTECT, the copies of protected pieces are unprotected")

    (* ;; "OPERATION keys which imageobject function to apply, if any")

    (PROG (NEWPC SRCPFILE (CROSSCOPY (NEQ FROMOBJ TOOBJ)))   (* ; 
                                                             "No matter what, we need a fresh copy.")
          (SETQ NEWPC
           (create PIECE using PC PNEW _ T PREVPIECE _ NIL NEXTPIECE _ NIL PTREENODE _ NIL))
          (SELECTQ (PTYPE PC)
              (FILE.PTYPES (CL:WHEN CROSSCOPY

                               (* ;; "If this is a cross-document copy, and the text comes from a file, we must REALLY make a copy of the text, lest the source file be deleted. We want to preserve the external format, so that we can just copy the bytes.")

                               (SETQ SRCPFILE (PCONTENTS PC))
                               (CL:UNLESS (\GETSTREAM SRCPFILE 'INPUT T)
                                                             (* ; 
                                               "The source file was CLOSED -- reopen it, for our use")
                                   [SETQ SRCPFILE (OPENSTREAM SRCPFILE 'INPUT 'OLD
                                                         '((TYPE TEXT])
                               [freplace (PIECE PCONTENTS) of PC
                                  with (OPENSTREAM '{NODIRCORE} 'BOTH 'NEW
                                              `((:EXTERNAL-FORMAT ,(STREAMPROP SRCPFILE 
                                                                          :EXTERNAL-FORMAT]
                               [COPYBYTES SRCPFILE (PCONTENTS PC)
                                      (PFPOS PC)
                                      (IPLUS (PFPOS PC)
                                             (CL:IF (EQ FATFILE2.PTYPE (PTYPE PC))
                                                 (UNFOLD (PLEN PC)
                                                        2)
                                                 (PLEN PC))]
                               (freplace (PIECE PFPOS) of PC with 0)))
              (STRING.PTYPES                                 (* ; 
                                                    "In case this is in the current insertion string")
                             (change (ffetch (PIECE PCONTENTS) of NEWPC)
                                    (CONCAT DATUM)))
              (OBJECT.PTYPE                                  (* ; 
                              "No copy if object doesn't allow it.  Caller must be prepared for NIL?")
                            (freplace (PIECE PCONTENTS) of NEWPC
                               with (OR (\TEDIT.APPLY.OBJFN (PCONTENTS NEWPC)
                                               OPERATION FROMOBJ TOOBJ PROMPTTEXTOBJ)
                                        (RETURN NIL))))
              NIL)

     (* ;; "If moving from one text to another, we have to register the looks.")

          (if (AND UNPROTECT (fetch CLPROTECTED of (PLOOKS NEWPC)))
              then (freplace (PIECE PLOOKS) of NEWPC
                      with (\TEDIT.UNIQUIFY.CHARLOOKS (create CHARLOOKS
                                                         using (PLOOKS PC)
                                                               CLPROTECTED _ NIL CLSELHERE _ NIL)
                                  TOOBJ))
            elseif CROSSCOPY
              then (freplace (PIECE PLOOKS) of NEWPC with (\TEDIT.UNIQUIFY.CHARLOOKS (PLOOKS PC)
                                                                 TOOBJ))
                   (freplace (PIECE PPARALOOKS) of NEWPC with (\TEDIT.UNIQUIFY.PARALOOKS (PPARALOOKS
                                                                                          PC)
                                                                     TOOBJ)))
          (RETURN NEWPC])

(\TEDIT.APPLY.OBJFN
  [LAMBDA (OBJ OPERATION FROMTOBJ TOTOBJ PROMPTTEXTOBJ)      (* ; "Edited  6-Jun-2023 13:35 by rmk")
                                                             (* ; "Edited 30-May-2023 08:15 by rmk")
                                                             (* ; "Edited 19-May-2023 21:37 by rmk")
                                                             (* ; "Edited  7-May-2023 11:46 by rmk")

    (* ;; "As part of an OPERATION on an image object piece, we execute the appropriate object functions.  If any of them returns DONT, we print a message in the prompt window of  PROMPTTEXTOBJ  or  FROMTOBJ, and return NIL.  Otherwise, we return an object, either OBJ or a copy.")

    (CL:UNLESS TOTOBJ (SETQ TOTOBJ FROMTOBJ))
    (PROG [NEWOBJ (OBJFN (IMAGEOBJPROP OBJ (SELECTQ OPERATION
                                               (COPY 'COPYFN)
                                               (MOVE 'WHENMOVEDFN)
                                               (INSERT 'WHENINSERTEDFN)
                                               (DELETE       (* ; 
                                                          "This may want to apply to the first pane?")
                                                       'WHENDELETEDFN)
                                               NIL]
          (SETQ NEWOBJ (if OBJFN
                           then (APPLY* OBJFN OBJ (fetch (TEXTOBJ STREAMHINT) of FROMTOBJ)
                                       (fetch (TEXTOBJ STREAMHINT) of TOTOBJ))
                         elseif (EQ OPERATION 'COPY)
                           then (COPYALL OBJ)
                         else OBJ))
          (CL:WHEN (MEMB NEWOBJ '(DON'T DONT))
              (TEDIT.PROMPTPRINT (OR PROMPTTEXTOBJ FROMTOBJ)
                     (CONCAT (L-CASE OPERATION T)
                            " of this object not allowed.")
                     T)
              (RETURN))

     (* ;; "The WHENCOPIEDFN wants the display stream, not just the text stream.  ")

          (CL:WHEN [AND (EQ OPERATION 'COPY)
                        (SETQ OBJFN (IMAGEOBJPROP OBJ 'WHENCOPIEDFN))
                        (MEMB (APPLY* OBJFN OBJ (WINDOWPROP (CAR (fetch (TEXTOBJ \WINDOW)
                                                                    of TOTOBJ))
                                                       'DSP)
                                     (fetch (TEXTOBJ STREAMHINT) of FROMTOBJ)
                                     (fetch (TEXTOBJ STREAMHINT) of TOTOBJ))
                              '(DON'T DONT]
                 (RETURN NIL))
          (RETURN (OR NEWOBJ OBJ])

(\TEDIT.DELETE
  [LAMBDA (TEXTOBJ SEL DONTDISPLAY)                          (* ; "Edited  6-Jun-2023 12:48 by rmk")
                                                             (* ; "Edited 29-May-91 18:22 by jds")

    (* ;; "Delete the DCH characters selected by SEL. Unlike insert, the initial position of the caret doesn't matter.")

    (* ;; "On return, the pieces, lines, and selection are complete and correct, and the display is correct unless DONTDISPLAY.")

    (CL:UNLESS [OR (fetch (TEXTOBJ TXTREADONLY) of TEXTOBJ)
                   (NOT (AND (GETSEL SEL SET)
                             (IGEQ (GETSEL SEL DCH)
                                   1]
        (CL:UNLESS DONTDISPLAY (TEDIT.NORMALIZECARET TEXTOBJ SEL))
                                                             (* ; 
                                                    "Move the deletion point on-screen in every pane")
        (\TEDIT.SHOWSELS TEXTOBJ NIL)                        (* ; 
                                                             "Turn off the selection's highlighting")
        (LET ((CHNO (GETSEL SEL CH#)))                       (* ; 
                                               "Grap the CH# before acting, to update the selection.")
             (\TEDIT.DELETE.SELPIECES TEXTOBJ SEL)           (* ; "Delete the SELected characters")

             (* ;; "The current lines are not aligned with the text/pieces.")

             (\TEDIT.UPDATE.LINES TEXTOBJ 'DELETION SEL NIL DONTDISPLAY)

             (* ;; 
       "Adjust the other selections to point to the right of the character just before the deletion ")

             (\TEDIT.ADJUST.SELS TEXTOBJ 'DELETION SEL)
             (TEDIT.MAKEPOINT SEL (SUB1 CHNO)
                    'RIGHT)
             (CL:UNLESS DONTDISPLAY (\SHOWSEL SEL T))
             T))])

(\TEDIT.DIFFUSE.PARALOOKS
  [LAMBDA (PRIORPC SUCCEEDINGPC)                             (* ; "Edited 11-Apr-2023 00:08 by rmk")
                                                             (* ; "Edited 22-Oct-2022 22:22 by rmk")
                                                             (* ; "Edited  5-Sep-2022 14:32 by rmk")
                                                             (* ; "Edited 23-Aug-2022 08:40 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:48 by mitani")

    (* ;; "Given a discontinuity in paragraph looks, caused by an insertion or by a deletion: Diffuse the existing paragraph looks across the discontinuity, so that all the pieces in a single paragraph have consistent paragraph looks.  Give preference to diffusion toward the END of the document.  This means that if you delete an EOL between paragraphs, the second para is absorbed into the first.")

    (* ;; "PRIORPC and SUCCEEDINGPC are the pieces that bound the area of potential discontinuity: the change will occur at one boundary or the other.")

    (CL:WHEN (AND PRIORPC (NOT (PPARALAST PRIORPC)))         (* ; 
              "The discontinuity is inside a paragraph.  Must copy para looks forward into the text.")
        (for PC (PPLOOKS _ (PPARALOOKS PRIORPC)) inpieces (NEXTPIECE PRIORPC)
           do                                                (* ; 
                                "Copy para looks info in from the left, up the the first para break.")
              (replace (PIECE PPARALOOKS) of PC with PPLOOKS) repeatuntil (PPARALAST PC)))
                                                             (* ; 
                                                        "If this piece ends a paragraph, we're done.")
    (CL:WHEN SUCCEEDINGPC                                    (* ; 
                                   "Copy para looks  back from the right, up to the first para break")
        (for PC (PPLOOKS _ (PPARALOOKS SUCCEEDINGPC)) backpieces (PREVPIECE SUCCEEDINGPC)
           until (OR (EQ PC PRIORPC)
                     (PPARALAST PC)) do (replace (PIECE PPARALOOKS) of PC with PPLOOKS)))])

(\TEDIT.FOREIGN.COPY?
  [LAMBDA (SEL)                                              (* ; "Edited  6-Jun-2023 15:29 by rmk")
                                                             (* ; "Edited 18-Apr-2023 23:37 by rmk")
                                                             (* ; "Edited 21-Oct-2022 18:44 by rmk")
                                                             (* ; "Edited  7-Sep-2022 23:36 by rmk")
                                                             (* ; "Edited  1-Aug-2022 12:04 by rmk")
                                                             (* ; "Edited 21-Jan-93 11:46 by jds")

    (* ;; "IF the current process's window isn't a TEdit window, do a 'Copy' by BKSYSBUFing the selected text.  Then turn off all the various indicators.")

    (PROG (PROCW (SOURCE.TEXTOBJ (GETSEL SEL SELTEXTOBJ))
                 CH DEST.TEDIT? DEST.TEXTOBJ)
          [SETQ DEST.TEDIT? (AND (SETQ PROCW (PROCESSPROP (TTY.PROCESS)
                                                    'WINDOW))
                                 (SETQ DEST.TEXTOBJ (WINDOWPROP PROCW 'TEXTOBJ))
                                 (NOT (TEXTPROP DEST.TEXTOBJ 'COPYBYBKSYSBUF]
                                                             (* ; "Treat the destination specially if (1) the recipient process has a window, and (2) it's a TEdit window, and (3) the TEdit isn't declining special treatment by having COPYBYBKSYSBUF set in its props.")
          (COND
             ((ZEROP (GETSEL SEL DCH))                       (* ; 
                                         "Nothing to copy (0 characters in selection); don't bother.")
              (SETQ TEDIT.COPY.PENDING NIL))
             ((IGREATERP (GETSEL SEL CH#)
                     (FETCH (TEXTOBJ TEXTLEN) OF SOURCE.TEXTOBJ))
                                                             (* ; 
                                   "Trying to copy from beyond the end of the document; don't bother")
              (SETQ TEDIT.COPY.PENDING NIL))
             ((OR (NOT DEST.TEDIT?)
                  (AND PROCW DEST.TEXTOBJ (NEQ SOURCE.TEXTOBJ DEST.TEXTOBJ)
                       (fetch (TEXTOBJ EDITOPACTIVE) of DEST.TEXTOBJ)))
                                                             (* ; 
                                                   "OK -- receiver isn't TEdit.  Do it the hard way.")
              [COND
                 [(AND (WINDOWPROP [OR PROCW (WFROMDS (PROCESS.TTY (TTY.PROCESS]
                              'COPYINSERTFN)
                       (PROGN                                (* ; 
                                                         "This is the exit for looked-string objects")
                              (OBJECTOUTOFTEDIT SOURCE.TEXTOBJ SEL]
                 (T                                          (* ; 
                         "Old tedit method, run if OBJECTOUTOFTEDIT is NILL (ie., not installed yet)")

                    (* ;; "Still used because COPYINSERT does (PRIN2 BKSYSBUF) if there's no insertfn, which cretes undesired string quotes.")
                                                             (* ; 
                                                             "Go to the first character to be copied")
                    (for I (TSTREAM _ (fetch (TEXTOBJ STREAMHINT) of SOURCE.TEXTOBJ)) from 1
                       to (GETSEL SEL DCH) first (SETFILEPTR TSTREAM (SUB1 (GETSEL SEL CH# SEL)))
                       do 
                          (* ;; "Run thru the selected text, copying only those items that really ARE characters--IMAGEOBJs don't get copied by this route.")

                          (COND
                             ((FIXP (SETQ CH (BIN TSTREAM)))
                              (BKSYSBUF (CHARACTER CH)))
                             (T (COPYINSERT CH]
              (\SHOWSEL SEL NIL)                             (* ; 
                                                             "Then reset the copy-pending flags.")
              (SETQ TEDIT.COPY.PENDING NIL])

(\TEDIT.QUIT
  [LAMBDA (W NOFORCE)                                   (* ; "Edited 12-Jun-90 17:50 by mitani")
                                                             (* Called by the default 
                                                           TEDIT.DEFAULT.MENUFN to perform the 
                                                           QUIT command.)
    (PROG* ((TEXTOBJ (WINDOWPROP W 'TEXTOBJ))
            (QUITFNS (TEXTPROP TEXTOBJ 'QUITFN))
            QUITFLG RESP)
           [for QUITFN inside QUITFNS while (AND (NEQ QUITFLG 'DON'T)
                                                             (NEQ QUITFLG T))
              do (COND
                        ((EQ QUITFN T)
                         (SETQ QUITFLG T))
                        (T (AND QUITFN (NEQ QUITFN T)
                                (SETQ QUITFLG (APPLY* QUITFN W (fetch (TEXTOBJ STREAMHINT)
                                                                  of TEXTOBJ)
                                                     TEXTOBJ
                                                     (fetch (TEXTOBJ EDITPROPS) of 
                                                                                              TEXTOBJ
                                                            ]
           (COND
              ((EQ QUITFLG 'DON'T)

         (* The user supplied a QUITFN, and it returned "DON'T" %, so just ignore all 
       this Fooferaw and keep editing.)

               (RETURN))
              [(AND (fetch (TEXTOBJ \DIRTY) of TEXTOBJ)
                    (NOT (fetch (TEXTOBJ MENUFLG) of TEXTOBJ))
                    (NEQ QUITFNS T)
                    (NEQ QUITFLG T))

         (* If this document has changed, check with the user to make sure he really 
       wants to do it.)

               (replace (TEXTOBJ EDITFINISHEDFLG) of TEXTOBJ
                  with (MOUSECONFIRM "Not saved yet; LEFT to Quit anyway." T (fetch
                                                                                  (TEXTOBJ 
                                                                                         PROMPTWINDOW
                                                                                         )
                                                                                    of TEXTOBJ]
              (T                                             (* Go ahead and quit the next time 
                                                           we see the main command loop.)
                 (replace (TEXTOBJ EDITFINISHEDFLG) of TEXTOBJ with T)))
           [AND (fetch (TEXTOBJ \WINDOW) of TEXTOBJ)
                (NOT NOFORCE)
                (NEQ (\TEDIT.PRIMARYW TEXTOBJ)
                     (PROCESSPROP (TTY.PROCESS)
                            'WINDOW))
                (TTY.PROCESS (WINDOWPROP (\TEDIT.PRIMARYW TEXTOBJ)
                                    'PROCESS]
           (RETURN (fetch (TEXTOBJ EDITFINISHEDFLG) of TEXTOBJ])

(\TEDIT.WORDDELETE
  [LAMBDA (TEXTOBJ SEL)                                      (* ; "Edited 23-May-2023 16:37 by rmk")
                                                             (* ; "Edited 22-May-2023 10:52 by rmk")
                                                             (* ; "Edited 29-May-91 18:22 by jds")

    (* ;; "THIS FUNCTION IS FRAUGHT WITH FENCEPOST PROBLEM POTENTIAL, AND THE WHILE vs FOR LOGIC IS CONVOLUTED.  CAUTION, CAUTION.")

    (PROG ((TSTREAM (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ))
           (READSA (fetch READSA of (OR (fetch (TEXTOBJ TXTWTBL) of TEXTOBJ)
                                        TEDIT.WORDBOUND.READTABLE)))
           (INSCH# (SUB1 (TEDIT.GETPOINT SEL)))
           CH CHNO)

     (* ;; "INSCH# is the final (i.e., highest-numbered) character to be deleted.")

          (CL:UNLESS (IGREATERP INSCH# 0)                    (* ; "Don't back up at start of file.")
              (RETURN))
          (\TEXTSETFILEPTR TSTREAM (SUB1 INSCH#))
          (SETQ CH (BIN TSTREAM))

     (* ;; "Skip over any preceding separator characters")

          (for old CHNO from INSCH# to 1 by -1
             while [AND (SELECTC (COND
                                    ((CHARCODEP CH)
                                     (\SYNCODE READSA CH))
                                    (T                       (* ; "It's an object--stop")
                                       TEXT.TTC))
                            (TEXT.TTC NIL)
                            T)
                        (NOT (fetch CLPROTECTED of (PLOOKS (fetch (TEXTSTREAM PIECE) of TSTREAM]
             do (SETQ CH (\BACKBIN TSTREAM)))

     (* ;; "CH is the first non-separator character, and CHNO is the character number of the character BEFORE that one. Skip over the next group of non-separators (= a 'word')")

          (for old CHNO from CHNO to 1 by -1
             while [AND (SELECTC (COND
                                    ((CHARCODEP CH)
                                     (\SYNCODE READSA CH))
                                    (T                       (* ; "It's an object!")
                                       TEXT.TTC))
                            (TEXT.TTC T)
                            NIL)
                        (NOT (fetch CLPROTECTED of (PLOOKS (fetch (TEXTSTREAM PIECE) of TSTREAM]
             do (SETQ CH (\BACKBIN TSTREAM)))

     (* ;; "CH is now the first separator character before the word, and CHNO is the character number of the character BEFORE it, or 0 if you hit the front of the document.")

          (\SHOWSEL SEL NIL)

     (* ;; "If not at the front of the document, the first character to delete is 1 later than the separator we hit, which is 2 higher than CHNO is now")

          (\TEDIT.UPDATE.SEL SEL (CL:IF (ILESSP CHNO 1)
                                     1
                                     (IPLUS 2 CHNO))
                 (SUB1 (IDIFFERENCE INSCH# CHNO)))
          (\TEDIT.DELETE TEXTOBJ SEL])

(\TEDIT1
  [LAMBDA (TEXT WINDOW UNSPAWNED PROPS)                 (* ; "Edited 12-Jun-90 17:50 by mitani")

         (* Does the actual editing work, and re-coercion or process kill when done.
       Called by TEDIT directly, or ADD.PROCESSed by it.)

    (SETQ TEXT (OPENTEXTSTREAM TEXT WINDOW NIL NIL PROPS))   (* Open the text for editing)
    (\TEDIT.COMMAND.LOOP (fetch (TEXTSTREAM TEXTOBJ) of TEXT))
                                                             (* Run the editing engine)
    (CLOSEW WINDOW)
    (replace (TEXTOBJ \WINDOW) of (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
       with NIL)
    (AND (TEXTPROP (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                'AFTERQUITFN)
         (APPLY* (TEXTPROP (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                        'AFTERQUITFN)
                WINDOW TEXT))                                (* Apply any post-window-close
                                                           (and post-QUIT) function)
    (COND
       (UNSPAWNED                                            (* We're not a distinct process%: 
                                                           Send back the edited text in some 
                                                           suitable form)
              (COND
                 ((NEQ (fetch (TEXTOBJ EDITFINISHEDFLG) of (fetch (TEXTSTREAM
                                                                                   TEXTOBJ)
                                                                          of TEXT))
                       T)
                  (PROG1 (fetch (TEXTOBJ EDITFINISHEDFLG) of (fetch (TEXTSTREAM
                                                                                     TEXTOBJ)
                                                                            of TEXT))
                      (replace (TEXTOBJ EDITFINISHEDFLG) of (fetch (TEXTSTREAM
                                                                                    TEXTOBJ)
                                                                           of TEXT) with
                                                                                        NIL)))
                 ((STRINGP (fetch (TEXTOBJ TXTFILE) of (fetch (TEXTSTREAM TEXTOBJ
                                                                                     ) of TEXT)))
                  (COERCETEXTOBJ (fetch (TEXTSTREAM TEXTOBJ) of TEXT)
                         'STRINGP))
                 (T TEXT])
)
(DEFINEQ

(\SELPIECES.COPY
  [LAMBDA (SELPIECES OPERATION TOTEXTOBJ FROMTEXTOBJ)        (* ; "Edited  2-Jun-2023 11:21 by rmk")
                                                             (* ; "Edited 26-May-2023 00:28 by rmk")
                                                             (* ; "Edited 21-May-2023 23:01 by rmk")
                                                             (* ; "Edited  7-May-2023 17:26 by rmk")

    (* ;; "Produces a copy of SELPIECES where the pieces from first to last are chained-together copies of the original pieces so that later inpieces can run from first to last.  OPERATION determines which imageobject functions will be invoked, if any.")

    (* ;; "FROMTEXTOBJ is optional.  Providing a FROMTEXTOBJ that is different from TOTEXTOBJ is a signal that this is a cross-copy needing to create private copies of strings and files.  ")

    (CL:UNLESS FROMTEXTOBJ (SETQ FROMTEXTOBJ TOTEXTOBJ))
    (for PC NPC PREVPC NEWFIRSTPIECE inselpieces SELPIECES
       do (SETQ NPC (\TEDIT.COPYPIECE PC FROMTEXTOBJ TOTEXTOBJ NIL OPERATION))
          (CL:UNLESS NPC                                     (* ; "Was an object-copy disallowed?")
              (RETURN)) 

          (* ;; "Linke the new pieces together")

          (if PREVPC
              then (replace (PIECE NEXTPIECE) of PREVPC with NPC)
            else (SETQ NEWFIRSTPIECE NPC))
          (replace (PIECE PREVPIECE) of NPC with PREVPC)
          (SETQ PREVPC NPC) finally (RETURN (create SELPIECES
                                               using SELPIECES SPFIRST _ NEWFIRSTPIECE SPLAST _ 
                                                     PREVPC])

(\SELPIECES.CONCAT
  [LAMBDA (SP1 SP2)                                          (* ; "Edited  3-Jun-2023 17:08 by rmk")
                                                             (* ; "Edited  2-Jun-2023 12:09 by rmk")
                                                             (* ; "Edited 21-May-2023 22:20 by rmk")

    (* ;; "The returned SELPIECE concatenates the pieces in SP1 and SP2.  Probably only sensible if those pieces are consecutive with respect to some textobj or some operation. ")

    (* ;; "NOTE:  This modifies the actual pieces to connect them together.  Caller is responsible for insuring that this is safe.")

    (if (NULL (fetch (SELPIECES SPFIRST) of SP1))
        then SP2
      elseif (NULL (fetch (SELPIECES SPFIRST) of SP2))
        then SP1
      elseif (EQ (fetch (SELPIECES SPTEXTOBJ) of SP1)
                 (fetch (SELPIECES SPTEXTOBJ) of SP2))
        then (replace (PIECE NEXTPIECE) of (fetch (SELPIECES SPLAST) of SP1)
                with (fetch (SELPIECES SPFIRST) of SP2))
             (replace (PIECE PREVPIECE) of (fetch (SELPIECES SPFIRST) of SP2)
                with (fetch (SELPIECES SPLAST) of SP1))
             (create SELPIECES
                    SPFIRST _ (fetch (SELPIECES SPFIRST) of SP1)
                    SPLAST _ (fetch (SELPIECES SPLAST) of SP2)
                    SPLEN _ (IPLUS (fetch (SELPIECES SPLEN) of SP1)
                                   (fetch (SELPIECES SPLEN) of SP2))
                    SPTEXTOBJ _ (fetch (SELPIECES SPTEXTOBJ) of SP2))
      else (SHOULDNT "CONCATENATING PIECES FROM DIFFERENT TEXTOBJS"])

(\SELPIECES.CHARTRANSFORM
  [LAMBDA (SELPIECES CHARFN OBJECTSTOO TEXTOBJ)              (* ; "Edited 24-May-2023 13:04 by rmk")

    (* ;; "This transforms the characters in SELPIECES according to CHARFN, skipping image objects unless OBJECTSTOO.  The purpose is to allow for character transformations (e.g. case switching) without depending on strings (TEDIT.SELAS.STRING) and character insertion (\INSERTCH) as intermediaries.  Strings can't hold image objects.")

    (* ;; 
    "This smashes the pieces, use crosscopy \SELPIECES.COPY first to protect the document pieces.")

    (* ;; 
    "If optional TEXTOBJ is provided (unlikely), it should be the original source of the SELPIECES.")

    (for PC PCONTENTS inselpieces SELPIECES
       do (SETQ PCONTENTS (PCONTENTS PC))
          (SELECTC (PTYPE PC)
              (STRING.PTYPES (for I CH (STR _ PCONTENTS) from 1 while (SETQ CH (NTHCHARCODE STR I))
                                do (RPLCHARCODE STR I (APPLY* CHARFN CH TEXTOBJ))))
              (FILE.PTYPES (SETFILEPTR PCONTENTS (PFPOS PC))
                           [if (AND NIL (\IOMODEP PCONTENTS 'BOTH T))
                               then 

                                 (* ;; "Not clear whether \TEDIT.COPYPIECeS has set things up to allow us to actually smash the underlying stream. So for now, copy into string space.")

                                    (for I from 1 to (PLEN PC)
                                       do (\OUTCHAR PCONTENTS (APPLY* CHARFN (\PEEKCCODE PCONTENTS T)
                                                                     TEXTOBJ)))
                             else 

                                 (* ;; "This assumes that no file piece has a PLEN greater than \MaxArrayLen characters. We rely on the piece-table reader and writer to guarantee this.  If not, ALLOCSTRING will cause an error.")

                                  (LET ((FATP (NEQ THINFILE.PTYPE (PTYPE PC)))
                                        STR)
                                       (SETQ STR (ALLOCSTRING (PLEN PC)
                                                        NIL NIL FATP))
                                       (for I from 1 to (PLEN PC)
                                          do (RPLCHARCODE STR I (APPLY* CHARFN (\INCCODE PCONTENTS)
                                                                       TEXTOBJ)))
                                       (replace (PIECE PCONTENTS) of PC with STR)
                                       (replace (PIECE PTYPE) of PC with (CL:IF FATP
                                                                             FATSTRING.PTYPE
                                                                             THINSTRING.PTYPE)])
              (OBJECT.PTYPE (CL:WHEN OBJECTSTOO
                                (replace (PIECE PCONTENTS) of PC with (APPLY* CHARFN PCONTENTS 
                                                                             TEXTOBJ))))
              (SUBSTREAM.PTYPE 
                   (HELP "SUBSTREAM PIECE ?"))
              (SHOULDNT)))
    SELPIECES])

(\SELPIECES.FROM.STRING
  [LAMBDA (STRING TEXTOBJ CHECKFOREOL CHARLOOKS PARALOOKS)   (* ; "Edited  2-Jun-2023 11:59 by rmk")
                                                             (* ; "Edited 24-May-2023 15:26 by rmk")

    (* ;; "Creates SELPIECES with pieces representing STRING.  If CHECKFOREOL and the string contains a paragraph-breaking character, then the string will be coded as a sequence of pieces with EOL-terminated pieces (but not necessarily the last piece) marked as PPARALAST. ")

    (* ;; "If TEXTOBJ is provided, the pieces will have its default looks. Otherwise the TEDIT global default looks.")

    (CL:UNLESS CHARLOOKS
        (SETQ CHARLOOKS (CL:IF TEXTOBJ
                            (fetch (TEXTOBJ DEFAULTCHARLOOKS) of TEXTOBJ)
                            TEDIT.DEFAULT.CHARLOOKS)))
    (CL:UNLESS PARALOOKS
        (SETQ PARALOOKS (CL:IF TEXTOBJ
                            (fetch (TEXTOBJ FMTSPEC) of TEXTOBJ)
                            TEDIT.DEFAULT.FMTSPEC)))
    (CL:WHEN (AND TEXTOBJ (fetch (TEXTOBJ FORMATTEDP) of TEXTOBJ))
           (SETQ CHECKFOREOL T))
    (LET (PTYPE FIRSTPIECE LASTPIECE)
         (SETQ STRING (CONCAT STRING))
         (SETQ PTYPE (CL:IF (fetch (STRINGP FATSTRINGP) of STRING)
                         FATSTRING.PTYPE
                         THINSTRING.PTYPE))
         _
         (if CHECKFOREOL
             then 
                  (* ;; "Break it up into PPARALAST pieces")

                  [bind EOLPOS PC PREVPC (PREVEOLPOS _ 0)
                     while [SETQ EOLPOS (find I C from (ADD1 PREVEOLPOS)
                                           while (SETQ C (NTHCHARCODE STRING I))
                                           suchthat (FMEMB C (CHARCODE (EOL %#EOL Meta,EOL]
                     do (SETQ PC (create PIECE
                                        PTYPE _ PTYPE
                                        PCONTENTS _ (SUBSTRING STRING (ADD1 PREVEOLPOS)
                                                           EOLPOS)
                                        PLEN _ (IDIFFERENCE EOLPOS PREVEOLPOS)
                                        PLOOKS _ CHARLOOKS
                                        PPARALOOKS _ PARALOOKS
                                        PPARALAST _ T
                                        PREVPIECE _ PREVPC))
                        (if FIRSTPIECE
                            then (replace (PIECE NEXTPIECE) of PREVPC with PC)
                          else (SETQ FIRSTPIECE PC))
                        (SETQ PREVEOLPOS EOLPOS)
                        (SETQ PREVPC PC)
                     finally (SETQ LASTPIECE PREVPC)
                           (CL:UNLESS (EQ PREVEOLPOS (NCHARS STRING))

                               (* ;; "Tail piece didn't end in EOL")

                               (SETQ LASTPIECE (create PIECE
                                                      PTYPE _ PTYPE
                                                      PCONTENTS _ (SUBSTRING STRING (ADD1 PREVEOLPOS)
                                                                         )
                                                      PLEN _ (IDIFFERENCE (NCHARS STRING)
                                                                    PREVEOLPOS)
                                                      PLOOKS _ CHARLOOKS
                                                      PPARALOOKS _ PARALOOKS
                                                      PREVPIECE _ LASTPIECE))
                               (CL:WHEN PREVPC
                                   (replace (PIECE NEXTPIECE) of PREVPC with LASTPIECE)))]
           else (SETQ FIRSTPIECE (create PIECE
                                        PTYPE _ PTYPE
                                        PCONTENTS _ STRING
                                        PLEN _ (NCHARS STRING)
                                        PLOOKS _ CHARLOOKS
                                        PPARALOOKS _ PARALOOKS))
                (SETQ LASTPIECE FIRSTPIECE))
         (create SELPIECES
                SPFIRST _ FIRSTPIECE
                SPLAST _ LASTPIECE
                SPLEN _ (NCHARS STRING)
                SPTEXTOBJ _ TEXTOBJ])

(\SELPIECES.TO.STRING
  [LAMBDA (SELPIECES OBJECTCHARCODE)                         (* ; "Edited  2-Jun-2023 12:07 by rmk")
                                                             (* ; "Edited 24-May-2023 20:00 by rmk")

    (* ;; "Produce a string representing the contents of SELPIECES.  Optional OBJECTCHARCODE is a code to be used to represent an image object.  If it is a TEXTOBJ with an OBJECTBYTE property, then that code is used.  Otherwise, arbitrarily the escape character.")

    (* ;; "Would it be better to take the chracters from the PREPRINTFN, if present?")

    (for PC PCONTENTS (TEXTOBJ _ (fetch (SELPIECES SPTEXTOBJ) of SELPIECES))
         (I _ 1)
         (RESULT _ (ALLOCSTRING (fetch (SELPIECES SPLEN) of SELPIECES))) inselpieces SELPIECES
       do (SETQ PCONTENTS (PCONTENTS PC))
          (SELECTC (PTYPE PC)
              (STRING.PTYPES (RPLSTRING RESULT I PCONTENTS)
                             (add I (PLEN PC)))
              (FILE.PTYPES (SETFILEPTR PCONTENTS (PFPOS PC))
                           (for J from 1 to (PLEN PC) do (RPLCHARCODE RESULT I (\INCCODE PCONTENTS))
                                                         (add I 1)))
              (OBJECT.PTYPE                                  (* ; 
                               "Could run the PREPRINTFN ?  But we then have to let the string grow.")
                            (RPLCHARCODE RESULT I (OR (SMALLP OBJECTCHARCODE)
                                                      [AND (SETQ OBJECTCHARCODE (GETTEXTPROP
                                                                                 TEXTOBJ
                                                                                 'OBJECTBYTE]
                                                      (CHARCODE ESCAPE)))
                            (add I 1))
              (SUBSTREAM.PTYPE 
                   (HELP "SUBSTREAM PIECE?"))
              (SHOULDNT)) finally (RETURN RESULT])
)

(MOVD? 'NILL 'OBJECTOUTOFTEDIT)



(* ; "HOOK for looked-string copy, etc. Used in \TEDIT.FOREIGN.COPY?.")




(* ; "Object-oriented editing")

(DEFINEQ

(TEDIT.INSERT.OBJECT
  [LAMBDA (OBJECT TSTREAM CH#)                               (* ; "Edited 20-Jun-2023 23:07 by rmk")
                                                             (* ; "Edited 17-Jun-2023 17:15 by rmk")
                                                             (* ; "Edited  2-Jun-2023 12:00 by rmk")
                                                             (* ; "Edited 31-May-2023 19:55 by rmk")
                                                             (* ; "Edited 27-May-2023 14:00 by rmk")
                                                             (* ; "Edited 19-May-2023 00:18 by rmk")
                                                             (* ; "Edited 21-Apr-93 00:52 by jds")

    (* ;; "Inserts the Image-object OBJECT into text STREAM in front of character CH.")

    (PROG ((TEXTOBJ (TEXTOBJ TSTREAM))
           SEL OBJPC OBJSELPIECES SUBSTREAM)
          (CL:WHEN (fetch (TEXTOBJ TXTREADONLY) of TEXTOBJ)
                 (RETURN))
          (CL:WHEN (type? SELECTION CH#)
              (SETQ CH# (TEDIT.GETPOINT CH#)))

     (* ;; "We construct and copy a trivial SELPIECES so that we can share the basic insertion code.")

          (SETQ OBJPC (create PIECE
                             PTYPE _ OBJECT.PTYPE
                             PCONTENTS _ OBJECT
                             PLEN _ 1
                             PLOOKS _ (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)))
                                                             (* ; "The new piece we're inserting")
          (CL:WHEN (SETQ SUBSTREAM (IMAGEOBJPROP OBJECT 'SUBSTREAM))
                                                             (* ; 
                                                  "If this is computed text in bulk, fix the length.")
              (freplace (PIECE PTYPE) of OBJPC with SUBSTREAM.PTYPE)
              (replace (PIECE PLEN) of OBJPC with (TEXTLEN (fetch (TEXTSTREAM TEXTOBJ) of SUBSTREAM))
                     ))
          (SETQ OBJSELPIECES
           (\SELPIECES.COPY (create SELPIECES
                                   SPLEN _ 1
                                   SPFIRST _ OBJPC
                                   SPLAST _ OBJPC
                                   SPTEXTOBJ _ TEXTOBJ)
                  'INSERT TEXTOBJ))
          (CL:UNLESS OBJSELPIECES (RETURN))

     (* ;; "")

     (* ;; 
     " OBJSELPIECES contains (a copy of) the object piece, and the object approved of insertion.")

          (SETQ SEL (TEXTSEL TEXTOBJ))
          (\SHOWSEL SEL NIL)
          (TEDIT.DO.BLUEPENDINGDELETE SEL TEXTOBJ)
          (CL:WHEN CH#
              (\TEDIT.UPDATE.SEL SEL (IMIN CH# (ADD1 (TEXTLEN TEXTOBJ)))
                     0
                     'LEFT))                                 (* ; 
                                                            "Do the pending delete, if there is one.")
          (\TEDIT.INSERT.SELPIECES OBJSELPIECES TEXTOBJ SEL)
          (\SHOWSEL SEL T)
          (\COPYSEL SEL TEDIT.SELECTION])

(TEDIT.EDIT.OBJECT
  [LAMBDA (STREAM OBJ)                                       (* ; "Edited 19-May-2023 21:35 by rmk")
                                                             (* ; "Edited 27-Apr-2023 00:14 by rmk")
                                                             (* ; "Edited 21-Oct-2022 18:37 by rmk")
                                                             (* ; "Edited 29-May-91 18:23 by jds")
    (LET ((TEXTOBJ (TEXTOBJ STREAM))
          SEL CH# SELOBJ EDITFN)
         [COND
            [(AND OBJ (IMAGEOBJP OBJ))
             (SETQ CH# (TEDIT.FIND.OBJECT TEXTOBJ OBJ))
             (COND
                (CH# (SETQ SEL (fetch (TEXTOBJ SCRATCHSEL) of TEXTOBJ))
                     (\TEDIT.UPDATE.SEL SEL CH# 1)
                     (SETSEL SEL SELOBJ OBJ)
                     (SETSEL SEL SELTEXTOBJ TEXTOBJ)
                     (\FIXSEL SEL TEXTOBJ))
                (T (TEDIT.PROMPTPRINT TEXTOBJ "Can't find specified object." T]
            (T (SETQ SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
               (SETQ OBJ (GETSEL SEL SELOBJ]
         (COND
            [OBJ                                             (* ; 
                                                           "OK There's an object selected.  Edit it.")
                 (SETQ EDITFN (IMAGEOBJPROP OBJ 'EDITFN))
                 (COND
                    ((AND EDITFN (APPLY* EDITFN OBJ))        (* ; 
                                                   "If the editfn makes a change, update the screen.")
                     (for LINE inside (GETSEL SEL L1) do (SETLD LINE DIRTY T))
                     (replace (TEXTOBJ TXTNEEDSUPDATE) of TEXTOBJ with T)
                     (TEDIT.UPDATE.SCREEN TEXTOBJ]
            (T                                               (* ; "No object selected.")
               (TEDIT.PROMPTPRINT TEXTOBJ "Please select an editable object first." T])

(TEDIT.FIND.OBJECT
  [LAMBDA (TEXTOBJ OBJ)                                      (* ; "Edited  6-Nov-2022 11:12 by rmk")
                                                             (* ; "Edited  3-May-93 12:52 by jds")
    (for CH# from 1 by (PLEN PC) as PC inpieces (\FIRSTPIECE (TEXTOBJ TEXTOBJ))
       when (EQ OBJ (PCONTENTS PC)) do (RETURN CH#])

(TEDIT.PUT.OBJECT
  [LAMBDA (PIECE OFILE FONTFILE CURCH#)                      (* ; "Edited  6-Aug-2022 10:02 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:49 by mitani")

    (* ;; "Given a piece which describes an object, put the object out there.")

    (LET ((OBJECT (fetch (PIECE PCONTENTS) of PIECE))
          (FONTCH# (GETFILEPTR FONTFILE))
          TOFILE LEN)
         (\DWOUT FONTFILE 0)                                 (* ; 
                                                 "Placeholder for length of the object's description")
         (\SMALLPOUT FONTFILE \PieceDescriptorOBJECT)        (* ; 
                                                             "Mark this as setting the piece's looks")
         (\ATMOUT FONTFILE (IMAGEOBJPROP OBJECT 'GETFN))     (* ; 
                                                          "The FN to apply to reconstruct the object")
         (APPLY* (IMAGEOBJPROP OBJECT 'PUTFN)
                OBJECT OFILE)
         (SETFILEPTR FONTFILE FONTCH#)                       (* ; 
                          "Now go back and fill in the length of the text description of the object.")
         [\DWOUT FONTFILE (SETQ LEN (ADD1 (IDIFFERENCE (GETEOFPTR OFILE)
                                                 CURCH#]
         (SETFILEPTR FONTFILE -1)                            (* ; 
                                                        "Make sure we're at the end of the font file")
         (AND (RANDACCESSP OFILE)
              (SETFILEPTR OFILE -1))                         (* ; "And the text part of the file")
         LEN])

(TEDIT.GET.OBJECT
  [LAMBDA (TSTREAM PIECE FILE CURFILEBYTE#)                  (* ; "Edited 12-Oct-2022 14:10 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:50 by mitani")

    (* ;; "Get an object from the file")

    (* ;; "CURFILEBYTE# = fileptr within the text section of the file where the object's text starts. On entry the file is positioned just before the object's GETFN in the looks section of the file. On exit, the fileptr points just after the GETFN in the looks section, after having been detoured to the text section for the getfn to read the object's data.")

    (PROG ((TEXTOBJ (fetch (TEXTSTREAM TEXTOBJ) of TSTREAM))
           FILEPTRSAVE NAMELEN GETFN OBJ NBYTES)

     (* ;; "rrb 10-AUG-87 --- calculate the length of the image object's data.  This assumes that the file is currently pointed at the end of the data which is where the GETFN is written {I think}")

          (SETQ NBYTES (DIFFERENCE (GETFILEPTR FILE)
                              CURFILEBYTE#))
          (SETQ GETFN (\ATMIN FILE))                         (* ; 
                                                             "The GETFN for this kind of IMAGEOBJ")
          (SETQ FILEPTRSAVE (GETFILEPTR FILE))               (* ; 
                                             "Save our file location thru the building of the object")
          (SETFILEPTR FILE CURFILEBYTE#)
          (SETQ OBJ (READIMAGEOBJ FILE GETFN NIL NBYTES))
          (CL:WHEN (IMAGEOBJPROP OBJ 'UNKNOWNGETFN)          (* ; 
      "If the object has an unknown getfn property, then it's an encapsulated object.  Warn the user")
              (TEDIT.PROMPTPRINT TSTREAM "WARNING: Document contains unknown image objects." T))
          (SETFILEPTR FILE FILEPTRSAVE)
          (replace (PIECE PCONTENTS) of PIECE with OBJ)
          [replace (PIECE PLOOKS) of PIECE with (COND
                                                   ((PREVPIECE PIECE)
                                                    (PLOOKS (PREVPIECE PIECE)))
                                                   (T (OR (fetch (TEXTOBJ DEFAULTCHARLOOKS)
                                                             of TEXTOBJ)
                                                          (\TEDIT.UNIQUIFY.CHARLOOKS (
                                                                                  CHARLOOKS.FROM.FONT
                                                                                      DEFAULTFONT)
                                                                 TEXTOBJ]
          (freplace (PIECE PTYPE) of PIECE WITH (CL:IF (IMAGEOBJPROP OBJ 'SUBSTREAM)
                                                    SUBSTREAM.PTYPE
                                                    OBJECT.PTYPE))
          (RETURN OBJ])

(TEDIT.OBJECT.CHANGED
  [LAMBDA (TSTREAM OBJECT)                                   (* ; "Edited 18-Apr-2023 23:57 by rmk")
                                                             (* ; "Edited 10-Apr-2023 00:02 by rmk")
                                                             (* ; "Edited  9-Sep-2022 09:32 by rmk")
                                                             (* ; "Edited  6-Aug-2022 09:37 by rmk")
                                                            (* ; "Edited 12-Jun-90 17:51 by mitani")
                                                             (* ; 
                   "Notify TEdit that an object has changed, and the display may need to be updated.")
    (LET ((TEXTOBJ (TEXTOBJ TSTREAM))
          OBJPIECE CHANGEDCH#)
         [SETQ OBJPIECE (find PC inpieces (\FIRSTPIECE TEXTOBJ) suchthat (EQ OBJECT (PCONTENTS PC]
                                                             (* ; 
                                                             "Find the piece containing this object")
         (CL:UNLESS OBJPIECE (HELP "Changed OBJECT not found!?"))
         (SETQ CHANGEDCH# (\PCTOCH OBJPIECE))                (* ; "Get the CH# of the changed object")
         (\TEDIT.MARK.LINES.DIRTY TEXTOBJ CHANGEDCH# CHANGEDCH#)
                                                             (* ; "Mark affected lines")
         (replace (TEXTOBJ \DIRTY) of TEXTOBJ with T)        (* ; "And mark the document dirty.")
         (\SHOWSEL (fetch (TEXTOBJ SEL) of TEXTOBJ)
                NIL)
         (TEDIT.UPDATE.SCREEN TEXTOBJ)
         (\FIXSEL (fetch (TEXTOBJ SEL) of TEXTOBJ)
                TEXTOBJ)
         (\SHOWSEL (fetch (TEXTOBJ SEL) of TEXTOBJ)
                T])

(TEDIT.MAP.OBJECTS
  [LAMBDA (TEXTOBJ FN FNARG COLLECT?)                        (* ; "Edited  6-Nov-2022 12:15 by rmk")

    (* ;; "Apply FN to each of the imageobjects in TEXTOBJ.  If COLLECT? value is the list of (CH# OBJ FNVAL) pairs that satisfy the predicate")

    (* ;; "FN is a function of 3 args (OBJ   CH#-of-OBJ    FNARG)")

    (for CH# OBJ FNVAL from 1 by (PLEN PC) as PC inpieces (\FIRSTPIECE (TEXTOBJ TEXTOBJ))
       when (AND (EQ OBJECT.PTYPE (PTYPE PC))
                 (type? IMAGEOBJ (SETQ OBJ (PCONTENTS PC)))
                 (SETQ FNVAL (APPLY* FN CH# OBJ FNARG)))
       do (CL:WHEN COLLECT?
              (PUSH $$VAL (LIST CH# OBJ FNVAL)))
          (CL:WHEN (EQ FNVAL 'STOP)
                 (GO $$OUT)) finally (RETURN (DREVERSE $$VAL])
)

(FILESLOAD IMAGEOBJ)



(* ;; 
"Would be nice to just do (DOFILESLOAD (CDR TEDITFILES)).  But the order for exports.all and the order for loading have to be aligned."
)


(FILESLOAD TEDIT-FIND TEDIT-HISTORY TEDIT-FILE TEDIT-WINDOW TEDIT-SELECTION TEDIT-TFBRAVO TEDIT-HCPY
       TEDIT-PAGE TEDIT-MENU TEDIT-FNKEYS)



(* ; "TEDIT Support information")


(RPAQQ TEDITSYSTEMDATE "20-Jun-2023 23:09:30")



(* ; "LISTFILES Interface, so the system can decide if a file is a TEdit file.")


(ADDTOVAR PRINTFILETYPES (TEDIT (TEST \TEDIT.FORMATTEDP1)
                                (EXTENSION (TEDIT))))
(PUTPROPS TEDIT COPYRIGHT ("Venue & Xerox Corporation" 1983 1984 1985 1986 1987 1988 1989 1990 1991 
1992 1993 1995 1999 2018 2021))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (4244 5788 (MAKE-TEDIT-EXPORTS.ALL 4254 . 4511) (UPDATE-TEDIT 4513 . 5126) (EDIT-TEDIT 
5128 . 5786)) (7006 7299 (\CREATE.TEDIT.RESTART.MENU 7016 . 7297)) (7674 40855 (TEDIT 7684 . 13020) (
COERCETEXTOBJ 13022 . 15899) (TEDITSTRING 15901 . 16662) (TEDIT-SEE 16664 . 18817) (TEDIT.CHARWIDTH 
18819 . 20843) (TEDIT.COPY 20845 . 21353) (\TEDIT.MOVE.MSG 21355 . 23233) (TEDIT.DELETE 23235 . 24208)
 (TEDIT.INSERT 24210 . 25868) (TEDIT.KILL 25870 . 27427) (TEDIT.MAPPIECES 27429 . 28491) (TEDIT.QUIT 
28493 . 30064) (TEDIT.DO.BLUEPENDINGDELETE 30066 . 30805) (TEDIT.MOVE 30807 . 35582) (
TEDIT.STRINGWIDTH 35584 . 36255) (TEXTOBJ 36257 . 38565) (TEDITMENUP 38567 . 39156) (TEXTSTREAM 39158
 . 40853)) (40901 86370 (\TEDIT2 40911 . 43662) (\TEDIT.INSERT 43664 . 48167) (\TEDIT.MOVE.SELPIECES 
48169 . 50435) (\TEDIT.REPLACE.SELPIECES 50437 . 51978) (\TEDIT.INSERT.SELPIECES 51980 . 53432) (
\TEDIT.OBJECT.SHOWSEL 53434 . 57307) (\TEDIT.RESTARTFN 57309 . 59382) (\TEDIT.CHARDELETE 59384 . 61593
) (\TEDIT.COPYPIECE 61595 . 66213) (\TEDIT.APPLY.OBJFN 66215 . 68958) (\TEDIT.DELETE 68960 . 70865) (
\TEDIT.DIFFUSE.PARALOOKS 70867 . 73162) (\TEDIT.FOREIGN.COPY? 73164 . 77354) (\TEDIT.QUIT 77356 . 
80502) (\TEDIT.WORDDELETE 80504 . 83670) (\TEDIT1 83672 . 86368)) (86371 99578 (\SELPIECES.COPY 86381
 . 88145) (\SELPIECES.CONCAT 88147 . 89932) (\SELPIECES.CHARTRANSFORM 89934 . 93166) (
\SELPIECES.FROM.STRING 93168 . 97530) (\SELPIECES.TO.STRING 97532 . 99576)) (99732 112663 (
TEDIT.INSERT.OBJECT 99742 . 102922) (TEDIT.EDIT.OBJECT 102924 . 104929) (TEDIT.FIND.OBJECT 104931 . 
105338) (TEDIT.PUT.OBJECT 105340 . 107039) (TEDIT.GET.OBJECT 107041 . 109967) (TEDIT.OBJECT.CHANGED 
109969 . 111827) (TEDIT.MAP.OBJECTS 111829 . 112661)))))
STOP
