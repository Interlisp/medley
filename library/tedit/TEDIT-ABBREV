(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 2-Jan-2026 23:57:09" {WMEDLEY}<library>TEDIT>TEDIT-ABBREV.;38 15982  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.ABBREV.PARSE \TEDIT.ABBREV.EXPAND \TEDIT.ABBREV.TREE \TEDIT.TRY.ABBREV)
                  (VARS TEDIT-ABBREVCOMS)

      :PREVIOUS-DATE " 6-Sep-2025 00:10:45" {WMEDLEY}<library>tedit>TEDIT-ABBREV.;30)


(PRETTYCOMPRINT TEDIT-ABBREVCOMS)

(RPAQQ TEDIT-ABBREVCOMS
       [(FNS \TEDIT.ABBREV.EXPAND \TEDIT.ABBREV.PARSE \TEDIT.TRY.ABBREV \TEDIT.ABBREV.TREE)
        (FNS \TEDIT.EXPAND.DATE)
        (GLOBALVARS TEDIT.ABBREVS \TEDIT.ABBREVS.TREE \TEDIT.ABBREVS.INTREE)
        [INITVARS (\TEDIT.ABBREVS.TREE NIL)
               (\TEDIT.ABBREVS.INTREE NIL)
               (TEDIT.ABBREVS '(("b" "357,146" Bullet)
                                ("n" "357,44" Endash)
                                ("--" "357,44" Endash)
                                ("m" EMDASH)
                                ("---" EMDASH)
                                ("T" THINSPACE)
                                ("d" "357,60" Dagger)
                                ("D" "357,61" DoubleDagger)
                                ("s" "0,247" Section)
                                ("'" "0,271" RSQ)
                                ("`" "0,251" LSQ)
                                ("%"" LEFT-DOUBLEQUOTE)
                                ("~" RIGHT-DOUBLEQUOTE)
                                ("1/4" "0,274")
                                ("1/2" "0,275")
                                ("3/4" "0,276")
                                ("1/3" "357,375")
                                ("2/3" "357,376")
                                ("c" "0,323" Copyright)
                                ("c/o" "357,100" c/o)
                                ("%%" "357,100" c/o)
                                ("->" "0,256" Rightarrow)
                                ("ra" "0,256" Rightarrow)
                                ("|" "0,257" Downarrow)
                                ("da" "0,257" Downarrow)
                                ("L" "0,243" English-pound)
                                ("o" "0,260" Degree)
                                ("Y" "0,245" Yen)
                                ("+" "0,261" PlusMinus)
                                ("x" "0,264" Times)
                                ("/" "0,270" Divide)
                                ("lra" "357,121")
                                ("p" "0,266" Paragraph)
                                ("r" "0,322" Register)
                                ("t" "0,324" Trademark)
                                ("tm" "0,324" Trademark)
                                ("bbox" "42,43" Blackbox)
                                ("wbox" "43,42" Whitebox)
                                ("-" SOFT-HYPHEN)
                                ("=" NONBREAKING-HYPHEN)
                                (" " NONBREAKING-SPACE)
                                ("un" "357,127")
                                ("int" "357,126")
                                ("subset" "357,131")
                                ("superset" "357,130")
                                ("&" "357,266")
                                ("or" "357,267")
                                ("not" "357,152")
                                ("all" "357,265")
                                ("exist" "357,264")
                                ("def" "357,162")
                                ("compose" "357,147")
                                ("!" "0,241")
                                                             (* ; " Inverted !")
                                ("?" "0,277")
                                                             (* ; " Inverted ?")
                                ("u" "0,265" MicroSign)
                                ("<<" "0,253")
                                                             (* ; " Left double guillemet")
                                (">>" "0,273")
                                                             (* ; " Right double guillemet")
                                ("DATE" \TEDIT.EXPAND.DATE)
                                (">>DATE<<" \TEDIT.EXPAND.DATE]
        (DECLARE%: DONTEVAL@LOAD DOCOPY (P (\TEDIT.ABBREV.TREE])
(DEFINEQ

(\TEDIT.ABBREV.EXPAND
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited  2-Jan-2026 23:41 by rmk")
                                                             (* ; "Edited 20-Apr-2025 23:30 by rmk")
                                                             (* ; "Edited 20-Mar-2025 21:52 by rmk")
                                                             (* ; "Edited 30-May-91 19:27 by jds")
                                                             (* ; "Expand an abbvreviation")
    (\TEDIT.ABBREV.TREE)
    (LET* ((MATCH (\TEDIT.ABBREV.PARSE TSTREAM SEL))
           (FIRSTCHAR (CAR MATCH))
           (ABBREV (CADR MATCH))
           (EXPANSION (\TEDIT.TRY.ABBREV ABBREV TSTREAM)))
          (if EXPANSION
              then (TEDIT.PROMPTCLEAR TSTREAM)
                   (\TEDIT.UPDATE.SEL SEL FIRSTCHAR (NCHARS (CAR ABBREV))
                          'RIGHT
                          'NORMAL)                           (* ; "Set the target")
                   (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.FROM.STRING EXPANSION TEXTOBJ NIL
                                                    (PCHARLOOKS (\TEDIT.CHTOPC FIRSTCHAR TEXTOBJ)))
                          TSTREAM SEL)
            else (TEDIT.PROMPTPRINT TSTREAM "No abbreviation to expand" T])

(\TEDIT.ABBREV.PARSE
  [LAMBDA (TSTREAM SEL)                                      (* ; "Edited  2-Jan-2026 23:56 by rmk")

    (* ;; "For the longest match returns (firstchar abbrev)) where abbrev is a (key expansion) pair.  If the longest explicit match is not found and the selection itself is a character name cname, a (cname cname) abbreviation is manufactured.")

    (LET ((LASTCHAR (SUB1 (FGETSEL SEL CHLIM)))
          (FIRSTCHAR (FGETSEL SEL CH#))
          ABBREV STRING)
         (CL:WHEN (SETQ ABBREV (SELECTQ (FGETSEL SEL SELKIND)
                                   (* ;; "We pick the longest match for the characters before the point. If nothing matches, then we try to decode  the prefix to the last word boundary as a character name.  In that case, we skip over comma (e.g. 357,201). ")
                                   ((CHAR LINE) 
                                        (if (ILEQ (FGETSEL SEL DCH)
                                                  1)
                                            then             (* ; 
                                                      "Point or 1-character selection, walk backward")
                                                 (SETQ FIRSTCHAR (ADD1 LASTCHAR))
                                                 (bind CH (TREE _ \TEDIT.ABBREVS.TREE)
                                                    eachtime (add FIRSTCHAR -1)
                                                    while (SETQ TREE (GETMULTI TREE (
                                                                                    TEDIT.NTHCHARCODE
                                                                                     TSTREAM 
                                                                                     FIRSTCHAR)))
                                                    do (SETQ $$VAL (GETMULTI TREE 'ABBREV))
                                                    finally (add FIRSTCHAR 1))
                                          else (SETQ STRING (TEDIT.SEL.AS.STRING TSTREAM FIRSTCHAR
                                                                   (FGETSEL SEL DCH)))
                                               (if [FETCHMULTI \TEDIT.ABBREVS.TREE
                                                          (DREVERSE (CONS 'ABBREV (CHCON STRING]
                                                 elseif (CHARCODE.DECODE STRING T)
                                                   then 

                                             (* ;; "The CHARCODE case: the whole selection must be the character name %"TAB%" or %"Greek,A%".  But no singletons:  E")

                                                        (LIST STRING STRING))))
                                   (WORD 
                                         (* ;; 
                  "A word selection either matches a whole key or it matches a whole character name.")

                                         (SETQ STRING (TEDIT.SEL.AS.STRING TSTREAM FIRSTCHAR
                                                             (FGETSEL SEL DCH)))
                                         (OR [FETCHMULTI \TEDIT.ABBREVS.TREE
                                                    (DREVERSE (CONS 'ABBREV (CHCON STRING]
                                             (AND (CHARCODE.DECODE STRING T)
                                                  (LIST STRING STRING))))
                                   (SHOULDNT)))
                (LIST FIRSTCHAR ABBREV])

(\TEDIT.TRY.ABBREV
  [LAMBDA (ABBREV TSTREAM)                                   (* ; "Edited  2-Jan-2026 22:46 by rmk")
                                                             (* ; "Edited  6-Sep-2025 00:09 by rmk")
                                                             (* ; "Edited 20-Mar-2025 21:52 by rmk")
                                                            (* ; "Edited  6-Aug-2020 14:41 by rmk:")
                                                             (* jds "11-Jul-85 12:46")

    (* ;; "Decode the expansion:")

    (* ;; "  A string may be a character name, otherwise itself. ")

    (* ;; 
   "  A litatom may be a character name,otherwise it is a function (if it has a GETD) to be applied.")

    (* ;; "  Anything else is evaled. ")

    (LET ((KEY (CAR ABBREV))
          (EXPANSION (CADR ABBREV))
          CH)
         (CL:WHEN (LISTP EXPANSION)                          (* ; 
                                      "Originally stored in the CDR. Now can be followed by comments")
             (SETQ EXPANSION (CAR EXPANSION)))
         (if (NULL EXPANSION)
             then 
                  (* ;; "So basically you can use any character name to insert its character")

                  (CL:WHEN (SETQ CH (CHARCODE.DECODE KEY T))
                         (CHARACTER CH))
           elseif (AND (OR (STRINGP EXPANSION)
                           (LITATOM EXPANSION))
                       (SETQ CH (CHARCODE.DECODE EXPANSION T)))
             then 
                  (* ;; "Could be a character code")

                  (CHARACTER CH)
           elseif (STRINGP EXPANSION)
             then 
                  (* ;; " Could be a character code")

                  (CL:IF (SETQ CH (CHARCODE.DECODE EXPANSION T))
                      (CHARACTER CH)
                      EXPANSION)
           elseif (SMALLP EXPANSION)
             then 
                  (* ;; "Treat a number as a character code.")

                  (CHARACTER EXPANSION)
           elseif (AND (LITATOM EXPANSION)
                       (OR (SETQ CH (CHARCODE.DECODE EXPANSION T))
                           (GETD EXPANSION)))
             then                                            (* ; 
                                                             " Either a character name or a function")
                  (CL:IF CH
                      (CHARACTER CH)
                      (APPLY* EXPANSION TSTREAM KEY))
           elseif (LISTP EXPANSION)
             then                                            (* ; "Form in the CADR, now")
                  (EVAL EXPANSION)
           elseif (AND (SETQ EXPANSION (CDR (SASSOC KEY TEDIT.ABBREVS)))
                       (LITATOM (CAR EXPANSION))
                       (GETD (CAR EXPANSION)))
             then 
                  (* ;; "Form in the CDR, originally.  Have to refetch EXPANSION")

                  (EVAL EXPANSION])

(\TEDIT.ABBREV.TREE
  [LAMBDA NIL                                                (* ; "Edited  2-Jan-2026 23:41 by rmk")
    (CL:UNLESS (EQ TEDIT.ABBREVS \TEDIT.ABBREVS.INTREE)
        (SETQ \TEDIT.ABBREVS.TREE NIL)
        (for A in TEDIT.ABBREVS unless (EQ (CAR A)
                                           '*) do (STOREMULTI \TEDIT.ABBREVS.TREE
                                                         [DREVERSE (CONS 'ABBREV (CHCON (CAR A]
                                                         A))
        (SETQ \TEDIT.ABBREVS.INTREE TEDIT.ABBREVS))])
)
(DEFINEQ

(\TEDIT.EXPAND.DATE
  [LAMBDA (STREAM CH)                                        (* ; "Edited 23-Feb-88 10:41 by jds")

    (* ;; "Provide the date as the expansion for an abbreviation")

    (PROG* ((DATE (\UNPACKDATE))
            (YEAR (pop DATE))
            (MONTH (pop DATE))
            (DAY (pop DATE)))
           (RETURN (CONCAT (CAR (NTH '("January" "February" "March" "April" "May" "June" "July" 
                                             "August" "September" "October" "November" "December")
                                     (ADD1 MONTH)))
                          " " DAY ", " YEAR])
)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDIT.ABBREVS \TEDIT.ABBREVS.TREE \TEDIT.ABBREVS.INTREE)
)

(RPAQ? \TEDIT.ABBREVS.TREE NIL)

(RPAQ? \TEDIT.ABBREVS.INTREE NIL)

(RPAQ? TEDIT.ABBREVS
       '(("b" "357,146" Bullet)
         ("n" "357,44" Endash)
         ("--" "357,44" Endash)
         ("m" EMDASH)
         ("---" EMDASH)
         ("T" THINSPACE)
         ("d" "357,60" Dagger)
         ("D" "357,61" DoubleDagger)
         ("s" "0,247" Section)
         ("'" "0,271" RSQ)
         ("`" "0,251" LSQ)
         ("%"" LEFT-DOUBLEQUOTE)
         ("~" RIGHT-DOUBLEQUOTE)
         ("1/4" "0,274")
         ("1/2" "0,275")
         ("3/4" "0,276")
         ("1/3" "357,375")
         ("2/3" "357,376")
         ("c" "0,323" Copyright)
         ("c/o" "357,100" c/o)
         ("%%" "357,100" c/o)
         ("->" "0,256" Rightarrow)
         ("ra" "0,256" Rightarrow)
         ("|" "0,257" Downarrow)
         ("da" "0,257" Downarrow)
         ("L" "0,243" English-pound)
         ("o" "0,260" Degree)
         ("Y" "0,245" Yen)
         ("+" "0,261" PlusMinus)
         ("x" "0,264" Times)
         ("/" "0,270" Divide)
         ("lra" "357,121")
         ("p" "0,266" Paragraph)
         ("r" "0,322" Register)
         ("t" "0,324" Trademark)
         ("tm" "0,324" Trademark)
         ("bbox" "42,43" Blackbox)
         ("wbox" "43,42" Whitebox)
         ("-" SOFT-HYPHEN)
         ("=" NONBREAKING-HYPHEN)
         (" " NONBREAKING-SPACE)
         ("un" "357,127")
         ("int" "357,126")
         ("subset" "357,131")
         ("superset" "357,130")
         ("&" "357,266")
         ("or" "357,267")
         ("not" "357,152")
         ("all" "357,265")
         ("exist" "357,264")
         ("def" "357,162")
         ("compose" "357,147")
         ("!" "0,241")
                                                             (* ; " Inverted !")
         ("?" "0,277")
                                                             (* ; " Inverted ?")
         ("u" "0,265" MicroSign)
         ("<<" "0,253")
                                                             (* ; " Left double guillemet")
         (">>" "0,273")
                                                             (* ; " Right double guillemet")
         ("DATE" \TEDIT.EXPAND.DATE)
         (">>DATE<<" \TEDIT.EXPAND.DATE)))
(DECLARE%: DONTEVAL@LOAD DOCOPY 

(\TEDIT.ABBREV.TREE)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (4321 12912 (\TEDIT.ABBREV.EXPAND 4331 . 5698) (\TEDIT.ABBREV.PARSE 5700 . 9261) (
\TEDIT.TRY.ABBREV 9263 . 12321) (\TEDIT.ABBREV.TREE 12323 . 12910)) (12913 13558 (\TEDIT.EXPAND.DATE 
12923 . 13556)))))
STOP
