(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "13-Sep-2022 12:37:41" 
{DSK}<Users>kaplan>Local>medley3.5>working-medley>library>tedit>TEDIT-ABBREV.;4 9597   

      :CHANGES-TO (VARS TEDIT-ABBREVCOMS)

      :PREVIOUS-DATE " 8-Sep-2022 23:53:37" 
{DSK}<Users>kaplan>Local>medley3.5>working-medley>library>tedit>TEDIT-ABBREV.;3)


(PRETTYCOMPRINT TEDIT-ABBREVCOMS)

(RPAQQ TEDIT-ABBREVCOMS
       [(FNS \TEDIT.ABBREV.EXPAND \TEDIT.EXPAND.DATE \TEDIT.TRY.ABBREV)
        (GLOBALVARS TEDIT.ABBREVS)
        (INITVARS (TEDIT.ABBREVS '(("b" . "357,146")
                                   ("n" . "357,44")
                                   ("m" . "357,45")
                                   ("T" . "357,57")
                                   ("d" . "357,60")
                                   ("D" . "357,61")
                                   ("s" . "0,247")
                                   ("'" . "0,271")
                                   ("`" . "0,251")
                                   ("%"" . "0,252")
                                   ("~" . "0,272")
                                   ("1/4" . "0,274")
                                   ("1/2" . "0,275")
                                   ("3/4" . "0,276")
                                   ("1/3" . "357,375")
                                   ("2/3" . "357,376")
                                   ("c" . "0,323")
                                   ("c/o" . "357,100")
                                   ("%%" . "357,100")
                                   ("->" . "0,256")
                                   ("ra" . "0,256")
                                   ("|" . "0,257")
                                   ("da" . "0,257")
                                   ("^" . "0,255")
                                   ("ua" . "0,255")
                                   ("<-" . "0,254")
                                   ("la" . "0,254")
                                   ("_" . "0,254")
                                   ("L" . "0,243")
                                   ("o" . "0,260")
                                   ("Y" . "0,245")
                                   ("+" . "0,261")
                                   ("x" . "0,264")
                                   ("/" . "0,270")
                                   ("=" . "357,121")
                                   ("p" . "0,266")
                                   ("r" . "0,322")
                                   ("t" . "0,324")
                                   ("tm" . "0,324")
                                   ("box" . "42,42")
                                   ("cbox" . "42,61")
                                   ("-" . "357,43")
                                   ("=" . "357,42")
                                   (" " . "357,41")
                                   ("DATE" . \TEDIT.EXPAND.DATE)
                                   (">>DATE<<" . \TEDIT.EXPAND.DATE])
(DEFINEQ

(\TEDIT.ABBREV.EXPAND
  [LAMBDA (TSTREAM)                                          (* ; "Edited  8-Sep-2022 23:53 by rmk")
                                                             (* ; "Edited  1-Aug-2022 12:04 by rmk")
                                                             (* ; "Edited 30-May-91 19:27 by jds")
                                                             (* ; "Expand an abbvreviation")
    (PROG ((TEXTOBJ (fetch (TEXTSTREAM TEXTOBJ) of TSTREAM))
           SEL CH# CH OLDLOOKS EXPANSION)
          (SETQ SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
          (SETQ CH# (SELECTQ (fetch (SELECTION POINT) of SEL)
                        (LEFT (SUB1 (fetch (SELECTION CH#) of SEL)))
                        (RIGHT (SUB1 (fetch (SELECTION CHLIM) of SEL)))
                        0))
          [COND
             ((ZEROP (fetch (SELECTION DCH) of SEL))         (* ; 
                                                  "Point Selection, so use the character to the left")
              (COND
                 ((ZEROP CH#)                                (* ; 
                                       "If we're off the front of the document, don't bother trying.")
                  (RETURN)))
              (\TEXTSETFILEPTR TSTREAM (SUB1 CH#)
                     CH#)
              [SETQ CH (MKSTRING (CHARACTER (BIN TSTREAM]
              (TEDIT.SETSEL TSTREAM CH# 1 'RIGHT))
             (T                                              (* ; 
                                              "We have a selection that isn't just a caret.  Use it.")
                (SETQ CH (TEDIT.SEL.AS.STRING TSTREAM]
          (SETQ EXPANSION (\TEDIT.TRY.ABBREV CH TSTREAM))    (* ; "Find the abbreviation's expansion --first try it as-is, then try the upper-case version to be safe.")
          (CL:WHEN EXPANSION                                 (* ; 
                                                "It exists, so insert it where the abbrev used to be")
              (replace (TEXTOBJ HINTPC) of TEXTOBJ with NIL) (* ; "Force it to abandon caching")
              (SETQ OLDLOOKS (TEDIT.GET.LOOKS TEXTOBJ))
              (TEDIT.DELETE TEXTOBJ SEL)                     (* ; 
                                                            "First, delete the thing being expanded.")
              (TEDIT.INSERT TSTREAM EXPANSION SEL OLDLOOKS))])

(\TEDIT.EXPAND.DATE
  [LAMBDA (STREAM CH)                                        (* ; "Edited 23-Feb-88 10:41 by jds")

    (* ;; "Provide the date as the expansion for an abbreviation")

    (PROG* ((DATE (\UNPACKDATE))
            (YEAR (pop DATE))
            (MONTH (pop DATE))
            (DAY (pop DATE)))
           (RETURN (CONCAT (CAR (NTH '("January" "February" "March" "April" "May" "June" "July" 
                                             "August" "September" "October" "November" "December")
                                     (ADD1 MONTH)))
                          " " DAY ", " YEAR])

(\TEDIT.TRY.ABBREV
  [LAMBDA (ABBREV STREAM)                                   (* ; "Edited  6-Aug-2020 14:41 by rmk:")
                                                             (* jds "11-Jul-85 12:46")

    (* ;; 
    "Try expanding ABBREV as an abbreviation.  Return the expansion;  NIL = no such abbreviation.")

    (* ;; "RMK:  Established that a character-code looking string (%"357,201%" or %"02FE%") or a number is a character code that converts to a character.")

    (PROG (SEL CH# (CH NIL)
               EXPANSION)
          (SETQ EXPANSION (OR (SASSOC ABBREV TEDIT.ABBREVS)
                              (SASSOC (U-CASE ABBREV)
                                     TEDIT.ABBREVS)))

         (* Find the abbreviation's expansion --first try it as-is, then try the 
         upper-case version to be safe.)

          (RETURN (COND
                     (EXPANSION                              (* There's an expansion.
                                                             Turn it into an insertable string.)
                            (COND
                               [(STRINGP (CDR EXPANSION))

                                (* ;; "Could be a character code")

                                (COND
                                   ((SETQ CH (CHARCODE.DECODE (CDR EXPANSION)
                                                    T))
                                    (CHARACTER CH))
                                   (T (CDR EXPANSION]
                               ((SMALLP (CDR EXPANSION))

                                (* ;; "Treat a number as a character code.")

                                (CHARACTER (CDR EXPANSION)))
                               ((AND (LITATOM (CDR EXPANSION))
                                     (GETD (CDR EXPANSION))) (* It's a function to be called.)
                                (APPLY* (CDR EXPANSION)
                                       STREAM CH))
                               (T                            (* Anything else is a form to EVAL.)
                                  (EVAL (CDR EXPANSION])
)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDIT.ABBREVS)
)

(RPAQ? TEDIT.ABBREVS
       '(("b" . "357,146")
         ("n" . "357,44")
         ("m" . "357,45")
         ("T" . "357,57")
         ("d" . "357,60")
         ("D" . "357,61")
         ("s" . "0,247")
         ("'" . "0,271")
         ("`" . "0,251")
         ("%"" . "0,252")
         ("~" . "0,272")
         ("1/4" . "0,274")
         ("1/2" . "0,275")
         ("3/4" . "0,276")
         ("1/3" . "357,375")
         ("2/3" . "357,376")
         ("c" . "0,323")
         ("c/o" . "357,100")
         ("%%" . "357,100")
         ("->" . "0,256")
         ("ra" . "0,256")
         ("|" . "0,257")
         ("da" . "0,257")
         ("^" . "0,255")
         ("ua" . "0,255")
         ("<-" . "0,254")
         ("la" . "0,254")
         ("_" . "0,254")
         ("L" . "0,243")
         ("o" . "0,260")
         ("Y" . "0,245")
         ("+" . "0,261")
         ("x" . "0,264")
         ("/" . "0,270")
         ("=" . "357,121")
         ("p" . "0,266")
         ("r" . "0,322")
         ("t" . "0,324")
         ("tm" . "0,324")
         ("box" . "42,42")
         ("cbox" . "42,61")
         ("-" . "357,43")
         ("=" . "357,42")
         (" " . "357,41")
         ("DATE" . \TEDIT.EXPAND.DATE)
         (">>DATE<<" . \TEDIT.EXPAND.DATE)))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (2971 8253 (\TEDIT.ABBREV.EXPAND 2981 . 5468) (\TEDIT.EXPAND.DATE 5470 . 6103) (
\TEDIT.TRY.ABBREV 6105 . 8251)))))
STOP
