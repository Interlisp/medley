(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 7-Jan-2026 21:53:33" {WMEDLEY}<library>TEDIT>TEDIT-ABBREV.;51 17556  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.ABBREV.PARSE.CHARCODE \TEDIT.ABBREV.EXPAND \TEDIT.ABBREV.PARSE 
                       \TEDIT.ABBREV.TREE \TEDIT.ABBREV.EXPANSION \TEDIT.TRY.ABBREV)
                  (VARS TEDIT-ABBREVCOMS)

      :PREVIOUS-DATE " 6-Sep-2025 00:10:45" {WMEDLEY}<library>tedit>TEDIT-ABBREV.;30)


(PRETTYCOMPRINT TEDIT-ABBREVCOMS)

(RPAQQ TEDIT-ABBREVCOMS
       [(FNS \TEDIT.ABBREV.EXPAND \TEDIT.ABBREV.EXPANSION \TEDIT.ABBREV.TREE \TEDIT.ABBREV.PARSE 
             \TEDIT.ABBREV.PARSE.CHARCODE)
        (FNS \TEDIT.EXPAND.DATE)
        (GLOBALVARS TEDIT.ABBREVS \TEDIT.ABBREVS.TREE \TEDIT.ABBREVS.INTREE)
        (INITVARS (\TEDIT.ABBREVS.TREE NIL)
               (\TEDIT.ABBREVS.INTREE NIL)
               (TEDIT.ABBREVS '(("b" "357,146" Bullet)
                                ("n" "357,44" Endash)
                                ("--" "357,44" Endash)
                                ("m" EMDASH)
                                ("---" EMDASH)
                                ("T" THINSPACE)
                                ("d" "357,60" Dagger)
                                ("D" "357,61" DoubleDagger)
                                ("s" "0,247" Section)
                                ("'" "0,271" RSQ)
                                ("`" "0,251" LSQ)
                                ("%"" LEFT-DOUBLEQUOTE)
                                ("~" RIGHT-DOUBLEQUOTE)
                                ("1/4" "0,274")
                                ("1/2" "0,275")
                                ("3/4" "0,276")
                                ("1/3" "357,375")
                                ("2/3" "357,376")
                                ("c" "0,323" Copyright)
                                ("c/o" "357,100" c/o)
                                ("%%" "357,100" c/o)
                                ("->" "0,256" Rightarrow)
                                ("ra" "0,256" Rightarrow)
                                ("|" "0,257" Downarrow)
                                ("da" "0,257" Downarrow)
                                ("L" "0,243" English-pound)
                                ("o" "0,260" Degree)
                                ("Y" "0,245" Yen)
                                ("+-" "0,261" PlusMinus)
                                ("x" "0,264" Times)
                                ("/" "0,270" Divide)
                                ("lra" "357,121")
                                ("p" "0,266" Paragraph)
                                ("r" "0,322" Register)
                                ("t" "0,324" Trademark)
                                ("tm" "0,324" Trademark)
                                ("bbox" "42,43" Blackbox)
                                ("wbox" "43,42" Whitebox)
                                ("-" SOFT-HYPHEN)
                                ("=" NONBREAKING-HYPHEN)
                                (" " NONBREAKING-SPACE)
                                ("un" "357,127")
                                ("int" "357,126")
                                ("subset" "357,131")
                                ("superset" "357,130")
                                ("&" "357,266")
                                ("or" "357,267")
                                ("not" "357,152")
                                ("all" "357,265")
                                ("exist" "357,264")
                                ("def" "357,162")
                                (in "357,112" Member)
                                ("compose" "357,147")
                                ("!" "0,241")
                                                             (* ; " Inverted !")
                                ("?" "0,277")
                                                             (* ; " Inverted ?")
                                ("u" "0,265" MicroSign)
                                ("<<" "0,253")
                                                             (* ; " Left double guillemet")
                                (">>" "0,273")
                                                             (* ; " Right double guillemet")
                                ("DATE" \TEDIT.EXPAND.DATE)
                                (">>DATE<<" \TEDIT.EXPAND.DATE])
(DEFINEQ

(\TEDIT.ABBREV.EXPAND
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited  7-Jan-2026 09:58 by rmk")
                                                             (* ; "Edited  3-Jan-2026 13:13 by rmk")
                                                             (* ; "Edited 20-Apr-2025 23:30 by rmk")
                                                             (* ; "Edited 20-Mar-2025 21:52 by rmk")
                                                             (* ; "Edited 30-May-91 19:27 by jds")
                                                             (* ; "Expand an abbvreviation")
    (\TEDIT.ABBREV.TREE)

    (* ;; "If a point selection (DCH <= 1), let the tree control the match, otherwise stop at the beginning of the selection.  If the character before the caret is \, then the match string consists of all characters between that \ and the first preceding one.")

    (LET* ((LASTCHNO (GETSEL SEL CHLAST))
           (POINTSELECTION (ILEQ (FGETSEL SEL DCH)
                                 1))
           (FIRSTCHNO (CL:IF POINTSELECTION
                          1
                          (FGETSEL SEL CH#)))
           (BACKSLASH 0)
           ABBREV EXPANSION LEN)
          (CL:WHEN (MEMB (TEDIT.NTHCHARCODE TSTREAM LASTCHNO)
                         (CHARCODE (EOL FORM Meta,EOL)))

              (* ;; "Line or paragraph selection: back up over the terminator.  Maybe we should back up over spaces too--except for the no-breaking space abbreviation?")

              (add LASTCHNO -1))
          (CL:WHEN (EQ (CHARCODE \)
                       (TEDIT.NTHCHARCODE TSTREAM LASTCHNO)) (* ; 
                        "But if selection ends with \, go back to previous \ to match/consume \xxx\ ")
              (SETQ BACKSLASH 1)                             (* ; 
                                                             "Started with backslash, extend match")
              (for I CH from (SUB1 LASTCHNO) by -1 as J from 1 to 25
                 do (SETQ CH (TEDIT.NTHCHARCODE TSTREAM I))  (* ; "Don't cross over an image obj")
                    (if (IMAGEOBJP CH)
                        then (RETURN)
                      elseif (EQ CH (CHARCODE \))
                        then (SETQ FIRSTCHNO I)
                             (RETURN)))
              (add LASTCHNO -1))
          (if (AND FIRSTCHNO [SETQ ABBREV (OR (\TEDIT.ABBREV.PARSE TSTREAM FIRSTCHNO LASTCHNO 
                                                     POINTSELECTION)
                                              (\TEDIT.ABBREV.PARSE TSTREAM FIRSTCHNO LASTCHNO 
                                                     POINTSELECTION T)
                                              (CL:UNLESS (AND POINTSELECTION (EQ 0 BACKSLASH))
                                                     (\TEDIT.ABBREV.PARSE.CHARCODE TSTREAM FIRSTCHNO
                                                            LASTCHNO]
                   (SETQ EXPANSION (\TEDIT.ABBREV.EXPANSION ABBREV TSTREAM)))
              then (TEDIT.PROMPTCLEAR TSTREAM)
                   (add LASTCHNO BACKSLASH)                  (* ; 
                                                       "LASTCHNO and LEN include the final backslash")
                   (SETQ LEN (IPLUS (NCHARS (CAR ABBREV))
                                    BACKSLASH))
                   (SETQ FIRSTCHNO (ADD1 (IDIFFERENCE LASTCHNO LEN)))
                   (\TEDIT.UPDATE.SEL SEL FIRSTCHNO LEN 'RIGHT 'NORMAL) 
                                                             (* ; "Set the target")
                   (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.FROM.STRING EXPANSION TEXTOBJ NIL
                                                    (PCHARLOOKS (\TEDIT.CHTOPC FIRSTCHNO TEXTOBJ)))
                          TSTREAM SEL)
            else (TEDIT.PROMPTPRINT TSTREAM "No abbreviation to expand" T])

(\TEDIT.ABBREV.EXPANSION
  [LAMBDA (ABBREV TSTREAM)                                   (* ; "Edited  2-Jan-2026 22:46 by rmk")
                                                             (* ; "Edited  6-Sep-2025 00:09 by rmk")
                                                             (* ; "Edited 20-Mar-2025 21:52 by rmk")
                                                            (* ; "Edited  6-Aug-2020 14:41 by rmk:")
                                                             (* jds "11-Jul-85 12:46")

    (* ;; "Decode the expansion:")

    (* ;; "  A string may be a character name, otherwise itself. ")

    (* ;; 
   "  A litatom may be a character name,otherwise it is a function (if it has a GETD) to be applied.")

    (* ;; "  Anything else is evaled. ")

    (LET ((KEY (CAR ABBREV))
          (EXPANSION (CADR ABBREV))
          CH)
         (CL:WHEN (LISTP EXPANSION)                          (* ; 
                                      "Originally stored in the CDR. Now can be followed by comments")
             (SETQ EXPANSION (CAR EXPANSION)))
         (if (NULL EXPANSION)
             then 
                  (* ;; "So basically you can use any character name to insert its character")

                  (CL:WHEN (SETQ CH (CHARCODE.DECODE KEY T))
                         (CHARACTER CH))
           elseif (AND (OR (STRINGP EXPANSION)
                           (LITATOM EXPANSION))
                       (SETQ CH (CHARCODE.DECODE EXPANSION T)))
             then 
                  (* ;; "Could be a character code")

                  (CHARACTER CH)
           elseif (STRINGP EXPANSION)
             then 
                  (* ;; " Could be a character code")

                  (CL:IF (SETQ CH (CHARCODE.DECODE EXPANSION T))
                      (CHARACTER CH)
                      EXPANSION)
           elseif (SMALLP EXPANSION)
             then 
                  (* ;; "Treat a number as a character code.")

                  (CHARACTER EXPANSION)
           elseif (AND (LITATOM EXPANSION)
                       (OR (SETQ CH (CHARCODE.DECODE EXPANSION T))
                           (GETD EXPANSION)))
             then                                            (* ; 
                                                             " Either a character name or a function")
                  (CL:IF CH
                      (CHARACTER CH)
                      (APPLY* EXPANSION TSTREAM KEY))
           elseif (LISTP EXPANSION)
             then                                            (* ; "Form in the CADR, now")
                  (EVAL EXPANSION)
           elseif (AND (SETQ EXPANSION (CDR (SASSOC KEY TEDIT.ABBREVS)))
                       (LITATOM (CAR EXPANSION))
                       (GETD (CAR EXPANSION)))
             then 
                  (* ;; "Form in the CDR, originally.  Have to refetch EXPANSION")

                  (EVAL EXPANSION])

(\TEDIT.ABBREV.TREE
  [LAMBDA (ALWAYS)                                           (* ; "Edited  6-Jan-2026 22:02 by rmk")
                                                             (* ; "Edited  4-Jan-2026 09:01 by rmk")
    (CL:UNLESS (AND (NOT ALWAYS)
                    (EQUAL TEDIT.ABBREVS \TEDIT.ABBREVS.INTREE))
        (SETQ \TEDIT.ABBREVS.TREE NIL)
        (for A in TEDIT.ABBREVS unless (EQ (CAR A)
                                           '*)
           do (STOREMULTI \TEDIT.ABBREVS.TREE [DREVERSE (LIST* 'ABBREV (UNPACK (CAR A]
                     A)
              (CL:UNLESS (EQ '\ (NTHCHAR (CAR A)
                                       1))                   (* ; 
                                  "Backslash at the beginning, if not already there, like Tex:  \cup")
                  (SETQ A (CONS (PACK* "\" (CAR A))
                                (CDR A)))
                  (STOREMULTI \TEDIT.ABBREVS.TREE [DREVERSE (LIST* 'ABBREV (UNPACK (CAR A]
                         A)))
        (SETQ \TEDIT.ABBREVS.INTREE TEDIT.ABBREVS)
        \TEDIT.ABBREVS.TREE)])

(\TEDIT.ABBREV.PARSE
  [LAMBDA (TSTREAM FIRSTCHNO LASTCHNO POINTSELECTION CASEINSENSITIVE)
                                                             (* ; "Edited  7-Jan-2026 09:55 by rmk")
                                                             (* ; "Edited  3-Jan-2026 22:50 by rmk")

    (* ;; "But if LA")

    (for CHNO CH MATCH (DCH _ (ADD1 (IDIFFERENCE LASTCHNO FIRSTCHNO)))
         (TREE _ \TEDIT.ABBREVS.TREE) by -1 from LASTCHNO to FIRSTCHNO
       while [PROGN (SETQ CH (TEDIT.NTHCHAR TSTREAM CHNO))
                    (SETQ TREE (CL:IF CASEINSENSITIVE
                                   (CL:ASSOC CH TREE :TEST (FUNCTION STRING.EQUAL))
                                   (ASSOC CH TREE))] when (SETQ MATCH (CDR (ASSOC 'ABBREV TREE)))
       do (SETQ $$VAL MATCH) finally 

                                   (* ;; 
           "Return NIL for a multi-char selection if the longest match doesn't cover the whole thing")

                                   (CL:UNLESS [OR POINTSELECTION (EQ DCH (NCHARS (CAR MATCH]
                                          (RETURN NIL])

(\TEDIT.ABBREV.PARSE.CHARCODE
  [LAMBDA (TSTREAM FIRSTCHNO LASTCHNO)                       (* ; "Edited  7-Jan-2026 21:53 by rmk")
    (LET ((STRING (TEDIT.SEL.AS.STRING TSTREAM FIRSTCHNO (ADD1 (IDIFFERENCE LASTCHNO FIRSTCHNO))
                         0))
          CHARCODE)
         (CL:WHEN (SETQ CHARCODE (CHARCODE.DECODE (CL:IF (EQ (CHARCODE \)
                                                             (CHCON1 STRING))
                                                      (SUBSTRING STRING 2)
                                                      STRING)
                                        T))
             (LIST STRING (CHARACTER CHARCODE)))])
)
(DEFINEQ

(\TEDIT.EXPAND.DATE
  [LAMBDA (STREAM CH)                                        (* ; "Edited 23-Feb-88 10:41 by jds")

    (* ;; "Provide the date as the expansion for an abbreviation")

    (PROG* ((DATE (\UNPACKDATE))
            (YEAR (pop DATE))
            (MONTH (pop DATE))
            (DAY (pop DATE)))
           (RETURN (CONCAT (CAR (NTH '("January" "February" "March" "April" "May" "June" "July" 
                                             "August" "September" "October" "November" "December")
                                     (ADD1 MONTH)))
                          " " DAY ", " YEAR])
)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDIT.ABBREVS \TEDIT.ABBREVS.TREE \TEDIT.ABBREVS.INTREE)
)

(RPAQ? \TEDIT.ABBREVS.TREE NIL)

(RPAQ? \TEDIT.ABBREVS.INTREE NIL)

(RPAQ? TEDIT.ABBREVS
       '(("b" "357,146" Bullet)
         ("n" "357,44" Endash)
         ("--" "357,44" Endash)
         ("m" EMDASH)
         ("---" EMDASH)
         ("T" THINSPACE)
         ("d" "357,60" Dagger)
         ("D" "357,61" DoubleDagger)
         ("s" "0,247" Section)
         ("'" "0,271" RSQ)
         ("`" "0,251" LSQ)
         ("%"" LEFT-DOUBLEQUOTE)
         ("~" RIGHT-DOUBLEQUOTE)
         ("1/4" "0,274")
         ("1/2" "0,275")
         ("3/4" "0,276")
         ("1/3" "357,375")
         ("2/3" "357,376")
         ("c" "0,323" Copyright)
         ("c/o" "357,100" c/o)
         ("%%" "357,100" c/o)
         ("->" "0,256" Rightarrow)
         ("ra" "0,256" Rightarrow)
         ("|" "0,257" Downarrow)
         ("da" "0,257" Downarrow)
         ("L" "0,243" English-pound)
         ("o" "0,260" Degree)
         ("Y" "0,245" Yen)
         ("+-" "0,261" PlusMinus)
         ("x" "0,264" Times)
         ("/" "0,270" Divide)
         ("lra" "357,121")
         ("p" "0,266" Paragraph)
         ("r" "0,322" Register)
         ("t" "0,324" Trademark)
         ("tm" "0,324" Trademark)
         ("bbox" "42,43" Blackbox)
         ("wbox" "43,42" Whitebox)
         ("-" SOFT-HYPHEN)
         ("=" NONBREAKING-HYPHEN)
         (" " NONBREAKING-SPACE)
         ("un" "357,127")
         ("int" "357,126")
         ("subset" "357,131")
         ("superset" "357,130")
         ("&" "357,266")
         ("or" "357,267")
         ("not" "357,152")
         ("all" "357,265")
         ("exist" "357,264")
         ("def" "357,162")
         (in "357,112" Member)
         ("compose" "357,147")
         ("!" "0,241")
                                                             (* ; " Inverted !")
         ("?" "0,277")
                                                             (* ; " Inverted ?")
         ("u" "0,265" MicroSign)
         ("<<" "0,253")
                                                             (* ; " Left double guillemet")
         (">>" "0,273")
                                                             (* ; " Right double guillemet")
         ("DATE" \TEDIT.EXPAND.DATE)
         (">>DATE<<" \TEDIT.EXPAND.DATE)))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (4438 14515 (\TEDIT.ABBREV.EXPAND 4448 . 8486) (\TEDIT.ABBREV.EXPANSION 8488 . 11552) (
\TEDIT.ABBREV.TREE 11554 . 12685) (\TEDIT.ABBREV.PARSE 12687 . 13839) (\TEDIT.ABBREV.PARSE.CHARCODE 
13841 . 14513)) (14516 15161 (\TEDIT.EXPAND.DATE 14526 . 15159)))))
STOP
