(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 6-Apr-2023 21:40:07" {WMEDLEY}<library>TEDIT>TEDIT-CHAT.;9 12585  

      :CHANGES-TO (FNS TEDIT.DISPLAYTEXT)

      :PREVIOUS-DATE "18-Mar-2023 20:10:09" {WMEDLEY}<library>TEDIT>TEDIT-CHAT.;8)


(PRETTYCOMPRINT TEDIT-CHATCOMS)

(RPAQQ TEDIT-CHATCOMS
       ((COMS                                                (* ; "character routines")
              (FNS TEDITCHAT.CHARFN))
        (COMS (FNS TEDITSTREAM.INIT TEDITCHAT.MENUFN))
        (COMS                                                (* ; "TEDIT update routines")
              (FNS TEDIT.DISPLAYTEXT))
        (GLOBALVARS TEDITCHAT.MENU CHAT.DRIVERTYPES CHAT.DISPLAYTYPES)
        (VARS TEDITCHAT.MENUITEMS (TEDITCHAT.MENU))
        (ADDVARS (CHAT.DRIVERTYPES (TEDIT TEDITCHAT.CHARFN NILL)))
        (DECLARE%: EVAL@COMPILE DONTCOPY (FILES (SOURCE)
                                                CHATDECLS))))



(* ; "character routines")

(DEFINEQ

(TEDITCHAT.CHARFN
  [LAMBDA (CH CHAT.STATE)                                    (* ; "Edited 18-Mar-2023 20:08 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:00 by mitani")
    (LET* [(TEXTSTREAM (fetch (CHAT.STATE TEXTSTREAM) of CHAT.STATE))
           (SEL (fetch (TEXTOBJ SEL) of (TEXTOBJ TEXTSTREAM]
          (\CARET.DOWN (fetch (TEXTOBJ DS) of (TEXTOBJ TEXTSTREAM)))
          (SELCHARQ CH
               (BS (\TEDIT.CHARDELETE TEXTSTREAM SEL)
                   [MOVETO (fetch X0 of SEL)
                          (fetch Y0 of SEL)
                          (CAR (fetch (TEXTOBJ \WINDOW) of (TEXTOBJ TEXTSTREAM])
               (LF NIL)
               (BOUT TEXTSTREAM CH])
)
(DEFINEQ

(TEDITSTREAM.INIT
  [LAMBDA (WINDOW MENUFN)                                    (* ; "Edited  4-Nov-2022 17:21 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:01 by mitani")

    (* ;; "Initialize and return TEDIT TEXTSTREAM")

    (PROG* ((TEXTSTREAM (OPENTEXTSTREAM NIL WINDOW NIL NIL))
            (TEXTOBJ (TEXTOBJ TEXTSTREAM)))                  (* ; 
                                             "force shift select typein to be put in keyboard buffer")
           (TEXTPROP TEXTSTREAM 'COPYBYBKSYSBUF T)
           (replace SET of (fetch (TEXTOBJ SEL) of TEXTOBJ) with T)
           [replace L1 of (fetch (TEXTOBJ SEL) of TEXTOBJ) with (LIST (fetch DESC
                                                                         of (fetch (TEXTOBJ THISLINE)
                                                                               of TEXTOBJ]
                                                             (* ; 
                                                    "hookup middle button menu instead of TEDIT menu")
           (WINDOWPROP WINDOW 'TEDIT.TITLEMENUFN MENUFN)
           (RETURN TEXTSTREAM])

(TEDITCHAT.MENUFN
  [LAMBDA (WINDOW)                                           (* || "20-Oct-86 15:03")
    (DECLARE (GLOBALVARS TEDITCHAT.MENU)
           (SPECVARS WINDOW STATE))                          (* MIDDLEBUTTON)
    (PROG ((STATE (WINDOWPROP WINDOW 'CHATSTATE))
           COMMAND)
          [COND
             ((NOT STATE)                                    (* No Connection here;
                                                             try to reestablish)
              (RETURN (COND
                         ((LASTMOUSESTATE MIDDLE)
                          (CHAT.RECONNECT WINDOW))
                         (T (TOTOPW WINDOW]
          (replace (CHAT.STATE HELD) of STATE with T)
          (\CHECKCARET WINDOW)
          (SELECTQ [SETQ COMMAND (MENU (OR TEDITCHAT.MENU (SETQ TEDITCHAT.MENU
                                                           (create MENU
                                                                  ITEMS _ TEDITCHAT.MENUITEMS]
              (Close (replace (CHAT.STATE RUNNING?) of STATE with 'CLOSE)
                                                             (* Ask CHAT.TYPEIN to shut things 
                                                             down.)
                     )
              (New (replace (CHAT.STATE RUNNING?) of STATE with 'CLOSE)
                   (WINDOWPROP WINDOW 'KEEPCHAT 'NEW))
              (Suspend (replace (CHAT.STATE RUNNING?) of STATE with 'CLOSE)
                       (WINDOWPROP WINDOW 'KEEPCHAT T))
              (Freeze                                        (* Leave in HELD state)
                      (RETURN))
              (NIL)
              (APPLY* COMMAND STATE WINDOW))
          (replace (CHAT.STATE HELD) of STATE with NIL])
)



(* ; "TEDIT update routines")

(DEFINEQ

(TEDIT.DISPLAYTEXT
  [LAMBDA (TEXTOBJ CH CHWIDTH LINE XPOINT DS SEL)            (* ; "Edited  6-Apr-2023 21:39 by rmk")
                                                             (* ; "Edited  4-Nov-2022 17:18 by rmk")
                                                             (* ; "Edited 25-Sep-2022 13:34 by rmk")
                                                             (* ; "Edited  6-Aug-2022 13:28 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:01 by mitani")
                                                             (* This function does the actual 
                                                             displaying of typed-in text on the 
                                                             edit window.)
    (PROG ((LOOKS (\TEDIT.APPLY.STYLES (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         (\TEDIT.CARETPIECE TEXTOBJ)
                         (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ)))
           (TERMSA (fetch (TEXTOBJ TXTTERMSA) of TEXTOBJ))
           DY FONT)
          (MOVETO XPOINT (IPLUS (fetch YBASE of LINE)
                                (OR (fetch CLOFFSET of LOOKS)
                                    0))
                 DS)                                         (* Set the display stream position)
          (COND
             [TERMSA                                         (* Special terminal table for 
                                                             controlling character display.
                                                             Use it.)
                    (RESETLST
                        (RESETSAVE \PRIMTERMSA TERMSA)
                        [COND
                           [(STRINGP CH)
                            (for CHAR instring CH
                               do (SELCHARQ CHAR
                                       (TAB                  (* Put down white)
                                            (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                                   36
                                                   (fetch LHEIGHT of LINE)
                                                   'TEXTURE
                                                   'REPLACE WHITESHADE)
                                            (RELMOVETO 36 0 DS))
                                       (CR (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                                  (IMAX 6 (CHARWIDTH CHAR FONT))
                                                  (fetch LHEIGHT of LINE)
                                                  'TEXTURE
                                                  'REPLACE WHITESHADE))
                                       (\DSPPRINTCHAR (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ)
                                              CHAR]
                           (T (SELCHARQ CH
                                   (TAB                      (* Put down white)
                                        (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                               36
                                               (fetch LHEIGHT of LINE)
                                               'TEXTURE
                                               'REPLACE WHITESHADE)
                                        (RELMOVETO 36 0 DS))
                                   (EOL (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                               (IMAX 6 (CHARWIDTH CH FONT))
                                               (fetch LHEIGHT of LINE)
                                               'TEXTURE
                                               'REPLACE WHITESHADE))
                                   (\DSPPRINTCHAR (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ)
                                          CH])]
             (T                                              (* No special handling;
                                                             just use native character codes)
                (COND
                   [(STRINGP CH)
                    (for CHAR instring CH do (SELCHARQ CHAR
                                                  (TAB       (* Put down white)
                                                       (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                                              (fetch YBOT of LINE)
                                                              36
                                                              (fetch LHEIGHT of LINE)
                                                              'TEXTURE
                                                              'REPLACE WHITESHADE)
                                                       (RELMOVETO 36 0 DS))
                                                  (EOL (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                                              (fetch YBOT of LINE)
                                                              (IMAX 6 (CHARWIDTH CHAR FONT))
                                                              (fetch LHEIGHT of LINE)
                                                              'TEXTURE
                                                              'REPLACE WHITESHADE))
                                                  (BLTCHAR CHAR DS]
                   (T (SELCHARQ CH
                           (TAB                              (* Put down white)
                                (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                       (fetch YBOT of LINE)
                                       36
                                       (fetch LHEIGHT of LINE)
                                       'TEXTURE
                                       'REPLACE WHITESHADE)
                                (RELMOVETO 36 0 DS))
                           (EOL                              (* Blank out the CR's width.)
                                (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                       (fetch YBOT of LINE)
                                       (IMAX 6 (CHARWIDTH CH FONT))
                                       (fetch LHEIGHT of LINE)
                                       'TEXTURE
                                       'REPLACE WHITESHADE))
                           (BLTCHAR CH DS])
)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDITCHAT.MENU CHAT.DRIVERTYPES CHAT.DISPLAYTYPES)
)

(RPAQQ TEDITCHAT.MENUITEMS
       ((Close 'Close "Closes the connection and returns")
        (Suspend 'Suspend "Closes the connection but leaves window up")
        (New 'New "Closes this connection and prompts for a new host")
        (Freeze 'Freeze "Holds typeout in this window until you bug it again")
        ("Dribble" (FUNCTION CHAT.TYPESCRIPT)
               "Starts a typescript of window typeout")
        ("Input" (FUNCTION CHAT.TAKE.INPUT)
               "Allows input from a file")
        ("Option" (FUNCTION DO.CHAT.OPTION)
               "Do protocol specific option")))

(RPAQQ TEDITCHAT.MENU NIL)

(ADDTOVAR CHAT.DRIVERTYPES (TEDIT TEDITCHAT.CHARFN NILL))
(DECLARE%: EVAL@COMPILE DONTCOPY 

(FILESLOAD (SOURCE)
       CHATDECLS)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1012 1819 (TEDITCHAT.CHARFN 1022 . 1817)) (1820 4923 (TEDITSTREAM.INIT 1830 . 3083) (
TEDITCHAT.MENUFN 3085 . 4921)) (4962 11697 (TEDIT.DISPLAYTEXT 4972 . 11695)))))
STOP
