(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "24-Jun-2024 00:05:09" {WMEDLEY}<library>tedit>TEDIT-CHAT.;16 12363  

      :EDIT-BY rmk

      :CHANGES-TO (FNS TEDITCHAT.CHARFN)

      :PREVIOUS-DATE " 2-May-2024 18:09:26" {WMEDLEY}<library>tedit>TEDIT-CHAT.;15)


(PRETTYCOMPRINT TEDIT-CHATCOMS)

(RPAQQ TEDIT-CHATCOMS
       ((FNS TEDITSTREAM.INIT TEDITCHAT.MENUFN TEDITCHAT.CHARFN)
        (COMS                                                (* ; "WHO CALLS TEDIT.DISPLAYTEXT ?")
              (FNS TEDIT.DISPLAYTEXT))
        (GLOBALVARS TEDITCHAT.MENU CHAT.DRIVERTYPES CHAT.DISPLAYTYPES)
        (VARS TEDITCHAT.MENUITEMS (TEDITCHAT.MENU))
        (ADDVARS (CHAT.DRIVERTYPES (TEDIT TEDITCHAT.CHARFN NILL)))
        (DECLARE%: EVAL@COMPILE DONTCOPY (FILES (SOURCE)
                                                CHATDECLS))))
(DEFINEQ

(TEDITSTREAM.INIT
  [LAMBDA (WINDOW MENUFN)                                    (* ; "Edited 23-Dec-2023 09:06 by rmk")
                                                             (* ; "Edited  4-Nov-2022 17:21 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:01 by mitani")

    (* ;; "Initialize and return TEDIT TEXTSTREAM on WINDOW.")

    (LET [(TEXTSTREAM (OPENTEXTSTREAM NIL WINDOW NIL NIL '(COPYBYBKSYSBUF T]
                                                             (* ; 
                                             "force shift select typein to be put in keyboard buffer")
                                                             (* ; 
                                                    "hookup middle button menu instead of TEDIT menu")
         (WINDOWPROP WINDOW 'TEDIT.TITLEMENUFN MENUFN)
         TEXTSTREAM])

(TEDITCHAT.MENUFN
  [LAMBDA (WINDOW)                                           (* || "20-Oct-86 15:03")
    (DECLARE (GLOBALVARS TEDITCHAT.MENU)
           (SPECVARS WINDOW STATE))                          (* MIDDLEBUTTON)
    (PROG ((STATE (WINDOWPROP WINDOW 'CHATSTATE))
           COMMAND)
          [COND
             ((NOT STATE)                                    (* No Connection here;
                                                             try to reestablish)
              (RETURN (COND
                         ((LASTMOUSESTATE MIDDLE)
                          (CHAT.RECONNECT WINDOW))
                         (T (TOTOPW WINDOW]
          (replace (CHAT.STATE HELD) of STATE with T)
          (\CHECKCARET WINDOW)
          (SELECTQ [SETQ COMMAND (MENU (OR TEDITCHAT.MENU (SETQ TEDITCHAT.MENU
                                                           (create MENU
                                                                  ITEMS _ TEDITCHAT.MENUITEMS]
              (Close (replace (CHAT.STATE RUNNING?) of STATE with 'CLOSE)
                                                             (* Ask CHAT.TYPEIN to shut things 
                                                             down.)
                     )
              (New (replace (CHAT.STATE RUNNING?) of STATE with 'CLOSE)
                   (WINDOWPROP WINDOW 'KEEPCHAT 'NEW))
              (Suspend (replace (CHAT.STATE RUNNING?) of STATE with 'CLOSE)
                       (WINDOWPROP WINDOW 'KEEPCHAT T))
              (Freeze                                        (* Leave in HELD state)
                      (RETURN))
              (NIL)
              (APPLY* COMMAND STATE WINDOW))
          (replace (CHAT.STATE HELD) of STATE with NIL])

(TEDITCHAT.CHARFN
  [LAMBDA (CH CHAT.STATE)                                    (* ; "Edited 24-Jun-2024 00:04 by rmk")
                                                             (* ; "Edited  2-May-2024 18:09 by rmk")
                                                             (* ; "Edited 22-Dec-2023 23:57 by rmk")
                                                             (* ; "Edited 18-Mar-2023 20:08 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:00 by mitani")
    (LET* ((TSTREAM (fetch (CHAT.STATE TEXTSTREAM) of CHAT.STATE))
           (TEXTOBJ (TEXTOBJ TSTREAM)))
          (\CARET.DOWN (FGETTOBJ TEXTOBJ DS))
          (SELCHARQ CH
               (BS (\TEDIT.CHARDELETE TSTREAM (FGETTOBJ TEXTOBJ SEL)))
               (LF NIL)
               (BOUT TSTREAM CH])
)



(* ; "WHO CALLS TEDIT.DISPLAYTEXT ?")

(DEFINEQ

(TEDIT.DISPLAYTEXT
  [LAMBDA (TEXTOBJ CH CHWIDTH LINE XPOINT DS SEL)            (* ; "Edited 23-Dec-2023 09:15 by rmk")
                                                             (* ; "Edited  6-Apr-2023 21:39 by rmk")
                                                             (* ; "Edited  4-Nov-2022 17:18 by rmk")
                                                             (* ; "Edited 25-Sep-2022 13:34 by rmk")
                                                             (* ; "Edited  6-Aug-2022 13:28 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:01 by mitani")
                                                             (* This function does the actual 
                                                             displaying of typed-in text on the 
                                                             edit window.)
    (HELP 'TEDIT.DISPLAYTEXT 'NOTUSED?)
    (PROG ((LOOKS (\TEDIT.APPLY.STYLES (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         (\TEDIT.CARETPIECE TEXTOBJ)
                         (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ)))
           (TERMSA (fetch (TEXTOBJ TXTTERMSA) of TEXTOBJ))
           DY FONT)
          (MOVETO XPOINT (IPLUS (fetch YBASE of LINE)
                                (OR (fetch CLOFFSET of LOOKS)
                                    0))
                 DS)                                         (* Set the display stream position)
          (COND
             [TERMSA                                         (* Special terminal table for 
                                                             controlling character display.
                                                             Use it.)
                    (RESETLST
                        (RESETSAVE \PRIMTERMSA TERMSA)
                        [COND
                           [(STRINGP CH)
                            (for CHAR instring CH
                               do (SELCHARQ CHAR
                                       (TAB                  (* Put down white)
                                            (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                                   36
                                                   (fetch LHEIGHT of LINE)
                                                   'TEXTURE
                                                   'REPLACE WHITESHADE)
                                            (RELMOVETO 36 0 DS))
                                       (CR (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                                  (IMAX 6 (CHARWIDTH CHAR FONT))
                                                  (fetch LHEIGHT of LINE)
                                                  'TEXTURE
                                                  'REPLACE WHITESHADE))
                                       (\DSPPRINTCHAR (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ)
                                              CHAR]
                           (T (SELCHARQ CH
                                   (TAB                      (* Put down white)
                                        (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                               36
                                               (fetch LHEIGHT of LINE)
                                               'TEXTURE
                                               'REPLACE WHITESHADE)
                                        (RELMOVETO 36 0 DS))
                                   (EOL (BITBLT NIL 0 0 DS XPOINT (fetch YBOT of LINE)
                                               (IMAX 6 (CHARWIDTH CH FONT))
                                               (fetch LHEIGHT of LINE)
                                               'TEXTURE
                                               'REPLACE WHITESHADE))
                                   (\DSPPRINTCHAR (fetch (TEXTOBJ STREAMHINT) of TEXTOBJ)
                                          CH])]
             (T                                              (* No special handling;
                                                             just use native character codes)
                (COND
                   [(STRINGP CH)
                    (for CHAR instring CH do (SELCHARQ CHAR
                                                  (TAB       (* Put down white)
                                                       (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                                              (fetch YBOT of LINE)
                                                              36
                                                              (fetch LHEIGHT of LINE)
                                                              'TEXTURE
                                                              'REPLACE WHITESHADE)
                                                       (RELMOVETO 36 0 DS))
                                                  (EOL (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                                              (fetch YBOT of LINE)
                                                              (IMAX 6 (CHARWIDTH CHAR FONT))
                                                              (fetch LHEIGHT of LINE)
                                                              'TEXTURE
                                                              'REPLACE WHITESHADE))
                                                  (BLTCHAR CHAR DS]
                   (T (SELCHARQ CH
                           (TAB                              (* Put down white)
                                (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                       (fetch YBOT of LINE)
                                       36
                                       (fetch LHEIGHT of LINE)
                                       'TEXTURE
                                       'REPLACE WHITESHADE)
                                (RELMOVETO 36 0 DS))
                           (EOL                              (* Blank out the CR's width.)
                                (BITBLT NIL 0 0 DS (DSPXPOSITION NIL DS)
                                       (fetch YBOT of LINE)
                                       (IMAX 6 (CHARWIDTH CH FONT))
                                       (fetch LHEIGHT of LINE)
                                       'TEXTURE
                                       'REPLACE WHITESHADE))
                           (BLTCHAR CH DS])
)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDITCHAT.MENU CHAT.DRIVERTYPES CHAT.DISPLAYTYPES)
)

(RPAQQ TEDITCHAT.MENUITEMS
       ((Close 'Close "Closes the connection and returns")
        (Suspend 'Suspend "Closes the connection but leaves window up")
        (New 'New "Closes this connection and prompts for a new host")
        (Freeze 'Freeze "Holds typeout in this window until you bug it again")
        ("Dribble" (FUNCTION CHAT.TYPESCRIPT)
               "Starts a typescript of window typeout")
        ("Input" (FUNCTION CHAT.TAKE.INPUT)
               "Allows input from a file")
        ("Option" (FUNCTION DO.CHAT.OPTION)
               "Do protocol specific option")))

(RPAQQ TEDITCHAT.MENU NIL)

(ADDTOVAR CHAT.DRIVERTYPES (TEDIT TEDITCHAT.CHARFN NILL))
(DECLARE%: EVAL@COMPILE DONTCOPY 

(FILESLOAD (SOURCE)
       CHATDECLS)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (886 4544 (TEDITSTREAM.INIT 896 . 1823) (TEDITCHAT.MENUFN 1825 . 3661) (TEDITCHAT.CHARFN
 3663 . 4542)) (4591 11475 (TEDIT.DISPLAYTEXT 4601 . 11473)))))
STOP
