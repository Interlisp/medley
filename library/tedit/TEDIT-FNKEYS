(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "16-Feb-2025 20:44:51" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;120 48055  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.ONECHAR.BACKWARD \TEDIT.ONECHAR.FORWARD \TEDIT.ONELINE.MOVE)

      :PREVIOUS-DATE "15-Feb-2025 08:51:16" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;117)


(PRETTYCOMPRINT TEDIT-FNKEYSCOMS)

(RPAQQ TEDIT-FNKEYSCOMS
       [(COMS 
              (* ;; "Functions that actually implement the commands for the function keys:")

              (FNS \TEDIT.BOLD.SEL.OFF \TEDIT.BOLD.SEL.ON \TEDIT.CENTER.SEL \TEDIT.CENTER.SEL.REV 
                   \TEDIT.DEFAULTS.CARET \TEDIT.DEFAULTSSEL \TEDIT.SETDEFAULT.FROM.SEL 
                   \TEDIT.KEY.FIND \TEDIT.KEY.FIND.SEARCHSTRING \TEDIT.GET.TARGET.STRING 
                   \TEDIT.KEY.FIND.BACKWARD \TEDIT.FINDAGAIN.BACKWARD \TEDIT.FINDAGAIN 
                   \TEDIT.ITALIC.SEL.OFF \TEDIT.ITALIC.SEL.ON \TEDIT.LARGERSEL \TEDIT.LCASE.SEL 
                   \TEDIT.SHOWCARETLOOKS \TEDIT.SMALLERSEL \TEDIT.SUBSCRIPTSEL \TEDIT.SUPERSCRIPTSEL
                   \TEDIT.UCASE.SEL \TEDIT.UNDERLINE.SEL.OFF \TEDIT.UNDERLINE.SEL.ON 
                   \TEDIT.STRIKEOUT.SEL.ON \TEDIT.STRIKEOUT.SEL.OFF \TEDIT.SELECT.ALL 
                   \TEDIT.KEY.SUBSTITUTE \TEDIT.MANPAGE \TEDIT.CALL.ED))
        (FNS \TEDIT.ONECHAR.BACKWARD \TEDIT.ONECHAR.FORWARD \TEDIT.ONELINE.UP \TEDIT.ONELINE.DOWN 
             \TEDIT.ONELINE.MOVE)
        (COMS 
              (* ;; "Auxiliary functions used in the above main functions:")

              (FNS \TEDIT.BOLD.CARET.OFF \TEDIT.BOLD.CARET.ON \TEDIT.ITALIC.CARET.OFF 
                   \TEDIT.ITALIC.CARET.ON \TEDIT.LARGER.CARET \TEDIT.SMALLER.CARET 
                   \TEDIT.SUBSCRIPT.CARET \TEDIT.SUPERSCRIPT.CARET \TEDIT.UNDERLINE.CARET.OFF 
                   \TEDIT.UNDERLINE.CARET.ON \TEDIT.STRIKEOUT.CARET.OFF \TEDIT.STRIKEOUT.CARET.ON))
        (COMS                                                (* ; 
                                                "little selection utilities etc., for building hacks")
              (FNS \TK.DESCRIBEFONT))
        [VARS (TEDIT.FNKEY.VERBOSE T)
              (\TEDIT.KEYS '(("Function,^D" UNDO)
                             ("Function,$" UNDO)
                             ("Function,^C" FN \TEDIT.KEY.FIND)
                             ("Function,#" FN \TEDIT.KEY.FIND)
                             ("Function,Bs" REDO)
                             ("Function,(" REDO)
                             ("Function,^R" NEXT)
                             ("Function,62" NEXT)
                             ("Esc" EXPAND)
                             ("Function,^T" EXPAND)
                             ("Function,A" FN \TEDIT.CENTER.SEL)
                             ("Function,a" FN \TEDIT.CENTER.SEL.REV)
                             ("Function,B" FN \TEDIT.BOLD.SEL.ON)
                             ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
                             ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
                             ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
                             ("Function,D" FN \TEDIT.UCASE.SEL)
                             ("Function,d" FN \TEDIT.LCASE.SEL)
                             ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
                             ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
                             ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
                             ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
                             ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,H" FN \TEDIT.SMALLERSEL)
                             ("Function,h" FN \TEDIT.LARGERSEL)
                             ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,M" FN \TEDIT.DEFAULTSSEL)
                             ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
                             ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
                             ("Meta,a" FN \TEDIT.SELECT.ALL)
                             ("Meta,A" FN \TEDIT.SELECT.ALL)
                             ("Meta,d" FN \TEDIT.MANPAGE)
                             ("Meta,D" FN \TEDIT.MANPAGE)
                             ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
                             ("Meta,f" FN \TEDIT.KEY.FIND)
                             ("Meta,g" FN \TEDIT.FINDAGAIN)
                             ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
                             ("Meta,N" NEXT)
                             ("Meta,n" NEXT)
                             ("Meta,o" FN \TEDIT.CALL.ED)
                             ("Meta,O" FN \TEDIT.CALL.ED)
                             ("Meta,p" FN \TEDIT.PRINT.MENU)
                             ("Meta,P" FN \TEDIT.PRINT.MENU)
                             ("Meta,r" REDO)
                             ("Meta,R" REDO)
                             ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,U" FN \TEDIT.UNDO.UNDO)
                             ("Meta,u" UNDO)
                             ("Meta,z" UNDO)
                             ("Meta,Z" \TEDIT.UNDO.UNDO)
                             ("Meta,<" FN \TEDIT.ONECHAR.BACKWARD)
                             ("Meta,," FN \TEDIT.ONECHAR.BACKWARD)
                             ("Meta,>" FN \TEDIT.ONECHAR.FORWARD)
                             ("Meta,." FN \TEDIT.ONECHAR.FORWARD)
                             ("Meta,^" FN \TEDIT.ONELINE.UP)
                             ("Meta,LF" FN \TEDIT.ONELINE.DOWN]
        (P (MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                                              (SELECTQ (CADR ENTRY)
                                                     (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                                                (CADDR ENTRY)))
                                                     (TEDIT.SETSYNTAX (CAR ENTRY)
                                                            (CADR ENTRY])



(* ;; "Functions that actually implement the commands for the function keys:")

(DEFINEQ

(\TEDIT.BOLD.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness off for the selected characters, and for future type-in.")

    (\TEDIT.BOLD.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT MEDIUM)
           SEL])

(\TEDIT.BOLD.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness on for selected characters and for future type-in.")

    (\TEDIT.BOLD.CARET.ON TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT BOLD)
           SEL])

(\TEDIT.CENTER.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL REVERSE)                   (* ; "Edited 11-Dec-2023 11:02 by rmk")
                                                             (* ; "Edited 28-Jul-2023 16:14 by rmk")
                                                             (* ; "Edited 11-Apr-2023 13:22 by rmk")
                                                             (* ; "Edited 10-Apr-2023 10:08 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "Changes the QUAD of the selected paragraphs in TEXTSTREAM, when the CENTER key is typed.  Rotates through the sequences (LEFT/RIGHT/CENTER) from the QUAD of the first paragraph to find the NEWQUAD that it will apply to all the paragraphs in SEL.  If REVERSE, cycles the quads in the opposite direction.")

    (CL:UNLESS (TEDITMENUP TEXTOBJ)
        (LET [(NEWQUAD (LIST 'QUAD (OR [CADR (MEMB (LISTGET (TEDIT.GET.PARALOOKS TEXTSTREAM SEL)
                                                          'QUAD)
                                                   (CL:IF REVERSE
                                                       '(LEFT CENTERED JUSTIFIED LEFT)
                                                       '(LEFT JUSTIFIED CENTERED RIGHT))]
                                       'LEFT]
             (TEDIT.PARALOOKS TEXTSTREAM NEWQUAD SEL)
             (CL:WHEN TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM NEWQUAD T))))])

(\TEDIT.CENTER.SEL.REV
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Dec-2023 11:02 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")
    (\TEDIT.CENTER.SEL TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.DEFAULTS.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 26-Feb-2024 08:45 by rmk")
                                                             (* ; "Edited 11-Nov-2023 16:00 by rmk")
                                                             (* jds "21-Sep-85 11:24")
    (TEDIT.CARETLOOKS TEXTSTREAM (create CHARLOOKS using (GETTOBJ TEXTOBJ DEFAULTCHARLOOKS)))
    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.DEFAULTSSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Nov-2023 15:55 by rmk")
                                                             (* ; "Edited 20-Oct-87 11:12 by jds")
                                                             (* ; "acts on the selection")
    (TEDIT.LOOKS TEXTSTREAM (create CHARLOOKS using (GETTOBJ TEXTOBJ DEFAULTCHARLOOKS))
           SEL])

(\TEDIT.SETDEFAULT.FROM.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 12-Nov-2023 16:40 by rmk")
                                                             (* ; "Edited 11-Nov-2023 16:03 by rmk")
                                                             (* jds " 8-Nov-85 15:22")
                                                             (* ; 
                                                       "Set the defaults from the current selection.")
    (SETTOBJ TEXTOBJ DEFAULTCHARLOOKS (\TEDIT.PARSE.CHARLOOKS.LIST (TEDIT.GET.LOOKS TEXTSTREAM SEL)
                                             NIL TEXTOBJ])

(\TEDIT.KEY.FIND
  [LAMBDA (TSTREAM TEXTOBJ SEL AGAIN BACKWARD SEARCHSTRING)  (* ; "Edited 26-Nov-2024 23:47 by rmk")
                                                             (* ; "Edited 23-Nov-2024 16:25 by rmk")
                                                             (* ; "Edited  7-Jul-2024 11:47 by rmk")
                                                             (* ; "Edited 29-Jun-2024 16:20 by rmk")
                                                             (* ; "Edited 22-Jun-2024 10:00 by rmk")
                                                             (* ; "Edited 18-May-2024 16:29 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:36 by rmk")
                                                             (* ; "Edited 24-Apr-2024 23:39 by rmk")
                                                             (* ; "Edited  9-Mar-2024 11:36 by rmk")
                                                             (* ; "Edited 14-Dec-2023 21:14 by rmk")
                                                             (* ; "Edited 12-Jul-2023 08:26 by rmk")
                                                             (* ; "Edited 20-Jun-2023 13:06 by rmk")
                                                            (* ; "Edited  6-May-2018 17:14 by rmk:")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "Case sensitive search, with * and # wildcards. Just calls the normal tedit.find starting at the right of the current selection.  SEL is passed from the FN key in the readtable, presumably always (fetch SEL of TEXTOBJ).")

    (* ;; "AGAIN suppresses confirmation of a previous target.")

    (SETQ TSTREAM (TEXTSTREAM TSTREAM))
    (CL:UNLESS TEXTOBJ
        (SETQ TEXTOBJ (fetch (TEXTSTREAM TEXTOBJ) of TSTREAM)))
    (RESETLST
        [RESETSAVE (\TEDIT.MARKACTIVE TEXTOBJ "Find")
               '(PROGN (\TEDIT.MARKINACTIVE OLDVALUE]
        (LET ((TEXTOBJ (TEXTOBJ TSTREAM))
              CH)
             (CL:UNLESS SEARCHSTRING
                 (SETQ SEARCHSTRING (\TEDIT.KEY.FIND.SEARCHSTRING TEXTOBJ AGAIN BACKWARD)))
             (CL:WHEN (AND SEARCHSTRING (IGEQ (NCHARS SEARCHSTRING)
                                              1))
                 (CL:UNLESS SEL
                     (SETQ SEL (FGETTOBJ TEXTOBJ SEL)))
                 (\TEDIT.SHOWSEL SEL NIL TEXTOBJ)
                 (SETQ CH (if BACKWARD
                              then (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching backward for %"" 
                                                                     SEARCHSTRING "%"")
                                          T)
                                   (TEDIT.FIND.BACKWARD TSTREAM (MKSTRING SEARCHSTRING)
                                          NIL NIL T)
                            else (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching for %"" SEARCHSTRING 
                                                                   "%"")
                                        T)
                                 (TEDIT.FIND TSTREAM (MKSTRING SEARCHSTRING)
                                        NIL NIL T)))
                 (if CH
                     then (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" SEARCHSTRING "%" found")
                                 T)                          (* ; "We found the target text.")
                          (\TEDIT.RESET.EXTEND.PENDING.DELETE TEXTOBJ) 
                                                             (* ; 
                                                             "Set up SELECTION to be the found text")
                          (\TEDIT.SHOWSEL SEL NIL TEXTOBJ)
                          (\TEDIT.UPDATE.SEL SEL (CAR CH)
                                 (ADD1 (IDIFFERENCE (CADR CH)
                                              (CAR CH)))
                                 (CL:IF BACKWARD
                                     'LEFT
                                     'RIGHT)
                                 (CL:IF (FGETTOBJ TEXTOBJ TXTREADONLY)
                                     'PENDINGDEL
                                     'NORMAL))
                          (\TEDIT.FIXSEL SEL TEXTOBJ)
                          (\TEDIT.SHOWSEL SEL T TEXTOBJ)
                          [SETSEL SEL SELKIND (CL:IF (IGREATERP (CADR CH)
                                                            (CAR CH)
                                                            'WORD
                                                            'CHAR]
                          (FSETTOBJ TEXTOBJ CARETLOOKS (\TEDIT.GET.INSERT.CHARLOOKS TEXTOBJ SEL))
                          (TEDIT.NORMALIZECARET TEXTOBJ)
                   else (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" SEARCHSTRING "%" not found")
                               T))
                 (\TEDIT.SHOWSEL SEL T TEXTOBJ))))])

(\TEDIT.KEY.FIND.SEARCHSTRING
  [LAMBDA (TEXTOBJ AGAIN BACKWARD)                           (* ; "Edited 22-Jun-2024 10:17 by rmk")

    (* ;; "TEDIT.LAST.FIND.STRING used to be stored as a window property.  But then it would only pertain to a particular pane.  Better store it on the textobj.")

    (LET (SEARCHSTRING)
         (CL:WHEN AGAIN
             (SETQ SEARCHSTRING (GETTEXTPROP TEXTOBJ 'TEDIT.LAST.FIND.STRING)))
         (CL:UNLESS SEARCHSTRING
             (SETQ SEARCHSTRING (\TEDIT.GET.TARGET.STRING TEXTOBJ 'TEDIT.LAST.FIND.STRING))
             (SETQ SEARCHSTRING (TEDIT.GETINPUT TEXTOBJ (CL:IF BACKWARD
                                                            "Backward search string: "
                                                            "Search string: ")
                                       SEARCHSTRING))
             (CL:WHEN SEARCHSTRING                           (* ; 
                                                            "Save for next search, even if not found")
                 (PUTTEXTPROP TEXTOBJ 'TEDIT.LAST.FIND.STRING SEARCHSTRING)))
         SEARCHSTRING])

(\TEDIT.GET.TARGET.STRING
  [LAMBDA (TEXTOBJ PROP)                                     (* ; "Edited 14-Jul-2024 00:09 by rmk")
                                                             (* ; "Edited 23-Jun-2024 23:06 by rmk")
                                                             (* ; "Edited 22-Jun-2024 12:03 by rmk")
                                                             (* ; "Edited 29-Feb-2024 17:08 by rmk")

    (* ;; "This is called from \TEDIT.KEY.FIND, TEDIT.DEFAULT.MENUFN.  It tries to determine the best tentative target string for a search.  PROP is presumably TEDIT.LAST.FIND.STRING.")

    (* ;; "Current heuristic:  If a previous string, use it if it contains wild cards, otherwise the current non-point selection.  Note that meta-G goes directly to the last search.")

    (* ;; "TEDIT.SUBSTITUTE doesn't call this because the current selection is the search domain")

    (LET [(PREV (STRINGP (GETTEXTPROP TEXTOBJ PROP]
         (if [AND PREV (find I from 1 to (NCHARS PREV)
                          suchthat (AND (MEMB (NTHCHARCODE PREV I)
                                              (CHARCODE (%# ESCAPE *)))
                                        (NEQ (CHARCODE %')
                                             (NTHCHARCODE PREV (SUB1 I]
             then PREV
           elseif (IGEQ (FGETSEL (FGETTOBJ TEXTOBJ SEL)
                               DCH)
                        1)
             then 
                  (* ;; "TEDIT.SEL.AS.STRING breaks on image objects, should be fixed there.")

                  (CAR (NLSETQ (TEDIT.SEL.AS.STRING TEXTOBJ)))
           else PREV])

(\TEDIT.KEY.FIND.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN)                     (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 17:59 by rmk")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL AGAIN T])

(\TEDIT.FINDAGAIN.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 18:03 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T T])

(\TEDIT.FINDAGAIN
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.ITALIC.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited 20-Oct-87 10:43 by jds")
    (\TEDIT.ITALIC.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE REGULAR)
           SEL])

(\TEDIT.ITALIC.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 10:43 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE ITALIC)
           SEL])

(\TEDIT.LARGERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.LARGER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT 2)
                 SEL])

(\TEDIT.LCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited  7-Jul-2024 09:05 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:57 by rmk")
                                                             (* ; "Edited  3-Mar-2024 12:28 by rmk")
                                                             (* ; "Edited 28-May-2023 00:34 by rmk")
                                                             (* ; "Edited 24-May-2023 22:46 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :LowerCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.  Or maybe the transformation function should be stored.")

    (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.CHARTRANSFORM (\TEDIT.SELPIECES.COPY (
                                                                                     \TEDIT.SELPIECES
                                                                                      SEL NIL TEXTOBJ
                                                                                      ))
                                     (FUNCTION L-CASECODE)
                                     NIL TEXTOBJ)
           TEXTOBJ SEL)
    (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
           THACTION :LowerCase])

(\TEDIT.SHOWCARETLOOKS
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:07 by rmk")
                                                             (* ; "Edited 30-May-91 21:09 by jds")
    (LET ((LOOKS (FGETTOBJ TEXTOBJ CARETLOOKS)))
         (TEDIT.PROMPTPRINT TEXTSTREAM (CONCAT (\TK.DESCRIBEFONT (fetch (CHARLOOKS CLFONT)
                                                                    of LOOKS))
                                              (COND
                                                 ((AND (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                       (NEQ (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                            0))
                                                  (CONCAT " offset " (fetch (CHARLOOKS CLOFFSET)
                                                                        of LOOKS)))
                                                 (T ""))
                                              (COND
                                                 ((fetch (CHARLOOKS CLOLINE) of LOOKS)
                                                  " overlined")
                                                 (T ""))
                                              (COND
                                                 ((fetch (CHARLOOKS CLULINE) of LOOKS)
                                                  " underlined")
                                                 (T "")))
                T])

(\TEDIT.SMALLERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.SMALLER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT -2)
                 SEL])

(\TEDIT.SUBSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT -2)
           SEL])

(\TEDIT.SUPERSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:13 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT 2)
           SEL])

(\TEDIT.UCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited  7-Jul-2024 09:04 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:57 by rmk")
                                                             (* ; "Edited  3-Mar-2024 12:56 by rmk")
                                                             (* ; "Edited 28-May-2023 00:33 by rmk")
                                                             (* ; "Edited 24-May-2023 22:45 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :UpperCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.")

    (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.CHARTRANSFORM (\TEDIT.SELPIECES.COPY (
                                                                                     \TEDIT.SELPIECES
                                                                                      SEL NIL TEXTOBJ
                                                                                      ))
                                     (FUNCTION U-CASECODE)
                                     NIL TEXTOBJ)
           TEXTOBJ SEL)
    (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
           THACTION :UpperCase])

(\TEDIT.UNDERLINE.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:26 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE OFF)
           SEL])

(\TEDIT.UNDERLINE.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT OFF)
           SEL])

(\TEDIT.SELECT.ALL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 29-Jun-2024 15:05 by rmk")
                                                            (* ; "Edited  6-May-2018 12:41 by rmk:")
    (TEDIT.SETSEL TEXTSTREAM 1 (GETTOBJ TEXTOBJ TEXTLEN)
           'LEFT])

(\TEDIT.KEY.SUBSTITUTE
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  8-May-2023 09:35 by rmk")

    (* ;; "Stub for function-key")

    (TEDIT.SUBSTITUTE TEXTSTREAM NIL NIL T])

(\TEDIT.MANPAGE
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 18-Jan-2025 21:48 by rmk")
                                                             (* ; "Edited 29-Dec-2024 08:40 by rmk")
                                                             (* ; "Edited 25-Jun-2024 11:59 by rmk")
                                                             (* ; "Edited 26-May-2024 21:53 by rmk")
                                                             (* ; "Edited 25-May-2024 14:50 by rmk")

    (* ;; "If meta-D is typed in an existing DINFO window, the new stuff comes up but then the window closes.  That could be debugged, but probably not worth it.  The DINFO window has its own links to things that it thought were worth indexing.")

    (CL:UNLESS (WINDOWPROP (\TEDIT.PRIMARYPANE TSTREAM)
                      'DINFOGRAPH)
        (TEDIT.PROMPTCLEAR TSTREAM)
        [LET ((KEY (TEDIT.SEL.AS.STRING TSTREAM SEL)))
             (if (OR (NULL KEY)
                     (EQ 0 (NCHARS KEY)))
                 then (TEDIT.PROMPTPRINT TSTREAM "Please select a man-page key" T T)
               else (GENERIC.MAN.LOOKUP (TEDIT.SEL.AS.STRING TSTREAM SEL])])

(\TEDIT.CALL.ED
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 18-Jan-2025 23:38 by rmk")
                                                             (* ; "Edited 29-Dec-2024 08:46 by rmk")
                                                             (* ; "Edited 25-May-2024 15:03 by rmk")
    (TEDIT.PROMPTCLEAR TSTREAM)
    (LET [(SYMBOL (MKATOM (CAR (MKLIST (TEDIT.SEL.AS.SEXPR TSTREAM SEL]
         (if (OR (NULL SYMBOL)
                 (EQ 0 (NCHARS SYMBOL)))
             then (TEDIT.PROMPTPRINT TSTREAM "Please select a symbol to edit" T T)
           elseif (TYPESOF SYMBOL)
             then (ED SYMBOL `(:DONTWAIT :DISPLAY))
           else (TEDIT.PROMPTPRINT TSTREAM (CONCAT SYMBOL " has no definitions to edit")
                       T T])
)
(DEFINEQ

(\TEDIT.ONECHAR.BACKWARD
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 16-Feb-2025 20:44 by rmk")
                                                             (* ; "Edited 24-Jan-2025 15:25 by rmk")
                                                             (* ; "Edited 21-Nov-2024 20:31 by rmk")
                                                             (* ; "Edited  1-Sep-2024 10:39 by rmk")
    (TEXTOBJ! TEXTOBJ)
    (SELECTION! SEL)
    (LET ((PT (TEDIT.GETPOINT TSTREAM SEL))
          OBJ)
         (CL:UNLESS [OR (ILEQ PT 1)
                        (AND (FGETTOBJ TEXTOBJ MENUFLG)
                             (SETQ OBJ (POBJ (\TEDIT.CHTOPC (SUB1 PT)
                                                    TEXTOBJ)))
                             (IMAGEOBJPROP OBJ 'FIELDPREFIX]
             (\TEDIT.SHOWSEL SEL NIL TEXTOBJ)
             (\TEDIT.UPDATE.SEL SEL (SUB1 PT)
                    0
                    'LEFT)
             (\TEDIT.FIXSEL SEL TEXTOBJ)
             (\TEDIT.SHOWSEL SEL T TEXTOBJ))])

(\TEDIT.ONECHAR.FORWARD
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 16-Feb-2025 20:43 by rmk")
                                                             (* ; "Edited 15-Feb-2025 08:50 by rmk")
                                                             (* ; "Edited 24-Jan-2025 15:27 by rmk")
                                                             (* ; "Edited 21-Nov-2024 20:31 by rmk")
                                                             (* ; "Edited  1-Sep-2024 10:39 by rmk")

    (* ;; "Moves caret to a point one character forward.")

    (SELECTION! SEL)
    (TEXTOBJ! TEXTOBJ)
    (LET ((PT (TEDIT.GETPOINT TSTREAM SEL))
          OBJ)
         (CL:UNLESS [OR (IGREATERP PT (TEXTLEN TEXTOBJ))
                        (AND (FGETTOBJ TEXTOBJ MENUFLG)
                             (SETQ OBJ (POBJ (\TEDIT.CHTOPC PT TEXTOBJ)))
                             (IMAGEOBJPROP OBJ 'FIELDSUFFIX]
             (\TEDIT.SHOWSEL SEL NIL TEXTOBJ)
             (\TEDIT.UPDATE.SEL SEL PT 0 'RIGHT)
             (\TEDIT.FIXSEL SEL TEXTOBJ)
             (\TEDIT.SHOWSEL SEL T TEXTOBJ))])

(\TEDIT.ONELINE.UP
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 13-Feb-2025 22:04 by rmk")
                                                             (* ; "Edited 12-Feb-2025 19:46 by rmk")
                                                             (* ; "Edited 24-Jan-2025 15:27 by rmk")
                                                             (* ; "Edited 21-Nov-2024 20:31 by rmk")
                                                             (* ; "Edited  1-Sep-2024 10:39 by rmk")

    (* ;; "Moves caret to the same x position one line up.  It gets the current  X (X0 or XLIM) of the caret in the current selection, which is common to all panes in which the caret is visible. It then finds the line in the first pane where the caret is visible, formats the previous line, and then figures out the character in previousline that would come closest to X.")

    (* ;; "We look for a pane that not only has a line with the caret, but also has the previous line.  Otherwise, we have to search backwards to find the start of that line.")

    (TEXTOBJ! TEXTOBJ)
    (SELECTION! SEL)
    (LET (LINE LINEPANE (CHNO (TEDIT.GETPOINT TSTREAM SEL)))
         (for PANE FIRSTONE inpanes (PROGN TEXTOBJ) as L1 in (FGETSEL SEL L1) as LN
            in (FGETSEL SEL LN) when [SETQ LINE (OR (AND L1 (WITHINLINEP CHNO L1))
                                                    (AND LN (WITHINLINEP CHNO LN]
            do (CL:UNLESS (FGETLD (FGETLD LINE PREVLINE)
                                 LDUMMY)
                      (RETURN))
               (CL:UNLESS FIRSTONE (SETQ FIRSTONE LINE)) finally 

                                                 (* ;; "The caret is blinking nowhere, or in the top line of every pane, we have to create a prevline above.")

                                                               (SETQ LINE FIRSTONE)
                                                               (SETQ LINEPANE PANE))

         (* ;; "Caret is blinking in LINE, move selection to the charno at the same X in the previous line, in all panes. ")

         (CL:WHEN [AND LINE (ILEQ 1 (SUB1 (FGETLD LINE LCHAR1]
             (\TEDIT.ONELINE.MOVE SEL (FGETLD (if (FGETLD (FGETLD LINE PREVLINE)
                                                         LDUMMY)
                                                  then 
                                                       (* ;; 
                                                       "Top of window, create the preceding line")

                                                       (\TEDIT.LASTVALIDLINE LINE CHNO LINEPANE 
                                                              TSTREAM)
                                                else (FGETLD LINE PREVLINE))
                                             LCHAR1)
                    TSTREAM))])

(\TEDIT.ONELINE.DOWN
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 13-Feb-2025 22:05 by rmk")
                                                             (* ; "Edited 12-Feb-2025 19:46 by rmk")
                                                             (* ; "Edited 24-Jan-2025 15:27 by rmk")
                                                             (* ; "Edited 21-Nov-2024 20:31 by rmk")
                                                             (* ; "Edited  1-Sep-2024 10:39 by rmk")

    (* ;; "Moves caret to the same x position one line down.  It gets the current  X (X0 or XLIM) of the caret in the current selection, which is common to all panes in which the caret is visible. It then finds the line in the first pane where the caret is visible, formats the nextline, and then figures out the character in nextline that would come closest to X.")

    (TEXTOBJ! TEXTOBJ)
    (SELECTION! SEL)
    (LET (LINE NEXTLINE NEXTCHNO)
         (for L1 (CHNO _ (TEDIT.GETPOINT TSTREAM SEL)) in (FGETSEL SEL L1) as LN
            in (FGETSEL SEL LN) when [SETQ LINE (OR (AND L1 (WITHINLINEP CHNO L1))
                                                    (AND LN (WITHINLINEP CHNO LN] do (RETURN))

         (* ;; "Caret is blinking in LINE, move selection to the charno at the same X in the next line, in all panes. ")

         (CL:WHEN (AND LINE (ILESSP (ADD1 (FGETLD LINE LCHARLAST))
                                   (TEXTLEN TEXTOBJ)))
             (\TEDIT.ONELINE.MOVE SEL (ADD1 (FGETLD LINE LCHARLAST))
                    TSTREAM))])

(\TEDIT.ONELINE.MOVE
  [LAMBDA (SEL CHNO TSTREAM)                                 (* ; "Edited 16-Feb-2025 16:20 by rmk")
                                                             (* ; "Edited 14-Feb-2025 09:49 by rmk")

    (* ;; 
    "Move caret from its previous X position to the same position in the line beginning at CHNO.")

    (* ;; "The scan part is basically a specialized variant of \TEDIT.SCAN.LINE.  ")

    (LET ((TARGETLINE (\TEDIT.FORMATLINE TSTREAM CHNO))
          (TEXTOBJ (FGETTSTR TSTREAM TEXTOBJ)))
         (CL:UNLESS (FGETTOBJ TEXTOBJ MENUFLG)
             (for CHARSLOT (THISLINE _ (FGETTOBJ TEXTOBJ THISLINE))
                  (TARGX _ (FGETLD TARGETLINE LX1))
                  [X _ (OR (FGETTOBJ TEXTOBJ LASTARROWX)
                           (FSETTOBJ TEXTOBJ LASTARROWX (SELECTQ (FGETSEL SEL POINT)
                                                            (LEFT (FGETSEL SEL X0))
                                                            (RIGHT (FGETSEL SEL XLIM))
                                                            NIL] incharslots (FGETTOBJ TEXTOBJ 
                                                                                    THISLINE)
                when CHAR do (add TARGX CHARW)
                             (CL:WHEN (IGEQ TARGX X)
                                 (CL:WHEN (IGEQ X (IDIFFERENCE TARGX (FOLDLO CHARW 2)))
                                                             (* ; 
                                                      "To RIGHT of target char if more than half way")
                                     (add CHNO 1))
                                 (RETURN))
                             (add CHNO 1) finally            (* ; 
                                                           "TARGETLINE must have been shorter than X")
                                                (SETQ CHNO (FGETLD TARGETLINE LCHARLAST)))
             (\TEDIT.SHOWSEL SEL NIL TEXTOBJ)
             (\TEDIT.UPDATE.SEL SEL CHNO 0 'LEFT)
             (\TEDIT.FIXSEL SEL TEXTOBJ)
             (\TEDIT.SHOWSEL SEL T TEXTOBJ)
             (\TEDIT.SCROLL.CARET TSTREAM))])
)



(* ;; "Auxiliary functions used in the above main functions:")

(DEFINEQ

(\TEDIT.BOLD.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:08 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT MEDIUM)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.BOLD.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:09 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT BOLD)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.ITALIC.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:09 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE REGULAR)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.ITALIC.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE ITALIC)
                        (GETTOBJ TEXTOBJ CARETLOOKS TEXTOBJ)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.LARGER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT 2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SMALLER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT -2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SUBSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:16 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT -2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SUPERSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:16 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT 2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.UNDERLINE.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:17 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE OFF)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.UNDERLINE.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:17 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE ON)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.STRIKEOUT.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:18 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT OFF)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.STRIKEOUT.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 10-Aug-2024 16:31 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.CHANGE.CHARLOOKS.NEW '(STRIKEOUT ON)
                        (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])
)



(* ; "little selection utilities etc., for building hacks")

(DEFINEQ

(\TK.DESCRIBEFONT
  [LAMBDA (FONT)                                             (* gbn "15-Dec-84 17:54")

         (* * returns a string which describes a font
         (in short. If it's not italic then no mention is made of slope, etc.))

    (CONCAT (L-CASE (FONTPROP FONT 'FAMILY))
           " "
           (FONTPROP FONT 'SIZE)
           (COND
              [(NEQ (FONTPROP FONT 'WEIGHT)
                    'MEDIUM)
               (CONCAT " " (L-CASE (FONTPROP FONT 'WEIGHT]
              (T ""))
           (COND
              [(NEQ (FONTPROP FONT 'SLOPE)
                    'REGULAR)
               (CONCAT " " (L-CASE (FONTPROP FONT 'SLOPE]
              (T ""])
)

(RPAQQ TEDIT.FNKEY.VERBOSE T)

(RPAQQ \TEDIT.KEYS
       (("Function,^D" UNDO)
        ("Function,$" UNDO)
        ("Function,^C" FN \TEDIT.KEY.FIND)
        ("Function,#" FN \TEDIT.KEY.FIND)
        ("Function,Bs" REDO)
        ("Function,(" REDO)
        ("Function,^R" NEXT)
        ("Function,62" NEXT)
        ("Esc" EXPAND)
        ("Function,^T" EXPAND)
        ("Function,A" FN \TEDIT.CENTER.SEL)
        ("Function,a" FN \TEDIT.CENTER.SEL.REV)
        ("Function,B" FN \TEDIT.BOLD.SEL.ON)
        ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
        ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
        ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
        ("Function,D" FN \TEDIT.UCASE.SEL)
        ("Function,d" FN \TEDIT.LCASE.SEL)
        ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
        ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
        ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
        ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
        ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,H" FN \TEDIT.SMALLERSEL)
        ("Function,h" FN \TEDIT.LARGERSEL)
        ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,M" FN \TEDIT.DEFAULTSSEL)
        ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
        ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
        ("Meta,a" FN \TEDIT.SELECT.ALL)
        ("Meta,A" FN \TEDIT.SELECT.ALL)
        ("Meta,d" FN \TEDIT.MANPAGE)
        ("Meta,D" FN \TEDIT.MANPAGE)
        ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
        ("Meta,f" FN \TEDIT.KEY.FIND)
        ("Meta,g" FN \TEDIT.FINDAGAIN)
        ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
        ("Meta,N" NEXT)
        ("Meta,n" NEXT)
        ("Meta,o" FN \TEDIT.CALL.ED)
        ("Meta,O" FN \TEDIT.CALL.ED)
        ("Meta,p" FN \TEDIT.PRINT.MENU)
        ("Meta,P" FN \TEDIT.PRINT.MENU)
        ("Meta,r" REDO)
        ("Meta,R" REDO)
        ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,U" FN \TEDIT.UNDO.UNDO)
        ("Meta,u" UNDO)
        ("Meta,z" UNDO)
        ("Meta,Z" \TEDIT.UNDO.UNDO)
        ("Meta,<" FN \TEDIT.ONECHAR.BACKWARD)
        ("Meta,," FN \TEDIT.ONECHAR.BACKWARD)
        ("Meta,>" FN \TEDIT.ONECHAR.FORWARD)
        ("Meta,." FN \TEDIT.ONECHAR.FORWARD)
        ("Meta,^" FN \TEDIT.ONELINE.UP)
        ("Meta,LF" FN \TEDIT.ONELINE.DOWN)))

[MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                              (SELECTQ (CADR ENTRY)
                                  (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                             (CADDR ENTRY)))
                                  (TEDIT.SETSYNTAX (CAR ENTRY)
                                         (CADR ENTRY]
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (6462 28722 (\TEDIT.BOLD.SEL.OFF 6472 . 6810) (\TEDIT.BOLD.SEL.ON 6812 . 7140) (
\TEDIT.CENTER.SEL 7142 . 8658) (\TEDIT.CENTER.SEL.REV 8660 . 8956) (\TEDIT.DEFAULTS.CARET 8958 . 9451)
 (\TEDIT.DEFAULTSSEL 9453 . 9900) (\TEDIT.SETDEFAULT.FROM.SEL 9902 . 10579) (\TEDIT.KEY.FIND 10581 . 
15648) (\TEDIT.KEY.FIND.SEARCHSTRING 15650 . 16790) (\TEDIT.GET.TARGET.STRING 16792 . 18506) (
\TEDIT.KEY.FIND.BACKWARD 18508 . 18813) (\TEDIT.FINDAGAIN.BACKWARD 18815 . 19226) (\TEDIT.FINDAGAIN 
19228 . 19519) (\TEDIT.ITALIC.SEL.OFF 19521 . 19773) (\TEDIT.ITALIC.SEL.ON 19775 . 19968) (
\TEDIT.LARGERSEL 19970 . 20258) (\TEDIT.LCASE.SEL 20260 . 21655) (\TEDIT.SHOWCARETLOOKS 21657 . 23257)
 (\TEDIT.SMALLERSEL 23259 . 23550) (\TEDIT.SUBSCRIPTSEL 23552 . 23755) (\TEDIT.SUPERSCRIPTSEL 23757 . 
23961) (\TEDIT.UCASE.SEL 23963 . 25302) (\TEDIT.UNDERLINE.SEL.OFF 25304 . 25502) (
\TEDIT.UNDERLINE.SEL.ON 25504 . 25700) (\TEDIT.STRIKEOUT.SEL.ON 25702 . 25898) (
\TEDIT.STRIKEOUT.SEL.OFF 25900 . 26098) (\TEDIT.SELECT.ALL 26100 . 26416) (\TEDIT.KEY.SUBSTITUTE 26418
 . 26639) (\TEDIT.MANPAGE 26641 . 27888) (\TEDIT.CALL.ED 27890 . 28720)) (28723 37828 (
\TEDIT.ONECHAR.BACKWARD 28733 . 29815) (\TEDIT.ONECHAR.FORWARD 29817 . 30988) (\TEDIT.ONELINE.UP 30990
 . 33951) (\TEDIT.ONELINE.DOWN 33953 . 35610) (\TEDIT.ONELINE.MOVE 35612 . 37826)) (37900 44411 (
\TEDIT.BOLD.CARET.OFF 37910 . 38445) (\TEDIT.BOLD.CARET.ON 38447 . 38979) (\TEDIT.ITALIC.CARET.OFF 
38981 . 39518) (\TEDIT.ITALIC.CARET.ON 39520 . 40063) (\TEDIT.LARGER.CARET 40065 . 40600) (
\TEDIT.SMALLER.CARET 40602 . 41139) (\TEDIT.SUBSCRIPT.CARET 41141 . 41682) (\TEDIT.SUPERSCRIPT.CARET 
41684 . 42226) (\TEDIT.UNDERLINE.CARET.OFF 42228 . 42768) (\TEDIT.UNDERLINE.CARET.ON 42770 . 43308) (
\TEDIT.STRIKEOUT.CARET.OFF 43310 . 43850) (\TEDIT.STRIKEOUT.CARET.ON 43852 . 44409)) (44480 45182 (
\TK.DESCRIBEFONT 44490 . 45180)))))
STOP
