(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "14-Oct-2023 14:53:17" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;47 30594  

      :EDIT-BY rmk

      :CHANGES-TO (VARS TEDIT-FNKEYSCOMS)

      :PREVIOUS-DATE " 2-Oct-2023 23:02:32" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;46)


(PRETTYCOMPRINT TEDIT-FNKEYSCOMS)

(RPAQQ TEDIT-FNKEYSCOMS
       [(COMS 
              (* ;; "Functions that actually implement the commands for the function keys:")

              (FNS \TEDIT.BOLD.SEL.OFF \TEDIT.BOLD.SEL.ON \TEDIT.CENTER.SEL \TEDIT.CENTER.SEL.REV 
                   \TEDIT.DEFAULTS.CARET \TEDIT.DEFAULTSSEL \TEDIT.SETDEFAULT.FROM.SEL 
                   \TEDIT.KEY.FIND \TEDIT.KEY.FIND.BACKWARD \TEDIT.FINDAGAIN.BACKWARD 
                   \TEDIT.FINDAGAIN \TEDIT.ITALIC.SEL.OFF \TEDIT.ITALIC.SEL.ON \TEDIT.LARGERSEL 
                   \TEDIT.LCASE.SEL \TEDIT.SHOWCARETLOOKS \TEDIT.SMALLERSEL \TEDIT.SUBSCRIPTSEL 
                   \TEDIT.SUPERSCRIPTSEL \TEDIT.UCASE.SEL \TEDIT.UNDERLINE.SEL.OFF 
                   \TEDIT.UNDERLINE.SEL.ON \TEDIT.STRIKEOUT.SEL.ON \TEDIT.STRIKEOUT.SEL.OFF 
                   \TEDIT.SELECT.ALL \TEDIT.KEY.SUBSTITUTE))
        (COMS 
              (* ;; "Auxiliary functions used in the above main functions:")

              (FNS \TEDIT.BOLD.CARET.OFF \TEDIT.BOLD.CARET.ON \TEDIT.ITALIC.CARET.OFF 
                   \TEDIT.ITALIC.CARET.ON \TEDIT.LARGER.CARET \TEDIT.SMALLER.CARET 
                   \TEDIT.SUBSCRIPT.CARET \TEDIT.SUPERSCRIPT.CARET \TEDIT.UNDERLINE.CARET.OFF 
                   \TEDIT.UNDERLINE.CARET.ON \TEDIT.STRIKEOUT.CARET.OFF \TEDIT.STRIKEOUT.CARET.ON))
        (COMS                                                (* ; 
                                                "little selection utilities etc., for building hacks")
              (FNS \SEL.LIMIT \TK.DESCRIBEFONT \PARAS.IN.SEL))
        [VARS (TEDIT.FNKEY.VERBOSE T)
              (\TEDIT.KEYS '(("Function,^D" UNDO)
                             ("Function,$" UNDO)
                             ("Function,^C" FN \TEDIT.KEY.FIND)
                             ("Function,#" FN \TEDIT.KEY.FIND)
                             ("Function,Bs" REDO)
                             ("Function,(" REDO)
                             ("Function,^R" NEXT)
                             ("Function,62" NEXT)
                             ("Esc" EXPAND)
                             ("Function,^T" EXPAND)
                             ("Function,A" FN \TEDIT.CENTER.SEL)
                             ("Function,a" FN \TEDIT.CENTER.SEL.REV)
                             ("Function,B" FN \TEDIT.BOLD.SEL.ON)
                             ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
                             ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
                             ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
                             ("Function,D" FN \TEDIT.UCASE.SEL)
                             ("Function,d" FN \TEDIT.LCASE.SEL)
                             ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
                             ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
                             ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
                             ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
                             ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,H" FN \TEDIT.SMALLERSEL)
                             ("Function,h" FN \TEDIT.LARGERSEL)
                             ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,M" FN \TEDIT.DEFAULTSSEL)
                             ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
                             ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
                             ("Meta,a" FN \TEDIT.SELECT.ALL)
                             ("Meta,A" FN \TEDIT.SELECT.ALL)
                             ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
                             ("Meta,f" FN \TEDIT.KEY.FIND)
                             ("Meta,g" FN \TEDIT.FINDAGAIN)
                             ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
                             ("Meta,N" NEXT)
                             ("Meta,n" NEXT)
                             ("Meta,p" FN \TEDIT.PRINT.MENU)
                             ("Meta,P" FN \TEDIT.PRINT.MENU)
                             ("Meta,r" REDO)
                             ("Meta,R" REDO)
                             ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,U" FN TEDIT.UNDO.UNDO)
                             ("Meta,u" UNDO)
                             ("Meta,z" UNDO)
                             ("Meta,Z" TEDIT.UNDO.UNDO]
        (P (MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                                              (SELECTQ (CADR ENTRY)
                                                     (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                                                (CADDR ENTRY)))
                                                     (TEDIT.SETSYNTAX (CAR ENTRY)
                                                            (CADR ENTRY])



(* ;; "Functions that actually implement the commands for the function keys:")

(DEFINEQ

(\TEDIT.BOLD.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness off for the selected characters, and for future type-in.")

    (\TEDIT.BOLD.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT MEDIUM)
           SEL])

(\TEDIT.BOLD.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness on for selected characters and for future type-in.")

    (\TEDIT.BOLD.CARET.ON TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT BOLD)
           SEL])

(\TEDIT.CENTER.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 28-Jul-2023 16:14 by rmk")
                                                             (* ; "Edited 11-Apr-2023 13:22 by rmk")
                                                             (* ; "Edited 10-Apr-2023 10:08 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "Changes the QUAD of the selected paragraphs in TEXTSTREAM, when the CENTER key is typed.  Rotates through the sequences (LEFT/RIGHT/CENTER) from the QUAD of the first paragraph to find the NEWQUAD that it will apply everywhere.")

    (CL:UNLESS (TEDITMENUP TEXTOBJ)
        (LET [(NEWQUAD (LIST 'QUAD (OR [CADR (MEMB (LISTGET (TEDIT.GET.PARALOOKS TEXTSTREAM SEL)
                                                          'QUAD)
                                                   '(LEFT JUSTIFIED CENTERED RIGHT]
                                       'LEFT]
             (TEDIT.PARALOOKS TEXTSTREAM NEWQUAD SEL)
             (CL:WHEN TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM NEWQUAD T))))])

(\TEDIT.CENTER.SEL.REV
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "acts like center.sel but cycles in the opposite direction")

    (PROG (LOOKS OLDQUAD NEWQUAD NEWQUADS PARASEL (SAVECH# (fetch (SELECTION CH#) of SEL))
                 (SAVEDCH (fetch (SELECTION DCH) of SEL)))
          (for PARA in (\PARAS.IN.SEL SEL TEXTOBJ)
             do (SETQ LOOKS (TEDIT.GET.PARALOOKS TEXTSTREAM PARA))
                (SETQ OLDQUAD (LISTGET LOOKS 'QUAD))
                [SETQ NEWQUAD (CADR (MEMB OLDQUAD (CONSTANT '(LEFT CENTERED JUSTIFIED LEFT]
                (LISTPUT LOOKS 'QUAD NEWQUAD)
                (SETQ PARASEL (TEDIT.SETSEL TEXTSTREAM PARA 1))
                (TEDIT.PARALOOKS TEXTSTREAM LOOKS PARASEL)
                (push NEWQUADS NEWQUAD))
          (TEDIT.SETSEL TEXTSTREAM SAVECH# SAVEDCH)
          (COND
             (TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM (DREVERSE NEWQUADS)
                                         T])

(\TEDIT.DEFAULTS.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 11:24")
    (PROGN (TEDIT.CARETLOOKS TEXTSTREAM (create CHARLOOKS using TEDIT.DEFAULT.CHARLOOKS))
           (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.DEFAULTSSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
                                                             (* acts on the selection)
    (TEDIT.LOOKS TEXTSTREAM (create CHARLOOKS using TEDIT.DEFAULT.CHARLOOKS)
           SEL])

(\TEDIT.SETDEFAULT.FROM.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds " 8-Nov-85 15:22")
                                                             (* Set the defaults from the current 
                                                             selection.)
    (PROG ((LOOKS (TEDIT.GET.LOOKS TEXTSTREAM SEL)))
          (SETQ TEDIT.DEFAULT.CHARLOOKS (\TEDIT.PARSE.CHARLOOKS.LIST LOOKS])

(\TEDIT.KEY.FIND
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN BACKWARD)            (* ; "Edited 12-Jul-2023 08:26 by rmk")
                                                             (* ; "Edited 20-Jun-2023 13:06 by rmk")
                                                            (* ; "Edited  6-May-2018 17:14 by rmk:")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "just calls the normal tedit.find starting at the right of the current selection.  SEL is passed from the FN key in the readtable, presumably always (fetch SEL of TEXTOBJ).")

    (* ;; "AGAIN suppresses confirmation of a previous target, but also assumes that the user is not interested in trying again at the current character position--starts forward or backward from there.")

    (LET (TARGET (TEXTOBJ (TEXTOBJ TEXTSTREAM))
                CH W)                                        (* ; 
                                                      "Case sensitive search, with * and # wildcards")
         [SETQ W (CAR (MKLIST (fetch (TEXTOBJ \WINDOW) of TEXTOBJ]
         (CL:WHEN AGAIN
             (SETQ TARGET (WINDOWPROP W 'TEDIT.LAST.FIND.STRING)))
         (CL:UNLESS TARGET
             (SETQ AGAIN NIL)                                (* ; 
                                                         "If no previous target, we aren't %"again%"")
             [SETQ TARGET (TEDIT.GETINPUT TEXTOBJ (CL:IF BACKWARD
                                                      "Backward search string: "
                                                      "Search string: ")
                                 (WINDOWPROP W 'TEDIT.LAST.FIND.STRING)
                                 (CHARCODE (EOL LF ESC])
         (CL:WHEN TARGET
             (CL:UNLESS SEL
                 (SETQ SEL (fetch (TEXTOBJ SEL) of TEXTOBJ)))
             (\SHOWSEL SEL NIL)                              (* ; 
                                                            "Save for next search, even if not found")
             (WINDOWPROP W 'TEDIT.LAST.FIND.STRING TARGET)
             (SETQ CH (if BACKWARD
                          then (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching backward for %"" TARGET
                                                                 "%"")
                                      T)
                               (TEDIT.FIND.BACKWARD TEXTOBJ (MKSTRING TARGET)
                                      NIL NIL T)
                        else (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching for %"" TARGET "%"")
                                    T)
                             (TEDIT.FIND TEXTOBJ (MKSTRING TARGET)
                                    NIL NIL T)))
             (COND
                (CH (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" TARGET "%" found")
                           T)                                (* ; "We found the target text.")
                    (TEDIT.RESET.EXTEND.PENDING.DELETE SEL)  (* ; 
                                                             "Set up SELECTION to be the found text")
                    (\TEDIT.UPDATE.SEL SEL (CAR CH)
                           (ADD1 (IDIFFERENCE (CADR CH)
                                        (CAR CH)))
                           (CL:IF BACKWARD
                               'LEFT
                               'RIGHT)
                           (CL:IF (fetch (TEXTOBJ TXTREADONLY) of TEXTOBJ)
                               'PENDINGDEL
                               'NORMAL))
                    [SETSEL SEL SELKIND (CL:IF (IGREATERP (CADR CH)
                                                      (CAR CH)
                                                      'WORD
                                                      'CHAR]
                    (replace (TEXTOBJ CARETLOOKS) of TEXTOBJ with (\TEDIT.GET.INSERT.CHARLOOKS 
                                                                         TEXTOBJ SEL))
                    (\FIXSEL SEL TEXTOBJ)
                    (TEDIT.NORMALIZECARET TEXTOBJ))
                (T (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" TARGET "%" not found")
                          T)))
             (\SHOWSEL SEL T))])

(\TEDIT.KEY.FIND.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN)                     (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 17:59 by rmk")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL AGAIN T])

(\TEDIT.FINDAGAIN.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 18:03 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T T])

(\TEDIT.FINDAGAIN
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.ITALIC.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited 20-Oct-87 10:43 by jds")
    (\TEDIT.ITALIC.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE REGULAR)
           SEL])

(\TEDIT.ITALIC.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 10:43 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE ITALIC)
           SEL])

(\TEDIT.LARGERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.LARGER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT 2)
                 SEL])

(\TEDIT.LCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 28-May-2023 00:34 by rmk")
                                                             (* ; "Edited 24-May-2023 22:46 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :LowerCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.  Or maybe the transformation function should be stored.")

    (\TEDIT.REPLACE.SELPIECES (\SELPIECES.CHARTRANSFORM (\SELPIECES.COPY (\SELPIECES SEL))
                                     (FUNCTION L-CASECODE))
           TEXTOBJ SEL)
    (replace (TEDITHISTORYEVENT THACTION) of (\TEDIT.LASTEVENT TEXTOBJ) with :LowerCase])

(\TEDIT.SHOWCARETLOOKS
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 30-May-91 21:09 by jds")

         (* * comment)

    (PROG ((LOOKS (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)))
          (TEDIT.PROMPTPRINT TEXTSTREAM (CONCAT (\TK.DESCRIBEFONT (fetch (CHARLOOKS CLFONT)
                                                                     of LOOKS))
                                               (COND
                                                  ((AND (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                        (NEQ (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                             0))
                                                   (CONCAT " offset " (fetch (CHARLOOKS CLOFFSET)
                                                                         of LOOKS)))
                                                  (T ""))
                                               (COND
                                                  ((fetch (CHARLOOKS CLOLINE) of LOOKS)
                                                   " overlined")
                                                  (T ""))
                                               (COND
                                                  ((fetch (CHARLOOKS CLULINE) of LOOKS)
                                                   " underlined")
                                                  (T "")))
                 T)
          (RETURN])

(\TEDIT.SMALLERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.SMALLER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT -2)
                 SEL])

(\TEDIT.SUBSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT -2)
           SEL])

(\TEDIT.SUPERSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:13 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT 2)
           SEL])

(\TEDIT.UCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 28-May-2023 00:33 by rmk")
                                                             (* ; "Edited 24-May-2023 22:45 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :UpperCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.")

    (\TEDIT.REPLACE.SELPIECES (\SELPIECES.CHARTRANSFORM (\SELPIECES.COPY (\SELPIECES SEL))
                                     (FUNCTION U-CASECODE))
           TEXTOBJ SEL)
    (replace (TEDITHISTORYEVENT THACTION) of (\TEDIT.LASTEVENT TEXTOBJ) with :UpperCase])

(\TEDIT.UNDERLINE.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:26 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE OFF)
           SEL])

(\TEDIT.UNDERLINE.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT OFF)
           SEL])

(\TEDIT.SELECT.ALL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited  6-May-2018 12:41 by rmk:")
    (TEDIT.SETSEL TEXTSTREAM 0 (ADD1 (fetch TEXTLEN of TEXTOBJ))
           'LEFT])

(\TEDIT.KEY.SUBSTITUTE
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  8-May-2023 09:35 by rmk")

    (* ;; "Stub for function-key")

    (TEDIT.SUBSTITUTE TEXTSTREAM NIL NIL T])
)



(* ;; "Auxiliary functions used in the above main functions:")

(DEFINEQ

(\TEDIT.BOLD.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT MEDIUM)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.BOLD.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT BOLD)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.ITALIC.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE REGULAR)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.ITALIC.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE ITALIC)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.LARGER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT 2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SMALLER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT -2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SUBSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT -2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SUPERSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT 2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.UNDERLINE.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE OFF)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.UNDERLINE.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.STRIKEOUT.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT OFF)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.STRIKEOUT.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])
)



(* ; "little selection utilities etc., for building hacks")

(DEFINEQ

(\SEL.LIMIT
  [LAMBDA (SEL)                                              (* ; "Edited 30-May-91 21:06 by jds")

         (* returns the character that delimits this selection.
         The first char if the point is left else the last)

    (COND
       ((EQ (fetch (SELECTION POINT) of SEL)
            'LEFT)
        (fetch (SELECTION CH#) of SEL))
       (T (SUB1 (fetch (SELECTION CHLIM) of SEL])

(\TK.DESCRIBEFONT
  [LAMBDA (FONT)                                             (* gbn "15-Dec-84 17:54")

         (* * returns a string which describes a font
         (in short. If it's not italic then no mention is made of slope, etc.))

    (CONCAT (L-CASE (FONTPROP FONT 'FAMILY))
           " "
           (FONTPROP FONT 'SIZE)
           (COND
              [(NEQ (FONTPROP FONT 'WEIGHT)
                    'MEDIUM)
               (CONCAT " " (L-CASE (FONTPROP FONT 'WEIGHT]
              (T ""))
           (COND
              [(NEQ (FONTPROP FONT 'SLOPE)
                    'REGULAR)
               (CONCAT " " (L-CASE (FONTPROP FONT 'SLOPE]
              (T ""])

(\PARAS.IN.SEL
  [LAMBDA (SEL TEXTOBJ)                                      (* ; "Edited  9-May-2023 13:20 by rmk")
                                                             (* ; "Edited 11-Apr-2023 14:47 by rmk")
                                                             (* ; "Edited 22-Oct-2022 15:14 by rmk")
                                                             (* ; "Edited 30-May-91 21:06 by jds")
    (CONS (fetch (SELECTION CH#) of SEL)
          (for PC LASTEND (CHNO _ 0) inselpieces (\SELPIECES SEL NIL TEXTOBJ)
             eachtime (add CHNO (PLEN PC)) when LASTEND collect (SETQ LASTEND (PPARALAST PC))
                                                              CHNO])
)

(RPAQQ TEDIT.FNKEY.VERBOSE T)

(RPAQQ \TEDIT.KEYS
       (("Function,^D" UNDO)
        ("Function,$" UNDO)
        ("Function,^C" FN \TEDIT.KEY.FIND)
        ("Function,#" FN \TEDIT.KEY.FIND)
        ("Function,Bs" REDO)
        ("Function,(" REDO)
        ("Function,^R" NEXT)
        ("Function,62" NEXT)
        ("Esc" EXPAND)
        ("Function,^T" EXPAND)
        ("Function,A" FN \TEDIT.CENTER.SEL)
        ("Function,a" FN \TEDIT.CENTER.SEL.REV)
        ("Function,B" FN \TEDIT.BOLD.SEL.ON)
        ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
        ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
        ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
        ("Function,D" FN \TEDIT.UCASE.SEL)
        ("Function,d" FN \TEDIT.LCASE.SEL)
        ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
        ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
        ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
        ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
        ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,H" FN \TEDIT.SMALLERSEL)
        ("Function,h" FN \TEDIT.LARGERSEL)
        ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,M" FN \TEDIT.DEFAULTSSEL)
        ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
        ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
        ("Meta,a" FN \TEDIT.SELECT.ALL)
        ("Meta,A" FN \TEDIT.SELECT.ALL)
        ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
        ("Meta,f" FN \TEDIT.KEY.FIND)
        ("Meta,g" FN \TEDIT.FINDAGAIN)
        ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
        ("Meta,N" NEXT)
        ("Meta,n" NEXT)
        ("Meta,p" FN \TEDIT.PRINT.MENU)
        ("Meta,P" FN \TEDIT.PRINT.MENU)
        ("Meta,r" REDO)
        ("Meta,R" REDO)
        ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,U" FN TEDIT.UNDO.UNDO)
        ("Meta,u" UNDO)
        ("Meta,z" UNDO)
        ("Meta,Z" TEDIT.UNDO.UNDO)))

[MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                              (SELECTQ (CADR ENTRY)
                                  (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                             (CADDR ENTRY)))
                                  (TEDIT.SETSYNTAX (CAR ENTRY)
                                         (CADR ENTRY]
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5575 20591 (\TEDIT.BOLD.SEL.OFF 5585 . 5923) (\TEDIT.BOLD.SEL.ON 5925 . 6253) (
\TEDIT.CENTER.SEL 6255 . 7428) (\TEDIT.CENTER.SEL.REV 7430 . 8496) (\TEDIT.DEFAULTS.CARET 8498 . 8776)
 (\TEDIT.DEFAULTSSEL 8778 . 9101) (\TEDIT.SETDEFAULT.FROM.SEL 9103 . 9535) (\TEDIT.KEY.FIND 9537 . 
13841) (\TEDIT.KEY.FIND.BACKWARD 13843 . 14148) (\TEDIT.FINDAGAIN.BACKWARD 14150 . 14561) (
\TEDIT.FINDAGAIN 14563 . 14854) (\TEDIT.ITALIC.SEL.OFF 14856 . 15108) (\TEDIT.ITALIC.SEL.ON 15110 . 
15303) (\TEDIT.LARGERSEL 15305 . 15593) (\TEDIT.LCASE.SEL 15595 . 16356) (\TEDIT.SHOWCARETLOOKS 16358
 . 17934) (\TEDIT.SMALLERSEL 17936 . 18227) (\TEDIT.SUBSCRIPTSEL 18229 . 18432) (\TEDIT.SUPERSCRIPTSEL
 18434 . 18638) (\TEDIT.UCASE.SEL 18640 . 19345) (\TEDIT.UNDERLINE.SEL.OFF 19347 . 19545) (
\TEDIT.UNDERLINE.SEL.ON 19547 . 19743) (\TEDIT.STRIKEOUT.SEL.ON 19745 . 19941) (
\TEDIT.STRIKEOUT.SEL.OFF 19943 . 20141) (\TEDIT.SELECT.ALL 20143 . 20366) (\TEDIT.KEY.SUBSTITUTE 20368
 . 20589)) (20663 26151 (\TEDIT.BOLD.CARET.OFF 20673 . 21125) (\TEDIT.BOLD.CARET.ON 21127 . 21576) (
\TEDIT.ITALIC.CARET.OFF 21578 . 22032) (\TEDIT.ITALIC.CARET.ON 22034 . 22486) (\TEDIT.LARGER.CARET 
22488 . 22940) (\TEDIT.SMALLER.CARET 22942 . 23396) (\TEDIT.SUBSCRIPT.CARET 23398 . 23856) (
\TEDIT.SUPERSCRIPT.CARET 23858 . 24317) (\TEDIT.UNDERLINE.CARET.OFF 24319 . 24776) (
\TEDIT.UNDERLINE.CARET.ON 24778 . 25233) (\TEDIT.STRIKEOUT.CARET.OFF 25235 . 25692) (
\TEDIT.STRIKEOUT.CARET.ON 25694 . 26149)) (26220 28136 (\SEL.LIMIT 26230 . 26674) (\TK.DESCRIBEFONT 
26676 . 27366) (\PARAS.IN.SEL 27368 . 28134)))))
STOP
