(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "25-Jun-2024 12:01:24" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;89 35600  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.MANPAGE)

      :PREVIOUS-DATE "23-Jun-2024 23:06:50" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;88)


(PRETTYCOMPRINT TEDIT-FNKEYSCOMS)

(RPAQQ TEDIT-FNKEYSCOMS
       [(COMS 
              (* ;; "Functions that actually implement the commands for the function keys:")

              (FNS \TEDIT.BOLD.SEL.OFF \TEDIT.BOLD.SEL.ON \TEDIT.CENTER.SEL \TEDIT.CENTER.SEL.REV 
                   \TEDIT.DEFAULTS.CARET \TEDIT.DEFAULTSSEL \TEDIT.SETDEFAULT.FROM.SEL 
                   \TEDIT.KEY.FIND \TEDIT.KEY.FIND.SEARCHSTRING \TEDIT.GET.TARGET.STRING 
                   \TEDIT.KEY.FIND.BACKWARD \TEDIT.FINDAGAIN.BACKWARD \TEDIT.FINDAGAIN 
                   \TEDIT.ITALIC.SEL.OFF \TEDIT.ITALIC.SEL.ON \TEDIT.LARGERSEL \TEDIT.LCASE.SEL 
                   \TEDIT.SHOWCARETLOOKS \TEDIT.SMALLERSEL \TEDIT.SUBSCRIPTSEL \TEDIT.SUPERSCRIPTSEL
                   \TEDIT.UCASE.SEL \TEDIT.UNDERLINE.SEL.OFF \TEDIT.UNDERLINE.SEL.ON 
                   \TEDIT.STRIKEOUT.SEL.ON \TEDIT.STRIKEOUT.SEL.OFF \TEDIT.SELECT.ALL 
                   \TEDIT.KEY.SUBSTITUTE \TEDIT.MANPAGE \TEDIT.CALL.ED))
        (COMS 
              (* ;; "Auxiliary functions used in the above main functions:")

              (FNS \TEDIT.BOLD.CARET.OFF \TEDIT.BOLD.CARET.ON \TEDIT.ITALIC.CARET.OFF 
                   \TEDIT.ITALIC.CARET.ON \TEDIT.LARGER.CARET \TEDIT.SMALLER.CARET 
                   \TEDIT.SUBSCRIPT.CARET \TEDIT.SUPERSCRIPT.CARET \TEDIT.UNDERLINE.CARET.OFF 
                   \TEDIT.UNDERLINE.CARET.ON \TEDIT.STRIKEOUT.CARET.OFF \TEDIT.STRIKEOUT.CARET.ON))
        (COMS                                                (* ; 
                                                "little selection utilities etc., for building hacks")
              (FNS \TK.DESCRIBEFONT))
        [VARS (TEDIT.FNKEY.VERBOSE T)
              (\TEDIT.KEYS '(("Function,^D" UNDO)
                             ("Function,$" UNDO)
                             ("Function,^C" FN \TEDIT.KEY.FIND)
                             ("Function,#" FN \TEDIT.KEY.FIND)
                             ("Function,Bs" REDO)
                             ("Function,(" REDO)
                             ("Function,^R" NEXT)
                             ("Function,62" NEXT)
                             ("Esc" EXPAND)
                             ("Function,^T" EXPAND)
                             ("Function,A" FN \TEDIT.CENTER.SEL)
                             ("Function,a" FN \TEDIT.CENTER.SEL.REV)
                             ("Function,B" FN \TEDIT.BOLD.SEL.ON)
                             ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
                             ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
                             ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
                             ("Function,D" FN \TEDIT.UCASE.SEL)
                             ("Function,d" FN \TEDIT.LCASE.SEL)
                             ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
                             ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
                             ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
                             ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
                             ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,H" FN \TEDIT.SMALLERSEL)
                             ("Function,h" FN \TEDIT.LARGERSEL)
                             ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,M" FN \TEDIT.DEFAULTSSEL)
                             ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
                             ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
                             ("Meta,a" FN \TEDIT.SELECT.ALL)
                             ("Meta,A" FN \TEDIT.SELECT.ALL)
                             ("Meta,d" FN \TEDIT.MANPAGE)
                             ("Meta,D" FN \TEDIT.MANPAGE)
                             ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
                             ("Meta,f" FN \TEDIT.KEY.FIND)
                             ("Meta,g" FN \TEDIT.FINDAGAIN)
                             ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
                             ("Meta,N" NEXT)
                             ("Meta,n" NEXT)
                             ("Meta,o" FN \TEDIT.CALL.ED)
                             ("Meta,O" FN \TEDIT.CALL.ED)
                             ("Meta,p" FN \TEDIT.PRINT.MENU)
                             ("Meta,P" FN \TEDIT.PRINT.MENU)
                             ("Meta,r" REDO)
                             ("Meta,R" REDO)
                             ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,U" FN \TEDIT.UNDO.UNDO)
                             ("Meta,u" UNDO)
                             ("Meta,z" UNDO)
                             ("Meta,Z" \TEDIT.UNDO.UNDO]
        (P (MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                                              (SELECTQ (CADR ENTRY)
                                                     (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                                                (CADDR ENTRY)))
                                                     (TEDIT.SETSYNTAX (CAR ENTRY)
                                                            (CADR ENTRY])



(* ;; "Functions that actually implement the commands for the function keys:")

(DEFINEQ

(\TEDIT.BOLD.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness off for the selected characters, and for future type-in.")

    (\TEDIT.BOLD.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT MEDIUM)
           SEL])

(\TEDIT.BOLD.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness on for selected characters and for future type-in.")

    (\TEDIT.BOLD.CARET.ON TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT BOLD)
           SEL])

(\TEDIT.CENTER.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL REVERSE)                   (* ; "Edited 11-Dec-2023 11:02 by rmk")
                                                             (* ; "Edited 28-Jul-2023 16:14 by rmk")
                                                             (* ; "Edited 11-Apr-2023 13:22 by rmk")
                                                             (* ; "Edited 10-Apr-2023 10:08 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "Changes the QUAD of the selected paragraphs in TEXTSTREAM, when the CENTER key is typed.  Rotates through the sequences (LEFT/RIGHT/CENTER) from the QUAD of the first paragraph to find the NEWQUAD that it will apply to all the paragraphs in SEL.  If REVERSE, cycles the quads in the opposite direction.")

    (CL:UNLESS (TEDITMENUP TEXTOBJ)
        (LET [(NEWQUAD (LIST 'QUAD (OR [CADR (MEMB (LISTGET (TEDIT.GET.PARALOOKS TEXTSTREAM SEL)
                                                          'QUAD)
                                                   (CL:IF REVERSE
                                                       '(LEFT CENTERED JUSTIFIED LEFT)
                                                       '(LEFT JUSTIFIED CENTERED RIGHT))]
                                       'LEFT]
             (TEDIT.PARALOOKS TEXTSTREAM NEWQUAD SEL)
             (CL:WHEN TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM NEWQUAD T))))])

(\TEDIT.CENTER.SEL.REV
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Dec-2023 11:02 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")
    (\TEDIT.CENTER.SEL TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.DEFAULTS.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 26-Feb-2024 08:45 by rmk")
                                                             (* ; "Edited 11-Nov-2023 16:00 by rmk")
                                                             (* jds "21-Sep-85 11:24")
    (TEDIT.CARETLOOKS TEXTSTREAM (create CHARLOOKS using (GETTOBJ TEXTOBJ DEFAULTCHARLOOKS)))
    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.DEFAULTSSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Nov-2023 15:55 by rmk")
                                                             (* ; "Edited 20-Oct-87 11:12 by jds")
                                                             (* ; "acts on the selection")
    (TEDIT.LOOKS TEXTSTREAM (create CHARLOOKS using (GETTOBJ TEXTOBJ DEFAULTCHARLOOKS))
           SEL])

(\TEDIT.SETDEFAULT.FROM.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 12-Nov-2023 16:40 by rmk")
                                                             (* ; "Edited 11-Nov-2023 16:03 by rmk")
                                                             (* jds " 8-Nov-85 15:22")
                                                             (* ; 
                                                       "Set the defaults from the current selection.")
    (SETTOBJ TEXTOBJ DEFAULTCHARLOOKS (\TEDIT.PARSE.CHARLOOKS.LIST (TEDIT.GET.LOOKS TEXTSTREAM SEL)
                                             NIL TEXTOBJ])

(\TEDIT.KEY.FIND
  [LAMBDA (TSTREAM TEXTOBJ SEL AGAIN BACKWARD)               (* ; "Edited 22-Jun-2024 10:00 by rmk")
                                                             (* ; "Edited 18-May-2024 16:29 by rmk")
                                                             (* ; "Edited 12-May-2024 21:33 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:36 by rmk")
                                                             (* ; "Edited 24-Apr-2024 23:39 by rmk")
                                                             (* ; "Edited  9-Mar-2024 11:36 by rmk")
                                                             (* ; "Edited 29-Feb-2024 17:06 by rmk")
                                                             (* ; "Edited 27-Feb-2024 00:22 by rmk")
                                                             (* ; "Edited 16-Feb-2024 23:43 by rmk")
                                                             (* ; "Edited 14-Dec-2023 21:14 by rmk")
                                                             (* ; "Edited 12-Jul-2023 08:26 by rmk")
                                                             (* ; "Edited 20-Jun-2023 13:06 by rmk")
                                                            (* ; "Edited  6-May-2018 17:14 by rmk:")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "Case sensitive search, with * and # wildcards. Just calls the normal tedit.find starting at the right of the current selection.  SEL is passed from the FN key in the readtable, presumably always (fetch SEL of TEXTOBJ).")

    (* ;; "AGAIN suppresses confirmation of a previous target.")

    (SETQ TSTREAM (TEXTSTREAM TSTREAM))
    (LET ((TEXTOBJ (TEXTOBJ TSTREAM))
          (SEARCHSTRING (\TEDIT.KEY.FIND.SEARCHSTRING TEXTOBJ AGAIN BACKWARD))
          CH)                                                (* ; "")
         (CL:WHEN SEARCHSTRING
             (CL:UNLESS SEL
                 (SETQ SEL (FGETTOBJ TEXTOBJ SEL)))
             (\TEDIT.SHOWSEL SEL NIL TEXTOBJ)
             (SETQ CH (if BACKWARD
                          then (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching backward for %"" 
                                                                 SEARCHSTRING "%"")
                                      T)
                               (TEDIT.FIND.BACKWARD TSTREAM (MKSTRING SEARCHSTRING)
                                      NIL NIL T)
                        else (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching for %"" SEARCHSTRING "%"")
                                    T)
                             (TEDIT.FIND TSTREAM (MKSTRING SEARCHSTRING)
                                    NIL NIL T)))
             (COND
                (CH (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" SEARCHSTRING "%" found")
                           T)                                (* ; "We found the target text.")
                    (\TEDIT.RESET.EXTEND.PENDING.DELETE SEL TEXTOBJ)
                                                             (* ; 
                                                             "Set up SELECTION to be the found text")
                    (\TEDIT.UPDATE.SEL SEL (CAR CH)
                           (ADD1 (IDIFFERENCE (CADR CH)
                                        (CAR CH)))
                           (CL:IF BACKWARD
                               'LEFT
                               'RIGHT))
                    (TEDIT.SET.SEL.LOOKS SEL (CL:IF (FGETTOBJ TEXTOBJ TXTREADONLY)
                                                 'PENDINGDEL
                                                 'NORMAL))
                    [SETSEL SEL SELKIND (CL:IF (IGREATERP (CADR CH)
                                                      (CAR CH)
                                                      'WORD
                                                      'CHAR]
                    (FSETTOBJ TEXTOBJ CARETLOOKS (\TEDIT.GET.INSERT.CHARLOOKS TEXTOBJ SEL))
                    (\TEDIT.FIXSEL SEL TEXTOBJ)
                    (TEDIT.NORMALIZECARET TEXTOBJ))
                (T (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" SEARCHSTRING "%" not found")
                          T)))
             (\TEDIT.SHOWSEL SEL T TEXTOBJ))])

(\TEDIT.KEY.FIND.SEARCHSTRING
  [LAMBDA (TEXTOBJ AGAIN BACKWARD)                           (* ; "Edited 22-Jun-2024 10:17 by rmk")

    (* ;; "TEDIT.LAST.FIND.STRING used to be stored as a window property.  But then it would only pertain to a particular pane.  Better store it on the textobj.")

    (LET (SEARCHSTRING)
         (CL:WHEN AGAIN
             (SETQ SEARCHSTRING (GETTEXTPROP TEXTOBJ 'TEDIT.LAST.FIND.STRING)))
         (CL:UNLESS SEARCHSTRING
             (SETQ SEARCHSTRING (\TEDIT.GET.TARGET.STRING TEXTOBJ 'TEDIT.LAST.FIND.STRING))
             (SETQ SEARCHSTRING (TEDIT.GETINPUT TEXTOBJ (CL:IF BACKWARD
                                                            "Backward search string: "
                                                            "Search string: ")
                                       SEARCHSTRING))
             (CL:WHEN SEARCHSTRING                           (* ; 
                                                            "Save for next search, even if not found")
                 (PUTTEXTPROP TEXTOBJ 'TEDIT.LAST.FIND.STRING SEARCHSTRING)))
         SEARCHSTRING])

(\TEDIT.GET.TARGET.STRING
  [LAMBDA (TEXTOBJ PROP)                                     (* ; "Edited 23-Jun-2024 23:06 by rmk")
                                                             (* ; "Edited 22-Jun-2024 12:03 by rmk")
                                                             (* ; "Edited 29-Feb-2024 17:08 by rmk")

    (* ;; "This is called from \TEDIT.KEY.FIND, TEDIT.DEFAULT.MENUFN, TEDIT.SUBSTITUTE.  It tries to determine the best tentative target string for a search.  PROP is either TEDIT.LAST.FIND.STRING or TEDIT.LAST.SUBSTITUTE.STRING.")

    (* ;; "Current heuristic:  If a previous string, use it if it contains wild cards, otherwise the current non-point selection.  Note that meta-G goes directly to the last search.")

    (LET [(PREV (STRINGP (GETTEXTPROP TEXTOBJ PROP]
         (if [AND PREV (find I from 1 to (NCHARS PREV)
                          suchthat (AND (MEMB (NTHCHARCODE PREV I)
                                              (CHARCODE (%# ESCAPE *)))
                                        (NEQ (CHARCODE %')
                                             (NTHCHARCODE PREV (SUB1 I]
             then PREV
           elseif (IGEQ (FGETSEL (FGETTOBJ TEXTOBJ SEL)
                               DCH)
                        1)
             then 
                  (* ;; "TEDIT.SEL.AS.STRING breaks on image objects, should be fixed there.")

                  (CAR (NLSETQ (TEDIT.SEL.AS.STRING TEXTOBJ)))
           else PREV])

(\TEDIT.KEY.FIND.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN)                     (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 17:59 by rmk")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL AGAIN T])

(\TEDIT.FINDAGAIN.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 18:03 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T T])

(\TEDIT.FINDAGAIN
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.ITALIC.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited 20-Oct-87 10:43 by jds")
    (\TEDIT.ITALIC.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE REGULAR)
           SEL])

(\TEDIT.ITALIC.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 10:43 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE ITALIC)
           SEL])

(\TEDIT.LARGERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.LARGER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT 2)
                 SEL])

(\TEDIT.LCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 15-Mar-2024 13:57 by rmk")
                                                             (* ; "Edited  3-Mar-2024 12:28 by rmk")
                                                             (* ; "Edited 28-May-2023 00:34 by rmk")
                                                             (* ; "Edited 24-May-2023 22:46 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :LowerCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.  Or maybe the transformation function should be stored.")

    (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.CHARTRANSFORM (\TEDIT.SELPIECES.COPY (
                                                                                     \TEDIT.SELPIECES
                                                                                      SEL))
                                     (FUNCTION L-CASECODE)
                                     NIL TEXTOBJ)
           TEXTOBJ SEL)
    (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
           THACTION :LowerCase])

(\TEDIT.SHOWCARETLOOKS
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:07 by rmk")
                                                             (* ; "Edited 30-May-91 21:09 by jds")
    (LET ((LOOKS (FGETTOBJ TEXTOBJ CARETLOOKS)))
         (TEDIT.PROMPTPRINT TEXTSTREAM (CONCAT (\TK.DESCRIBEFONT (fetch (CHARLOOKS CLFONT)
                                                                    of LOOKS))
                                              (COND
                                                 ((AND (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                       (NEQ (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                            0))
                                                  (CONCAT " offset " (fetch (CHARLOOKS CLOFFSET)
                                                                        of LOOKS)))
                                                 (T ""))
                                              (COND
                                                 ((fetch (CHARLOOKS CLOLINE) of LOOKS)
                                                  " overlined")
                                                 (T ""))
                                              (COND
                                                 ((fetch (CHARLOOKS CLULINE) of LOOKS)
                                                  " underlined")
                                                 (T "")))
                T])

(\TEDIT.SMALLERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.SMALLER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT -2)
                 SEL])

(\TEDIT.SUBSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT -2)
           SEL])

(\TEDIT.SUPERSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:13 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT 2)
           SEL])

(\TEDIT.UCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 15-Mar-2024 13:57 by rmk")
                                                             (* ; "Edited  3-Mar-2024 12:56 by rmk")
                                                             (* ; "Edited 28-May-2023 00:33 by rmk")
                                                             (* ; "Edited 24-May-2023 22:45 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :UpperCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.")

    (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.CHARTRANSFORM (\TEDIT.SELPIECES.COPY (
                                                                                     \TEDIT.SELPIECES
                                                                                      SEL))
                                     (FUNCTION U-CASECODE)
                                     NIL TEXTOBJ)
           TEXTOBJ SEL)
    (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
           THACTION :UpperCase])

(\TEDIT.UNDERLINE.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:26 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE OFF)
           SEL])

(\TEDIT.UNDERLINE.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT OFF)
           SEL])

(\TEDIT.SELECT.ALL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited  6-May-2018 12:41 by rmk:")
    (TEDIT.SETSEL TEXTSTREAM 0 (ADD1 (fetch TEXTLEN of TEXTOBJ))
           'LEFT])

(\TEDIT.KEY.SUBSTITUTE
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  8-May-2023 09:35 by rmk")

    (* ;; "Stub for function-key")

    (TEDIT.SUBSTITUTE TEXTSTREAM NIL NIL T])

(\TEDIT.MANPAGE
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 25-Jun-2024 11:59 by rmk")
                                                             (* ; "Edited 26-May-2024 21:53 by rmk")
                                                             (* ; "Edited 25-May-2024 14:50 by rmk")

    (* ;; "If meta-D is typed in an existing DINFO window, the new stuff comes up but then the window closes.  That could be debugged, but probably not worth it.  The DINFO window has its own links to things that it thought were worth indexing.")

    (CL:UNLESS (WINDOWPROP (\TEDIT.PRIMARYPANE TSTREAM)
                      'DINFOGRAPH)
        (GENERIC.MAN.LOOKUP (TEDIT.SEL.AS.STRING TSTREAM SEL)))])

(\TEDIT.CALL.ED
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 25-May-2024 15:03 by rmk")
    (ED [MKATOM (CAR (MKLIST (TEDIT.SEL.AS.SEXPR TSTREAM SEL]
        '(:DONTWAIT])
)



(* ;; "Auxiliary functions used in the above main functions:")

(DEFINEQ

(\TEDIT.BOLD.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:08 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT MEDIUM)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.BOLD.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:09 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT BOLD)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.ITALIC.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:09 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE REGULAR)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.ITALIC.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE ITALIC)
                        (GETTOBJ TEXTOBJ CARETLOOKS TEXTOBJ)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.LARGER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT 2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SMALLER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT -2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SUBSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:16 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT -2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SUPERSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:16 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT 2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.UNDERLINE.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:17 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE OFF)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.UNDERLINE.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:17 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE ON)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.STRIKEOUT.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:18 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT OFF)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.STRIKEOUT.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])
)



(* ; "little selection utilities etc., for building hacks")

(DEFINEQ

(\TK.DESCRIBEFONT
  [LAMBDA (FONT)                                             (* gbn "15-Dec-84 17:54")

         (* * returns a string which describes a font
         (in short. If it's not italic then no mention is made of slope, etc.))

    (CONCAT (L-CASE (FONTPROP FONT 'FAMILY))
           " "
           (FONTPROP FONT 'SIZE)
           (COND
              [(NEQ (FONTPROP FONT 'WEIGHT)
                    'MEDIUM)
               (CONCAT " " (L-CASE (FONTPROP FONT 'WEIGHT]
              (T ""))
           (COND
              [(NEQ (FONTPROP FONT 'SLOPE)
                    'REGULAR)
               (CONCAT " " (L-CASE (FONTPROP FONT 'SLOPE]
              (T ""])
)

(RPAQQ TEDIT.FNKEY.VERBOSE T)

(RPAQQ \TEDIT.KEYS
       (("Function,^D" UNDO)
        ("Function,$" UNDO)
        ("Function,^C" FN \TEDIT.KEY.FIND)
        ("Function,#" FN \TEDIT.KEY.FIND)
        ("Function,Bs" REDO)
        ("Function,(" REDO)
        ("Function,^R" NEXT)
        ("Function,62" NEXT)
        ("Esc" EXPAND)
        ("Function,^T" EXPAND)
        ("Function,A" FN \TEDIT.CENTER.SEL)
        ("Function,a" FN \TEDIT.CENTER.SEL.REV)
        ("Function,B" FN \TEDIT.BOLD.SEL.ON)
        ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
        ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
        ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
        ("Function,D" FN \TEDIT.UCASE.SEL)
        ("Function,d" FN \TEDIT.LCASE.SEL)
        ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
        ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
        ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
        ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
        ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,H" FN \TEDIT.SMALLERSEL)
        ("Function,h" FN \TEDIT.LARGERSEL)
        ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,M" FN \TEDIT.DEFAULTSSEL)
        ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
        ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
        ("Meta,a" FN \TEDIT.SELECT.ALL)
        ("Meta,A" FN \TEDIT.SELECT.ALL)
        ("Meta,d" FN \TEDIT.MANPAGE)
        ("Meta,D" FN \TEDIT.MANPAGE)
        ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
        ("Meta,f" FN \TEDIT.KEY.FIND)
        ("Meta,g" FN \TEDIT.FINDAGAIN)
        ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
        ("Meta,N" NEXT)
        ("Meta,n" NEXT)
        ("Meta,o" FN \TEDIT.CALL.ED)
        ("Meta,O" FN \TEDIT.CALL.ED)
        ("Meta,p" FN \TEDIT.PRINT.MENU)
        ("Meta,P" FN \TEDIT.PRINT.MENU)
        ("Meta,r" REDO)
        ("Meta,R" REDO)
        ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,U" FN \TEDIT.UNDO.UNDO)
        ("Meta,u" UNDO)
        ("Meta,z" UNDO)
        ("Meta,Z" \TEDIT.UNDO.UNDO)))

[MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                              (SELECTQ (CADR ENTRY)
                                  (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                             (CADDR ENTRY)))
                                  (TEDIT.SETSYNTAX (CAR ENTRY)
                                         (CADR ENTRY]
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5884 25740 (\TEDIT.BOLD.SEL.OFF 5894 . 6232) (\TEDIT.BOLD.SEL.ON 6234 . 6562) (
\TEDIT.CENTER.SEL 6564 . 8080) (\TEDIT.CENTER.SEL.REV 8082 . 8378) (\TEDIT.DEFAULTS.CARET 8380 . 8873)
 (\TEDIT.DEFAULTSSEL 8875 . 9322) (\TEDIT.SETDEFAULT.FROM.SEL 9324 . 10001) (\TEDIT.KEY.FIND 10003 . 
14451) (\TEDIT.KEY.FIND.SEARCHSTRING 14453 . 15593) (\TEDIT.GET.TARGET.STRING 15595 . 17142) (
\TEDIT.KEY.FIND.BACKWARD 17144 . 17449) (\TEDIT.FINDAGAIN.BACKWARD 17451 . 17862) (\TEDIT.FINDAGAIN 
17864 . 18155) (\TEDIT.ITALIC.SEL.OFF 18157 . 18409) (\TEDIT.ITALIC.SEL.ON 18411 . 18604) (
\TEDIT.LARGERSEL 18606 . 18894) (\TEDIT.LCASE.SEL 18896 . 20083) (\TEDIT.SHOWCARETLOOKS 20085 . 21685)
 (\TEDIT.SMALLERSEL 21687 . 21978) (\TEDIT.SUBSCRIPTSEL 21980 . 22183) (\TEDIT.SUPERSCRIPTSEL 22185 . 
22389) (\TEDIT.UCASE.SEL 22391 . 23522) (\TEDIT.UNDERLINE.SEL.OFF 23524 . 23722) (
\TEDIT.UNDERLINE.SEL.ON 23724 . 23920) (\TEDIT.STRIKEOUT.SEL.ON 23922 . 24118) (
\TEDIT.STRIKEOUT.SEL.OFF 24120 . 24318) (\TEDIT.SELECT.ALL 24320 . 24543) (\TEDIT.KEY.SUBSTITUTE 24545
 . 24766) (\TEDIT.MANPAGE 24768 . 25524) (\TEDIT.CALL.ED 25526 . 25738)) (25812 32221 (
\TEDIT.BOLD.CARET.OFF 25822 . 26357) (\TEDIT.BOLD.CARET.ON 26359 . 26891) (\TEDIT.ITALIC.CARET.OFF 
26893 . 27430) (\TEDIT.ITALIC.CARET.ON 27432 . 27975) (\TEDIT.LARGER.CARET 27977 . 28512) (
\TEDIT.SMALLER.CARET 28514 . 29051) (\TEDIT.SUBSCRIPT.CARET 29053 . 29594) (\TEDIT.SUPERSCRIPT.CARET 
29596 . 30138) (\TEDIT.UNDERLINE.CARET.OFF 30140 . 30680) (\TEDIT.UNDERLINE.CARET.ON 30682 . 31220) (
\TEDIT.STRIKEOUT.CARET.OFF 31222 . 31762) (\TEDIT.STRIKEOUT.CARET.ON 31764 . 32219)) (32290 32992 (
\TK.DESCRIBEFONT 32300 . 32990)))))
STOP
