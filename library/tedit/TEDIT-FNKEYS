(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 9-May-2023 22:39:25" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;20 29313  

      :EDIT-BY rmk

      :CHANGES-TO (VARS TEDIT-FNKEYSCOMS)

      :PREVIOUS-DATE " 9-May-2023 13:21:14" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;19)


(PRETTYCOMPRINT TEDIT-FNKEYSCOMS)

(RPAQQ TEDIT-FNKEYSCOMS
       [(COMS 
              (* ;; "Functions that actually implement the commands for the function keys:")

              (FNS \TEDIT.BOLD.SEL.OFF \TEDIT.BOLD.SEL.ON \TEDIT.CENTER.SEL \TEDIT.CENTER.SEL.REV 
                   \TEDIT.DEFAULTS.CARET \TEDIT.DEFAULTSSEL \TEDIT.SETDEFAULT.FROM.SEL \TEDIT.FIND 
                   \TEDIT.FINDAGAIN \TEDIT.ITALIC.SEL.OFF \TEDIT.ITALIC.SEL.ON \TEDIT.LARGERSEL 
                   \TEDIT.LCASE.SEL \TEDIT.SHOWCARETLOOKS \TEDIT.SMALLERSEL \TEDIT.SUBSCRIPTSEL 
                   \TEDIT.SUPERSCRIPTSEL \TEDIT.UCASE.SEL \TEDIT.UNDERLINE.SEL.OFF 
                   \TEDIT.UNDERLINE.SEL.ON \TEDIT.STRIKEOUT.SEL.ON \TEDIT.STRIKEOUT.SEL.OFF 
                   \TEDIT.SELECT.ALL \TEDIT.SUBSTITUTE))
        (COMS 
              (* ;; "Auxiliary functions used in the above main functions:")

              (FNS \TEDIT.BOLD.CARET.OFF \TEDIT.BOLD.CARET.ON \TEDIT.ITALIC.CARET.OFF 
                   \TEDIT.ITALIC.CARET.ON \TEDIT.LARGER.CARET \TEDIT.SMALLER.CARET 
                   \TEDIT.SUBSCRIPT.CARET \TEDIT.SUPERSCRIPT.CARET \TEDIT.UNDERLINE.CARET.OFF 
                   \TEDIT.UNDERLINE.CARET.ON \TEDIT.STRIKEOUT.CARET.OFF \TEDIT.STRIKEOUT.CARET.ON))
        (COMS                                                (* ; 
                                                "little selection utilities etc., for building hacks")
              (FNS \SEL.LIMIT \SEL.LINEDESC \TK.DESCRIBEFONT \PARAS.IN.SEL))
        [VARS (TEDIT.FNKEY.VERBOSE T)
              (\TEDIT.KEYS '(("Function,^D" UNDO)
                             ("Function,$" UNDO)
                             ("Function,^C" FN \TEDIT.FIND)
                             ("Function,#" FN \TEDIT.FIND)
                             ("Function,Bs" REDO)
                             ("Function,(" REDO)
                             ("Function,^R" NEXT)
                             ("Function,62" NEXT)
                             ("Esc" EXPAND)
                             ("Function,^T" EXPAND)
                             ("Function,A" FN \TEDIT.CENTER.SEL)
                             ("Function,a" FN \TEDIT.CENTER.SEL.REV)
                             ("Function,B" FN \TEDIT.BOLD.SEL.ON)
                             ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
                             ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
                             ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
                             ("Function,D" FN \TEDIT.UCASE.SEL)
                             ("Function,d" FN \TEDIT.LCASE.SEL)
                             ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
                             ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
                             ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
                             ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
                             ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,H" FN \TEDIT.SMALLERSEL)
                             ("Function,h" FN \TEDIT.LARGERSEL)
                             ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,M" FN \TEDIT.DEFAULTSSEL)
                             ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
                             ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
                             ("Meta,a" FN \TEDIT.SELECT.ALL)
                             ("Meta,A" FN \TEDIT.SELECT.ALL)
                             ("Meta,F" FN \TEDIT.FIND)
                             ("Meta,f" FN \TEDIT.FIND)
                             ("Meta,g" FN \TEDIT.FINDAGAIN)
                             ("Meta,G" FN \TEDIT.FINDAGAIN)
                             ("Meta,N" NEXT)
                             ("Meta,n" NEXT)
                             ("Meta,r" REDO)
                             ("Meta,R" REDO)
                             ("Meta,s" FN \TEDIT.SUBSTITUTE)
                             ("Meta,S" FN \TEDIT.SUBSTITUTE)
                             ("Meta,U" UNDO)
                             ("Meta,u" UNDO)
                             ("Meta,z" UNDO)
                             ("Meta,Z" UNDO]
        (P (MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                                              (SELECTQ (CADR ENTRY)
                                                     (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                                                (CADDR ENTRY)))
                                                     (TEDIT.SETSYNTAX (CAR ENTRY)
                                                            (CADR ENTRY])



(* ;; "Functions that actually implement the commands for the function keys:")

(DEFINEQ

(\TEDIT.BOLD.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness off for the selected characters, and for future type-in.")

    (\TEDIT.BOLD.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT MEDIUM)
           SEL])

(\TEDIT.BOLD.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness on for selected characters and for future type-in.")

    (\TEDIT.BOLD.CARET.ON TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT BOLD)
           SEL])

(\TEDIT.CENTER.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Apr-2023 13:22 by rmk")
                                                             (* ; "Edited 10-Apr-2023 10:08 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "Changes the QUAD of the selected paragraphs in TEXTSTREAM, when the CENTER key is typed.  Rotates through the sequence determined by the QUAD of the first paragraph, so they all selected paragraphs end up the same.")

    (CL:UNLESS (TEDITMENUP TEXTOBJ)
        (for PCHNO NEWQUAD (CH# _ (fetch (SELECTION CH#) of SEL))
             (DCH _ (fetch (SELECTION DCH) of SEL))
             (POINT _ (fetch (SELECTION POINT) of SEL)) in (\PARAS.IN.SEL SEL TEXTOBJ)
           first [SETQ NEWQUAD (LIST 'QUAD (OR [CADR (MEMB (LISTGET (TEDIT.GET.PARALOOKS TEXTSTREAM 
                                                                           PCHNO)
                                                                  'QUAD)
                                                           '(LEFT JUSTIFIED CENTERED RIGHT]
                                               'LEFT] do (TEDIT.PARALOOKS TEXTSTREAM NEWQUAD
                                                                (TEDIT.SETSEL TEXTSTREAM PCHNO 1))
           finally (TEDIT.SETSEL TEXTSTREAM CH# DCH POINT)
                 (CL:WHEN TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM NEWQUAD T))))])

(\TEDIT.CENTER.SEL.REV
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "acts like center.sel but cycles in the opposite direction")

    (PROG (LOOKS OLDQUAD NEWQUAD NEWQUADS PARASEL (SAVECH# (fetch (SELECTION CH#) of SEL))
                 (SAVEDCH (fetch (SELECTION DCH) of SEL)))
          (for PARA in (\PARAS.IN.SEL SEL TEXTOBJ)
             do (SETQ LOOKS (TEDIT.GET.PARALOOKS TEXTSTREAM PARA))
                (SETQ OLDQUAD (LISTGET LOOKS 'QUAD))
                [SETQ NEWQUAD (CADR (MEMB OLDQUAD (CONSTANT '(LEFT CENTERED JUSTIFIED LEFT]
                (LISTPUT LOOKS 'QUAD NEWQUAD)
                (SETQ PARASEL (TEDIT.SETSEL TEXTSTREAM PARA 1))
                (TEDIT.PARALOOKS TEXTSTREAM LOOKS PARASEL)
                (push NEWQUADS NEWQUAD))
          (TEDIT.SETSEL TEXTSTREAM SAVECH# SAVEDCH)
          (COND
             (TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM (DREVERSE NEWQUADS)
                                         T])

(\TEDIT.DEFAULTS.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 11:24")
    (PROGN (TEDIT.CARETLOOKS TEXTSTREAM (create CHARLOOKS using TEDIT.DEFAULT.CHARLOOKS))
           (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.DEFAULTSSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
                                                             (* acts on the selection)
    (TEDIT.LOOKS TEXTSTREAM (create CHARLOOKS using TEDIT.DEFAULT.CHARLOOKS)
           SEL])

(\TEDIT.SETDEFAULT.FROM.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds " 8-Nov-85 15:22")
                                                             (* Set the defaults from the current 
                                                             selection.)
    (PROG ((LOOKS (TEDIT.GET.LOOKS TEXTSTREAM SEL)))
          (SETQ TEDIT.DEFAULT.CHARLOOKS (\TEDIT.PARSE.CHARLOOKS.LIST LOOKS])

(\TEDIT.FIND
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN)                     (* ; "Edited 18-Apr-2023 23:36 by rmk")
                                                            (* ; "Edited  6-May-2018 17:14 by rmk:")
                                                             (* ; "Edited 30-May-91 21:05 by jds")
                                                             (* ; 
                    "just calls the normal tedit.find starting at the right of the current selection")
    (PROG (TARGET (TEXTOBJ (TEXTOBJ TEXTSTREAM))
                 SEL CH W)                                   (* ; 
                                                      "Case sensitive search, with * and # wildcards")
          [SETQ W (CAR (MKLIST (fetch (TEXTOBJ \WINDOW) of TEXTOBJ]
          (CL:WHEN AGAIN
              (SETQ TARGET (WINDOWPROP W 'TEDIT.LAST.FIND.STRING)))
          (CL:UNLESS TARGET
              [SETQ TARGET (TEDIT.GETINPUT TEXTOBJ "Text to find: " (WINDOWPROP W 
                                                                           'TEDIT.LAST.FIND.STRING)
                                  (CHARCODE (EOL LF ESC])
          (CL:WHEN TARGET
              (SETQ SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
              (\SHOWSEL SEL NIL)
              (TEDIT.PROMPTPRINT TEXTOBJ "Searching..." T)
              (SETQ CH (TEDIT.FIND TEXTOBJ (MKSTRING TARGET)
                              NIL NIL T))
              (COND
                 (CH                                         (* ; "We found the target text.")
                     (TEDIT.PROMPTPRINT TEXTOBJ "Done.")
                     (replace (SELECTION CH#) of SEL with (CAR CH))
                                                             (* ; 
                                                             "Set up SELECTION to be the found text")
                     (replace (SELECTION CHLIM) of SEL with (ADD1 (CADR CH)))
                     [replace (SELECTION DCH) of SEL with (ADD1 (IDIFFERENCE (CADR CH)
                                                                       (CAR CH]
                     (replace (SELECTION POINT) of SEL with 'RIGHT)
                     [replace (SELECTION SELKIND) of SEL with (CL:IF (IGREATERP (CADR CH)
                                                                            (CAR CH)
                                                                            'WORD
                                                                            'CHAR]
                     (replace (TEXTOBJ CARETLOOKS) of TEXTOBJ with (\TEDIT.GET.INSERT.CHARLOOKS
                                                                    TEXTOBJ SEL))
                     (TEDIT.RESET.EXTEND.PENDING.DELETE SEL) (* ; "And never pending a deletion.")
                     (\FIXSEL SEL TEXTOBJ)
                     (TEDIT.NORMALIZECARET TEXTOBJ)
                     (\SHOWSEL SEL T)                        (* ; "And get it into the window")
                     (WINDOWPROP W 'TEDIT.LAST.FIND.STRING TARGET))
                 (T (TEDIT.PROMPTPRINT TEXTOBJ "(not found)")
                    (\SHOWSEL SEL T))))])

(\TEDIT.FINDAGAIN
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.FIND TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.ITALIC.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited 20-Oct-87 10:43 by jds")
    (\TEDIT.ITALIC.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE REGULAR)
           SEL])

(\TEDIT.ITALIC.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 10:43 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE ITALIC)
           SEL])

(\TEDIT.LARGERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.LARGER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT 2)
                 SEL])

(\TEDIT.LCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "LOWER-CASEs the selection")

    (PROG ((STR (TEDIT.SEL.AS.STRING STREAM SEL))
           (POS (fetch (SELECTION CH#) of SEL))
           (LEN (fetch (SELECTION DCH) of SEL))
           (POINT (fetch (SELECTION POINT) of SEL)))
          (TEDIT.DELETE STREAM SEL)
          (TEDIT.INSERT STREAM (L-CASE STR))
          (TEDIT.SETSEL STREAM POS LEN POINT)
          (TEDIT.NORMALIZECARET TEXTOBJ)
          (replace (TEDITHISTORYEVENT THACTION) of (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ)
             with 'LowerCase])

(\TEDIT.SHOWCARETLOOKS
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 30-May-91 21:09 by jds")

         (* * comment)

    (PROG ((LOOKS (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)))
          (TEDIT.PROMPTPRINT TEXTSTREAM (CONCAT (\TK.DESCRIBEFONT (fetch (CHARLOOKS CLFONT)
                                                                     of LOOKS))
                                               (COND
                                                  ((AND (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                        (NEQ (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                             0))
                                                   (CONCAT " offset " (fetch (CHARLOOKS CLOFFSET)
                                                                         of LOOKS)))
                                                  (T ""))
                                               (COND
                                                  ((fetch (CHARLOOKS CLOLINE) of LOOKS)
                                                   " overlined")
                                                  (T ""))
                                               (COND
                                                  ((fetch (CHARLOOKS CLULINE) of LOOKS)
                                                   " underlined")
                                                  (T "")))
                 T)
          (RETURN])

(\TEDIT.SMALLERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.SMALLER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT -2)
                 SEL])

(\TEDIT.SUBSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT -2)
           SEL])

(\TEDIT.SUPERSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:13 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT 2)
           SEL])

(\TEDIT.UCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 30-May-91 21:05 by jds")
                                                             (* ; "uppercasifies the selection")
    (PROG ((STR (TEDIT.SEL.AS.STRING STREAM SEL))
           (POS (fetch (SELECTION CH#) of SEL))
           (LEN (fetch (SELECTION DCH) of SEL))
           (POINT (fetch (SELECTION POINT) of SEL)))
          (TEDIT.DELETE STREAM SEL)
          (TEDIT.INSERT STREAM (U-CASE STR))
          (TEDIT.SETSEL STREAM POS LEN POINT)
          (TEDIT.NORMALIZECARET TEXTOBJ)
          (replace (TEDITHISTORYEVENT THACTION) of (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ)
             with 'UpperCase])

(\TEDIT.UNDERLINE.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:26 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE OFF)
           SEL])

(\TEDIT.UNDERLINE.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT OFF)
           SEL])

(\TEDIT.SELECT.ALL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited  6-May-2018 12:41 by rmk:")
    (TEDIT.SETSEL TEXTSTREAM 0 (ADD1 (fetch TEXTLEN of TEXTOBJ))
           'LEFT])

(\TEDIT.SUBSTITUTE
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  8-May-2023 09:35 by rmk")

    (* ;; "Stub for function-key")

    (TEDIT.SUBSTITUTE TEXTSTREAM NIL NIL T])
)



(* ;; "Auxiliary functions used in the above main functions:")

(DEFINEQ

(\TEDIT.BOLD.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT MEDIUM)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.BOLD.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT BOLD)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.ITALIC.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE REGULAR)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.ITALIC.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE ITALIC)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.LARGER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT 2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SMALLER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT -2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SUBSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT -2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.SUPERSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT 2)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.UNDERLINE.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE OFF)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.UNDERLINE.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.STRIKEOUT.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT OFF)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.STRIKEOUT.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])
)



(* ; "little selection utilities etc., for building hacks")

(DEFINEQ

(\SEL.LIMIT
  [LAMBDA (SEL)                                              (* ; "Edited 30-May-91 21:06 by jds")

         (* returns the character that delimits this selection.
         The first char if the point is left else the last)

    (COND
       ((EQ (fetch (SELECTION POINT) of SEL)
            'LEFT)
        (fetch (SELECTION CH#) of SEL))
       (T (SUB1 (fetch (SELECTION CHLIM) of SEL])

(\SEL.LINEDESC
  [LAMBDA (SEL)                                              (* ; "Edited 30-May-91 21:06 by jds")
                                                             (* returns the first line descriptor 
                                                             if the point is left, otherwise the 
                                                             last)
    (COND
       [(EQ (fetch (SELECTION POINT) of SEL)
            'LEFT)
        (CAR (MKLIST (fetch (SELECTION L1) of SEL]
       (T (CAR (MKLIST (fetch (SELECTION LN) of SEL])

(\TK.DESCRIBEFONT
  [LAMBDA (FONT)                                             (* gbn "15-Dec-84 17:54")

         (* * returns a string which describes a font
         (in short. If it's not italic then no mention is made of slope, etc.))

    (CONCAT (L-CASE (FONTPROP FONT 'FAMILY))
           " "
           (FONTPROP FONT 'SIZE)
           (COND
              [(NEQ (FONTPROP FONT 'WEIGHT)
                    'MEDIUM)
               (CONCAT " " (L-CASE (FONTPROP FONT 'WEIGHT]
              (T ""))
           (COND
              [(NEQ (FONTPROP FONT 'SLOPE)
                    'REGULAR)
               (CONCAT " " (L-CASE (FONTPROP FONT 'SLOPE]
              (T ""])

(\PARAS.IN.SEL
  [LAMBDA (SEL TEXTOBJ)                                      (* ; "Edited  9-May-2023 13:20 by rmk")
                                                             (* ; "Edited 11-Apr-2023 14:47 by rmk")
                                                             (* ; "Edited 22-Oct-2022 15:14 by rmk")
                                                             (* ; "Edited 30-May-91 21:06 by jds")
    (CONS (fetch (SELECTION CH#) of SEL)
          (for PC LASTEND (CHNO _ 0) inselpieces (\SELPIECES SEL NIL TEXTOBJ)
             eachtime (add CHNO (PLEN PC)) when LASTEND collect (SETQ LASTEND (PPARALAST PC))
                                                              CHNO])
)

(RPAQQ TEDIT.FNKEY.VERBOSE T)

(RPAQQ \TEDIT.KEYS
       (("Function,^D" UNDO)
        ("Function,$" UNDO)
        ("Function,^C" FN \TEDIT.FIND)
        ("Function,#" FN \TEDIT.FIND)
        ("Function,Bs" REDO)
        ("Function,(" REDO)
        ("Function,^R" NEXT)
        ("Function,62" NEXT)
        ("Esc" EXPAND)
        ("Function,^T" EXPAND)
        ("Function,A" FN \TEDIT.CENTER.SEL)
        ("Function,a" FN \TEDIT.CENTER.SEL.REV)
        ("Function,B" FN \TEDIT.BOLD.SEL.ON)
        ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
        ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
        ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
        ("Function,D" FN \TEDIT.UCASE.SEL)
        ("Function,d" FN \TEDIT.LCASE.SEL)
        ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
        ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
        ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
        ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
        ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,H" FN \TEDIT.SMALLERSEL)
        ("Function,h" FN \TEDIT.LARGERSEL)
        ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,M" FN \TEDIT.DEFAULTSSEL)
        ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
        ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
        ("Meta,a" FN \TEDIT.SELECT.ALL)
        ("Meta,A" FN \TEDIT.SELECT.ALL)
        ("Meta,F" FN \TEDIT.FIND)
        ("Meta,f" FN \TEDIT.FIND)
        ("Meta,g" FN \TEDIT.FINDAGAIN)
        ("Meta,G" FN \TEDIT.FINDAGAIN)
        ("Meta,N" NEXT)
        ("Meta,n" NEXT)
        ("Meta,r" REDO)
        ("Meta,R" REDO)
        ("Meta,s" FN \TEDIT.SUBSTITUTE)
        ("Meta,S" FN \TEDIT.SUBSTITUTE)
        ("Meta,U" UNDO)
        ("Meta,u" UNDO)
        ("Meta,z" UNDO)
        ("Meta,Z" UNDO)))

[MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                              (SELECTQ (CADR ENTRY)
                                  (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                             (CADDR ENTRY)))
                                  (TEDIT.SETSYNTAX (CAR ENTRY)
                                         (CADR ENTRY]
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5321 18851 (\TEDIT.BOLD.SEL.OFF 5331 . 5669) (\TEDIT.BOLD.SEL.ON 5671 . 5999) (
\TEDIT.CENTER.SEL 6001 . 7562) (\TEDIT.CENTER.SEL.REV 7564 . 8630) (\TEDIT.DEFAULTS.CARET 8632 . 8910)
 (\TEDIT.DEFAULTSSEL 8912 . 9235) (\TEDIT.SETDEFAULT.FROM.SEL 9237 . 9669) (\TEDIT.FIND 9671 . 12936) 
(\TEDIT.FINDAGAIN 12938 . 13116) (\TEDIT.ITALIC.SEL.OFF 13118 . 13370) (\TEDIT.ITALIC.SEL.ON 13372 . 
13565) (\TEDIT.LARGERSEL 13567 . 13855) (\TEDIT.LCASE.SEL 13857 . 14563) (\TEDIT.SHOWCARETLOOKS 14565
 . 16141) (\TEDIT.SMALLERSEL 16143 . 16434) (\TEDIT.SUBSCRIPTSEL 16436 . 16639) (\TEDIT.SUPERSCRIPTSEL
 16641 . 16845) (\TEDIT.UCASE.SEL 16847 . 17609) (\TEDIT.UNDERLINE.SEL.OFF 17611 . 17809) (
\TEDIT.UNDERLINE.SEL.ON 17811 . 18007) (\TEDIT.STRIKEOUT.SEL.ON 18009 . 18205) (
\TEDIT.STRIKEOUT.SEL.OFF 18207 . 18405) (\TEDIT.SELECT.ALL 18407 . 18630) (\TEDIT.SUBSTITUTE 18632 . 
18849)) (18923 24411 (\TEDIT.BOLD.CARET.OFF 18933 . 19385) (\TEDIT.BOLD.CARET.ON 19387 . 19836) (
\TEDIT.ITALIC.CARET.OFF 19838 . 20292) (\TEDIT.ITALIC.CARET.ON 20294 . 20746) (\TEDIT.LARGER.CARET 
20748 . 21200) (\TEDIT.SMALLER.CARET 21202 . 21656) (\TEDIT.SUBSCRIPT.CARET 21658 . 22116) (
\TEDIT.SUPERSCRIPT.CARET 22118 . 22577) (\TEDIT.UNDERLINE.CARET.OFF 22579 . 23036) (
\TEDIT.UNDERLINE.CARET.ON 23038 . 23493) (\TEDIT.STRIKEOUT.CARET.OFF 23495 . 23952) (
\TEDIT.STRIKEOUT.CARET.ON 23954 . 24409)) (24480 27002 (\SEL.LIMIT 24490 . 24934) (\SEL.LINEDESC 24936
 . 25540) (\TK.DESCRIBEFONT 25542 . 26232) (\PARAS.IN.SEL 26234 . 27000)))))
STOP
