(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "14-Dec-2023 21:18:52" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;55 30681  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.KEY.FIND \TEDIT.SHOWCARETLOOKS \TEDIT.BOLD.CARET.OFF 
                       \TEDIT.BOLD.CARET.ON \TEDIT.ITALIC.CARET.OFF \TEDIT.ITALIC.CARET.ON 
                       \TEDIT.LARGER.CARET \TEDIT.SMALLER.CARET \TEDIT.SUBSCRIPT.CARET 
                       \TEDIT.SUPERSCRIPT.CARET \TEDIT.UNDERLINE.CARET.OFF \TEDIT.UNDERLINE.CARET.ON
                       \TEDIT.STRIKEOUT.CARET.OFF)

      :PREVIOUS-DATE "11-Dec-2023 11:54:04" {WMEDLEY}<library>tedit>TEDIT-FNKEYS.;54)


(PRETTYCOMPRINT TEDIT-FNKEYSCOMS)

(RPAQQ TEDIT-FNKEYSCOMS
       [(COMS 
              (* ;; "Functions that actually implement the commands for the function keys:")

              (FNS \TEDIT.BOLD.SEL.OFF \TEDIT.BOLD.SEL.ON \TEDIT.CENTER.SEL \TEDIT.CENTER.SEL.REV 
                   \TEDIT.DEFAULTS.CARET \TEDIT.DEFAULTSSEL \TEDIT.SETDEFAULT.FROM.SEL 
                   \TEDIT.KEY.FIND \TEDIT.KEY.FIND.BACKWARD \TEDIT.FINDAGAIN.BACKWARD 
                   \TEDIT.FINDAGAIN \TEDIT.ITALIC.SEL.OFF \TEDIT.ITALIC.SEL.ON \TEDIT.LARGERSEL 
                   \TEDIT.LCASE.SEL \TEDIT.SHOWCARETLOOKS \TEDIT.SMALLERSEL \TEDIT.SUBSCRIPTSEL 
                   \TEDIT.SUPERSCRIPTSEL \TEDIT.UCASE.SEL \TEDIT.UNDERLINE.SEL.OFF 
                   \TEDIT.UNDERLINE.SEL.ON \TEDIT.STRIKEOUT.SEL.ON \TEDIT.STRIKEOUT.SEL.OFF 
                   \TEDIT.SELECT.ALL \TEDIT.KEY.SUBSTITUTE))
        (COMS 
              (* ;; "Auxiliary functions used in the above main functions:")

              (FNS \TEDIT.BOLD.CARET.OFF \TEDIT.BOLD.CARET.ON \TEDIT.ITALIC.CARET.OFF 
                   \TEDIT.ITALIC.CARET.ON \TEDIT.LARGER.CARET \TEDIT.SMALLER.CARET 
                   \TEDIT.SUBSCRIPT.CARET \TEDIT.SUPERSCRIPT.CARET \TEDIT.UNDERLINE.CARET.OFF 
                   \TEDIT.UNDERLINE.CARET.ON \TEDIT.STRIKEOUT.CARET.OFF \TEDIT.STRIKEOUT.CARET.ON))
        (COMS                                                (* ; 
                                                "little selection utilities etc., for building hacks")
              (FNS \TK.DESCRIBEFONT))
        [VARS (TEDIT.FNKEY.VERBOSE T)
              (\TEDIT.KEYS '(("Function,^D" UNDO)
                             ("Function,$" UNDO)
                             ("Function,^C" FN \TEDIT.KEY.FIND)
                             ("Function,#" FN \TEDIT.KEY.FIND)
                             ("Function,Bs" REDO)
                             ("Function,(" REDO)
                             ("Function,^R" NEXT)
                             ("Function,62" NEXT)
                             ("Esc" EXPAND)
                             ("Function,^T" EXPAND)
                             ("Function,A" FN \TEDIT.CENTER.SEL)
                             ("Function,a" FN \TEDIT.CENTER.SEL.REV)
                             ("Function,B" FN \TEDIT.BOLD.SEL.ON)
                             ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
                             ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
                             ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
                             ("Function,D" FN \TEDIT.UCASE.SEL)
                             ("Function,d" FN \TEDIT.LCASE.SEL)
                             ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
                             ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
                             ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
                             ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
                             ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,H" FN \TEDIT.SMALLERSEL)
                             ("Function,h" FN \TEDIT.LARGERSEL)
                             ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
                             ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
                             ("Function,M" FN \TEDIT.DEFAULTSSEL)
                             ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
                             ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
                             ("Meta,a" FN \TEDIT.SELECT.ALL)
                             ("Meta,A" FN \TEDIT.SELECT.ALL)
                             ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
                             ("Meta,f" FN \TEDIT.KEY.FIND)
                             ("Meta,g" FN \TEDIT.FINDAGAIN)
                             ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
                             ("Meta,N" NEXT)
                             ("Meta,n" NEXT)
                             ("Meta,p" FN \TEDIT.PRINT.MENU)
                             ("Meta,P" FN \TEDIT.PRINT.MENU)
                             ("Meta,r" REDO)
                             ("Meta,R" REDO)
                             ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
                             ("Meta,U" FN TEDIT.UNDO.UNDO)
                             ("Meta,u" UNDO)
                             ("Meta,z" UNDO)
                             ("Meta,Z" TEDIT.UNDO.UNDO]
        (P (MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                                              (SELECTQ (CADR ENTRY)
                                                     (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                                                (CADDR ENTRY)))
                                                     (TEDIT.SETSYNTAX (CAR ENTRY)
                                                            (CADR ENTRY])



(* ;; "Functions that actually implement the commands for the function keys:")

(DEFINEQ

(\TEDIT.BOLD.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness off for the selected characters, and for future type-in.")

    (\TEDIT.BOLD.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT MEDIUM)
           SEL])

(\TEDIT.BOLD.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  6-Nov-87 11:00 by jds")

    (* ;; "Turn boldness on for selected characters and for future type-in.")

    (\TEDIT.BOLD.CARET.ON TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(WEIGHT BOLD)
           SEL])

(\TEDIT.CENTER.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL REVERSE)                   (* ; "Edited 11-Dec-2023 11:02 by rmk")
                                                             (* ; "Edited 28-Jul-2023 16:14 by rmk")
                                                             (* ; "Edited 11-Apr-2023 13:22 by rmk")
                                                             (* ; "Edited 10-Apr-2023 10:08 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "Changes the QUAD of the selected paragraphs in TEXTSTREAM, when the CENTER key is typed.  Rotates through the sequences (LEFT/RIGHT/CENTER) from the QUAD of the first paragraph to find the NEWQUAD that it will apply to all the paragraphs in SEL.  If REVERSE, cycles the quads in the opposite direction.")

    (CL:UNLESS (TEDITMENUP TEXTOBJ)
        (LET [(NEWQUAD (LIST 'QUAD (OR [CADR (MEMB (LISTGET (TEDIT.GET.PARALOOKS TEXTSTREAM SEL)
                                                          'QUAD)
                                                   (CL:IF REVERSE
                                                       '(LEFT CENTERED JUSTIFIED LEFT)
                                                       '(LEFT JUSTIFIED CENTERED RIGHT))]
                                       'LEFT]
             (TEDIT.PARALOOKS TEXTSTREAM NEWQUAD SEL)
             (CL:WHEN TEDIT.FNKEY.VERBOSE (TEDIT.PROMPTPRINT TEXTSTREAM NEWQUAD T))))])

(\TEDIT.CENTER.SEL.REV
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Dec-2023 11:02 by rmk")
                                                             (* ; "Edited 30-May-91 21:05 by jds")
    (\TEDIT.CENTER.SEL TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.DEFAULTS.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Nov-2023 16:00 by rmk")
                                                             (* jds "21-Sep-85 11:24")
    (PROGN (TEDIT.CARETLOOKS TEXTSTREAM (create CHARLOOKS using (GETTOBJ TEXTOBJ DEFAULTCHARLOOKS)))
           (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])

(\TEDIT.DEFAULTSSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 11-Nov-2023 15:55 by rmk")
                                                             (* ; "Edited 20-Oct-87 11:12 by jds")
                                                             (* ; "acts on the selection")
    (TEDIT.LOOKS TEXTSTREAM (create CHARLOOKS using (GETTOBJ TEXTOBJ DEFAULTCHARLOOKS))
           SEL])

(\TEDIT.SETDEFAULT.FROM.SEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 12-Nov-2023 16:40 by rmk")
                                                             (* ; "Edited 11-Nov-2023 16:03 by rmk")
                                                             (* jds " 8-Nov-85 15:22")
                                                             (* ; 
                                                       "Set the defaults from the current selection.")
    (SETTOBJ TEXTOBJ DEFAULTCHARLOOKS (\TEDIT.PARSE.CHARLOOKS.LIST (TEDIT.GET.LOOKS TEXTSTREAM SEL)
                                             NIL TEXTOBJ])

(\TEDIT.KEY.FIND
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN BACKWARD)            (* ; "Edited 14-Dec-2023 21:14 by rmk")
                                                             (* ; "Edited 12-Jul-2023 08:26 by rmk")
                                                             (* ; "Edited 20-Jun-2023 13:06 by rmk")
                                                            (* ; "Edited  6-May-2018 17:14 by rmk:")
                                                             (* ; "Edited 30-May-91 21:05 by jds")

    (* ;; "just calls the normal tedit.find starting at the right of the current selection.  SEL is passed from the FN key in the readtable, presumably always (fetch SEL of TEXTOBJ).")

    (* ;; "AGAIN suppresses confirmation of a previous target, but also assumes that the user is not interested in trying again at the current character position--starts forward or backward from there.")

    (LET ((TEXTOBJ (TEXTOBJ TEXTSTREAM))
          TARGET CH W)                                       (* ; 
                                                      "Case sensitive search, with * and # wildcards")
         [SETQ W (CAR (MKLIST (FGETTOBJ TEXTOBJ \WINDOW]
         (CL:WHEN AGAIN
             (SETQ TARGET (WINDOWPROP W 'TEDIT.LAST.FIND.STRING)))
         (CL:UNLESS TARGET
             (SETQ AGAIN NIL)                                (* ; 
                                                         "If no previous target, we aren't %"again%"")
             [SETQ TARGET (TEDIT.GETINPUT TEXTOBJ (CL:IF BACKWARD
                                                      "Backward search string: "
                                                      "Search string: ")
                                 (WINDOWPROP W 'TEDIT.LAST.FIND.STRING)
                                 (CHARCODE (EOL LF ESC])
         (CL:WHEN TARGET
             (CL:UNLESS SEL
                 (SETQ SEL (FGETTOBJ TEXTOBJ SEL)))
             (\SHOWSEL SEL NIL)                              (* ; 
                                                            "Save for next search, even if not found")
             (WINDOWPROP W 'TEDIT.LAST.FIND.STRING TARGET)
             (SETQ CH (if BACKWARD
                          then (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching backward for %"" TARGET
                                                                 "%"")
                                      T)
                               (TEDIT.FIND.BACKWARD TEXTOBJ (MKSTRING TARGET)
                                      NIL NIL T)
                        else (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Searching for %"" TARGET "%"")
                                    T)
                             (TEDIT.FIND TEXTOBJ (MKSTRING TARGET)
                                    NIL NIL T)))
             (COND
                (CH (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" TARGET "%" found")
                           T)                                (* ; "We found the target text.")
                    (TEDIT.RESET.EXTEND.PENDING.DELETE SEL)  (* ; 
                                                             "Set up SELECTION to be the found text")
                    (\TEDIT.UPDATE.SEL SEL (CAR CH)
                           (ADD1 (IDIFFERENCE (CADR CH)
                                        (CAR CH)))
                           (CL:IF BACKWARD
                               'LEFT
                               'RIGHT)
                           (CL:IF (FGETTOBJ TEXTOBJ TXTREADONLY)
                               'PENDINGDEL
                               'NORMAL))
                    [SETSEL SEL SELKIND (CL:IF (IGREATERP (CADR CH)
                                                      (CAR CH)
                                                      'WORD
                                                      'CHAR]
                    (FSETTOBJ TEXTOBJ CARETLOOKS (\TEDIT.GET.INSERT.CHARLOOKS TEXTOBJ SEL))
                    (\FIXSEL SEL TEXTOBJ)
                    (TEDIT.NORMALIZECARET TEXTOBJ))
                (T (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "%"" TARGET "%" not found")
                          T)))
             (\SHOWSEL SEL T))])

(\TEDIT.KEY.FIND.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL AGAIN)                     (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 17:59 by rmk")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL AGAIN T])

(\TEDIT.FINDAGAIN.BACKWARD
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                             (* ; "Edited 18-Jun-2023 18:03 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T T])

(\TEDIT.FINDAGAIN
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Jun-2023 13:57 by rmk")
                                                            (* ; "Edited  6-May-2018 17:12 by rmk:")
    (\TEDIT.KEY.FIND TEXTSTREAM TEXTOBJ SEL T])

(\TEDIT.ITALIC.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL CHARCODE)                  (* ; "Edited 20-Oct-87 10:43 by jds")
    (\TEDIT.ITALIC.CARET.OFF TEXTSTREAM TEXTOBJ SEL)
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE REGULAR)
           SEL])

(\TEDIT.ITALIC.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 10:43 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(SLOPE ITALIC)
           SEL])

(\TEDIT.LARGERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.LARGER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT 2)
                 SEL])

(\TEDIT.LCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 28-May-2023 00:34 by rmk")
                                                             (* ; "Edited 24-May-2023 22:46 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :LowerCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.  Or maybe the transformation function should be stored.")

    (\TEDIT.REPLACE.SELPIECES (\SELPIECES.CHARTRANSFORM (\SELPIECES.COPY (\SELPIECES SEL))
                                     (FUNCTION L-CASECODE))
           TEXTOBJ SEL)
    (replace (TEDITHISTORYEVENT THACTION) of (\TEDIT.LASTEVENT TEXTOBJ) with :LowerCase])

(\TEDIT.SHOWCARETLOOKS
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:07 by rmk")
                                                             (* ; "Edited 30-May-91 21:09 by jds")
    (LET ((LOOKS (FGETTOBJ TEXTOBJ CARETLOOKS)))
         (TEDIT.PROMPTPRINT TEXTSTREAM (CONCAT (\TK.DESCRIBEFONT (fetch (CHARLOOKS CLFONT)
                                                                    of LOOKS))
                                              (COND
                                                 ((AND (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                       (NEQ (fetch (CHARLOOKS CLOFFSET) of LOOKS)
                                                            0))
                                                  (CONCAT " offset " (fetch (CHARLOOKS CLOFFSET)
                                                                        of LOOKS)))
                                                 (T ""))
                                              (COND
                                                 ((fetch (CHARLOOKS CLOLINE) of LOOKS)
                                                  " overlined")
                                                 (T ""))
                                              (COND
                                                 ((fetch (CHARLOOKS CLULINE) of LOOKS)
                                                  " underlined")
                                                 (T "")))
                T])

(\TEDIT.SMALLERSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* jds "21-Sep-85 08:58")
    (COND
       ((SHIFTDOWNP 'META)
        (\TEDIT.SMALLER.CARET TEXTSTREAM TEXTOBJ SEL))
       (T (TEDIT.LOOKS TEXTSTREAM (LIST 'SIZEINCREMENT -2)
                 SEL])

(\TEDIT.SUBSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:12 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT -2)
           SEL])

(\TEDIT.SUPERSCRIPTSEL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:13 by jds")
    (TEDIT.LOOKS TEXTSTREAM (LIST 'OFFSETINCREMENT 2)
           SEL])

(\TEDIT.UCASE.SEL
  [LAMBDA (STREAM TEXTOBJ SEL)                               (* ; "Edited 28-May-2023 00:33 by rmk")
                                                             (* ; "Edited 24-May-2023 22:45 by rmk")

    (* ;; "uppercasifies the selection.  This changes the :Replace THACTION to :UpperCase for REDO. That could be stored in another field, in which case undo wouldn't need to know.")

    (\TEDIT.REPLACE.SELPIECES (\SELPIECES.CHARTRANSFORM (\SELPIECES.COPY (\SELPIECES SEL))
                                     (FUNCTION U-CASECODE))
           TEXTOBJ SEL)
    (replace (TEDITHISTORYEVENT THACTION) of (\TEDIT.LASTEVENT TEXTOBJ) with :UpperCase])

(\TEDIT.UNDERLINE.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:26 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE OFF)
           SEL])

(\TEDIT.UNDERLINE.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(UNDERLINE ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT ON)
           SEL])

(\TEDIT.STRIKEOUT.SEL.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 20-Oct-87 11:27 by jds")
    (TEDIT.LOOKS TEXTSTREAM '(STRIKEOUT OFF)
           SEL])

(\TEDIT.SELECT.ALL
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited  6-May-2018 12:41 by rmk:")
    (TEDIT.SETSEL TEXTSTREAM 0 (ADD1 (fetch TEXTLEN of TEXTOBJ))
           'LEFT])

(\TEDIT.KEY.SUBSTITUTE
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited  8-May-2023 09:35 by rmk")

    (* ;; "Stub for function-key")

    (TEDIT.SUBSTITUTE TEXTSTREAM NIL NIL T])
)



(* ;; "Auxiliary functions used in the above main functions:")

(DEFINEQ

(\TEDIT.BOLD.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:08 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT MEDIUM)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.BOLD.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:09 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(WEIGHT BOLD)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.ITALIC.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:09 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE REGULAR)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.ITALIC.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SLOPE ITALIC)
                        (GETTOBJ TEXTOBJ CARETLOOKS TEXTOBJ)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.LARGER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT 2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SMALLER.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:15 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(SIZEINCREMENT -2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SUBSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:16 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT -2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.SUPERSCRIPT.CARET
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:16 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(OFFSETINCREMENT 2)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.UNDERLINE.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:17 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE OFF)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.UNDERLINE.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:17 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(UNDERLINE ON)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.STRIKEOUT.CARET.OFF
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                           (* ; "Edited 14-Dec-2023 21:18 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:32 by mitani")
    (LET ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT OFF)
                        (GETTOBJ TEXTOBJ CARETLOOKS)
                        TEXTOBJ)))
         (CL:WHEN LOOKS
             (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
             (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL))])

(\TEDIT.STRIKEOUT.CARET.ON
  [LAMBDA (TEXTSTREAM TEXTOBJ SEL)                          (* ; "Edited 12-Jun-90 18:32 by mitani")
    (PROG ((LOOKS (\TEDIT.PARSE.CHARLOOKS.LIST '(STRIKEOUT ON)
                         (fetch (TEXTOBJ CARETLOOKS) of TEXTOBJ)
                         TEXTOBJ)))
          (COND
             (LOOKS (TEDIT.CARETLOOKS TEXTSTREAM LOOKS)
                    (\TEDIT.SHOWCARETLOOKS TEXTSTREAM TEXTOBJ SEL])
)



(* ; "little selection utilities etc., for building hacks")

(DEFINEQ

(\TK.DESCRIBEFONT
  [LAMBDA (FONT)                                             (* gbn "15-Dec-84 17:54")

         (* * returns a string which describes a font
         (in short. If it's not italic then no mention is made of slope, etc.))

    (CONCAT (L-CASE (FONTPROP FONT 'FAMILY))
           " "
           (FONTPROP FONT 'SIZE)
           (COND
              [(NEQ (FONTPROP FONT 'WEIGHT)
                    'MEDIUM)
               (CONCAT " " (L-CASE (FONTPROP FONT 'WEIGHT]
              (T ""))
           (COND
              [(NEQ (FONTPROP FONT 'SLOPE)
                    'REGULAR)
               (CONCAT " " (L-CASE (FONTPROP FONT 'SLOPE]
              (T ""])
)

(RPAQQ TEDIT.FNKEY.VERBOSE T)

(RPAQQ \TEDIT.KEYS
       (("Function,^D" UNDO)
        ("Function,$" UNDO)
        ("Function,^C" FN \TEDIT.KEY.FIND)
        ("Function,#" FN \TEDIT.KEY.FIND)
        ("Function,Bs" REDO)
        ("Function,(" REDO)
        ("Function,^R" NEXT)
        ("Function,62" NEXT)
        ("Esc" EXPAND)
        ("Function,^T" EXPAND)
        ("Function,A" FN \TEDIT.CENTER.SEL)
        ("Function,a" FN \TEDIT.CENTER.SEL.REV)
        ("Function,B" FN \TEDIT.BOLD.SEL.ON)
        ("Function,b" FN \TEDIT.BOLD.SEL.OFF)
        ("Function,C" FN \TEDIT.ITALIC.SEL.ON)
        ("Function,c" FN \TEDIT.ITALIC.SEL.OFF)
        ("Function,D" FN \TEDIT.UCASE.SEL)
        ("Function,d" FN \TEDIT.LCASE.SEL)
        ("Function,E" FN \TEDIT.STRIKEOUT.SEL.ON)
        ("Function,e" FN \TEDIT.STRIKEOUT.SEL.OFF)
        ("Function,F" FN \TEDIT.UNDERLINE.SEL.ON)
        ("Function,f" FN \TEDIT.UNDERLINE.SEL.OFF)
        ("Function,G" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,g" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,H" FN \TEDIT.SMALLERSEL)
        ("Function,h" FN \TEDIT.LARGERSEL)
        ("Function,K" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,k" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,L" FN \TEDIT.SUBSCRIPTSEL)
        ("Function,l" FN \TEDIT.SUPERSCRIPTSEL)
        ("Function,M" FN \TEDIT.DEFAULTSSEL)
        ("Function,m" FN \TEDIT.SETDEFAULT.FROM.SEL)
        ("Function,^A" FN \TEDIT.SHOWCARETLOOKS)
        ("Meta,a" FN \TEDIT.SELECT.ALL)
        ("Meta,A" FN \TEDIT.SELECT.ALL)
        ("Meta,F" FN \TEDIT.KEY.FIND.BACKWARD)
        ("Meta,f" FN \TEDIT.KEY.FIND)
        ("Meta,g" FN \TEDIT.FINDAGAIN)
        ("Meta,G" FN \TEDIT.FINDAGAIN.BACKWARD)
        ("Meta,N" NEXT)
        ("Meta,n" NEXT)
        ("Meta,p" FN \TEDIT.PRINT.MENU)
        ("Meta,P" FN \TEDIT.PRINT.MENU)
        ("Meta,r" REDO)
        ("Meta,R" REDO)
        ("Meta,s" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,S" FN \TEDIT.KEY.SUBSTITUTE)
        ("Meta,U" FN TEDIT.UNDO.UNDO)
        ("Meta,u" UNDO)
        ("Meta,z" UNDO)
        ("Meta,Z" TEDIT.UNDO.UNDO)))

[MAPC \TEDIT.KEYS (FUNCTION (LAMBDA (ENTRY)
                              (SELECTQ (CADR ENTRY)
                                  (FN (TEDIT.SETFUNCTION (CAR ENTRY)
                                             (CADDR ENTRY)))
                                  (TEDIT.SETSYNTAX (CAR ENTRY)
                                         (CADR ENTRY]
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5924 20971 (\TEDIT.BOLD.SEL.OFF 5934 . 6272) (\TEDIT.BOLD.SEL.ON 6274 . 6602) (
\TEDIT.CENTER.SEL 6604 . 8120) (\TEDIT.CENTER.SEL.REV 8122 . 8418) (\TEDIT.DEFAULTS.CARET 8420 . 8818)
 (\TEDIT.DEFAULTSSEL 8820 . 9267) (\TEDIT.SETDEFAULT.FROM.SEL 9269 . 9946) (\TEDIT.KEY.FIND 9948 . 
14197) (\TEDIT.KEY.FIND.BACKWARD 14199 . 14504) (\TEDIT.FINDAGAIN.BACKWARD 14506 . 14917) (
\TEDIT.FINDAGAIN 14919 . 15210) (\TEDIT.ITALIC.SEL.OFF 15212 . 15464) (\TEDIT.ITALIC.SEL.ON 15466 . 
15659) (\TEDIT.LARGERSEL 15661 . 15949) (\TEDIT.LCASE.SEL 15951 . 16712) (\TEDIT.SHOWCARETLOOKS 16714
 . 18314) (\TEDIT.SMALLERSEL 18316 . 18607) (\TEDIT.SUBSCRIPTSEL 18609 . 18812) (\TEDIT.SUPERSCRIPTSEL
 18814 . 19018) (\TEDIT.UCASE.SEL 19020 . 19725) (\TEDIT.UNDERLINE.SEL.OFF 19727 . 19925) (
\TEDIT.UNDERLINE.SEL.ON 19927 . 20123) (\TEDIT.STRIKEOUT.SEL.ON 20125 . 20321) (
\TEDIT.STRIKEOUT.SEL.OFF 20323 . 20521) (\TEDIT.SELECT.ALL 20523 . 20746) (\TEDIT.KEY.SUBSTITUTE 20748
 . 20969)) (21043 27452 (\TEDIT.BOLD.CARET.OFF 21053 . 21588) (\TEDIT.BOLD.CARET.ON 21590 . 22122) (
\TEDIT.ITALIC.CARET.OFF 22124 . 22661) (\TEDIT.ITALIC.CARET.ON 22663 . 23206) (\TEDIT.LARGER.CARET 
23208 . 23743) (\TEDIT.SMALLER.CARET 23745 . 24282) (\TEDIT.SUBSCRIPT.CARET 24284 . 24825) (
\TEDIT.SUPERSCRIPT.CARET 24827 . 25369) (\TEDIT.UNDERLINE.CARET.OFF 25371 . 25911) (
\TEDIT.UNDERLINE.CARET.ON 25913 . 26451) (\TEDIT.STRIKEOUT.CARET.OFF 26453 . 26993) (
\TEDIT.STRIKEOUT.CARET.ON 26995 . 27450)) (27521 28223 (\TK.DESCRIBEFONT 27531 . 28221)))))
STOP
