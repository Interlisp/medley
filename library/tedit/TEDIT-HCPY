(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 8-Feb-2025 23:42:18" {WMEDLEY}<library>tedit>TEDIT-HCPY.;169 33719  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.HARDCOPY.DISPLAYLINE)

      :PREVIOUS-DATE " 8-Feb-2025 22:36:24" {WMEDLEY}<library>tedit>TEDIT-HCPY.;168)


(PRETTYCOMPRINT TEDIT-HCPYCOMS)

(RPAQQ TEDIT-HCPYCOMS
       ((COMS 
              (* ;; "Generic interface functions and common code")

              (FNS TEDIT.HARDCOPY \TEDIT.PRINT.MENU TEDIT.HCPYFILE \TEDIT.HARDCOPY.DISPLAYLINE 
                   \TEDIT.HARDCOPY.FORMATLINE.HEADINGS \TEDIT.HARDCOPY.MODIFYLOOKS \TEDIT.HCPYFMTSPEC
                   \TEDIT.INTEGER.IMAGEBOX \TEDIT.DISPLAY.DIACRITIC))
        (COMS 
              (* ;; "Functions for scaling regions as needed during hardcopy.")

              (FNS \TEDIT.SCALEREGION))
        (COMS 
              (* ;; "PRESS-specific code")

              (VARS (TEDIT.DEFAULTPAGEREGION (CREATEREGION 2794 1905 16256 23495)))
                                                             (* ; 
                                                             "0.75 inches from bottom, 1 from top"))
        [COMS 
              (* ;; "Support for the window-menu's HARDCOPY button, LISTFILES, etc.  THIS IS SCREWY")

              (FNS TEDIT.HARDCOPYFN \TEDIT.HARDCOPYFILEFN \TEDIT.POSTSCRIPT.HARDCOPY 
                   \TEDIT.PRESS.HARDCOPY)
              [P (LET [(IPVALUES (ASSOC 'CONVERSION (ASSOC 'INTERPRESS PRINTFILETYPES]
                      (CL:WHEN IPVALUES
                                                             (* ; 
                                          "Only install INTERPRESS printing if INTERPRESS is loaded.")
                          (LISTPUT IPVALUES 'TEDIT (FUNCTION \TEDIT.POSTSCRIPT.HARDCOPY)))]
              (P (LET [(PRESSVALUES (ASSOC 'CONVERSION (ASSOC 'PRESS PRINTFILETYPES]
                      (CL:WHEN PRESSVALUES
                                                             (* ; 
                                                    "Only install PRESS printing if PRESS is loaded.")
                          (LISTPUT PRESSVALUES 'TEDIT (FUNCTION \TEDIT.PRESS.HARDCOPY)))]
        [COMS 
              (* ;; "vars for Japanese Line Break")

              (INITVARS (TEDIT.DONT.BREAK.CHARS (CHARCODE ("41,42" "41,43" "41,53" "41,54" "41,74" 
                                                                 "41,115" "41,133" "41,131" "41,127"
                                                                 "Hira,41" "Hira,43" "Hira,45" 
                                                                 "Hira,47" "Hira,51" "Hira,103" 
                                                                 "Hira,143" "Hira,145" "Hira,147" 
                                                                 "Hira,156" "Kata,41" "Kata,43" 
                                                                 "Kata,45" "Kata,47" "Kata,51" 
                                                                 "Kata,103" "Kata,143" "Kata,145" 
                                                                 "Kata,147" "Kata,156")))
                     (TEDIT.DONT.LAST.CHARS (CHARCODE ("41,114" "41,132" "41,130" "41,126"]
        (COMS 
              (* ;; "Support for hardcopying several files as one document")

              (FNS TEDIT-BOOK))))



(* ;; "Generic interface functions and common code")

(DEFINEQ

(TEDIT.HARDCOPY
  [LAMBDA (STREAM FILE DONTSEND BREAKPAGETITLE SERVER PRINTOPTIONS)
                                                             (* ; "Edited  6-Mar-2024 23:33 by rmk")
                                                             (* ; "Edited  5-Jan-88 16:09 by jds")

    (* ;; "Send the text to a printer, unless DONTSEND.   If DONTSEND and we can't find a server, we'll get the DEFAULTPRINTERTYPE.")

    (CL:UNLESS SERVER (SETQ SERVER DEFAULTPRINTINGHOST))
    (COND
       [(OR SERVER DONTSEND)
        (for IMAGETYPE in (PRINTERPROP (PRINTERTYPE SERVER)
                                 'CANPRINT)
           do (RETURN (TEDIT.FORMAT.HARDCOPY STREAM FILE DONTSEND BREAKPAGETITLE SERVER PRINTOPTIONS
                             IMAGETYPE)) finally (ERROR (CONCAT "Can't print TEDIT documents on a "
                                                               (PRINTERTYPE SERVER)
                                                               " printer."]
       (T (TEDIT.PROMPTPRINT (TEXTOBJ STREAM)
                 "Can't HARDCOPY:  No print server specified." T])

(\TEDIT.PRINT.MENU
  [LAMBDA (TSTREAM)                                          (* ; "Edited 28-Jun-2024 22:09 by rmk")
                                                             (* ; "Edited 25-Jun-2023 13:16 by rmk")
                                                             (* ; "Edited  6-Jun-2023 17:48 by rmk")
    (LET ((W (GETTOBJ (TEXTOBJ TSTREAM)
                    PRIMARYPANE)))
         (SELECTQ [MENU (create MENU
                               ITEMS _ '(("Print to a file" 'FILE 
                                              "Puts image on a file; prompts for filename and format"
                                                )
                                         ("Send to a printer" 'PRINTER 
                                                "Sends image to a printer of your choosing"]
             (FILE (HARDCOPYIMAGEW.TOFILE W))
             (PRINTER (HARDCOPYIMAGEW.TOPRINTER W))
             NIL])

(TEDIT.HCPYFILE
  [LAMBDA (TSTREAM FILE BREAKPAGETITLE)                      (* ; "Edited 29-Jun-2024 16:33 by rmk")
                                                             (* ; "Edited  4-Oct-2022 09:23 by rmk")
                                                             (* ; "Edited  1-Oct-2022 22:12 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:36 by mitani")

    (* ;; "Create a hardcopy-format FILE from the text on TSTREAM, with the file type depending on what the default printer is.")

    (LET ([IMAGETYPE (CAR (PRINTERPROP (PRINTERTYPE)
                                 'CANPRINT]
          (TEXTOBJ (TEXTOBJ TSTREAM))
          FILENM TXTFILE)
         (CL:WHEN [SETQ FILENM (OR FILE (\TEDIT.MAKEFILENAME
                                         (TEDIT.GETINPUT TEXTOBJ (CONCAT IMAGETYPE " file name:  ")
                                                (COND
                                                   ((type? STREAM (SETQ TXTFILE (fetch (TEXTOBJ
                                                                                        TXTFILE)
                                                                                   of TEXTOBJ)))
                                                             (* ; 
                                                             "There was a file, so supply default")
                                                    (PACKFILENAME 'VERSION NIL 'EXTENSION
                                                           (OR (CAR (PRINTFILETYPE IMAGETYPE
                                                                           'EXTENSION))
                                                               'HCPY)
                                                           'BODY
                                                           (fetch (STREAM FULLFILENAME) of TXTFILE]
             (if FILENM
                 then (TEDIT.FORMAT.HARDCOPY TSTREAM FILENM T BREAKPAGETITLE NIL NIL IMAGETYPE)
               else (TEDIT.PROMPTPRINT TSTREAM "No hardcopy file--aborted" T T)))])

(\TEDIT.HARDCOPY.DISPLAYLINE
  [LAMBDA (TEXTOBJ LINE REGION PRSTREAM FORMATTINGSTATE)     (* ; "Edited  8-Feb-2025 23:39 by rmk")
                                                             (* ; "Edited 13-Dec-2024 23:49 by rmk")
                                                             (* ; "Edited 13-Jun-2024 17:13 by rmk")
                                                             (* ; "Edited 19-Apr-2024 09:09 by rmk")
                                                             (* ; "Edited 20-Mar-2024 11:04 by rmk")
                                                             (* ; "Edited 15-Mar-2024 19:23 by rmk")
                                                             (* ; "Edited 24-Dec-2023 22:07 by rmk")
                                                             (* ; "Edited  2-Dec-2023 11:17 by rmk")
                                                             (* ; "Edited 28-Oct-2023 23:52 by rmk")
                                                             (* ; "Edited  6-May-2023 20:03 by rmk")
                                                             (* ; "Edited  7-Mar-2023 23:10 by rmk")
                                                             (* ; "Edited 29-Mar-94 13:44 by jds")

    (* ;; "Display LINE on the HARDCOPY file under way. Original FORM-terminated lines end with EOL.")

    (* ;; "If possible, use the information cached in THISLINE")

    (TEXTOBJ! TEXTOBJ)
    (\DTEST LINE 'LINEDESCRIPTOR)

    (* ;; "Only display the line if it appears before the end of the text!")

    (CL:UNLESS (IGREATERP (FGETLD LINE LCHAR1 LINE)
                      (FGETTOBJ TEXTOBJ TEXTLEN))
        [LET ((THISLINE (FGETTOBJ TEXTOBJ THISLINE)))
             (CL:UNLESS (EQ LINE (fetch DESC of THISLINE))
                 (\TEDIT.FORMATLINE (FGETTOBJ TEXTOBJ STREAMHINT)
                        (FGETLD LINE LCHAR1)
                        LINE REGION PRSTREAM FORMATTINGSTATE))

             (* ;; "Use the characters cached in THISLINE.")

             (for CHARSLOT CLOOKS CURY LOOKSTARTX SCALESPACES (SPACEFACTOR _ (fetch (THISLINE 
                                                                                        TLSPACEFACTOR
                                                                                           )
                                                                                of THISLINE))
                  (FIRST-SCALEDSPACE-SLOT _ (ffetch (THISLINE TLFIRSTSPACE) of THISLINE))
                  (SCALE _ (DSPSCALE NIL PRSTREAM))
                  (TX _ (FGETLD LINE LX1)) incharslots THISLINE first (DSPSPACEFACTOR 1 PRSTREAM)
                                                                      (DSPXPOSITION TX PRSTREAM)
                do 
                   (* ;; 
              "Display the line character by character.  CHAR and CHARW are bound to CHARSLOT values")

                   (SELCHARQ CHAR
                        (SPACE (CL:WHEN (EQ CHARSLOT FIRST-SCALEDSPACE-SLOT)
                                                             (* ; "Time to turn on space scaling.")
                                   (DSPSPACEFACTOR SPACEFACTOR PRSTREAM)
                                   (SETQ SCALESPACES T))
                               (\OUTCHAR PRSTREAM (CHARCODE SPACE))
                               (add TX (CL:IF SCALESPACES
                                           (HCSCALE SPACEFACTOR CHARW)
                                           CHARW)))
                        ((TAB Meta,TAB)                      (* ; 
                                                  "Dotted leaders are meta-TAB, or are DOTTEDLEADER.")
                             (CL:WHEN (OR (EQ CHAR (CHARCODE Meta,TAB))
                                          (fetch CLLEADER of CLOOKS)
                                          (EQ (fetch CLUSERINFO of CLOOKS)
                                              'DOTTEDLEADER))
                                 (LET* [(DOTWIDTH (CHARWIDTH (CHARCODE %.)
                                                         (FONTCOPY (fetch (CHARLOOKS CLFONT)
                                                                      of CLOOKS)
                                                                'DEVICE PRSTREAM)))
                                        (TTX (IPLUS TX DOTWIDTH (IDIFFERENCE DOTWIDTH
                                                                       (IREMAINDER TX DOTWIDTH]
                                       (DSPXPOSITION (IDIFFERENCE TTX DOTWIDTH)
                                              PRSTREAM)      (* ; 
                                              "Move over to the next even multiple of a dot's width.")
                                       (while (ILEQ TTX (IPLUS TX CHARW))
                                          do (\OUTCHAR PRSTREAM (CHARCODE %.))
                                             (add TTX DOTWIDTH))))
                             (add TX CHARW)
                             (DSPXPOSITION TX PRSTREAM))
                        ((EOL LF CR) 
                             NIL)
                        (NIL 
                             (* ;; 
                         "LOOKS. Line-start looks are guaranteed to come before any character/object")

                             (if (type? CHARLOOKS CHARW)
                                 then (CL:WHEN CLOOKS

                                          (* ;; 
                                          "Underline/overline/strike the just-finished looks run")
                                                             (* ; "DISPLAY ALSO PASES LINE DESCENT")
                                          (\TEDIT.HARDCOPY.MODIFYLOOKS LINE LOOKSTARTX TX
                                                 (FGETLD LINE YBASE)
                                                 CLOOKS PRSTREAM))
                                      (SETQ CLOOKS CHARW)
                                      (DSPFONT (fetch CLFONT of CLOOKS)
                                             PRSTREAM)
                                      [SETQ CURY (COND
                                                    [(AND (fetch (CHARLOOKS CLOFFSET) of CLOOKS)
                                                          (NEQ 0 (fetch (CHARLOOKS CLOFFSET)
                                                                    of CLOOKS)))
                                                     (IPLUS (FGETLD LINE YBASE)
                                                            (HCSCALE SCALE (fetch (CHARLOOKS CLOFFSET
                                                                                         )
                                                                              of CLOOKS]
                                                    (T (FGETLD LINE YBASE]
                                      (DSPYPOSITION CURY PRSTREAM) 

                                      (* ;; "LOOKSTARTX: Starting X position for this CLOOKS.")

                                      (SETQ LOOKSTARTX TX)))
                        (PROGN (if (IMAGEOBJP CHAR)
                                   then 
                                        (* ;; "Go to the base line, left edge of the image region.")

                                        (SETQ CURY (DSPYPOSITION NIL PRSTREAM))
                                        (APPLY* (IMAGEOBJPROP CHAR 'DISPLAYFN)
                                               CHAR PRSTREAM (IMAGESTREAMTYPE PRSTREAM)
                                               (ffetch (TEXTOBJ STREAMHINT) of TEXTOBJ))
                                        (DSPFONT (fetch CLFONT of CLOOKS)
                                               PRSTREAM)     (* ; 
                                                 "Restore the font, move to after the object's image")
                                        (MOVETO (IPLUS TX CHARW)
                                               CURY PRSTREAM)
                                 elseif (DIACRITICP CHAR)
                                   then 
                                        (* ;; "Special placement for diacritics")

                                        (SETQ CHARW (\TEDIT.DISPLAY.DIACRITIC CHARSLOT THISLINE 
                                                           PRSTREAM))
                                 elseif (EQ 'KERN CHAR)
                                   then (RELMOVETO 0 CHARW PRSTREAM)
                                 else (\OUTCHAR PRSTREAM CHAR))
                               (add TX CHARW))) finally 

                                                (* ;; "Do any last-minute underlining or similar looks fix-ups, and print a revision mark, if one is needed:")

                                                      (CL:WHEN CLOOKS
                                                          (\TEDIT.HARDCOPY.MODIFYLOOKS LINE 
                                                                 LOOKSTARTX TX (FGETLD LINE YBASE)
                                                                 CLOOKS PRSTREAM))
                                                      (CL:WHEN (GETPARA (FGETLD LINE LPARALOOKS)
                                                                      FMTREVISED)
                                                             (* ; 
                                                       "This paragraph has been revised, so mark it.")
                                                          (\TEDIT.MARK.REVISION TEXTOBJ
                                                                 (FGETLD LINE LPARALOOKS)
                                                                 PRSTREAM LINE))])])

(\TEDIT.HARDCOPY.FORMATLINE.HEADINGS
  [LAMBDA (TEXTOBJ TSTREAM LINE PARALOOKS CHNO IMAGESTREAM FORMATTINGSTATE)
                                                             (* ; "Edited  8-Feb-2025 21:13 by rmk")
                                                             (* ; "Edited 26-Oct-2024 11:04 by rmk")
                                                             (* ; "Edited 17-Mar-2024 17:22 by rmk")
                                                             (* ; "Edited 19-Jan-2024 23:19 by rmk")
                                                             (* ; "Edited  3-Oct-2022 13:05 by rmk")

    (* ;; "Return setup LINE to skip a sequence of heading pieces STATE")

    (SELECTQ (GETPARA PARALOOKS FMTPARATYPE)
        (PAGEHEADING 
                     (* ;; "This paragraph is the content for a page heading. The pieces are stashed away in the FORMATTING STATE.")

                     (\TEDIT.HARDCOPY.PAGEHEADING TEXTOBJ TSTREAM LINE PARALOOKS CHNO IMAGESTREAM 
                            FORMATTINGSTATE)
                     T)
        (EVEN                                                (* ; "Skip an odd page.")
              (CL:WHEN (ODDP (GETPFS FORMATTINGSTATE PAGE#))
                  (\TEDIT.SKIP.SPECIALCOND TSTREAM LINE PARALOOKS CHNO)
                  T))
        (ODD                                                 (* ; "Skip an even page")
             (CL:WHEN (EVENP (GETPFS FORMATTINGSTATE PAGE#))
                 (\TEDIT.SKIP.SPECIALCOND TSTREAM LINE PARALOOKS CHNO)
                 T))
        NIL])

(\TEDIT.HARDCOPY.MODIFYLOOKS
  [LAMBDA (LINE STARTX CURX CURY LOOKS PRSTREAM)             (* ; "Edited 27-May-2023 12:16 by rmk")
                                                             (* ; "Edited 30-May-91 21:17 by jds")

    (* ;; "Do underlining, overlining, etc.  for hardcopy files")

    [PROG ((STREAMSCALE (DSPSCALE NIL PRSTREAM))
           [RULEWIDTH (FIXR (FTIMES 0.75 (DSPSCALE NIL PRSTREAM]
           (ONEPOINT (FIXR (DSPSCALE NIL PRSTREAM)))
           YOFFSET)
          (COND
             ((fetch (CHARLOOKS CLULINE) of LOOKS)           (* ; "It's underlined.")
              (DRAWLINE STARTX (IDIFFERENCE (GETLD LINE YBASE)
                                      (GETLD LINE LTRUEDESCENT LINE))
                     CURX
                     (IDIFFERENCE (GETLD LINE YBASE)
                            (GETLD LINE LTRUEDESCENT LINE))
                     RULEWIDTH
                     'PAINT PRSTREAM)                        (* ; "A 1/2-pt underline")
              ))
          (COND
             ((fetch (CHARLOOKS CLOLINE) of LOOKS)           (* ; "Over-line")
              (DRAWLINE STARTX (IPLUS (GETLD LINE YBASE)
                                      (GETLD LINE LTRUEASCENT LINE))
                     CURX
                     (IPLUS (GETLD LINE YBASE LINE)
                            (GETLD LINE LTRUEASCENT LINE))
                     RULEWIDTH
                     'PAINT PRSTREAM)))
          (COND
             ((fetch (CHARLOOKS CLSTRIKE) of LOOKS)          (* ; "Struch-thru")
              (DRAWLINE STARTX (SETQ YOFFSET (IPLUS (GETLD LINE YBASE LINE)
                                                    (IQUOTIENT
                                                     [FIXR (FTIMES STREAMSCALE
                                                                  (FONTPROP (fetch (CHARLOOKS CLFONT)
                                                                               of LOOKS)
                                                                         'ASCENT]
                                                     3)))
                     CURX YOFFSET RULEWIDTH 'PAINT PRSTREAM]
    (MOVETO CURX CURY PRSTREAM])

(\TEDIT.HCPYFMTSPEC
  [LAMBDA (DISPLAYFMT IMAGESTREAM)                           (* ; "Edited  8-Feb-2025 22:36 by rmk")
                                                             (* ; "Edited 28-Jul-2024 22:25 by rmk")
                                                             (* ; "Edited 15-Mar-2024 19:34 by rmk")
                                                             (* ; "Edited  7-Mar-2023 21:03 by rmk")
                                                             (* ; "Edited  6-Mar-2023 15:14 by rmk")
                                                             (* ; "Edited 20-Oct-2022 22:35 by rmk")
                                                             (* ; "Edited 29-Sep-2022 23:32 by rmk")
                                                             (* ; "Edited 30-May-91 21:18 by jds")

    (* ;; "Given a display-type PARALOOKS, create a hardcopy equivalent.  (Special positions are made paper-relative first.).  ")

    (LET* ((SCALE (DSPSCALE NIL IMAGESTREAM)))
          (create PARALOOKS using DISPLAYFMT FMTHARDCOPYSCALE _ SCALE 1STLEFTMAR _
                                  (HCSCALE SCALE (FGETPARA DISPLAYFMT 1STLEFTMAR))
                                  LEFTMAR _ (HCSCALE SCALE (FGETPARA DISPLAYFMT LEFTMAR))
                                  RIGHTMAR _ (HCSCALE SCALE (FGETPARA DISPLAYFMT RIGHTMAR))
                                  QUAD _ (FGETPARA DISPLAYFMT QUAD DISPLAYFMT)
                                  FMTDEFAULTTAB _ (HCSCALE SCALE (FGETPARA DISPLAYFMT FMTDEFAULTTAB))
                                  FMTTABS _ (\TEDIT.SCALE.TABS (FGETPARA DISPLAYFMT FMTTABS)
                                                   SCALE)
                                  FMTSPECIALX _ (AND (FGETPARA DISPLAYFMT FMTSPECIALX)
                                                     (HCSCALE SCALE (SCALEPAGEUNITS (FGETPARA 
                                                                                           DISPLAYFMT
                                                                                           
                                                                                          FMTSPECIALX
                                                                                           )
                                                                           1.0 NIL)))
                                  FMTSPECIALY _ (AND (FGETPARA DISPLAYFMT FMTSPECIALY)
                                                     (HCSCALE SCALE (SCALEPAGEUNITS (FGETPARA 
                                                                                           DISPLAYFMT
                                                                                           
                                                                                          FMTSPECIALY
                                                                                           )
                                                                           1.0 NIL)))
                                  LEADBEFORE _ (HCSCALE SCALE (FGETPARA DISPLAYFMT LEADBEFORE))
                                  LEADAFTER _ (HCSCALE SCALE (FGETPARA DISPLAYFMT LEADAFTER))
                                  LINELEAD _ (HCSCALE SCALE (FGETPARA DISPLAYFMT LINELEAD))
                                  FMTBASETOBASE _ (AND (FGETPARA DISPLAYFMT FMTBASETOBASE)
                                                       (HCSCALE SCALE (FGETPARA DISPLAYFMT 
                                                                             FMTBASETOBASE])

(\TEDIT.INTEGER.IMAGEBOX
  [LAMBDA (OLDBOX)                                           (* jds "23-Oct-84 13:52")
                                                             (* Take an IMAGEBOX, and assure that 
                                                             its contents are integers)
    (replace XKERN of OLDBOX with (FIXR (fetch XKERN of OLDBOX)))
    (replace YDESC of OLDBOX with (FIXR (fetch YDESC of OLDBOX)))
    (replace YSIZE of OLDBOX with (FIXR (fetch YSIZE of OLDBOX)))
    (replace XSIZE of OLDBOX with (FIXR (fetch XSIZE of OLDBOX)))
    OLDBOX])

(\TEDIT.DISPLAY.DIACRITIC
  [LAMBDA (CHARSLOT THISLINE IMAGESTREAM)                    (* ; "Edited  2-Dec-2023 16:44 by rmk")
                                                             (* ; "Edited 28-Oct-2023 23:51 by rmk")

    (* ;; "Called when CHARSLOT contains a diacritic.  This moves to a position so that the diacritic will be centered over the next character, prints the diacritic, and then moves the stream back to its starting position. ")

    (* ;; "Returns the %"width%" of what was displayed, so the affected character can be positioned wrt the diacritic.  0 unless the diacritic is wider than the character (shift lt 0).  TBD")

    (DSPXPOSITION (PROG1 (DSPXPOSITION (IPLUS (DSPXPOSITION NIL IMAGESTREAM)
                                              (\TEDIT.DIACRITIC.SHIFT CHARSLOT THISLINE IMAGESTREAM))
                                IMAGESTREAM)
                      (\OUTCHAR IMAGESTREAM (CHAR CHARSLOT)))
           IMAGESTREAM)
    0])
)



(* ;; "Functions for scaling regions as needed during hardcopy.")

(DEFINEQ

(\TEDIT.SCALEREGION
  [LAMBDA (SCALEFACTOR REGION)                               (* ; "Edited  8-Mar-2023 18:20 by rmk")
                                                             (* ; "Edited  2-Jan-87 12:13 by jds")

    (* ;; "Scale the region REGION by SCALEFACTOR, rounding all the dimensions to integers.  Used to scale page-boundary regions during hardcopy.")

    (* ;; "SCALEREGION in Interpress uses FIX instead of FIXR.  ")

    (create REGION
           LEFT _ (HCSCALE SCALEFACTOR (fetch (REGION LEFT) of REGION))
           BOTTOM _ (HCSCALE SCALEFACTOR (fetch (REGION BOTTOM) of REGION))
           WIDTH _ (HCSCALE SCALEFACTOR (fetch (REGION WIDTH) of REGION))
           HEIGHT _ (HCSCALE SCALEFACTOR (fetch (REGION HEIGHT) of REGION])
)



(* ;; "PRESS-specific code")


(RPAQ TEDIT.DEFAULTPAGEREGION (CREATEREGION 2794 1905 16256 23495))



(* ; "0.75 inches from bottom, 1 from top")




(* ;; "Support for the window-menu's HARDCOPY button, LISTFILES, etc.  THIS IS SCREWY")

(DEFINEQ

(TEDIT.HARDCOPYFN
  [LAMBDA (WINDOW IMAGESTREAM)                               (* ; "Edited 13-Dec-2024 22:33 by rmk")
                                                             (* ; "Edited 29-Jun-2024 14:42 by rmk")
                                                             (* ; "Edited 20-Mar-2024 10:49 by rmk")
                                                             (* ; "Edited 25-Sep-2023 16:29 by rmk")
                                                             (* ; "Edited  4-Jul-2023 11:16 by rmk")
                                                            (* ; "Edited 21-Sep-2021 15:33 by rmk:")

    (* ;; 
   "This is the TEdit HARDCOPYFN, hooking into the system's standard Hardcopy window-menu operation.")

    (LET ((TEXTSTREAM (TEXTSTREAM WINDOW)))

         (* ;; "TEXTSTREAM is bound here so we don't drop the steam on the floor if the window goes away, since the TEXTOBJ only has an XPOINTER to the stream in it.  Please don't remove this binding!")

         (TEDIT.FORMAT.HARDCOPY (CL:IF (FGETTOBJ (TEXTOBJ WINDOW)
                                              MENUFLG)
                                    (\TEDIT.MAINW WINDOW)
                                    WINDOW)
                IMAGESTREAM])

(\TEDIT.HARDCOPYFILEFN
  [LAMBDA (W EXT)                                            (* ; "Edited 25-Sep-2023 16:19 by rmk")
    (LET [(STRM (OR (GETTOBJ (TEXTOBJ W)
                           TXTFILE)
                    (AND (GETTOBJ (TEXTOBJ W)
                                MENUFLG)
                         (GETTOBJ (TEXTOBJ (\TEDIT.MAINW W))
                                TXTFILE]
         (CL:WHEN STRM
             (PACKFILENAME 'VERSION NIL 'EXTENSION (OR EXT 'IMAGEFILE)
                    'BODY
                    (FULLNAME STRM)))])

(\TEDIT.POSTSCRIPT.HARDCOPY
  [LAMBDA (FILE PFILE)                                       (* ; "Edited  4-Oct-2022 10:40 by rmk")
                                                             (* ; "Edited  1-Oct-2022 22:08 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:35 by mitani")

    (* ;; "Send the document FILE to the printer (or to a print file, as determined by PFILE).")

    (CL:WITH-OPEN-STREAM (TEXT-STREAM (OPENTEXTSTREAM FILE))
           (RESETLST
               [RESETSAVE (\TEDIT.MARKACTIVE (TEXTOBJ TEXT-STREAM))
                      '(AND (\TEDIT.MARKINACTIVE OLDVALUE]
               [RESETSAVE NIL `(AND (CLOSEF? ',PFILE]
               (replace (TEXTOBJ EDITOPACTIVE) of (TEXTOBJ TEXT-STREAM) with 'Hardcopy)
               (TEDIT.FORMAT.HARDCOPY TEXT-STREAM PFILE T NIL NIL NIL 'POSTSCRIPT)
               PFILE)])

(\TEDIT.PRESS.HARDCOPY
  [LAMBDA (FILE PFILE)                                      (* ; "Edited 12-Jun-90 18:36 by mitani")
                                                             (* Send the text to the printer.)
    [SETQ FILE (OPENTEXTSTREAM (COND
                                  ((STRINGP FILE)
                                   (MKATOM FILE))
                                  (T FILE]
    (RESETLST
        [RESETSAVE (\TEDIT.MARKACTIVE (TEXTOBJ FILE))
               '(AND (\TEDIT.MARKINACTIVE OLDVALUE]
        (replace (TEXTOBJ EDITOPACTIVE) of (TEXTOBJ FILE) with 'Hardcopy)
        (TEDIT.FORMAT.HARDCOPY FILE PFILE T NIL NIL NIL 'PRESS)
        (CLOSEF? PFILE)
        PFILE)])
)

[LET [(IPVALUES (ASSOC 'CONVERSION (ASSOC 'INTERPRESS PRINTFILETYPES]
     (CL:WHEN IPVALUES                                       (* ; 
                                          "Only install INTERPRESS printing if INTERPRESS is loaded.")
         (LISTPUT IPVALUES 'TEDIT (FUNCTION \TEDIT.POSTSCRIPT.HARDCOPY)))]

[LET [(PRESSVALUES (ASSOC 'CONVERSION (ASSOC 'PRESS PRINTFILETYPES]
     (CL:WHEN PRESSVALUES                                    (* ; 
                                                    "Only install PRESS printing if PRESS is loaded.")
         (LISTPUT PRESSVALUES 'TEDIT (FUNCTION \TEDIT.PRESS.HARDCOPY)))]



(* ;; "vars for Japanese Line Break")


(RPAQ? TEDIT.DONT.BREAK.CHARS
       (CHARCODE ("41,42" "41,43" "41,53" "41,54" "41,74" "41,115" "41,133" "41,131" "41,127" 
                        "Hira,41" "Hira,43" "Hira,45" "Hira,47" "Hira,51" "Hira,103" "Hira,143" 
                        "Hira,145" "Hira,147" "Hira,156" "Kata,41" "Kata,43" "Kata,45" "Kata,47" 
                        "Kata,51" "Kata,103" "Kata,143" "Kata,145" "Kata,147" "Kata,156")))

(RPAQ? TEDIT.DONT.LAST.CHARS (CHARCODE ("41,114" "41,132" "41,130" "41,126")))



(* ;; "Support for hardcopying several files as one document")

(DEFINEQ

(TEDIT-BOOK
  [LAMBDA (FILES DIRECTORY PRINT-FILE DONT-SEND)             (* ; "Edited 22-Mar-93 23:55 by jds")
    (LET ((DOC (OPENTEXTSTREAM (MKATOM (CAR FILES))
                      NIL)))

         (* ;; "Gather all the files into one document:")

         (for FILE in (CDR FILES) do (TEDIT.SETSEL DOC 1 (fetch (TEXTOBJ TEXTLEN) of (TEXTOBJ DOC))
                                            'RIGHT NIL NIL)
                                     (TEDIT.INCLUDE DOC (PACK* (OR DIRECTORY "")
                                                               FILE)))
                                                             (* ; "Set page layout")
         (TEDIT.FORMAT.HARDCOPY DOC PRINT-FILE DONT-SEND NIL NIL NIL NIL NIL)
         (CLOSEF DOC])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (3475 26928 (TEDIT.HARDCOPY 3485 . 4618) (\TEDIT.PRINT.MENU 4620 . 5586) (TEDIT.HCPYFILE
 5588 . 7762) (\TEDIT.HARDCOPY.DISPLAYLINE 7764 . 17763) (\TEDIT.HARDCOPY.FORMATLINE.HEADINGS 17765 . 
19383) (\TEDIT.HARDCOPY.MODIFYLOOKS 19385 . 21619) (\TEDIT.HCPYFMTSPEC 21621 . 25257) (
\TEDIT.INTEGER.IMAGEBOX 25259 . 25930) (\TEDIT.DISPLAY.DIACRITIC 25932 . 26926)) (27003 27833 (
\TEDIT.SCALEREGION 27013 . 27831)) (28092 31632 (TEDIT.HARDCOPYFN 28102 . 29407) (
\TEDIT.HARDCOPYFILEFN 29409 . 29970) (\TEDIT.POSTSCRIPT.HARDCOPY 29972 . 30903) (\TEDIT.PRESS.HARDCOPY
 30905 . 31630)) (32895 33696 (TEDIT-BOOK 32905 . 33694)))))
STOP
