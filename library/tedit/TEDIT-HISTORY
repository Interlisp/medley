(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "21-Apr-2025 22:42:33" {WMEDLEY}<library>tedit>TEDIT-HISTORY.;250 58952  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.UNDO.DELETE \TEDIT.REDO.INSERT \TEDIT.REDO.REPLACE \TEDIT.UNDO.REPLACE
                       \TEDIT.UNDO.CHARLOOKS \TEDIT.UNDO.PARALOOKS TEDIT.UNDO)

      :PREVIOUS-DATE "20-Apr-2025 23:30:57" {WMEDLEY}<library>tedit>TEDIT-HISTORY.;247)


(PRETTYCOMPRINT TEDIT-HISTORYCOMS)

(RPAQQ TEDIT-HISTORYCOMS
       ((DECLARE%: EVAL@COMPILE DONTCOPY (EXPORT (RECORDS TEDITHISTORYEVENT)
                                                (MACROS \TEDIT.LASTEVENT GETTH SETTH)))
        (FNS \TEDIT.HISTORYEVENT.DEFPRINT)
        (MACROS \TEDIT.HISTORYADD1)
        (INITRECORDS TEDITHISTORYEVENT)
        (GLOBALVARS TEDIT.HISTORY.TYPELST TEDIT.HISTORYLST)
        (INITVARS (TEDIT.HISTORY.TYPELST NIL)
               (TEDIT.HISTORYLST NIL))
        (COMS 
              (* ;; "History-list maintenance functions")

              (FNS \TEDIT.HISTORYADD \TEDIT.HISTORYADD.COMPOSITE \TEDIT.CUMULATE.EVENTS 
                   \TEDIT.COMPOSITE.EVENT \TEDIT.HISTORY.PROP \TEDIT.HISTORY.EVENT \TEDIT.POPEVENT))
        (COMS 
              (* ;; "Specialized UNDO & REDO functions.")

              (FNS TEDIT.UNDO \TEDIT.UNDO1 TEDIT.REDO \TEDIT.UNDO.UNDO)
              (FNS \TEDIT.UNDO.INSERT \TEDIT.UNDO.DELETE \TEDIT.UNDO.MOVE \TEDIT.UNDO.REPLACE 
                   \TEDIT.UNDO.CHARLOOKS \TEDIT.UNDO.PARALOOKS \TEDIT.UNDO.PAGELOOKS 
                   \TEDIT.UNDO.COMPOSITE \TEDIT.UNDO.REPLACECODE \TEDIT.UNDO.WRAP \TEDIT.UNDO.SEL)
              (FNS \TEDIT.REDO.INSERT \TEDIT.REDO.REPLACE \TEDIT.REDO.COMPOSITE))))
(DECLARE%: EVAL@COMPILE DONTCOPY 
(* "FOLLOWING DEFINITIONS EXPORTED")(DECLARE%: EVAL@COMPILE

(DATATYPE TEDITHISTORYEVENT (
                             (* ;; "Describes one event on the TEdit edit history list.")

                             THACTION                        (* ; 
                                                            "A keyword specifying what the event was")
                             THPOINT                         (* ; 
                                                            "Was the selection to the left or right?")
                             THLEN                           (* ; "The # of chars involved")
                             THCH#                           (* ; "The starting ch#")
                             THFIRSTPIECE                    (* ; "First piece involved")
                             THOLDINFO                       (* ; "Old info, for undo")
                             NIL                             (* ; 
                                  "Was THAUXINFO: Auxiliary info about the event, primarily for redo")
                             THDELETEDPIECES)
                            [ACCESSFNS TEDITHISTORYEVENT ((THCHLIM (IPLUS (OR (fetch (
                                                                                    TEDITHISTORYEVENT
                                                                                      THCH#)
                                                                                 of DATUM)
                                                                              0)
                                                                          (OR (fetch (
                                                                                    TEDITHISTORYEVENT
                                                                                      THLEN)
                                                                                 of DATUM)
                                                                              0]
                            (INIT (DEFPRINT 'TEDITHISTORYEVENT (FUNCTION \TEDIT.HISTORYEVENT.DEFPRINT
                                                                )))
                            THPOINT _ 'LEFT)
)

(/DECLAREDATATYPE 'TEDITHISTORYEVENT '(POINTER POINTER POINTER POINTER POINTER POINTER POINTER 
                                             POINTER)
       '((TEDITHISTORYEVENT 0 POINTER)
         (TEDITHISTORYEVENT 2 POINTER)
         (TEDITHISTORYEVENT 4 POINTER)
         (TEDITHISTORYEVENT 6 POINTER)
         (TEDITHISTORYEVENT 8 POINTER)
         (TEDITHISTORYEVENT 10 POINTER)
         (TEDITHISTORYEVENT 12 POINTER)
         (TEDITHISTORYEVENT 14 POINTER))
       '16)

(DEFPRINT 'TEDITHISTORYEVENT (FUNCTION \TEDIT.HISTORYEVENT.DEFPRINT))
(DECLARE%: EVAL@COMPILE 

(PUTPROPS \TEDIT.LASTEVENT MACRO ((TOBJ)
                                  (CAR (fetch (TEXTOBJ TXTHISTORY) of TOBJ))))

(PUTPROPS GETTH MACRO ((EVENT FIELD)
                       (fetch (TEDITHISTORYEVENT FIELD) of EVENT)))

(PUTPROPS SETTH MACRO ((EVENT FIELD NEWVALUE)
                       (replace (TEDITHISTORYEVENT FIELD) of EVENT with NEWVALUE)))
)

(* "END EXPORTED DEFINITIONS")

)
(DEFINEQ

(\TEDIT.HISTORYEVENT.DEFPRINT
  [LAMBDA (EVENT STREAM)                                     (* ; "Edited 24-May-2023 23:36 by rmk")
                                                             (* ; "Edited 22-May-2023 14:42 by rmk")
                                                             (* ; "Edited 21-May-2023 09:15 by rmk")
    (LET (INFO LOC)
         (SETQ INFO (CONCAT (fetch (TEDITHISTORYEVENT THACTION) of EVENT)
                           " "
                           (fetch (TEDITHISTORYEVENT THCH#) of EVENT)
                           "-"
                           (fetch (TEDITHISTORYEVENT THLEN) of EVENT)
                           "-"
                           (NTHCHAR (fetch (TEDITHISTORYEVENT THPOINT) of EVENT)
                                  1)))
         (SETQ LOC (LOC EVENT))
         (CONS (CONCAT "{TH" ":" INFO " " (CAR LOC)
                      "/"
                      (CDR LOC)
                      "}"])
)
(DECLARE%: EVAL@COMPILE 

(PUTPROPS \TEDIT.HISTORYADD1 MACRO ((TEXTOBJ EVENT)

                                    (* ;; "This is the primitive, to be upgraded if we go to a ring.")

                                    (push (FGETTOBJ TEXTOBJ TXTHISTORY)
                                          EVENT)))
)

(/DECLAREDATATYPE 'TEDITHISTORYEVENT '(POINTER POINTER POINTER POINTER POINTER POINTER POINTER 
                                             POINTER)
       '((TEDITHISTORYEVENT 0 POINTER)
         (TEDITHISTORYEVENT 2 POINTER)
         (TEDITHISTORYEVENT 4 POINTER)
         (TEDITHISTORYEVENT 6 POINTER)
         (TEDITHISTORYEVENT 8 POINTER)
         (TEDITHISTORYEVENT 10 POINTER)
         (TEDITHISTORYEVENT 12 POINTER)
         (TEDITHISTORYEVENT 14 POINTER))
       '16)

(DEFPRINT 'TEDITHISTORYEVENT (FUNCTION \TEDIT.HISTORYEVENT.DEFPRINT))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDIT.HISTORY.TYPELST TEDIT.HISTORYLST)
)

(RPAQ? TEDIT.HISTORY.TYPELST NIL)

(RPAQ? TEDIT.HISTORYLST NIL)



(* ;; "History-list maintenance functions")

(DEFINEQ

(\TEDIT.HISTORYADD
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited  6-Apr-2025 11:22 by rmk")
                                                             (* ; "Edited  8-Dec-2024 17:32 by rmk")
                                                             (* ; "Edited 29-Aug-2024 12:30 by rmk")
                                                             (* ; "Edited 11-Aug-2024 21:57 by rmk")
                                                             (* ; "Edited 30-Apr-2024 22:51 by rmk")
                                                             (* ; "Edited  3-Mar-2024 12:15 by rmk")
                                                             (* ; "Edited 19-Feb-2024 12:09 by rmk")
                                                             (* ; "Edited 30-Dec-2023 22:19 by rmk")
                                                             (* ; "Edited 11-Aug-2023 14:25 by rmk")
                                                             (* ; "Edited 14-Jun-2023 16:04 by rmk")
                                                             (* ; "Edited 12-Jun-2023 10:26 by rmk")
                                                             (* ; "Edited  3-Jun-2023 20:41 by rmk")
                                                             (* ; "Edited 28-May-2023 00:07 by rmk")
                                                             (* ; "Edited  3-Sep-87 10:36 by jds")

    (* ;; "Add a new event to the history list, unless the list is currently DON'T (as in middle of foreign get).")

    (* ;; "Not sure what should happen if the second one is to the right of the first, deleting forwards.  Old code seemed to treat those as separate events, and  only the second/right one could be undone.")

    [LET [(TEXTOBJ (FTEXTOBJ TSTREAM (type? TEXTOBJ TSTREAM]
         (if (GETTOBJ TEXTOBJ TXTHISTORYINACTIVE)
             then 
                  (* ;; "Maybe the first event after setting the textprop--now's the time to flush")

                  (FSETTOBJ TEXTOBJ TXTHISTORY NIL)
                  (FSETTOBJ TEXTOBJ TXTHISTORYUNDONE NIL)
           else (if (type? TEDITHISTORYEVENT EVENT)
                    then (CL:WHEN (MEMB (GETTH EVENT THACTION)
                                        (CONSTANT (LIST :Put :Get)))
                                                             (* ; 
                                                     "Can't back up over Put/Get, flush the history.")
                             (FSETTOBJ TEXTOBJ TXTHISTORY NIL)) 

                         (* ;; "Somebody may have already done there own fixup.")

                         (LET ((OLDEVENT (\TEDIT.LASTEVENT TEXTOBJ)))
                              (CL:WHEN (AND (type? TEDITHISTORYEVENT OLDEVENT)
                                            (EQ :Delete (GETTH EVENT THACTION))
                                            (EQ :Delete (GETTH OLDEVENT THACTION)))

                                  (* ;; 
                           "Repeated successive deletions, we can combine them if they are adjacent.")

                                  (CL:WHEN (IEQP (GETTH EVENT THCHLIM)
                                                 (GETTH OLDEVENT THCH#))
                                                             (* ; 
                                                           "OLDEVENT is first, EVENT is still delete")
                                      (SETQ EVENT (\TEDIT.CUMULATE.EVENTS EVENT OLDEVENT TEXTOBJ))
                                      (\TEDIT.POPEVENT TEXTOBJ)
                                                             (* ; "Pop OLDEVENT before repushing")
                                      (SETQ OLDEVENT (\TEDIT.LASTEVENT TEXTOBJ)))

                                  (* ;; "This may have created a new adjacency, if the accumulation of later deletes comes into with an earlier accumulation")

                                  (CL:WHEN [AND OLDEVENT (type? TEDITHISTORYEVENT OLDEVENT)
                                                (EQ :Delete (GETTH OLDEVENT THACTION))
                                                (IEQP (GETTH OLDEVENT THCHLIM)
                                                      (IPLUS (GETTH EVENT THCH#)
                                                             (GETTH OLDEVENT THLEN]

                                 (* ;; "The OLDEEVENT deleted in front of EVENT, and itsTCHLIM are in its original coordinates.  EVENT came later, with its TCH# in a coordinate system reduced by THLEN.  So we have to add it back.")

                                      (SETQ EVENT (\TEDIT.CUMULATE.EVENTS OLDEVENT EVENT))
                                      (\TEDIT.POPEVENT TEXTOBJ)))
                              (\TEDIT.HISTORYADD1 TEXTOBJ EVENT))
                  elseif (LISTP EVENT)
                    then 
                         (* ;; "A monolithic sequence of undoable events")

                         (* ;; "SHOULDNT HAPPEN ?")

                         (\TEDIT.HISTORYADD1 TEXTOBJ EVENT]
    EVENT])

(\TEDIT.HISTORYADD.COMPOSITE
  [LAMBDA (TEXTOBJ EVENTS ACTION EXTRA)                      (* ; "Edited  1-Apr-2025 17:50 by rmk")
                                                             (* ; "Edited  6-Feb-2025 15:31 by rmk")
                                                             (* ; "Edited  8-Dec-2024 19:31 by rmk")
                                                             (* ; "Edited 22-Sep-2024 18:47 by rmk")
                                                             (* ; "Edited  3-Jul-2024 08:02 by rmk")
                                                             (* ; "Edited  8-May-2024 12:34 by rmk")
    (SETQ EVENTS (REMOVE NIL EVENTS))
    (CL:WHEN EVENTS
        (\TEDIT.HISTORYADD TEXTOBJ (CL:IF (CDR EVENTS)
                                       (\TEDIT.HISTORY.EVENT TEXTOBJ (OR ACTION :Composite)
                                              NIL NIL NIL NIL EVENTS EXTRA)
                                       (CAR EVENTS))))])

(\TEDIT.CUMULATE.EVENTS
  [LAMBDA (EVENT1 EVENT2 TEXTOBJ)                            (* ; "Edited  8-Dec-2024 17:35 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:54 by rmk")
                                                             (* ; "Edited  3-Mar-2024 12:15 by rmk")
                                                             (* ; "Edited  3-Jun-2023 17:09 by rmk")
                                                             (* ; "Edited 27-May-2023 00:54 by rmk")
                                                             (* ; "Edited 25-May-2023 23:58 by rmk")
                                                             (* ; "Edited 21-May-2023 13:14 by rmk")
                                                             (* ; "Edited 17-May-2023 14:55 by rmk")
                                                             (* ; "Edited  3-Sep-87 10:42 by jds")

    (* ;; "Accumulate history events that should be combined into a undoable single even.")

    (* ;; "For now, this assumes they're events of the same type.  Actually, this should be able to cumulate a delete/insert pair into a replacement, etc.")

    (SETTH EVENT1 THDELETEDPIECES (\TEDIT.SELPIECES.CONCAT (GETTH EVENT1 THDELETEDPIECES)
                                         (GETTH EVENT2 THDELETEDPIECES)
                                         TEXTOBJ))
    (SETTH EVENT1 THLEN (GETSPC (GETTH EVENT1 THDELETEDPIECES)
                               SPLEN))
    EVENT1])

(\TEDIT.COMPOSITE.EVENT
  [LAMBDA (TEXTOBJ EVENTS)                                   (* ; "Edited  8-Dec-2024 15:47 by rmk")
                                                             (* ; "Edited 22-Sep-2024 18:47 by rmk")
                                                             (* ; "Edited  3-Jul-2024 08:02 by rmk")
                                                             (* ; "Edited  8-May-2024 12:34 by rmk")
    (CL:WHEN EVENTS
        (\TEDIT.HISTORYADD (CL:IF (CDR EVENTS)
                               (\TEDIT.HISTORY.EVENT TEXTOBJ (OR ACTION :Composite)
                                      NIL NIL NIL NIL NEWEVENTS)
                               (CAR EVENTS))))])

(\TEDIT.HISTORY.PROP
  [LAMBDA (TEXTOBJ SETNEWVALUE NEWVALUE)                     (* ; "Edited 22-Sep-2024 08:42 by rmk")

    (* ;; "Called fromTEDIT.TEXT.PROP to manage the history list.  History is ON by default, and the events always correspond to the current state of the document.  If it's OFF, the next document-changing event will cause HISTORYADD to flush the past and no further events will be recorded until it is turned ON again to start a new epoch.  CLEAR flushes old events but then turns on collection.")

    (PROG1 (CL:IF (FGETTOBJ TEXTOBJ TXTHISTORYINACTIVE)
               'OFF
               'ON)
        (CL:WHEN SETNEWVALUE
            (SELECTQ NEWVALUE
                ((ON T) 
                     (FSETTOBJ TEXTOBJ TXTHISTORYINACTIVE NIL))
                ((OFF NIL) 
                           (* ;; 
   "HISTORYADD will wipe out everything the next time it is called event--gives a chance to back out")

                     (FSETTOBJ TEXTOBJ TXTHISTORYINACTIVE T))
                (CLEAR                                       (* ; 
                                             "Wipes out current history now, then resumes collection")
                       (FSETTOBJ TEXTOBJ TXTHISTORY NIL)
                       (FSETTOBJ TEXTOBJ TXTHISTORYINACTIVE NIL))
                (\ILLEGAL.ARG NEWVALUE))))])

(\TEDIT.HISTORY.EVENT
  [LAMBDA (TSTREAM ACTION CH# LEN POINT FIRSTPIECE OLDINFO DELETEDPIECES)
                                                             (* ; "Edited  6-Apr-2025 11:20 by rmk")
                                                             (* ; "Edited 26-Sep-2024 15:44 by rmk")
                                                             (* ; "Edited 23-Sep-2024 16:47 by rmk")

    (* ;; "Don't create if it's inactive")

    (CL:UNLESS (GETTOBJ (FTEXTOBJ TSTREAM)
                      TXTHISTORYINACTIVE)
        (CL:WHEN (AND (NULL LEN)
                      (type? SELPIECES CH#))
            (SETQ LEN (fetch (SELPIECES SPLEN) of CH#))
            (SETQ CH# (fetch (SELPIECES SPFIRSTCHAR) of CH#)))
        (create TEDITHISTORYEVENT
               THACTION _ ACTION
               THCH# _ CH#
               THLEN _ LEN
               THPOINT _ (OR POINT 'LEFT)
               THFIRSTPIECE _ FIRSTPIECE
               THOLDINFO _ OLDINFO
               THDELETEDPIECES _ DELETEDPIECES))])

(\TEDIT.POPEVENT
  [LAMBDA (TEXTOBJ)                                          (* ; "Edited  7-Dec-2024 21:24 by rmk")
    (pop (GETTOBJ TEXTOBJ TXTHISTORY])
)



(* ;; "Specialized UNDO & REDO functions.")

(DEFINEQ

(TEDIT.UNDO
  [LAMBDA (TSTREAM NOUNDOUNDO)                               (* ; "Edited 21-Apr-2025 20:16 by rmk")
                                                             (* ; "Edited  6-Apr-2025 14:42 by rmk")
                                                             (* ; "Edited  5-Apr-2025 13:49 by rmk")
                                                             (* ; "Edited 13-Mar-2025 15:47 by rmk")
                                                             (* ; "Edited  8-Dec-2024 19:41 by rmk")
                                                             (* ; "Edited 25-Nov-2024 13:17 by rmk")
                                                             (* ; "Edited 12-Aug-2024 10:49 by rmk")
                                                             (* ; "Edited  3-Jul-2024 21:21 by rmk")
                                                             (* ; "Edited 18-May-2024 16:23 by rmk")
                                                             (* ; "Edited 12-May-2024 21:08 by rmk")
                                                             (* ; "Edited 20-Mar-2024 11:04 by rmk")
                                                             (* ; "Edited  8-May-2024 11:16 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:36 by rmk")
                                                             (* ; "Edited  7-Mar-2024 12:48 by rmk")
                                                             (* ; "Edited  3-Mar-2024 20:02 by rmk")
                                                             (* ; "Edited 22-Nov-2023 18:17 by rmk")
                                                             (* ; "Edited 27-Sep-2023 00:14 by rmk")
                                                             (* ; "Edited 23-Jun-2023 00:19 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:41 by mitani")

    (* ;; "Undo the last thing this guy did.  This could be a sequence of subevents for a single user-level action that has more than one component:  e.g. move or replace is (Insert Delete). Undoing  each (sub)event must restore the status quo ante (pieces, lines, looks, SEL).  ")

    (* ;; "We push information for undoing the undo onto the TXTHISTORYUNDO list.")

    (SETQ TSTREAM (TEXTSTREAM TSTREAM))
    (PROG* ((TEXTOBJ (GETTSTR TSTREAM TEXTOBJ))
            (SEL (TEXTSEL TEXTOBJ))
            EVENT PREVEVENT UNDOEVENT)
           (CL:WHEN (FGETTOBJ TEXTOBJ TXTREADONLY)
                  (RETURN))
           (SETQ EVENT (\TEDIT.LASTEVENT TEXTOBJ))
           (CL:UNLESS EVENT
               (TEDIT.PROMPTPRINT TEXTOBJ "Nothing to undo" T)
               (RETURN))
           (CL:WHEN (MEMB (GETTH EVENT THACTION)
                          '(:Get :Put))
               (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "You can't undo a " (GETTH EVENT THACTION))
                      T)
               (RETURN))
           (TEDIT.PROMPTCLEAR TEXTOBJ)
           (SETQ EVENT (\TEDIT.POPEVENT TEXTOBJ))
           (SETQ PREVEVENT (\TEDIT.LASTEVENT TEXTOBJ))       (* ; 
                                                             "So we can test for the undoundo event.")
           (CL:UNLESS EVENT
               (TEDIT.PROMPTPRINT TSTREAM "Nothing to undo" T)
               (RETURN))

     (* ;; "Each main event was popped.  Each subfunction must put back on the history-undo  list one or more new events that would undo its undoing.  ")

     (* ;; "We can get into trouble if there is an interrupt in the middle of undoing the full set of events for a previous action, or even in the middle of a singleton event.")

           (\TEDIT.RESET.EXTEND.PENDING.DELETE TEXTOBJ)
           (TEDIT.PROMPTCLEAR TSTREAM)
           (\TEDIT.NOSEL TSTREAM)
           (\TEDIT.UNDO1 TSTREAM EVENT)

     (* ;; "Get the event that undid EVENT--if it was pushed in front of PREVENT ")

           (CL:UNLESS (EQ PREVEVENT (\TEDIT.LASTEVENT TEXTOBJ))
               (SETQ UNDOEVENT (\TEDIT.POPEVENT TEXTOBJ)))
           (CL:WHEN [OR (NULL PREVEVENT)
                        (MEMB (GETTH PREVEVENT THACTION)
                              (CONSTANT (LIST :Get :Put]
                  (FSETTOBJ TEXTOBJ \DIRTY NIL))
           (CL:UNLESS NOUNDOUNDO

               (* ;; "The undone list keeps the event that would undo the undoing, the event that was just undone, and the history event that would be undone next (by M-u).  This is so that M-U can undo the undoing by redoing the original event.")

               (push (FGETTOBJ TEXTOBJ TXTHISTORYUNDONE)
                     (LIST PREVEVENT UNDOEVENT EVENT)))
           (\TEDIT.SHOWSEL SEL T TSTREAM])

(\TEDIT.UNDO1
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited  6-Apr-2025 14:42 by rmk")
                                                             (* ; "Edited  1-Apr-2025 21:22 by rmk")
                                                             (* ; "Edited 28-Mar-2025 14:22 by rmk")
                                                             (* ; "Edited 16-Mar-2025 18:46 by rmk")
                                                             (* ; "Edited 25-Nov-2024 13:56 by rmk")
                                                             (* ; "Edited 29-Sep-2024 13:51 by rmk")
                                                             (* ; "Edited 22-Sep-2024 21:41 by rmk")
                                                             (* ; "Edited 19-Aug-2024 00:11 by rmk")
                                                             (* ; "Edited 12-Aug-2024 23:42 by rmk")
                                                             (* ; "Edited  7-May-2024 23:10 by rmk")
                                                             (* ; "Edited  4-Mar-2024 14:55 by rmk")
                                                             (* ; "Edited 16-Jul-2023 11:14 by rmk")
                                                             (* ; "Edited 30-May-2023 23:50 by rmk")
                                                             (* ; "Edited 25-May-2023 00:33 by rmk")
    (CL:WHEN (GETTH EVENT THCH#)
        (\TEDIT.NOSEL TSTREAM)
        (\TEDIT.UPDATE.SEL TSTREAM EVENT)
        (\TEDIT.SCROLL.CARET TSTREAM))
    (PROG1 (SELECTC (GETTH EVENT THACTION)
               ((LIST :Insert :Copy) 
                    (\TEDIT.UNDO.INSERT TSTREAM EVENT))
               (:Move (\TEDIT.UNDO.MOVE TSTREAM EVENT))
               (:Delete                                      (* ; "Deletion or case-shift")
                        (\TEDIT.UNDO.DELETE TSTREAM EVENT))
               (:CharLooks                                   (* ; "Character-looks change")
                           (\TEDIT.UNDO.CHARLOOKS TSTREAM EVENT))
               (:ParaLooks                                   (* ; "PARA looks change")
                           (\TEDIT.UNDO.PARALOOKS TSTREAM EVENT))
               (:PageFormat                                  (* ; "Pageframe change")
                            (\TEDIT.UNDO.PAGELOOKS TSTREAM EVENT))
               ((LIST :Replace :Transform) 
                                           (* ;; "He replaced one portion of text with another ; Transforms have the same undo event but different REDO's.")

                    (\TEDIT.UNDO.REPLACE TSTREAM EVENT (GETTH EVENT THACTION)))
               (:ReplaceCode (\TEDIT.UNDO.REPLACECODE TSTREAM EVENT))
               (:Closefile                                   (* ; "Closes an included file")
                           (CL:WHEN (STREAMP (GETTH EVENT THOLDINFO))
                               (CLOSEF? (GETTH EVENT THOLDINFO))))
               (:Composite (\TEDIT.UNDO.COMPOSITE TSTREAM EVENT))
               (:Wrap (\TEDIT.UNDO.WRAP TSTREAM EVENT))
               (:Sel (\TEDIT.UNDO.SEL TSTREAM EVENT))
               ((LIST :Get :Put)                             (* ; 
                                                             "He did a GET or PUT-- not undoable.")
                    (TEDIT.PROMPTPRINT TSTREAM (CONCAT "You can't undo a " (GETTH EVENT THACTION))
                           T))
               (LET [(UNDOFN (CADDR (ASSOC (GETTH EVENT THACTION)
                                           TEDIT.HISTORY.TYPELST]
                    (COND
                       (UNDOFN 

                              (* ;; "ùTEDIT.HISTORY.TYPELST is an ALST of form (type redofn undofn)")

                              (APPLY* UNDOFN TSTREAM EVENT (GETTH EVENT THLEN)
                                     (GETTH EVENT THCH#)
                                     (GETTH EVENT THFIRSTPIECE)))
                       (T (TEDIT.PROMPTPRINT TSTREAM (CONCAT "UNDO not implemented for "
                                                            (GETTH EVENT THACTION))
                                 T])

(TEDIT.REDO
  [LAMBDA (TSTREAM)                                          (* ; "Edited  6-Apr-2025 14:43 by rmk")
                                                             (* ; "Edited  1-Apr-2025 21:42 by rmk")
                                                             (* ; "Edited 16-Mar-2025 18:48 by rmk")
                                                             (* ; "Edited  2-Feb-2025 11:28 by rmk")
                                                             (* ; "Edited  8-Dec-2024 17:53 by rmk")
                                                             (* ; "Edited 27-Nov-2024 23:11 by rmk")
                                                             (* ; "Edited 26-Sep-2024 16:49 by rmk")
                                                             (* ; "Edited 29-Jul-2024 23:58 by rmk")
                                                             (* ; "Edited  3-Jul-2024 07:41 by rmk")
                                                             (* ; "Edited 18-May-2024 16:23 by rmk")
                                                             (* ; "Edited 12-May-2024 21:08 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:36 by rmk")
                                                             (* ; "Edited  7-May-2024 23:13 by rmk")
                                                             (* ; "Edited  4-Mar-2024 21:33 by rmk")
                                                             (* ; "Edited  2-Mar-2024 09:41 by rmk")
                                                             (* ; "Edited 21-Dec-2023 11:57 by rmk")
                                                             (* ; "Edited 27-May-2023 11:19 by rmk")
                                                             (* ; "Edited 30-May-91 21:27 by jds")

    (* ;; "REDO the last thing this guy did.")

    (SETQ TSTREAM (TEXTSTREAM TSTREAM))
    (PROG* ((TEXTOBJ (FTEXTOBJ TSTREAM))
            (SEL (FGETTOBJ TEXTOBJ SEL))
            (EVENT (\TEDIT.LASTEVENT TEXTOBJ))
            CH)
           (CL:WHEN (\TEDIT.READONLY TSTREAM)
                  (RETURN NIL))
           (CL:UNLESS EVENT
               (TEDIT.PROMPTPRINT TSTREAM "Nothing to redo" T)
               (RETURN))
           (CL:UNLESS (GETSEL SEL SET)
               (TEDIT.PROMPTPRINT TSTREAM "Please select a target for the repeated action" T)
               (RETURN))

     (* ;; "There really is something to redo and something to do it to.")

           (\TEDIT.NOSEL TSTREAM)
           (SELECTC (GETTH EVENT THACTION)
               ((LIST :Insert :Copy :Move)                   (* ; "It was an insertion")
                    (\TEDIT.REDO.INSERT TSTREAM EVENT SEL))
               (:Delete                                      (* ; "It was a deletion")
                        (\TEDIT.DELETE TSTREAM SEL))
               (:Replace                                     (* ; 
                                                          "It was a replacement (a del/insert combo)")
                         (\TEDIT.REDO.REPLACE TSTREAM EVENT (GETTH EVENT THACTION)))
               (:Transform (\TEDIT.KEY.TRANSFORM TSTREAM (GETTH EVENT THOLDINFO)))
               (:LowerCase                                   (* ; "He lower-cased something")
                           (\TEDIT.KEY.TRANSFORM TSTREAM (FUNCTION L-CASECODE)))
               (:UpperCase                                   (* ; "He upper-cased something")
                           (\TEDIT.KEY.TRANSFORM TSTREAM (FUNCTION U-CASECODE)))
               (:InitialCap (\TEDIT.KEY.TRANSFORM TSTREAM (FUNCTION CAP-CASECODE)))
               (:CharLooks                                   (* ; "It was a character looks change")
                           (\TEDIT.CHANGE.CHARLOOKS TSTREAM (CAR (GETTH EVENT THOLDINFO))
                                  SEL))
               (:ParaLooks                                   (* ; "It was a Paragraph looks change")
                           (\TEDIT.CHANGE.PARALOOKS TSTREAM (CAR (GETTH EVENT THOLDINFO))
                                  SEL))
               (:PageFormat (TEDIT.PROMPTPRINT TSTREAM "You can't redo a page-format change" T T))
               (:Find                                        (* ; "EXACT-MATCH SEARCH COMMAND")
                                                             (* (* ;; "RESTLST ?")
                                                             (AND NIL (RESETSAVE (CURSOR 
                                                             WAITINGCURSOR))) (TEDIT.PROMPTPRINT 
                                                             TSTREAM "Searching..." T)
                                                             (SETQ SEL (TEXTSEL TEXTOBJ))
                                                             (\TEDIT.NOSEL TSTREAM)
                                                             (SETQ CH (TEDIT.FIND TEXTOBJ
                                                             (GETTH EVENT THAUXINFO)))
                                                             (if CH then (\TEDIT.UPDATE.SEL TSTREAM 
                                                             CH (NCHARS (GETTH EVENT THAUXINFO))
                                                             (QUOTE RIGHT)) (TEDIT.NORMALIZECARET 
                                                             TSTREAM) (TEDIT.PROMPTPRINT TSTREAM 
                                                             "done.") else (TEDIT.PROMPTPRINT 
                                                             TSTREAM "[Not found]")))
                      )
               (:Move 
                      (* ;; "It doesn't make sense to do the deletion part of a move in the same place or a different place.  The insert part is probably OK--that maps to the :Insert clause above.")

                      (TEDIT.PROMPTPRINT TSTREAM (CONCAT "You can't redo a " (GETTH EVENT THACTION))
                             T T))
               (:Composite (\TEDIT.REDO.COMPOSITE TSTREAM EVENT SEL))
               (:Wrap (\TEDIT.KEY.WRAP TSTREAM (CAR (GETTH EVENT THDELETEDPIECES))
                             (CADR (GETTH EVENT THDELETEDPIECES))))
               ((LIST :Get :Put NIL)                         (* ; "Why can't you redo a get or put ?")
                    (TEDIT.PROMPTPRINT TSTREAM (CONCAT "You can't redo a " (GETTH EVENT THACTION))
                           T T))
               (TEDIT.PROMPTPRINT TSTREAM (CONCAT "Redoing the action " (GETTH EVENT THACTION)
                                                 " isn't implemented.")
                      T))
           (\TEDIT.SHOWSEL SEL T TSTREAM])

(\TEDIT.UNDO.UNDO
  [LAMBDA (TSTREAM)                                          (* ; "Edited  8-Dec-2024 18:24 by rmk")
                                                             (* ; "Edited 26-Sep-2024 22:57 by rmk")
                                                             (* ; "Edited 22-Sep-2024 11:08 by rmk")
                                                             (* ; "Edited 12-Aug-2024 23:45 by rmk")
                                                             (* ; "Edited  3-Jul-2024 09:50 by rmk")
                                                             (* ; "Edited  3-Mar-2024 21:27 by rmk")
                                                             (* ; "Edited 13-Jun-2023 15:05 by rmk")
                                                             (* ; "Edited  3-Jun-2023 23:04 by rmk")
                                                             (* ; "Edited  1-Jun-2023 23:53 by rmk")

    (* ;; 
  "This undoes a preceding undo, by pushing the undoing event on the history list, and undoing that.")

    (* ;; "The state is recorded as the event that would be undone next")

    (* ;; "This makes sense only if the document is now in the state immediately after the undoing--if any other events have intervened, the character positions and the general state of the document are unrelated. So the elements of the undo list also contain the state of the (forward) history list after the undoing was undone.  If we have moved back to the same point in history, we can do the undoing.")

    (SETQ TSTREAM (TEXTSTREAM TSTREAM))
    (LET* [(TEXTOBJ (GETTSTR TSTREAM TEXTOBJ))
           (LASTUNDONE (pop (FGETTOBJ TEXTOBJ TXTHISTORYUNDONE]
          (TEDIT.PROMPTCLEAR TSTREAM)
          (if (NULL LASTUNDONE)
              then (TEDIT.PROMPTPRINT TSTREAM "There is no action whose undoing can be reversed")
            elseif (EQ (CAR LASTUNDONE)
                       (\TEDIT.LASTEVENT TEXTOBJ))
              then 
                   (* ;; "We tell TEDIT.UNDO that LASTUNDONE is the one we now want to undo.")

                   (\TEDIT.HISTORYADD1 TEXTOBJ (CADR LASTUNDONE))
                   (TEDIT.UNDO TSTREAM)
                   (TEDIT.PROMPTPRINT TSTREAM "Undo undone" T) 

                   (* ;; "This undoing saved what we just undid, don't want to keep reundoing it.")

                   (pop (FGETTOBJ TEXTOBJ TXTHISTORYUNDONE))
                   (\TEDIT.HISTORYADD1 TEXTOBJ (CADDR LASTUNDONE))
            else (SETTOBJ TEXTOBJ TXTHISTORYUNDONE NIL)      (* ; 
                                        "If something else has happened, there are no undos to undo.")
                 (TEDIT.PROMPTPRINT TSTREAM "Cannot undo the previous undo" T])
)
(DEFINEQ

(\TEDIT.UNDO.INSERT
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited  6-Apr-2025 12:15 by rmk")
                                                             (* ; "Edited  8-Jul-2024 00:07 by rmk")
                                                             (* ; "Edited 30-May-2023 22:54 by rmk")
                                                             (* ; "Edited 26-May-2023 23:49 by rmk")
                                                             (* ; "Edited 24-May-2023 23:53 by rmk")
                                                             (* ; "Edited  2-May-2023 23:26 by rmk")
                                                             (* ; "Edited 21-Apr-93 01:33 by jds")

    (* ;; "UNDO a prior Insert, Copy, or Include. ")

    (* ;; "If it is OK to show, we don't need the FIX or the TEXTSEL--use the stream")

    (\TEDIT.DELETE TSTREAM (\TEDIT.FIXSEL (\TEDIT.UPDATE.SEL (TEXTSEL (FTEXTOBJ TSTREAM))
                                                 EVENT)
                                  TSTREAM])

(\TEDIT.UNDO.DELETE
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited 21-Apr-2025 22:22 by rmk")
                                                             (* ; "Edited  6-Apr-2025 11:49 by rmk")
                                                             (* ; "Edited 29-Sep-2024 00:23 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:54 by rmk")
                                                             (* ; "Edited 30-May-2023 23:31 by rmk")
                                                             (* ; "Edited 27-May-2023 23:39 by rmk")
                                                             (* ; "Edited 21-Apr-93 12:01 by jds")

    (* ;; "UNDO a prior deletion ")

    (\TEDIT.INSERT.SELPIECES (\TEDIT.SELPIECES.COPY (GETTH EVENT THDELETEDPIECES)
                                    'INSERT TSTREAM)
           TSTREAM
           (GETTH EVENT THCH#])

(\TEDIT.UNDO.MOVE
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited  6-Apr-2025 11:51 by rmk")
                                                             (* ; "Edited  8-Dec-2024 19:38 by rmk")
                                                             (* ; "Edited 25-Nov-2024 14:12 by rmk")
                                                             (* ; "Edited 29-Sep-2024 00:23 by rmk")
                                                             (* ; "Edited  7-Jul-2024 11:50 by rmk")
                                                             (* ; "Edited  3-Jul-2024 10:17 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:54 by rmk")
                                                             (* ; "Edited  4-Mar-2024 16:08 by rmk")

    (* ;; "This event includes a deletion and an insert/replace both within TEXTOBJ. (The deletion from a from a foreign textobj is in that document's history.)")

    (LET* [(TEXTOBJ (FTEXTOBJ TSTREAM))
           (SEL (TEXTSEL TEXTOBJ))
           (REPLACE (EQ :Replace (GETTH (CAR (GETTH EVENT THOLDINFO))
                                        THACTION]
          (\TEDIT.UNDO.COMPOSITE TSTREAM EVENT)
          (\TEDIT.UPDATE.SEL SEL EVENT NIL NIL (if REPLACE
                                                   then (FSETTOBJ TEXTOBJ BLUEPENDINGDELETE T)
                                                        'PENDINGDEL
                                                 else 'NORMAL))
          (\TEDIT.SHOWSEL SEL T TSTREAM])

(\TEDIT.UNDO.REPLACE
  [LAMBDA (TSTREAM EVENT ACTION)                             (* ; "Edited 21-Apr-2025 22:22 by rmk")
                                                             (* ; "Edited  6-Apr-2025 11:58 by rmk")
                                                             (* ; "Edited 15-Mar-2025 22:35 by rmk")
                                                             (* ; "Edited 13-Sep-2024 23:50 by rmk")
                                                             (* ; "Edited  7-Jul-2024 11:59 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:54 by rmk")
                                                             (* ; "Edited 30-May-2023 23:10 by rmk")
                                                             (* ; "Edited 27-May-2023 16:49 by rmk")
                                                             (* ; "Edited 24-May-2023 22:43 by rmk")

    (* ;; "This undoes the replacement, but tracks for REDO whether the action was replace, lowercase, uppercase, or initialcap.")

    (LET ((TEXTOBJ (FTEXTOBJ TSTREAM)))
         (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.COPY (GETTH EVENT THDELETEDPIECES)
                                          NIL TSTREAM)
                TSTREAM
                (\TEDIT.UPDATE.SEL (TEXTSEL TEXTOBJ)
                       EVENT))
         (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
                THACTION ACTION])

(\TEDIT.UNDO.CHARLOOKS
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited 21-Apr-2025 20:31 by rmk")
                                                             (* ; "Edited 20-Apr-2025 13:39 by rmk")
                                                             (* ; "Edited  6-Apr-2025 14:44 by rmk")
                                                             (* ; "Edited 25-Nov-2024 21:59 by rmk")
                                                             (* ; "Edited 28-Sep-2024 22:37 by rmk")
                                                             (* ; "Edited 26-Sep-2024 16:06 by rmk")
                                                             (* ; "Edited 11-Aug-2024 22:11 by rmk")
                                                             (* ; "Edited  5-Jul-2024 22:54 by rmk")
                                                             (* ; "Edited 18-May-2024 16:21 by rmk")
                                                             (* ; "Edited 19-Feb-2024 11:32 by rmk")
                                                             (* ; "Edited 14-Dec-2023 21:01 by rmk")
                                                             (* ; "Edited 30-May-2023 22:56 by rmk")
                                                             (* ; "Edited 18-Apr-2023 23:56 by rmk")
                                                             (* ; "Edited 30-May-91 21:44 by jds")

    (* ;; "Undo the setting of character looks. The undolist is a list of (NEXTCHNO . OLDCHARLOOKS) pairs, where OLDCHARLOOKS NIL means nothing changed.  We have to track the character numbers because pieces may have been split by future events that were then undone. NEXTCHNO is the first character number of the next original piece")

    (LET ((TEXTOBJ (FTEXTOBJ TSTREAM)))
         (for U OLDLOOKS NEWUNDOLIST NEXTCHNO (PC _ (\TEDIT.CHTOPC (GETTH EVENT THCH#)
                                                           TEXTOBJ))
              (CHNO _ (GETTH EVENT THCH#))
              (SEL _ (FGETTOBJ TEXTOBJ SEL))
              (CARETPC _ (\TEDIT.CARETPIECE TEXTOBJ)) in (CDR (GETTH EVENT THOLDINFO))
            do 
               (* ;; "Revert changes until we see the character number of the next changed piece. The initial NEXTCHNO is ")

               (* ;; "Perhaps we should also save the CHNO of the CARETPC")

               (SETQ NEXTCHNO (CAR U))
               (SETQ OLDLOOKS (CDR U))
               (CL:WHEN (AND OLDLOOKS (EQ PC CARETPC))
                   (FSETTOBJ TEXTOBJ CARETLOOKS (\TEDIT.CARETLOOKS.VERIFY TEXTOBJ OLDLOOKS)))
               [push NEWUNDOLIST (CONS NEXTCHNO (CL:IF OLDLOOKS (PLOOKS PC] 

               (* ;; "U starts at the first piece.  We want CHNO to be the start of the next piece, i.e. initialize to (CAR(CDR ...))  But then, what about the last piece.  Maybe we have to do our own popping, or look at UTAIL.  Or end in (NEXTPC-CHNO . NIL ).  Or text for IGEQ THCHLIM")

               (for P inpieces PC do (FSETPC P PLOOKS OLDLOOKS)
                                     (add CHNO (PLEN P))
                                     (CL:WHEN (IEQP CHNO NEXTCHNO)
                                                             (* ; "First piece of the next run")
                                         (SETQ PC P)
                                         (RETURN))) finally 

                                                          (* ;; 
                     "Remember the previous looks in case we UNDO the UNDO. (CAR DATUM) is for redo.")

                                                          (CL:WHEN NEWUNDOLIST
                                                              (change (GETTH EVENT THOLDINFO)
                                                                     (CONS (CAR DATUM)
                                                                           (DREVERSE NEWUNDOLIST)))
                                                              (\TEDIT.NOSEL TSTREAM)
                                                              (\TEDIT.UPDATE.SEL SEL EVENT NIL NIL
                                                                     'NORMAL)
                                                              (\TEDIT.UPDATE.LINES TSTREAM
                                                                     'LOOKS
                                                                     (GETTH EVENT THCH#)
                                                                     (GETTH EVENT THLEN))
                                                              (\TEDIT.SHOWSEL SEL T TSTREAM)
                                                              (TEDIT.PROMPTPRINT TEXTOBJ 
                                                                     "Character looks restored" T)) 

                                                          (* ;; 
                                        "Save the event for REDO, even if these pieces didn't change")

                                                          (\TEDIT.HISTORYADD TEXTOBJ EVENT])

(\TEDIT.UNDO.PARALOOKS
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited 21-Apr-2025 20:31 by rmk")
                                                             (* ; "Edited 20-Apr-2025 13:38 by rmk")
                                                             (* ; "Edited  6-Apr-2025 14:44 by rmk")
                                                             (* ; "Edited 25-Nov-2024 22:00 by rmk")
                                                             (* ; "Edited 28-Sep-2024 22:38 by rmk")
                                                             (* ; "Edited 27-Sep-2024 12:23 by rmk")
                                                             (* ; "Edited 11-Aug-2024 22:10 by rmk")
                                                             (* ; "Edited  5-Jul-2024 22:54 by rmk")
                                                             (* ; "Edited 18-May-2024 16:22 by rmk")
                                                             (* ; "Edited 19-Feb-2024 11:32 by rmk")
                                                             (* ; "Edited 11-Dec-2023 11:10 by rmk")
                                                             (* ; "Edited 21-Sep-2023 23:51 by rmk")
                                                             (* ; "Edited 30-May-2023 22:55 by rmk")
                                                             (* ; "Edited 18-Apr-2023 23:57 by rmk")
                                                             (* ; "Edited 30-May-91 21:44 by jds")

    (* ;; "Undo the setting of paragraph looks.")

    (LET ((TEXTOBJ (FTEXTOBJ TSTREAM)))
         (for U OLDLOOKS NEWUNDOLIST (PC _ (\TEDIT.CHTOPC (GETTH EVENT THCH#)
                                                  TEXTOBJ))
              (CHNO _ (GETTH EVENT THCH#))
              (SEL _ (FGETTOBJ TEXTOBJ SEL)) in (CDR (GETTH EVENT THOLDINFO))
            do 
               (* ;; "Find the first piece of the next changed paragraph")

               (for P inpieces PC do (CL:WHEN (IEQP CHNO (CAR U))
                                         (SETQ PC P)
                                         (RETURN))
                                     (add CHNO (PLEN P)))
               (SETQ OLDLOOKS (CDR U))
               (push NEWUNDOLIST (CONS CHNO (PPARALOOKS PC))) 
                                                             (* ; "Save for UNDO UNDO")

               (* ;; "Change all the pieces in this paragraph")

               (for P inpieces PC do (FSETPC P PPARALOOKS OLDLOOKS)
                                     (CL:WHEN (PPARALAST P)
                                         (SETQ PC P)
                                         (RETURN))
                                     (add CHNO (PLEN P)))
            finally 

                  (* ;; 
                  "Remember the previous looks in case we UNDO the UNDO. (CAR DATUM) is for redo.")

                  (CL:WHEN NEWUNDOLIST
                      (change (GETTH EVENT THOLDINFO)
                             (CONS (CAR DATUM)
                                   (DREVERSE NEWUNDOLIST)))
                      (\TEDIT.NOSEL TSTREAM)
                      (\TEDIT.UPDATE.SEL SEL EVENT NIL NIL 'NORMAL)
                      (\TEDIT.UPDATE.LINES TSTREAM 'LOOKS (GETTH EVENT THCH#)
                             (GETTH EVENT THLEN))
                      (\TEDIT.SHOWSEL SEL T TSTREAM)
                      (TEDIT.PROMPTPRINT TEXTOBJ "Paragraph looks restored" T)) 

                  (* ;; "Save the event for REDO, even if these pieces didn't change")

                  (\TEDIT.HISTORYADD TEXTOBJ EVENT])

(\TEDIT.UNDO.PAGELOOKS
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited  6-Apr-2025 11:49 by rmk")
                                                             (* ; "Edited 12-Aug-2024 10:28 by rmk")
    (SETQ TEXTOBJ (FTEXTOBJ TEXTOBJ))
    [FSETTOBJ TEXTOBJ TXTPAGEFRAMES (PROG1 (COPYALL (GETTH EVENT THOLDINFO))
                                        (SETTH EVENT THOLDINFO (GETTOBJ TEXTOBJ TXTPAGEFRAMES)))]
    (TEDIT.PROMPTPRINT TEXTOBJ "Page formats restored" T)
    (\TEDIT.HISTORYADD TEXTOBJ EVENT])

(\TEDIT.UNDO.COMPOSITE
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited  6-Apr-2025 14:44 by rmk")
                                                             (* ; "Edited  1-Apr-2025 17:34 by rmk")
                                                             (* ; "Edited  8-Dec-2024 15:47 by rmk")
                                                             (* ; "Edited 25-Nov-2024 22:27 by rmk")
                                                             (* ; "Edited 15-Aug-2024 10:14 by rmk")
                                                             (* ; "Edited  7-May-2024 23:17 by rmk")

    (* ;; "A composite event is a group of other events that are to be undone at the same time.  Only show the selection of the last undo event.  We want to end up with a single event on history. We don't want to bump the count.  (Presumably EVENT was alread popped)")

    (for E EVENTS CUREVENT (TEXTOBJ _ (GETTSTR TSTREAM TEXTOBJ)) in (GETTH EVENT THOLDINFO)
       do (SETQ CUREVENT (\TEDIT.LASTEVENT TEXTOBJ))
          (\TEDIT.UNDO1 TSTREAM E)
          (CL:UNLESS (EQ CUREVENT (\TEDIT.LASTEVENT TEXTOBJ))(* ; "Something changed")
              (push EVENTS (\TEDIT.POPEVENT TEXTOBJ)))
          (\TEDIT.NOSEL TSTREAM) finally (\TEDIT.HISTORYADD.COMPOSITE TEXTOBJ EVENTS (GETTH EVENT 
                                                                                            THACTION)
                                                ))
    (\TEDIT.SCROLL.CARET TSTREAM])

(\TEDIT.UNDO.REPLACECODE
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited 28-Mar-2025 14:22 by rmk")
                                                             (* ; "Edited 23-Sep-2024 00:45 by rmk")
    (\TEDIT.RPLCHARCODE TSTREAM (GETTH EVENT THCH#)
           (GETTH EVENT THOLDINFO])

(\TEDIT.UNDO.WRAP
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited  4-Apr-2025 11:01 by rmk")

    (* ;; "Undo the deletions and restore the original selection.  But also update the undo event so that  undo-undo will select the whole span.")

    (LET* ((TEXTOBJ (GETTSTR TSTREAM TEXTOBJ))
           (SEL (TEXTSEL TEXTOBJ))
           (CH# (GETSEL SEL CH#))
           (DCH (FGETSEL SEL DCH))
           (POINT (FGETSEL SEL POINT))
           UNDOEVENT)
          (\TEDIT.UNDO.COMPOSITE TSTREAM EVENT)
          (SETQ UNDOEVENT (\TEDIT.LASTEVENT TEXTOBJ))
          (CL:WHEN (AND UNDOEVENT (EQ :Sel (GETTH (CAR (GETTH UNDOEVENT THOLDINFO))
                                                  THACTION)))
              (change (GETTH UNDOEVENT THOLDINFO)
                     (NCONC1 (CDR DATUM)
                            (\TEDIT.HISTORY.EVENT TEXTOBJ :Sel CH# DCH POINT))))])

(\TEDIT.UNDO.SEL
  [LAMBDA (TSTREAM EVENT)                                    (* ; "Edited  6-Apr-2025 14:45 by rmk")
                                                             (* ; "Edited  4-Apr-2025 10:55 by rmk")
    (LET* ((SEL (TEXTSEL (FTEXTOBJ TSTREAM)))
           (CH# (GETSEL SEL CH#))
           (DCH (FGETSEL SEL DCH))
           (POINT (FGETSEL SEL POINT)))
          (\TEDIT.NOSEL TSTREAM)
          (\TEDIT.UPDATE.SEL TSTREAM (GETTH EVENT THCH#)
                 (GETTH EVENT THLEN)
                 (GETTH EVENT THPOINT))
          (\TEDIT.HISTORYADD TSTREAM (\TEDIT.HISTORY.EVENT TSTREAM :Sel CH# DCH POINT])
)
(DEFINEQ

(\TEDIT.REDO.INSERT
  [LAMBDA (TSTREAM EVENT SEL)                                (* ; "Edited 21-Apr-2025 22:19 by rmk")
                                                             (* ; "Edited  6-Apr-2025 12:09 by rmk")
                                                             (* ; "Edited 15-Aug-2024 10:47 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:54 by rmk")
                                                             (* ; "Edited 31-May-2023 10:26 by rmk")
                                                             (* ; "Edited 18-May-2023 19:24 by rmk")
                                                             (* ; "Edited 21-Apr-93 01:06 by jds")
    (\TEDIT.INSERT.SELPIECES (\TEDIT.SELPIECES.COPY (\TEDIT.SELPIECES EVENT NIL (FTEXTOBJ TSTREAM))
                                    'INSERT TSTREAM)
           TSTREAM SEL])

(\TEDIT.REDO.REPLACE
  [LAMBDA (TSTREAM EVENT ACTION)                             (* ; "Edited 21-Apr-2025 22:22 by rmk")
                                                             (* ; "Edited  6-Apr-2025 12:14 by rmk")
                                                             (* ; "Edited  7-Jul-2024 11:59 by rmk")
                                                             (* ; "Edited 15-Mar-2024 13:54 by rmk")
                                                             (* ; "Edited  2-Oct-2023 11:43 by rmk")
                                                             (* ; "Edited 31-May-2023 10:25 by rmk")
                                                             (* ; "Edited 27-May-2023 11:16 by rmk")
                                                             (* ; "Edited 16-May-2023 22:05 by rmk")
                                                             (* ; "Edited 30-May-91 21:28 by jds")

    (* ;; "We get the replacement from where EVENT just installed it in the text (assume that it is still there unchanged), and then we use it to replace what is now at the current selection.  EVENT's deleted pieces are not relevant.")

    (LET ((TEXTOBJ (FTEXTOBJ TSTREAM)))
         (\TEDIT.UPDATE.SEL (TEXTSEL TEXTOBJ)
                EVENT)
         (\TEDIT.REPLACE.SELPIECES (\TEDIT.SELPIECES.COPY (\TEDIT.SELPIECES EVENT NIL TEXTOBJ)
                                          NIL TSTREAM)
                TSTREAM)
         (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
                THACTION ACTION])

(\TEDIT.REDO.COMPOSITE
  [LAMBDA (TSTREAM EVENT SEL)                                (* ; "Edited  6-Apr-2025 12:12 by rmk")
                                                             (* ; "Edited 21-Oct-2024 00:26 by rmk")
                                                             (* ; "Edited  7-May-2024 23:12 by rmk")
    (\TEDIT.THELP 'Redo-composite])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5074 6095 (\TEDIT.HISTORYEVENT.DEFPRINT 5084 . 6093)) (7185 18439 (\TEDIT.HISTORYADD 
7195 . 12457) (\TEDIT.HISTORYADD.COMPOSITE 12459 . 13491) (\TEDIT.CUMULATE.EVENTS 13493 . 15087) (
\TEDIT.COMPOSITE.EVENT 15089 . 15825) (\TEDIT.HISTORY.PROP 15827 . 17190) (\TEDIT.HISTORY.EVENT 17192
 . 18263) (\TEDIT.POPEVENT 18265 . 18437)) (18492 37479 (TEDIT.UNDO 18502 . 23378) (\TEDIT.UNDO1 23380
 . 27718) (TEDIT.REDO 27720 . 34633) (\TEDIT.UNDO.UNDO 34635 . 37477)) (37480 55955 (
\TEDIT.UNDO.INSERT 37490 . 38615) (\TEDIT.UNDO.DELETE 38617 . 39629) (\TEDIT.UNDO.MOVE 39631 . 41284) 
(\TEDIT.UNDO.REPLACE 41286 . 42796) (\TEDIT.UNDO.CHARLOOKS 42798 . 48035) (\TEDIT.UNDO.PARALOOKS 48037
 . 51866) (\TEDIT.UNDO.PAGELOOKS 51868 . 52426) (\TEDIT.UNDO.COMPOSITE 52428 . 54028) (
\TEDIT.UNDO.REPLACECODE 54030 . 54364) (\TEDIT.UNDO.WRAP 54366 . 55295) (\TEDIT.UNDO.SEL 55297 . 55953
)) (55956 58929 (\TEDIT.REDO.INSERT 55966 . 56928) (\TEDIT.REDO.REPLACE 56930 . 58536) (
\TEDIT.REDO.COMPOSITE 58538 . 58927)))))
STOP
