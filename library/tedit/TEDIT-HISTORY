(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 7-May-2023 22:25:33" 
{DSK}<Users>kaplan>Local>medley3.5>working-medley>library>tedit>TEDIT-HISTORY.;43 29845  

      :EDIT-BY rmk

      :CHANGES-TO (FNS TEDIT.UNDO.MOVE)

      :PREVIOUS-DATE " 7-May-2023 20:40:19" 
{DSK}<Users>kaplan>Local>medley3.5>working-medley>library>tedit>TEDIT-HISTORY.;42)


(PRETTYCOMPRINT TEDIT-HISTORYCOMS)

(RPAQQ TEDIT-HISTORYCOMS
       ((DECLARE%: EVAL@COMPILE DONTCOPY (EXPORT (RECORDS TEDITHISTORYEVENT)))
        (INITRECORDS TEDITHISTORYEVENT)
        (GLOBALVARS TEDIT.HISTORY.TYPELST TEDIT.HISTORYLST)
        (INITVARS (TEDIT.HISTORY.TYPELST NIL)
               (TEDIT.HISTORYLST NIL))
        (COMS 
              (* ;; "History-list maintenance functions")

              (FNS \TEDIT.HISTORYADD \TEDIT.CUMULATE.EVENTS))
        (COMS 
              (* ;; "Specialized UNDO & REDO functions.")

              (FNS TEDIT.UNDO TEDIT.UNDO.INSERTION TEDIT.UNDO.DELETION TEDIT.REDO 
                   TEDIT.REDO.INSERTION TEDIT.UNDO.MOVE TEDIT.UNDO.REPLACE TEDIT.REDO.REPLACE 
                   TEDIT.REDO.MOVE))))
(DECLARE%: EVAL@COMPILE DONTCOPY 
(* "FOLLOWING DEFINITIONS EXPORTED")(DECLARE%: EVAL@COMPILE

(DATATYPE TEDITHISTORYEVENT (
                             (* ;; "Describes one event on the TEdit edit history list.")

                             THACTION                        (* ; 
                                                           "A LITATOM, specifying what the event was")
                             THPOINT                         (* ; 
                                                            "Was the selection to the left or right?")
                             THLEN                           (* ; "The # of chars involved")
                             THCH#                           (* ; "The starting ch#")
                             THFIRSTPIECE                    (* ; "First piece involved")
                             THOLDINFO                       (* ; "Old info, for undo")
                             THAUXINFO                       (* ; 
                                                 "Auxiliary info about the event, primarily for redo")
                             THTEXTOBJ

                             (* ;; "Place to remember a second textobj, for those like MOVE who need to remember both a source and a destination.")

                             )
                            THPOINT _ 'LEFT)
)

(/DECLAREDATATYPE 'TEDITHISTORYEVENT '(POINTER POINTER POINTER POINTER POINTER POINTER POINTER 
                                             POINTER)
       '((TEDITHISTORYEVENT 0 POINTER)
         (TEDITHISTORYEVENT 2 POINTER)
         (TEDITHISTORYEVENT 4 POINTER)
         (TEDITHISTORYEVENT 6 POINTER)
         (TEDITHISTORYEVENT 8 POINTER)
         (TEDITHISTORYEVENT 10 POINTER)
         (TEDITHISTORYEVENT 12 POINTER)
         (TEDITHISTORYEVENT 14 POINTER))
       '16)

(* "END EXPORTED DEFINITIONS")

)

(/DECLAREDATATYPE 'TEDITHISTORYEVENT '(POINTER POINTER POINTER POINTER POINTER POINTER POINTER 
                                             POINTER)
       '((TEDITHISTORYEVENT 0 POINTER)
         (TEDITHISTORYEVENT 2 POINTER)
         (TEDITHISTORYEVENT 4 POINTER)
         (TEDITHISTORYEVENT 6 POINTER)
         (TEDITHISTORYEVENT 8 POINTER)
         (TEDITHISTORYEVENT 10 POINTER)
         (TEDITHISTORYEVENT 12 POINTER)
         (TEDITHISTORYEVENT 14 POINTER))
       '16)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDIT.HISTORY.TYPELST TEDIT.HISTORYLST)
)

(RPAQ? TEDIT.HISTORY.TYPELST NIL)

(RPAQ? TEDIT.HISTORYLST NIL)



(* ;; "History-list maintenance functions")

(DEFINEQ

(\TEDIT.HISTORYADD
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited  4-May-2023 18:29 by rmk")
                                                             (* ; "Edited  3-Sep-87 10:36 by jds")

    (* ;; "Add a new event to the history list.  For now, this just re-sets the whole list to be the one event...")

    (* ;; "This function also takes care of cumulating cumulative events, like successive deletions.")

    (LET ((OLDEVENT (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ))
          (REALEVENT EVENT)
          ETYPE)
         (CL:WHEN OLDEVENT
             (SETQ ETYPE (fetch (TEDITHISTORYEVENT THACTION) of EVENT))
             [COND
                ((AND (EQ ETYPE 'Delete)
                      (EQ ETYPE (fetch (TEDITHISTORYEVENT THACTION) of OLDEVENT)))
                                                             (* ; 
                                        "Repeated successive deletions.  See if we can combine them.")
                 (LET* [(OSTART (fetch (TEDITHISTORYEVENT THCH#) of OLDEVENT))
                        (NSTART (fetch (TEDITHISTORYEVENT THCH#) of EVENT))
                        (OLDEND (+ OSTART (fetch (TEDITHISTORYEVENT THLEN) of OLDEVENT)))
                        (NEWEND (+ NSTART (fetch (TEDITHISTORYEVENT THLEN) of EVENT]
                       (COND
                          ((IEQP OLDEND NSTART)              (* ; 
                              "The old deletion was just in front of the current one; cumulate them.")
                           (SETQ REALEVENT (\TEDIT.CUMULATE.EVENTS OLDEVENT EVENT T)))
                          ((IEQP NEWEND OSTART)              (* ; 
                                  "The new deletion was just in front of the old one; cumulate them.")
                           (SETQ REALEVENT (\TEDIT.CUMULATE.EVENTS EVENT OLDEVENT T])
         (replace (TEXTOBJ TXTHISTORY) of TEXTOBJ with REALEVENT])

(\TEDIT.CUMULATE.EVENTS
  [LAMBDA (EVENT1 EVENT2 PIECES-TO-SAVE?)                    (* ; "Edited  3-Sep-87 10:42 by jds")

    (* ;; "Accumulate history events that should really be combined into a single event.")

    (* ;; "For now, this assumes they're events of the same type.  Actually, this should be able to cumulate a delete/insert pair into a replacement, etc.")

    (LET* [(OLDLEN (fetch (TEDITHISTORYEVENT THLEN) of EVENT1))
           (NEWPC1 (fetch (TEDITHISTORYEVENT THFIRSTPIECE) of EVENT2))
           (REALEVENT (create TEDITHISTORYEVENT using EVENT1 THLEN _ (+ OLDLEN (fetch (
                                                                                    TEDITHISTORYEVENT
                                                                                       THLEN)
                                                                                  of EVENT2]
          (bind (PC _ (fetch (TEDITHISTORYEVENT THFIRSTPIECE) of EVENT1))
                (CHCOUNT _ 0) while (< (SETQ CHCOUNT (+ CHCOUNT (fetch (PIECE PLEN) of PC)))
                                       OLDLEN) do (SETQ PC (fetch (PIECE NEXTPIECE) of PC))
             finally (replace (PIECE NEXTPIECE) of PC with NEWPC1)
                   (replace (PIECE PREVPIECE) of NEWPC1 with PC)
                   (RETURN))
          REALEVENT])
)



(* ;; "Specialized UNDO & REDO functions.")

(DEFINEQ

(TEDIT.UNDO
  [LAMBDA (TEXTOBJ)                                          (* ; "Edited  4-May-2023 14:57 by rmk")
                                                             (* ; "Edited 18-Apr-2023 23:57 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:41 by mitani")

    (* ;; "Undo the last thing this guy did.")

    (CL:UNLESS (FETCH (TEXTOBJ TXTREADONLY) OF TEXTOBJ)

        (* ;; "Only undo things if the document is allowed to change.")

        (PROG ((SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
               (EVENT (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ))
               UNDOFN)
              (CL:UNLESS EVENT
                  (TEDIT.PROMPTPRINT TEXTOBJ "Nothing to undo" T)
                  (RETURN))

         (* ;; "There really is something to UNDO.")

              (RESETLST
                  (RESETSAVE (CURSOR WAITINGCURSOR))
                  (\SHOWSEL SEL NIL)
                  [SELECTQ (fetch THACTION of EVENT)
                      ((Insert Copy Include)                 (* ; "It was an insertion")
                           (TEDIT.UNDO.INSERTION TEXTOBJ EVENT))
                      (Delete                                (* ; "It was a deletion")
                              (TEDIT.UNDO.DELETION TEXTOBJ EVENT))
                      (Looks                                 (* ; "It was a character-looks change")
                             (TEDIT.UNDO.LOOKS TEXTOBJ EVENT))
                      (ParaLooks                             (* ; "It was a PARA looks change")
                                 (TEDIT.UNDO.PARALOOKS TEXTOBJ EVENT))
                      (Move                                  (* ; "He moved some text")
                            (TEDIT.UNDO.MOVE TEXTOBJ EVENT))
                      ((Replace LowerCase UpperCase) 

                                                 (* ;; "He replaced one piece of text with another ; Lower-casing and upper-casing have the same undo event.")

                           (TEDIT.UNDO.REPLACE TEXTOBJ EVENT))
                      (Get                                   (* ; "He did a GET -- not undoable.")
                           (TEDIT.PROMPTPRINT TEXTOBJ "You can't undo a Get" T))
                      (Put                                   (* ; "He did a PUT -- not undoable.")
                           (TEDIT.PROMPTPRINT TEXTOBJ "You can't undo a Put" T))
                      (COND
                         ((SETQ UNDOFN (CADDR (ASSOC (fetch THACTION of EVENT)
                                                     TEDIT.HISTORY.TYPELST)))

                          (* ;; "TEDIT.HISTORY.TYPELST is an ALST of form (type redofn undofn)")

                          (APPLY* UNDOFN TEXTOBJ EVENT (fetch THLEN of EVENT)
                                 (fetch THCH# of EVENT)
                                 (fetch THFIRSTPIECE of EVENT)))
                         (T (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "UNDO not implemented for "
                                                              (fetch THACTION of EVENT))
                                   T]
                  (\SHOWSEL SEL T))))])

(TEDIT.UNDO.INSERTION
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited  4-May-2023 14:39 by rmk")
                                                             (* ; "Edited  2-May-2023 23:26 by rmk")
                                                             (* ; "Edited 21-Apr-93 01:33 by jds")

    (* ;; "UNDO a prior Insert, Copy, or Include.")

    (LET ((SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
          (CH# (fetch THCH# of EVENT)))
         (SETSEL SEL CH# CH#)
         (SETSEL SEL CHLIM (IPLUS CH# (fetch THLEN of EVENT)))
         (SETSEL SEL DCH (IDIFFERENCE (GETSEL SEL CHLIM)
                                CH#))
         (\TEDIT.DELETE SEL TEXTOBJ)
         (SETSEL SEL POINT 'LEFT)
         (\FIXSEL SEL TEXTOBJ)

         (* ;; "Make the UNDO be UNDOable, by changing the event to a deletion.")

         (replace THACTION of EVENT with 'Delete])

(TEDIT.UNDO.DELETION
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited  5-May-2023 13:12 by rmk")
                                                             (* ; "Edited  1-May-2023 23:26 by rmk")
                                                             (* ; "Edited 21-Apr-93 12:01 by jds")

    (* ;; "UNDO a prior Deletion of text.")

    (CL:UNLESS (fetch (TEXTOBJ TXTREADONLY) of TEXTOBJ)
        (LET ((FIRSTPIECE (fetch THFIRSTPIECE of EVENT))
              (SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
              (LEN (fetch THLEN of EVENT))
              (CH# (fetch THCH# of EVENT))
              NEWPIECE NPC INSPC OBJECT INSERTFN)
             (SETQ NPC (NEXTPIECE FIRSTPIECE))
             (SETQ INSPC (\ALIGNEDPIECE CH# TEXTOBJ))
             (SETQ NEWPIECE (create PIECE using FIRSTPIECE))
             (replace THFIRSTPIECE of EVENT with NEWPIECE)
             (bind (TL _ 0) while (ILESSP TL LEN) do (\INSERTPIECE NEWPIECE INSPC TEXTOBJ) 
                                                             (* ; "Insert the piece back in")
                                                     (CL:WHEN [AND (EQ OBJECT.PTYPE (PTYPE NEWPIECE))
                                                                   (SETQ OBJECT (PCONTENTS NEWPIECE))
                                                                   (SETQ INSERTFN (IMAGEOBJPROP
                                                                                   OBJECT
                                                                                   'WHENINSERTEDFN]
                                                             (* ; 
                                        "If this is an imageobject, and it has an insertfn, call it.")
                                                         (APPLY* INSERTFN OBJECT (\TEDIT.PRIMARYW
                                                                                  TEXTOBJ)
                                                                NIL
                                                                (TEXTSTREAM TEXTOBJ)))
                                                     (add TL (PLEN FIRSTPIECE)) 
                                                             (* ; 
                                                           "Keep track of how much we've re-inserted")
                                                     (SETQ FIRSTPIECE NPC) 
                                                             (* ; "Move to the next piece to insert")
                                                     (AND NPC (SETQ NPC (NEXTPIECE NPC)))
                                                     (SETQ NEWPIECE (create PIECE using FIRSTPIECE)))
                                                             (* ; 
                                         "Done here because \INSERTPIECE creams the NEXTPIECE field.")
             (\TEDIT.UPDATE.LINES TEXTOBJ CH# LEN)           (* ; 
                                               "Fix up the lines and display, we'll do the selection")
             (SETSEL SEL CH# CH#)                            (* ; 
                                                   "Make the selection point at the re-inserted text")
             (SETSEL SEL CHLIM (IPLUS CH# LEN))
             (SETSEL SEL DCH LEN)
             (SETSEL SEL POINT (fetch THPOINT of EVENT))
             (\TEDIT.SET.SEL.LOOKS SEL 'NORMAL)
             (\FIXSEL SEL TEXTOBJ)                           (* ; "Really fix the selection")
                                                             (* ; 
                                   "Make the UNDO be UNDOable, by changing the event to a insertion.")
             (replace THACTION of EVENT with 'Insert)))])

(TEDIT.REDO
  [LAMBDA (TEXTOBJ)                                          (* ; "Edited  4-May-2023 18:18 by rmk")
                                                             (* ; "Edited  1-May-2023 23:37 by rmk")
                                                             (* ; "Edited 18-Apr-2023 23:57 by rmk")
                                                             (* ; "Edited  9-Sep-2022 17:00 by rmk")
                                                             (* ; "Edited 30-May-91 21:27 by jds")

    (* ;; "REDO the last thing this guy did.")

    (CL:UNLESS (FETCH (TEXTOBJ TXTREADONLY) OF TEXTOBJ)
        (PROG ((SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
               EVENT CH)
              (CL:UNLESS (SETQ EVENT (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ))
                     (TEDIT.PROMPTPRINT TEXTOBJ "Nothing to redo" T))

         (* ;; "There really is something to REDO Decide what, & do it.")

              (RESETLST
                  (RESETSAVE (CURSOR WAITINGCURSOR))
                  (\SHOWSEL SEL NIL)
                  (SELECTQ (fetch THACTION of EVENT)
                      ((Insert Copy Include)                 (* ; "It was an insertion")
                           (TEDIT.REDO.INSERTION TEXTOBJ EVENT (IMAX 1 (TEDIT.GETPOINT NIL SEL))))
                      (Delete                                (* ; "It was a deletion")
                              (\TEDIT.DELETE SEL TEXTOBJ))
                      (Replace                               (* ; 
                                                          "It was a replacement (a del/insert combo)")
                               (TEDIT.REDO.REPLACE TEXTOBJ EVENT))
                      (LowerCase                             (* ; "He lower-cased something")
                                 (\TEDIT.LCASE.SEL TEXTOBJ TEXTOBJ SEL))
                      (UpperCase                             (* ; "He upper-cased something")
                                 (\TEDIT.LCASE.SEL TEXTOBJ TEXTOBJ SEL))
                      (Looks                                 (* ; "It was a looks change")
                             (TEDIT.REDO.LOOKS TEXTOBJ EVENT (IMAX 1 (TEDIT.GETPOINT NIL SEL))))
                      (ParaLooks                             (* ; "It was a Paragraph looks change")
                                 (TEDIT.REDO.PARALOOKS TEXTOBJ EVENT (IMAX 1 (TEDIT.GETPOINT NIL SEL)
                                                                           )))
                      (Find                                  (* ; "EXACT-MATCH SEARCH COMMAND")
                            (RESETLST
                                (RESETSAVE (CURSOR WAITINGCURSOR))
                                (TEDIT.PROMPTPRINT TEXTOBJ "Searching..." T)
                                (SETQ SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
                                (\SHOWSEL SEL NIL)
                                (SETQ CH (TEDIT.FIND TEXTOBJ (fetch THAUXINFO of EVENT)))
                                (COND
                                   (CH (TEDIT.PROMPTPRINT TEXTOBJ "done.")
                                       (SETSEL SEL CH# CH)
                                       [SETSEL SEL CHLIM (IPLUS CH (NCHARS (fetch THAUXINFO
                                                                              of EVENT]
                                       (SETSEL SEL DCH (NCHARS (fetch THAUXINFO of EVENT)))
                                       (SETSEL SEL POINT 'RIGHT)
                                       (\FIXSEL SEL TEXTOBJ)
                                       (TEDIT.NORMALIZECARET TEXTOBJ)
                                       (\SHOWSEL SEL T))
                                   (T (TEDIT.PROMPTPRINT TEXTOBJ "[Not found]"))))
                            (replace (TEXTOBJ HINTPC) of TEXTOBJ with NIL)
                                                             (* ; "Drop the cached piece.  WHY??")
                            )
                      ((Move ReplaceMove)                    (* ; "He moved some text")
                           (TEDIT.REDO.MOVE TEXTOBJ EVENT (fetch THLEN of EVENT)
                                  (IMAX 1 (TEDIT.GETPOINT NIL SEL))
                                  (fetch THFIRSTPIECE of EVENT)))
                      (Get                                   (* ; "He did a GET -- not undoable.")
                           (TEDIT.PROMPTPRINT TEXTOBJ "You can't redo a Get" T))
                      (Put                                   (* ; "He did a PUT -- not undoable.")
                           (TEDIT.PROMPTPRINT TEXTOBJ "You can't redo a Put" T))
                      (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Redo of the action " (fetch THACTION
                                                                                  of EVENT)
                                                        " isn't implemented.")
                             T))
                  (\SHOWSEL SEL T))))])

(TEDIT.REDO.INSERTION
  [LAMBDA (TEXTOBJ EVENT CH#)                                (* ; "Edited  7-May-2023 11:42 by rmk")
                                                             (* ; "Edited  6-May-2023 10:44 by rmk")
                                                             (* ; "Edited 21-Apr-93 01:06 by jds")

    (* ;; "REDO a prior Insert/Copy/Include of text.")

    (BTVALIDATE 'TEDIT.REDO.INSERTION 'START TEXTOBJ)
    (for PC NPC NEWFIRSTPIECE (SEL _ (fetch (TEXTOBJ SEL) of TEXTOBJ))
         (INSPC _ (\ALIGNEDPIECE CH# TEXTOBJ))
         (LEN _ (fetch THLEN of EVENT))
         (TL _ 0) first (\SHOWSEL SEL NIL) inpieces (fetch THFIRSTPIECE of EVENT)
       while (ILESSP TL LEN) do (SETQ NPC (\TEDIT.COPYPIECE PC TEXTOBJ TEXTOBJ))
                                (CL:UNLESS NEWFIRSTPIECE     (* ; "for undo")
                                    (SETQ NEWFIRSTPIECE NPC))
                                (\INSERTPIECE NPC INSPC TEXTOBJ)
                                (add TL (PLEN PC)) finally   (* ; 
                                              "propagate paragraph formatting into the new insertion")
                                                         (\TEDIT.DIFFUSE.PARALOOKS
                                                          (PREVPIECE (fetch THFIRSTPIECE of EVENT))
                                                          INSPC)
                                                         (\TEDIT.UPDATE.LINES TEXTOBJ CH#
                                                                (IPLUS CH# LEN))
                                                         (SETSEL SEL CH# CH#) 
                                                             (* ; 
                                                   "Make the selection point at the re-inserted text")
                                                         (SETSEL SEL CHLIM (IPLUS CH# LEN))
                                                         (SETSEL SEL DCH LEN)
                                                         (\TEDIT.SET.SEL.LOOKS SEL 'NORMAL)
                                                         (\FIXSEL SEL TEXTOBJ)
                                                         (\SHOWSEL SEL T) 

                                               (* ;; "Make the REDO be UNDOable, by putting in the copied first piece and changing the event to an insertion.")

                                                         (replace THFIRSTPIECE of EVENT with 
                                                                                        NEWFIRSTPIECE
                                                                )
                                                         (replace THACTION of EVENT
                                                            with 'Insert))
    (BTVALIDATE 'TEDIT.REDO.INSERTION 'END TEXTOBJ])

(TEDIT.UNDO.MOVE
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited  7-May-2023 22:25 by rmk")
                                                             (* ; "Edited  5-May-2023 13:12 by rmk")
                                                             (* ; "Edited 30-May-91 21:27 by jds")

    (* ;; "UNDO a MOVE command.  TEDIT.MOVE first deletes and then inserts.  So, going backwards, we first undo the insert with a delete, and then undo the delete with an insert.x")

    (LET ((TOOBJ (fetch THTEXTOBJ of EVENT))
          (FROMCH# (fetch THOLDINFO of EVENT))
          (TOCH# (fetch THCH# of EVENT))
          (LEN (fetch THLEN of EVENT))
          TOSEL)

         (* ;; "Delete the moved material from the original TO destination")

         (SETQ TOSEL (fetch (TEXTOBJ SEL) of TOOBJ))
         (\TEDIT.UPDATE.SEL TOSEL TOCH# LEN)
         (\TEDIT.DELETE TOSEL TOOBJ)

         (* ;; "Reinsert the moved pieces back where they were in FROM (no need to copy them, since we did that on the original move.)")

         (* ;; "If the deletion that undoes the insertion is before the insertion that undoes the deletion, the text got lengthened and the insertion point move to the right by the length")
                                                             (* ; "Update and display the source")
         (\TEDIT.INSERT.PIECERANGE (fetch THFIRSTPIECE of EVENT)
                (fetch THAUXINFO of EVENT)
                FROMCH#
                'NOCOPY)
         (\TEDIT.UPDATE.LINES TOOBJ FROMCH# LEN NIL (fetch (TEXTOBJ SEL) of (fetch THAUXINFO
                                                                               of EVENT)))
                                                             (* ; 
                                                             "Say this is the last selection made")
         (replace THACTION of EVENT with 'Move)              (* ; 
   "If the move started with a bluepending delete, that would have to be undone as a separate event.")
         (swap (fetch THTEXTOBJ of EVENT)
               (fetch THAUXINFO of EVENT))                   (* ; "Swap to undo the undo")
         (swap (fetch THCH# of EVENT)
               (fetch THOLDINFO of EVENT))
         (\TEDIT.HISTORYADD TOOBJ EVENT])

(TEDIT.UNDO.REPLACE
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited  4-May-2023 14:50 by rmk")
                                                             (* ; "Edited  3-May-2023 09:18 by rmk")
                                                             (* ; "Edited 18-Apr-2023 23:37 by rmk")
                                                             (* ; "Edited 30-May-91 21:27 by jds")
    (PROG ((OLDEVENT (fetch THOLDINFO of EVENT))
           (CH# (fetch THCH# of EVENT))
           (SEL (fetch (TEXTOBJ SEL) of TEXTOBJ)))
          (\SHOWSEL SEL NIL)
          (TEDIT.UNDO.INSERTION TEXTOBJ EVENT (fetch THLEN of EVENT)
                 CH#
                 (fetch THFIRSTPIECE of EVENT))
          (\SHOWSEL SEL NIL)
          (TEDIT.UNDO.DELETION TEXTOBJ OLDEVENT (fetch THLEN of OLDEVENT)
                 CH#
                 (fetch THFIRSTPIECE of OLDEVENT))
          (replace THOLDINFO of OLDEVENT with EVENT)
          (replace THACTION of OLDEVENT with 'Replace)
          (replace THOLDINFO of EVENT with NIL)
          (\TEDIT.HISTORYADD TEXTOBJ OLDEVENT)
          (SETSEL SEL CH# CH#)
          (FSETSEL SEL CHLIM (IPLUS CH# (fetch THLEN of OLDEVENT)))
          (FSETSEL SEL DCH (fetch THLEN of OLDEVENT))
          (FSETSEL SEL POINT (fetch THPOINT of EVENT))
          (replace THPOINT of OLDEVENT with (fetch THPOINT of EVENT))
          (\FIXSEL SEL TEXTOBJ)
          (\SHOWSEL SEL T])

(TEDIT.REDO.REPLACE
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited 23-Apr-2023 15:13 by rmk")
                                                             (* ; "Edited 18-Apr-2023 23:43 by rmk")
                                                             (* ; "Edited 19-Mar-2023 12:26 by rmk")
                                                             (* ; "Edited 22-Oct-2022 14:09 by rmk")
                                                             (* ; "Edited 30-May-91 21:28 by jds")
    (PROG ((OLDEVENT (fetch THOLDINFO of EVENT))
           (SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
           CH#)
          (SETQ CH# (GETSEL SEL CH#))
          (\TEDIT.DELETE SEL TEXTOBJ)
          (SETSEL SEL POINT 'LEFT)
          (TEDIT.REDO.INSERTION TEXTOBJ EVENT CH#)
          (SETQ OLDEVENT (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ))
          (replace THOLDINFO of EVENT with OLDEVENT)
          (replace THACTION of OLDEVENT with 'Replace)
          (replace THACTION of EVENT with 'Replace)
          (replace THCH# of EVENT with CH#)
          (\TEDIT.HISTORYADD TEXTOBJ EVENT])

(TEDIT.REDO.MOVE
  [LAMBDA (TEXTOBJ EVENT LEN CH# FIRSTPIECE)                 (* ; "Edited 30-May-91 21:28 by jds")
    (PROG ((FROMOBJ TEXTOBJ)
           (SOURCECH# (fetch THOLDINFO of EVENT))
           (OLDCH# (fetch THCH# of EVENT))
           (SEL (fetch (TEXTOBJ SEL) of TEXTOBJ))
           (MOVESEL (fetch (TEXTOBJ MOVESEL) of TEXTOBJ))
           OLDCHLIM)
          (replace (SELECTION CH#) of MOVESEL with OLDCH#)
          (replace (SELECTION CHLIM) of MOVESEL with (IPLUS OLDCH# LEN))
          (replace (SELECTION DCH) of MOVESEL with LEN)
          (replace (SELECTION SET) of MOVESEL with T)
          (\FIXSEL MOVESEL TEXTOBJ)
          (\TEDIT.SET.SEL.LOOKS MOVESEL 'MOVE)
          (TEDIT.MOVE MOVESEL SEL])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (3772 7254 (\TEDIT.HISTORYADD 3782 . 5807) (\TEDIT.CUMULATE.EVENTS 5809 . 7252)) (7307 
29822 (TEDIT.UNDO 7317 . 10629) (TEDIT.UNDO.INSERTION 10631 . 11593) (TEDIT.UNDO.DELETION 11595 . 
15511) (TEDIT.REDO 15513 . 20674) (TEDIT.REDO.INSERTION 20676 . 23665) (TEDIT.UNDO.MOVE 23667 . 26129)
 (TEDIT.UNDO.REPLACE 26131 . 27759) (TEDIT.REDO.REPLACE 27761 . 28999) (TEDIT.REDO.MOVE 29001 . 29820)
))))
STOP
