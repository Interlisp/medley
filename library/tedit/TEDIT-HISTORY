(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "30-Dec-2023 22:19:39" {WMEDLEY}<library>tedit>TEDIT-HISTORY.;133 32121  

      :EDIT-BY rmk

      :CHANGES-TO (FNS \TEDIT.HISTORYADD \TEDIT.CUMULATE.EVENTS TEDIT.UNDO \TEDIT.UNDO1)

      :PREVIOUS-DATE "21-Dec-2023 11:59:58" {WMEDLEY}<library>tedit>TEDIT-HISTORY.;131)


(PRETTYCOMPRINT TEDIT-HISTORYCOMS)

(RPAQQ TEDIT-HISTORYCOMS
       ((DECLARE%: EVAL@COMPILE DONTCOPY (EXPORT (RECORDS TEDITHISTORYEVENT)
                                                (MACROS \TEDIT.LASTEVENT \TEDIT.POPEVENT GETTH SETTH)
                                                ))
        (FNS \TEDIT.HISTORYEVENT.DEFPRINT)
        (INITRECORDS TEDITHISTORYEVENT)
        (GLOBALVARS TEDIT.HISTORY.TYPELST TEDIT.HISTORYLST)
        (INITVARS (TEDIT.HISTORY.TYPELST NIL)
               (TEDIT.HISTORYLST NIL))
        (COMS 
              (* ;; "History-list maintenance functions")

              (FNS \TEDIT.HISTORYADD \TEDIT.CUMULATE.EVENTS))
        (COMS 
              (* ;; "Specialized UNDO & REDO functions.")

              (FNS TEDIT.UNDO \TEDIT.UNDO1 TEDIT.REDO TEDIT.UNDO.UNDO)
              (FNS \TEDIT.UNDO.INSERTION \TEDIT.UNDO.DELETION \TEDIT.UNDO.REPLACE)
              (FNS \TEDIT.REDO.INSERTION \TEDIT.REDO.REPLACE \TEDIT.REDO.MOVE))))
(DECLARE%: EVAL@COMPILE DONTCOPY 
(* "FOLLOWING DEFINITIONS EXPORTED")(DECLARE%: EVAL@COMPILE

(DATATYPE TEDITHISTORYEVENT (
                             (* ;; "Describes one event on the TEdit edit history list.")

                             THACTION                        (* ; 
                                                           "A LITATOM, specifying what the event was")
                             THPOINT                         (* ; 
                                                            "Was the selection to the left or right?")
                             THLEN                           (* ; "The # of chars involved")
                             THCH#                           (* ; "The starting ch#")
                             THFIRSTPIECE                    (* ; "First piece involved")
                             THOLDINFO                       (* ; "Old info, for undo")
                             NIL                             (* ; 
                                  "Was THAUXINFO: Auxiliary info about the event, primarily for redo")
                             THTEXTOBJ

                             (* ;; "Place to remember a different textobj, for those like MOVE who need to remember both a source and a destination. Only used by TEDIT.UNDO.MOVE, but that's because it is trying to undo both sides of a cross-copy from an undo in the original source.  Old code wasn't good")

                             )
                            (SYNONYM THTEXTOBJ (THDELETEDPIECES))
                            [ACCESSFNS TEDITHISTORYEVENT ((THCHLIM (AND (fetch (TEDITHISTORYEVENT
                                                                                THCH#) of DATUM)
                                                                        (IPLUS (fetch (
                                                                                    TEDITHISTORYEVENT
                                                                                       THCH#)
                                                                                  of DATUM)
                                                                               (fetch (
                                                                                    TEDITHISTORYEVENT
                                                                                       THLEN)
                                                                                  of DATUM]
                            (INIT (DEFPRINT 'TEDITHISTORYEVENT (FUNCTION \TEDIT.HISTORYEVENT.DEFPRINT
                                                                )))
                            THPOINT _ 'LEFT)
)

(/DECLAREDATATYPE 'TEDITHISTORYEVENT '(POINTER POINTER POINTER POINTER POINTER POINTER POINTER 
                                             POINTER)
       '((TEDITHISTORYEVENT 0 POINTER)
         (TEDITHISTORYEVENT 2 POINTER)
         (TEDITHISTORYEVENT 4 POINTER)
         (TEDITHISTORYEVENT 6 POINTER)
         (TEDITHISTORYEVENT 8 POINTER)
         (TEDITHISTORYEVENT 10 POINTER)
         (TEDITHISTORYEVENT 12 POINTER)
         (TEDITHISTORYEVENT 14 POINTER))
       '16)

(DEFPRINT 'TEDITHISTORYEVENT (FUNCTION \TEDIT.HISTORYEVENT.DEFPRINT))
(DECLARE%: EVAL@COMPILE 

(PUTPROPS \TEDIT.LASTEVENT MACRO ((TOBJ)
                                  (CAR (fetch (TEXTOBJ TXTHISTORY) of TOBJ))))

(PUTPROPS \TEDIT.POPEVENT MACRO ((TOBJ)
                                 (pop (fetch (TEXTOBJ TXTHISTORY) of TOBJ))))

(PUTPROPS GETTH MACRO ((EVENT FIELD)
                       (fetch (TEDITHISTORYEVENT FIELD) of EVENT)))

(PUTPROPS SETTH MACRO ((EVENT FIELD NEWVALUE)
                       (replace (TEDITHISTORYEVENT FIELD) of EVENT with NEWVALUE)))
)

(* "END EXPORTED DEFINITIONS")

)
(DEFINEQ

(\TEDIT.HISTORYEVENT.DEFPRINT
  [LAMBDA (EVENT STREAM)                                     (* ; "Edited 24-May-2023 23:36 by rmk")
                                                             (* ; "Edited 22-May-2023 14:42 by rmk")
                                                             (* ; "Edited 21-May-2023 09:15 by rmk")
    (LET (INFO LOC)
         (SETQ INFO (CONCAT (fetch (TEDITHISTORYEVENT THACTION) of EVENT)
                           " "
                           (fetch (TEDITHISTORYEVENT THCH#) of EVENT)
                           "-"
                           (fetch (TEDITHISTORYEVENT THLEN) of EVENT)
                           "-"
                           (NTHCHAR (fetch (TEDITHISTORYEVENT THPOINT) of EVENT)
                                  1)))
         (SETQ LOC (LOC EVENT))
         (CONS (CONCAT "{TH" ":" INFO " " (CAR LOC)
                      "/"
                      (CDR LOC)
                      "}"])
)

(/DECLAREDATATYPE 'TEDITHISTORYEVENT '(POINTER POINTER POINTER POINTER POINTER POINTER POINTER 
                                             POINTER)
       '((TEDITHISTORYEVENT 0 POINTER)
         (TEDITHISTORYEVENT 2 POINTER)
         (TEDITHISTORYEVENT 4 POINTER)
         (TEDITHISTORYEVENT 6 POINTER)
         (TEDITHISTORYEVENT 8 POINTER)
         (TEDITHISTORYEVENT 10 POINTER)
         (TEDITHISTORYEVENT 12 POINTER)
         (TEDITHISTORYEVENT 14 POINTER))
       '16)

(DEFPRINT 'TEDITHISTORYEVENT (FUNCTION \TEDIT.HISTORYEVENT.DEFPRINT))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS TEDIT.HISTORY.TYPELST TEDIT.HISTORYLST)
)

(RPAQ? TEDIT.HISTORY.TYPELST NIL)

(RPAQ? TEDIT.HISTORYLST NIL)



(* ;; "History-list maintenance functions")

(DEFINEQ

(\TEDIT.HISTORYADD
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited 30-Dec-2023 22:19 by rmk")
                                                             (* ; "Edited 11-Aug-2023 14:25 by rmk")
                                                             (* ; "Edited 14-Jun-2023 16:04 by rmk")
                                                             (* ; "Edited 12-Jun-2023 10:26 by rmk")
                                                             (* ; "Edited  3-Jun-2023 20:41 by rmk")
                                                             (* ; "Edited 28-May-2023 00:07 by rmk")
                                                             (* ; "Edited  3-Sep-87 10:36 by jds")

    (* ;; "Add a new event to the history list, unless the list is currently DON'T (as in middle of foreign get).")

    (* ;; "Not sure what should happen if the second one is to the right of the first, deleting forwards.  Old code seemed to treat those as separate events, and  only the second/right one could be undone.")

    (CL:UNLESS (EQ 'DON'T (GETTOBJ TEXTOBJ TXTHISTORY))
        (if (type? TEDITHISTORYEVENT EVENT)
            then (CL:WHEN (MEMB (GETTH EVENT THACTION)
                                (CONSTANT (LIST :Put :Get))) (* ; 
                                                     "Can't back up over Put/Get, flush the history.")
                     (FSETTOBJ TEXTOBJ TXTHISTORY NIL)) 

                 (* ;; "Somebody may have already done there own fixup.")

                 (LET ((OLDEVENT (\TEDIT.LASTEVENT TEXTOBJ)))
                      (CL:WHEN (AND (type? TEDITHISTORYEVENT OLDEVENT)
                                    (EQ :Delete (GETTH EVENT THACTION))
                                    (EQ :Delete (GETTH OLDEVENT THACTION)))

                          (* ;; 
                          "Repeated successive deletions, we can combine them if they are adjacent.")

                          (CL:WHEN (IEQP (GETTH EVENT THCHLIM)
                                         (GETTH OLDEVENT THCH#))
                                                             (* ; 
                                                           "OLDEVENT is first, EVENT is still delete")
                              (SETQ EVENT (\TEDIT.CUMULATE.EVENTS EVENT OLDEVENT))
                              (\TEDIT.POPEVENT TEXTOBJ)      (* ; "Pop OLDEVENT before repushing")
                              (SETQ OLDEVENT (\TEDIT.LASTEVENT TEXTOBJ)))

                          (* ;; "This may have created a new adjacency, if the accumulation of later deletes comes into with an earlier accumulation")

                          (CL:WHEN [AND OLDEVENT (EQ :Delete (GETTH OLDEVENT THACTION))
                                        (IEQP (GETTH OLDEVENT THCHLIM)
                                              (IPLUS (GETTH EVENT THCH#)
                                                     (GETTH OLDEVENT THLEN]

                              (* ;; "The OLDEEVENT deleted in front of EVENT, and itsTCHLIM are in its original coordinates.  EVENT came later, with its TCH# in a coordinate system reduced by THLEN.  So we have to add it back.")

                              (SETQ EVENT (\TEDIT.CUMULATE.EVENTS OLDEVENT EVENT))
                              (\TEDIT.POPEVENT TEXTOBJ)))
                      (push (GETTOBJ TEXTOBJ TXTHISTORY)
                            EVENT))
          elseif (LISTP EVENT)
            then 
                 (* ;; "A monolithic sequence of undoable events")

                 (push (GETTOBJ TEXTOBJ TXTHISTORY)
                       EVENT)))
    EVENT])

(\TEDIT.CUMULATE.EVENTS
  [LAMBDA (EVENT1 EVENT2)                                    (* ; "Edited  3-Jun-2023 17:09 by rmk")
                                                             (* ; "Edited 27-May-2023 00:54 by rmk")
                                                             (* ; "Edited 25-May-2023 23:58 by rmk")
                                                             (* ; "Edited 21-May-2023 13:14 by rmk")
                                                             (* ; "Edited 17-May-2023 14:55 by rmk")
                                                             (* ; "Edited  3-Sep-87 10:42 by jds")

    (* ;; "Accumulate history events that should be combined into a undoable single even.")

    (* ;; "For now, this assumes they're events of the same type.  Actually, this should be able to cumulate a delete/insert pair into a replacement, etc.")

    (SETTH EVENT1 THDELETEDPIECES (\SELPIECES.CONCAT (GETTH EVENT1 THDELETEDPIECES)
                                         (GETTH EVENT2 THDELETEDPIECES)))
    (SETTH EVENT1 THLEN (fetch (SELPIECES SPLEN) of (GETTH EVENT1 THDELETEDPIECES)))
    EVENT1])
)



(* ;; "Specialized UNDO & REDO functions.")

(DEFINEQ

(TEDIT.UNDO
  [LAMBDA (TEXTOBJ)                                          (* ; "Edited 22-Nov-2023 18:17 by rmk")
                                                             (* ; "Edited 27-Sep-2023 00:14 by rmk")
                                                             (* ; "Edited 23-Jun-2023 00:19 by rmk")
                                                            (* ; "Edited 12-Jun-90 18:41 by mitani")

    (* ;; "Undo the last thing this guy did.  This could be a sequence of subevents for a single user-level action that has more than one component:  e.g. move or replace is (Insert Delete). Undoing  each (sub)event must restore the status quo ante (pieces, lines, looks, SEL).  ")

    (* ;; "We push information for undoing the undo onto the TXTHISTORYUNDO list.")

    (\DTEST TEXTOBJ 'TEXTOBJ)
    (CL:UNLESS (FGETTOBJ TEXTOBJ TXTREADONLY)

        (* ;; "Only undo things if the document is allowed to change.")

        (TEDIT.PROMPTPRINT TEXTOBJ "" T)
        (PROG ((SEL (TEXTSEL TEXTOBJ))
               (EVENT (\TEDIT.POPEVENT TEXTOBJ))
               PREVEVENTS UNDOEVENT)
              (CL:UNLESS EVENT
                  (TEDIT.PROMPTPRINT TEXTOBJ "Nothing to undo" T)
                  (RETURN))
              (PROGN                                         (* ; "RESETSAVE for cursor ?")
                     (AND NIL (RESETSAVE (CURSOR WAITINGCURSOR)))
                                                             (* ; "No longer needed?")
                     (\SHOWSEL SEL NIL)

                     (* ;; "Each main event was popped.  Each subfunction must put back on the history-undo  list one or more new events that would undo its undoing.  ")

                     (* ;; "We can get into trouble if there is an interrupt in the middle of undoing the full set of events for a previous action, or even in the middle of a singleton event.")

                     (SETQ PREVEVENTS (FGETTOBJ TEXTOBJ TXTHISTORY))
                     (for E inside EVENT do (\TEDIT.UNDO1 TEXTOBJ E)
                                            (\SHOWSEL SEL NIL))
                                                             (* ; "Turn off intermediate selections")

                     (* ;; 
         "Gather into a single (list) event all the events that were created as part of the undo.  .")

                     [SETQ UNDOEVENT (for ETAIL on (FGETTOBJ TEXTOBJ TXTHISTORY)
                                        until (EQ ETAIL PREVEVENTS) collect (CAR ETAIL)
                                        finally (RETURN (CL:IF (CDR $$VAL)
                                                            $$VAL
                                                            (CAR $$VAL))]
                     (FSETTOBJ TEXTOBJ TXTHISTORY PREVEVENTS)
                     (CL:WHEN [OR (NULL PREVEVENTS)
                                  (AND (type? TEDITHISTORYEVENT (CAR (LISTP PREVEVENTS)))
                                       (MEMB (GETTH (CAR PREVEVENTS)
                                                    THACTION)
                                             (CONSTANT (LIST :Get :Put]
                            (SETTOBJ TEXTOBJ \DIRTY NIL))

                     (* ;; "The undone list keeps the event that would undo the undoing, the event that was just undone, and the history event that would be undone next (by M-u).  This is so that M-U can undo the undoing.")

                     (push (FGETTOBJ TEXTOBJ TXTHISTORYUNDONE TEXTOBJ)
                           (LIST (CAR PREVEVENTS)
                                 UNDOEVENT EVENT))
                     (\FIXSEL SEL TEXTOBJ)
                     (\SHOWSEL SEL T))))])

(\TEDIT.UNDO1
  [LAMBDA (TEXTOBJ E)                                        (* ; "Edited 21-Dec-2023 11:59 by rmk")
                                                             (* ; "Edited 22-Nov-2023 18:27 by rmk")
                                                             (* ; "Edited 16-Jul-2023 11:14 by rmk")
                                                             (* ; "Edited 30-May-2023 23:50 by rmk")
                                                             (* ; "Edited 25-May-2023 00:33 by rmk")
    (SELECTC (GETTH E THACTION)
        ((LIST :Insert :Copy) 
             (\TEDIT.UNDO.INSERTION TEXTOBJ E))
        (:Delete                                             (* ; "Deletion or case-shift")
                 (\TEDIT.UNDO.DELETION TEXTOBJ E))
        (:Looks                                              (* ; "Character-looks change")
                (\TEDIT.UNDO.LOOKS TEXTOBJ E))
        (:ParaLooks                                          (* ; "PARA looks change")
                    (\TEDIT.UNDO.PARALOOKS TEXTOBJ E))
        (:PageFormat                                         (* ; "Pageframe change")
                     [SETTOBJ TEXTOBJ TXTPAGEFRAMES (PROG1 (GETTH E THOLDINFO)
                                                        (SETTH E THOLDINFO (GETTOBJ TEXTOBJ 
                                                                                  TXTPAGEFRAMES)))]
                     (\TEDIT.HISTORYADD TEXTOBJ E))
        ((LIST :Replace :LowerCase :UpperCase) 
                                               (* ;; "He replaced one piece of text with another ; Lower-casing and upper-casing have the same undo event.")

             (\TEDIT.UNDO.REPLACE TEXTOBJ E (GETTH E THACTION)))
        (:Closefile                                          (* ; "Closes an included file")
                    (CL:WHEN (STREAMP (GETTH E THOLDINFO))
                        (CLOSEF? (GETTH E THOLDINFO))))
        ((LIST :Get :Put)                                    (* ; 
                                                             "He did a GET or PUT-- not undoable.")
             (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "You can't undo a " (GETTH E THACTION))
                    T))
        (LET [(UNDOFN (CADDR (ASSOC (GETTH E THACTION)
                                    TEDIT.HISTORY.TYPELST]
             (COND
                (UNDOFN 

                       (* ;; "ùTEDIT.HISTORY.TYPELST is an ALST of form (type redofn undofn)")

                       (APPLY* UNDOFN TEXTOBJ E (GETTH E THLEN)
                              (GETTH E THCH#)
                              (GETTH E THFIRSTPIECE)))
                (T (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "UNDO not implemented for " (GETTH E THACTION))
                          T])

(TEDIT.REDO
  [LAMBDA (TEXTOBJ)                                          (* ; "Edited 21-Dec-2023 11:57 by rmk")
                                                             (* ; "Edited 22-Nov-2023 18:28 by rmk")
                                                             (* ; "Edited 16-Jul-2023 08:07 by rmk")
                                                             (* ; "Edited  7-Jun-2023 23:14 by rmk")
                                                             (* ; "Edited  6-Jun-2023 21:33 by rmk")
                                                             (* ; "Edited 31-May-2023 10:20 by rmk")
                                                             (* ; "Edited 28-May-2023 19:15 by rmk")
                                                             (* ; "Edited 27-May-2023 11:19 by rmk")
                                                             (* ; "Edited 30-May-91 21:27 by jds")

    (* ;; "REDO the last thing this guy did.")

    (CL:UNLESS (GETTOBJ TEXTOBJ TXTREADONLY)
        (PROG ((SEL (GETTOBJ TEXTOBJ SEL))
               (EVENT (\TEDIT.LASTEVENT TEXTOBJ))
               CH)
              (CL:UNLESS EVENT
                  (TEDIT.PROMPTPRINT TEXTOBJ "Nothing to redo" T)
                  (RETURN))
              (CL:UNLESS (GETSEL SEL SET)
                  (TEDIT.PROMPTPRINT TEXTOBJ "Please select a target for the repeated action" T)
                  (RETURN))

         (* ;; "There really is something to redo and something to do it to.")

              (PROGN                                         (* ; 
                                                         "Was RESETLST: No need for waiting cursor ?")
                     (AND NIL (RESETSAVE (CURSOR WAITINGCURSOR)))
                     (\SHOWSEL SEL NIL)
                     (SELECTC (GETTH EVENT THACTION)
                         ((LIST :Insert :Copy)               (* ; "It was an insertion")
                              (\TEDIT.REDO.INSERTION TEXTOBJ EVENT SEL))
                         (:Delete                            (* ; "It was a deletion")
                                  (\TEDIT.DELETE TEXTOBJ SEL))
                         (:Replace                           (* ; 
                                                          "It was a replacement (a del/insert combo)")
                                   (\TEDIT.REDO.REPLACE TEXTOBJ EVENT (GETTH EVENT THACTION)))
                         (:LowerCase                         (* ; "He lower-cased something")
                                     (\TEDIT.LCASE.SEL TEXTOBJ TEXTOBJ SEL))
                         (:UpperCase                         (* ; "He upper-cased something")
                                     (\TEDIT.UCASE.SEL TEXTOBJ TEXTOBJ SEL))
                         (:Looks                             (* ; "It was a character looks change")
                                 (TEDIT.LOOKS TEXTOBJ (PLOOKS (GETTH EVENT THFIRSTPIECE))
                                        SEL))
                         (:ParaLooks                         (* ; "It was a Paragraph looks change")
                                     (TEDIT.PARALOOKS TEXTOBJ (PPARALOOKS (GETTH EVENT THFIRSTPIECE))
                                            SEL))
                         (:PageFormat (TEDIT.PROMPTPRINT TEXTOBJ 
                                             "You can't redo a page-format change" T T))
                         (:Find                              (* ; "EXACT-MATCH SEARCH COMMAND")
                                                             (* (* ;; "RESTLST ?")
                                                             (AND NIL (RESETSAVE (CURSOR 
                                                             WAITINGCURSOR))) (TEDIT.PROMPTPRINT 
                                                             TEXTOBJ "Searching..." T)
                                                             (SETQ SEL (fetch (TEXTOBJ SEL) of 
                                                             TEXTOBJ)) (\SHOWSEL SEL NIL)
                                                             (SETQ CH (TEDIT.FIND TEXTOBJ
                                                             (GETTH EVENT THAUXINFO)))
                                                             (COND (CH (TEDIT.PROMPTPRINT TEXTOBJ 
                                                             "done.") (\TEDIT.UPDATE.SEL SEL CH
                                                             (NCHARS (GETTH EVENT THAUXINFO))
                                                             (QUOTE RIGHT)) (\FIXSEL SEL TEXTOBJ)
                                                             (TEDIT.NORMALIZECARET TEXTOBJ)
                                                             (\SHOWSEL SEL T)) (T
                                                             (TEDIT.PROMPTPRINT TEXTOBJ "[Not found]"))))
                                )
                         (:Move                              (* ; "He moved some text")
                                (\TEDIT.REDO.MOVE TEXTOBJ EVENT (GETTH EVENT THLEN)
                                       (IMAX 1 (TEDIT.GETPOINT NIL SEL))
                                       (GETTH EVENT THFIRSTPIECE)))
                         ((LIST :Get :Put)                   (* ; "Why can't you redo a get or put ?")
                              (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "You can't redo a " (GETTH EVENT 
                                                                                            THACTION)
                                                                )
                                     T T))
                         (TEDIT.PROMPTPRINT TEXTOBJ (CONCAT "Redoing the action " (GETTH EVENT 
                                                                                         THACTION)
                                                           " isn't implemented.")
                                T))
                     (\SHOWSEL SEL T))))])

(TEDIT.UNDO.UNDO
  [LAMBDA (TEXTOBJ)                                          (* ; "Edited 13-Jun-2023 15:05 by rmk")
                                                             (* ; "Edited  3-Jun-2023 23:04 by rmk")
                                                             (* ; "Edited  1-Jun-2023 23:53 by rmk")

    (* ;; 
  "This undoes a preceding undo, by pushing the undoing event on the history list, and undoing that.")

    (* ;; "The state is recorded as the event that would be undone next")

    (* ;; "This makes sense only if the document is now in the state immediately after the undoing--if any other events have intervened, the character positions and the general state of the document are unrelated. So the elements of the undo list also contain the state of the (forward) history list after the undoing was undone.  If we have moved back to the same point in history, we can do the undoing.")

    (SETQ TEXTOBJ (TEXTOBJ TEXTOBJ))
    (TEDIT.PROMPTPRINT TEXTOBJ "" T)
    (LET [(LASTUNDONE (pop (fetch (TEXTOBJ TXTHISTORYUNDONE) of TEXTOBJ]
         (if (NULL LASTUNDONE)
             then (TEDIT.PROMPTPRINT TEXTOBJ "There is no action whose undoing can be reversed" T)
           elseif (EQ (CAR LASTUNDONE)
                      (\TEDIT.LASTEVENT TEXTOBJ))
             then 
                  (* ;; "We tell TEDIT.UNDO that LASTUNDONE is the one we now want to undo.")

                  (push (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ)
                        (CADR LASTUNDONE))
                  (TEDIT.UNDO TEXTOBJ) 

                  (* ;; "This saved what we just undid, don't want to keep reundoing it.")

                  (pop (fetch (TEXTOBJ TXTHISTORYUNDONE) of TEXTOBJ))
                  (push (fetch (TEXTOBJ TXTHISTORY) of TEXTOBJ)
                        (CADDR LASTUNDONE))
           else (replace (TEXTOBJ TXTHISTORYUNDONE) of TEXTOBJ with NIL) 
                                                             (* ; 
                                        "If something else has happened, there are no undos to undo.")
                (TEDIT.PROMPTPRINT TEXTOBJ "Cannot undo the previous undo" T])
)
(DEFINEQ

(\TEDIT.UNDO.INSERTION
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited 30-May-2023 22:54 by rmk")
                                                             (* ; "Edited 26-May-2023 23:49 by rmk")
                                                             (* ; "Edited 24-May-2023 23:53 by rmk")
                                                             (* ; "Edited  2-May-2023 23:26 by rmk")
                                                             (* ; "Edited 21-Apr-93 01:33 by jds")

    (* ;; "UNDO a prior Insert, Copy, or Include. ")

    (\TEDIT.DELETE TEXTOBJ (\TEDIT.UPDATE.SEL (fetch (TEXTOBJ SEL) of TEXTOBJ)
                                  EVENT])

(\TEDIT.UNDO.DELETION
  [LAMBDA (TEXTOBJ EVENT)                                    (* ; "Edited 30-May-2023 23:31 by rmk")
                                                             (* ; "Edited 27-May-2023 23:39 by rmk")
                                                             (* ; "Edited 21-Apr-93 12:01 by jds")

    (* ;; "UNDO a prior deletion ")

    (\TEDIT.INSERT.SELPIECES (\SELPIECES.COPY (GETTH EVENT THDELETEDPIECES)
                                    'INSERT TEXTOBJ)
           TEXTOBJ
           (GETTH EVENT THCH#])

(\TEDIT.UNDO.REPLACE
  [LAMBDA (TEXTOBJ EVENT ACTION)                             (* ; "Edited 30-May-2023 23:10 by rmk")
                                                             (* ; "Edited 27-May-2023 16:49 by rmk")
                                                             (* ; "Edited 24-May-2023 22:43 by rmk")

    (* ;; "This undoes the replacement, but tracks for REDO whether the action was replace, lowercase, or uppercase.")

    (\TEDIT.REPLACE.SELPIECES (\SELPIECES.COPY (GETTH EVENT THDELETEDPIECES)
                                     NIL TEXTOBJ)
           TEXTOBJ
           (\TEDIT.UPDATE.SEL (fetch (TEXTOBJ SEL) of TEXTOBJ)
                  EVENT))
    (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
           THACTION ACTION])
)
(DEFINEQ

(\TEDIT.REDO.INSERTION
  [LAMBDA (TEXTOBJ EVENT SEL)                                (* ; "Edited 31-May-2023 10:26 by rmk")
                                                             (* ; "Edited 18-May-2023 19:24 by rmk")
                                                             (* ; "Edited 21-Apr-93 01:06 by jds")

    (* ;; "Copies of the pieces inserted at the previous insertion EVENT are inserted at SEL's caret.  We can extract the relevant pieces from the event's text position, because we know that either EVENT was the last event or other events after it have been undone, and the pieces are back to their original state.")

    (\TEDIT.INSERT.SELPIECES (\SELPIECES.COPY (\SELPIECES EVENT NIL TEXTOBJ)
                                    'INSERT TEXTOBJ)
           TEXTOBJ SEL])

(\TEDIT.REDO.REPLACE
  [LAMBDA (TEXTOBJ EVENT ACTION)                             (* ; "Edited  2-Oct-2023 11:43 by rmk")
                                                             (* ; "Edited 31-May-2023 10:25 by rmk")
                                                             (* ; "Edited 27-May-2023 11:16 by rmk")
                                                             (* ; "Edited 16-May-2023 22:05 by rmk")
                                                             (* ; "Edited 30-May-91 21:28 by jds")

    (* ;; "We get the replacement from where EVENT just installed it in the text (assume that it is still there unchanged), and then we use it to replace what is now at the current selection.  EVENT's deleted pieces are not relevant.")

    (\TEDIT.REPLACE.SELPIECES (\SELPIECES.COPY (\SELPIECES EVENT NIL TEXTOBJ)
                                     NIL TEXTOBJ)
           TEXTOBJ
           (\TEDIT.UPDATE.SEL (fetch (TEXTOBJ SEL) of TEXTOBJ)
                  EVENT))
    (SETTH (\TEDIT.LASTEVENT TEXTOBJ)
           THACTION ACTION])

(\TEDIT.REDO.MOVE
  [LAMBDA (TEXTOBJ EVENT LEN CH# FIRSTPIECE)                 (* ; "Edited  7-Jun-2023 23:19 by rmk")
                                                             (* ; "Edited 27-May-2023 11:18 by rmk")
                                                             (* ; "Edited 23-May-2023 12:54 by rmk")
                                                             (* ; "Edited 30-May-91 21:28 by jds")
    (LET ((MOVESEL (fetch (TEXTOBJ MOVESEL) of TEXTOBJ)))
         (\TEDIT.UPDATE.SEL MOVESEL (GETTH EVENT THCH#)
                LEN)
         (SETSEL MOVESEL SET T)
         (\FIXSEL MOVESEL TEXTOBJ)
         (\TEDIT.SET.SEL.LOOKS MOVESEL 'MOVE)
         (TEDIT.MOVE MOVESEL (TEXTSEL TEXTOBJ])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (5273 6294 (\TEDIT.HISTORYEVENT.DEFPRINT 5283 . 6292)) (7060 12048 (\TEDIT.HISTORYADD 
7070 . 10841) (\TEDIT.CUMULATE.EVENTS 10843 . 12046)) (12101 27241 (TEDIT.UNDO 12111 . 15882) (
\TEDIT.UNDO1 15884 . 18753) (TEDIT.REDO 18755 . 24953) (TEDIT.UNDO.UNDO 24955 . 27239)) (27242 29373 (
\TEDIT.UNDO.INSERTION 27252 . 28009) (\TEDIT.UNDO.DELETION 28011 . 28583) (\TEDIT.UNDO.REPLACE 28585
 . 29371)) (29374 32098 (\TEDIT.REDO.INSERTION 29384 . 30213) (\TEDIT.REDO.REPLACE 30215 . 31335) (
\TEDIT.REDO.MOVE 31337 . 32096)))))
STOP
