(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "21-Oct-2024 00:27:47" {WMEDLEY}<library>tedit>TEDIT-STRESS.;71 15583  

      :EDIT-BY rmk

      :CHANGES-TO (FNS STRESSHC STRESSPUT EQTEXTSTREAM)

      :PREVIOUS-DATE "19-Mar-2024 21:34:32" {WMEDLEY}<library>tedit>TEDIT-STRESS.;70)


(PRETTYCOMPRINT TEDIT-STRESSCOMS)

(RPAQQ TEDIT-STRESSCOMS (                                    (* ; "Preload typical image objects")
                         (FNS STRESSHC STRESSRAND STRESSPUT STRESSOPEN STRESSREAD STRESSGREP 
                              STRESSPEEK)
                         (FNS EQTEXTSTREAM SYSOUTRING COPYTOCORE)))



(* ; "Preload typical image objects")

(DEFINEQ

(STRESSHC
  [LAMBDA (FILES NSYSOUTS REPS ERROR SEPARATEOUT PDF SYSOUTNAME SINGLESTEP)
                                                             (* ; "Edited 21-Oct-2024 00:26 by rmk")
                                                             (* ; "Edited 19-Mar-2024 21:33 by rmk")
                                                             (* ; "Edited 14-Mar-2024 15:15 by rmk")
                                                             (* ; "Edited 13-Mar-2024 00:23 by rmk")
    (DECLARE (SPECVARS SINGLESTEP))

    (* ;; "If all arguments are defaulted, runs through all TEDIT files in the current directory until it fails, doing SAVEVM before each file.  The HC files are made as {CORE}FOO.PS.")

    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (CL:UNLESS REPS (SETQ REPS MAX.SMALLP))
    (CL:UNLESS NSYSOUTS
        (SETQ NSYSOUTS 'SAVEVM))
    [SETQ SYSOUTNAME (PACKFILENAME 'VERSION NIL 'BODY (OR SYSOUTNAME (PACKFILENAME 'DIRECTORY 
                                                                            MEDLEYDIR 'NAME 
                                                                            "STRESSHC" 'EXTENSION
                                                                            'SYSOUT]
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files saving "
           (if (EQ NSYSOUTS 0)
               then "no sysouts"
             elseif (EQ NSYSOUTS 'SAVEVM)
               then " the virtual memory"
             else (PRINTOUT NIL NSYSOUTS " sysouts on " 3)
                  SYSOUTNAME)
           T)
    (PRINTOUT T "First file is " (CAR FILES)
           T T)
    (BKSYSBUF " ")
    (for R SYSOUTS (ITYPE _ (CL:IF PDF
                                'pdf
                                'ps))
         (N _ 0) from 1 to REPS
       do (PRINTOUT T "Rep " R T)
          (if (EQ NSYSOUTS 'SAVEVM)
              then (SAVEVM)
            else (SETQ SYSOUTS (SYSOUTRING NSYSOUTS SYSOUTNAME SYSOUTS)))
          [for F TSTRM HCFILE inside FILES
             do (PROMPTPRINT F)
                (SETQ HCFILE (CL:IF SEPARATEOUT
                                 (OUTFILEP (PACKFILENAME 'EXTENSION ITYPE 'VERSION 1 'BODY F))
                                 (CL:IF PDF
                                     "{CORE}FOO.PDF;1"
                                     "{CORE}FOO.PS;1")))
                (if [if ERROR
                        then (SETQ TSTRM (OPENTEXTSTREAM F))
                             (TEDIT.FORMAT.HARDCOPY TSTRM HCFILE T NIL NIL NIL (CL:IF PDF
                                                                                   'PDF
                                                                                   'POSTSCRIPT))
                      else (NLSETQ (SETQ TSTRM (OPENTEXTSTREAM F))
                                  (TEDIT.FORMAT.HARDCOPY TSTRM HCFILE T NIL NIL NIL
                                         (CL:IF PDF
                                             'PDF
                                             'POSTSCRIPT)]
                    then (add N 1)
                  else (PRINTOUT T "  Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T))
                (CLOSEF? TSTRM)
                (CL:WHEN SINGLESTEP
                    (\TEDIT.THELP (CONCAT "Just hardcopied " F " to " HCFILE)))]
          (PRINTOUT T "  Hardcopied " N " files without failure" T)
       finally (RETURN (LIST R N])

(STRESSRAND
  [LAMBDA (FILES REPS ERROR PROBESPERFILE)                   (* ; "Edited 19-Mar-2024 21:33 by rmk")
                                                             (* ; "Edited 12-Mar-2024 09:47 by rmk")

    (* ;; "Opens, fetches random characters")

    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (CL:UNLESS PROBESPERFILE (SETQ PROBESPERFILE 100))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files with " PROBESPERFILE " probes per file" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TSTRM inside FILES
             do (if (if ERROR
                        then (SETQ TSTRM (OPENTEXTSTREAM F))
                             (for I (LEN _ (TEDIT.NCHARS TSTRM)) from 1 to PROBESPERFILE
                                do (TEDIT.NTHCHARCODE TSTRM (RAND 1 LEN)))
                             T
                      else (CAR (NLSETQ (SETQ TSTRM (OPENTEXTSTREAM F))
                                       (for I (LEN _ (TEDIT.NCHARS TSTRM)) from 1 to PROBESPERFILE
                                          do (TEDIT.NTHCHARCODE TSTRM (RAND 1 LEN)))
                                       T)))
                    then (CLOSEF TSTRM)
                         (add N 1)
                  else (PRINTOUT T "  Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T)) repeatwhile (PROGN (CLOSEF? TSTRM)
                                                     T)) finally (RETURN (LIST R N])

(STRESSPUT
  [LAMBDA (FILES REPS NOERROR CHECKEQUIV STOP)               (* ; "Edited 21-Oct-2024 00:26 by rmk")
                                                             (* ; "Edited 19-Mar-2024 21:34 by rmk")
                                                             (* ; "Edited 12-Mar-2024 09:48 by rmk")

    (* ;; "Opens, puts, reopens and tests for equivalence")

    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TSTRM TSP inside FILES
             do (if (if NOERROR
                        then (NLSETQ (SETQ TSTRM (OPENTEXTSTREAM F))
                                    (TEDIT.PUT TSTRM "{CORE}FOO.TEDIT;1")
                                    (SETQ TSP (OPENTEXTSTREAM "{CORE}FOO.TEDIT;1"))
                                    (CL:WHEN (AND CHECKEQUIV (NOT (EQTEXTSTREAM TSTRM TSP STOP)))
                                           (\TEDIT.THELP "Get of put not equivalent" F))
                                    (CLOSEF TSP))
                      else (SETQ TSTRM (OPENTEXTSTREAM F))
                           (TEDIT.PUT TSTRM "{CORE}FOO.TEDIT;1")
                           (SETQ TSP (OPENTEXTSTREAM "{CORE}FOO.TEDIT;1"))
                           (CL:WHEN (AND CHECKEQUIV (NOT (EQTEXTSTREAM TSTRM TSP STOP)))
                                  (\TEDIT.THELP "Get of put not equivalent" F))
                           (CLOSEF TSP))
                    then (CLOSEF TSTRM)
                         (add N 1)
                  else (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T))) finally (RETURN (LIST R N])

(STRESSOPEN
  [LAMBDA (FILES REPS NOERROR)                               (* ; "Edited 19-Mar-2024 21:34 by rmk")
                                                             (* ; "Edited 12-Mar-2024 09:48 by rmk")
                                                             (* ; "Edited 11-Mar-2024 09:15 by rmk")
    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TSTRM inside FILES do (if (if NOERROR
                                               then (NLSETQ (SETQ TSTRM (OPENTEXTSTREAM F)))
                                             else (SETQ TSTRM (OPENTEXTSTREAM F)))
                                           then (CLOSEF TSTRM)
                                                (add N 1)
                                         else (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL
                                                                                'DIRECTORY NIL
                                                                                'BODY F)
                                                     T))) finally (RETURN (LIST R N])

(STRESSREAD
  [LAMBDA (FILES REPS NOERROR)                               (* ; "Edited 19-Mar-2024 21:34 by rmk")
                                                             (* ; "Edited 15-Mar-2024 15:19 by rmk")
                                                             (* ; "Edited 12-Mar-2024 09:48 by rmk")
                                                             (* ; "Edited 11-Mar-2024 09:13 by rmk")
    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TSTRM inside FILES
             do (if (if NOERROR
                        then (NLSETQ (SETQ TSTRM (OPENTEXTSTREAM F))
                                    (for I from 1 while (TEDIT.NTHCHARCODE TSTRM I)))
                      else (SETQ TSTRM (OPENTEXTSTREAM F))
                           (for I from 1 while (TEDIT.NTHCHARCODE TSTRM I))
                           T)
                    then (CLOSEF TSTRM)
                         (add N 1)
                  else (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T))) finally (RETURN (LIST (SUB1 R)
                                                         N])

(STRESSGREP
  [LAMBDA (FILES NOERROR TARGET)                             (* ; "Edited 17-Mar-2024 19:46 by rmk")

    (* ;; "GREP does forward bins and peekbins.  If it hits on something, it also runs the backfileptr function.  FOO appears in quite a few lispusers/ Tedit files.")

    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (CL:UNLESS TARGET (SETQ TARGET "FOO"))
    (FILESLOAD GREP)
    (for F inside FILES unless (if NOERROR
                                   then (NLSETQ (GREP TARGET F))
                                 else (GREP TARGET F)
                                      T) do (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL
                                                                              'DIRECTORY NIL
                                                                              'BODY F)
                                                   T])

(STRESSPEEK
  [LAMBDA (FILES ERROR)                                      (* ; "Edited 19-Mar-2024 21:33 by rmk")
    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (for F TSTRM inside FILES eachtime (SETQ TSTRM (OPENTEXTSTREAM F))
       unless [if ERROR
                  then (bind P while (SETQ P (PEEKCCODE TSTRM T)) always (EQ P (BIN TSTRM)))
                else (NLSETQ (bind P while (SETQ P (PEEKCCODE TSTRM T))
                                always (EQ P (BIN TSTRM] do (PRINTOUT T "Error for "
                                                                   (PACKFILENAME 'HOST NIL
                                                                          'DIRECTORY NIL 'BODY F)
                                                                   T)
       repeatwhile (PROGN (CLOSEF? TSTRM)
                          T])
)
(DEFINEQ

(EQTEXTSTREAM
  [LAMBDA (TS1 TS2 STOP)                                     (* ; "Edited 21-Oct-2024 00:26 by rmk")
                                                             (* ; "Edited 11-Mar-2024 16:53 by rmk")
    (AND (IEQP (TEDIT.NCHARS TS1)
               (TEDIT.NCHARS TS2))
         (OR (for I C1 C2 from 1 to (TEDIT.NCHARS TS1) eachtime (SETQ C1 (TEDIT.NTHCHARCODE TS1 I))
                                                             (SETQ C2 (TEDIT.NTHCHARCODE TS2 I))
                unless (OR (EQ C1 C2)
                           (AND (EQ C1 10)
                                (EQ C2 13))
                           (AND (EQ C1 13)
                                (EQ C2 10))
                           (AND (IMAGEOBJP C1)
                                (IMAGEOBJP C2)
                                (EQUALALL C1 C2))) do (CL:WHEN STOP
                                                          (\TEDIT.THELP "Different characters: "
                                                                 (LIST I C1 C2)))
                                                      (RETURN NIL) finally (RETURN T])

(SYSOUTRING
  [LAMBDA (NSYSOUTS SYSOUTNAME SYSOUTS)                      (* ; "Edited 12-Mar-2024 17:52 by rmk")

    (* ;; "SYSOUTS is the list of names of sysouts that currently exist.")

    (DECLARE (USEDFREE SINGLESTEP))
    (CL:WHEN (IGREATERP NSYSOUTS 0)                          (* ; 
                                                      "Keep NSYSOUT sysouts with increasing versions")
        (CL:WHEN (IGEQ (LENGTH SYSOUTS)
                       NSYSOUTS)
            (DELFILE (pop SYSOUTS)))                         (* ; 
                                                  "Drop the firstr (oldest), new one goes at the end")
        (SETQ SYSOUTNAME (SYSOUT SYSOUTNAME))
        (CL:WHEN (LISTP SYSOUTNAME)                          (* ; "Restarting")
            (SETQ SINGLESTEP T))
        (NCONC1 SYSOUTS SYSOUTNAME))])

(COPYTOCORE
  [LAMBDA (FILES NORECLAIM)                                  (* ; "Edited 12-Mar-2024 22:45 by rmk")

    (* ;; "Copy FILES to {CORE}, defaulting to TEDIT files in connected directory")

    (CL:UNLESS (LISTP FILES)
        (SETQ FILES (FILDIR (OR FILES "*.TEDIT;"))))
    (PRINTOUT T "Copying " (LENGTH FILES)
           " files to {CORE} "
           (CL:IF NORECLAIM
               "without "
               "with ")
           "reclaiming" T)
    (for F in FILES collect (COPYFILE F (PACKFILENAME 'HOST 'CORE 'DIRECTORY NIL 'BODY F))
       finally (CL:UNLESS NORECLAIM (RECLAIM])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (722 12866 (STRESSHC 732 . 4315) (STRESSRAND 4317 . 6053) (STRESSPUT 6055 . 8023) (
STRESSOPEN 8025 . 9458) (STRESSREAD 9460 . 10995) (STRESSGREP 10997 . 11940) (STRESSPEEK 11942 . 12864
)) (12867 15560 (EQTEXTSTREAM 12877 . 14046) (SYSOUTRING 14048 . 14928) (COPYTOCORE 14930 . 15558)))))
STOP
