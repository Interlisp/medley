(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "14-Mar-2024 15:16:05" {WMEDLEY}<library>tedit>TEDIT-STRESS.;49 12388  

      :EDIT-BY rmk

      :CHANGES-TO (FNS STRESSHC)

      :PREVIOUS-DATE "13-Mar-2024 00:24:06" {WMEDLEY}<library>tedit>TEDIT-STRESS.;48)


(PRETTYCOMPRINT TEDIT-STRESSCOMS)

(RPAQQ TEDIT-STRESSCOMS (                                    (* ; "Preload typical image objects")
                         (FILES SKETCH DATEFORMAT-EDITOR)
                         (FNS STRESSHC STRESSRAND STRESSPUT STRESSOPEN STRESSREAD)
                         (FNS EQTEXTSTREAM SYSOUTRING COPYTOCORE)))



(* ; "Preload typical image objects")


(FILESLOAD SKETCH DATEFORMAT-EDITOR)
(DEFINEQ

(STRESSHC
  [LAMBDA (FILES NSYSOUTS REPS NOERROR SEPARATEOUT PDF SYSOUTNAME SINGLESTEP)
                                                             (* ; "Edited 14-Mar-2024 15:15 by rmk")
                                                             (* ; "Edited 13-Mar-2024 00:23 by rmk")
    (DECLARE (SPECVARS SINGLESTEP))

    (* ;; "If all arguments are defaulted, runs through all TEDIT files in the current directory until it fails, doing SAVEVM before each file.  The HC files are made as {CORE}FOO.PS.")

    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (NIL MAX.SMALLP)
                   REPS))
    (SETQ NOERROR T)
    (CL:UNLESS NSYSOUTS
        (SETQ NSYSOUTS 'SAVEVM))
    [SETQ SYSOUTNAME (PACKFILENAME 'VERSION NIL 'BODY (OR SYSOUTNAME (PACKFILENAME 'DIRECTORY 
                                                                            MEDLEYDIR 'NAME 
                                                                            "STRESSHC" 'EXTENSION
                                                                            'SYSOUT]
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files saving "
           (if (EQ NSYSOUTS 0)
               then "no sysouts"
             elseif (EQ NSYSOUTS 'SAVEVM)
               then " the virtual memory"
             else (PRINTOUT NIL NSYSOUTS " sysouts on " 3)
                  SYSOUTNAME)
           T)
    (PRINTOUT T "First file is " (CAR FILES)
           T T)
    (BKSYSBUF " ")
    (for R SYSOUTS (ITYPE _ (CL:IF PDF
                                'pdf
                                'ps))
         (N _ 0) from 1 to REPS
       do (PRINTOUT T "Rep " R T)
          (if (EQ NSYSOUTS 'SAVEVM)
              then (SAVEVM)
            else (SETQ SYSOUTS (SYSOUTRING NSYSOUTS SYSOUTNAME SYSOUTS)))
          [for F TS HCFILE inside FILES
             do (PROMPTPRINT F)
                (SETQ HCFILE (CL:IF SEPARATEOUT
                                 (OUTFILEP (PACKFILENAME 'EXTENSION ITYPE 'VERSION 1 'BODY F))
                                 (CL:IF PDF
                                     "{CORE}FOO.PDF;1"
                                     "{CORE}FOO.PS;1")))
                (if [if NOERROR
                        then [NLSETQ (SETQ TS (OPENTEXTSTREAM F))
                                    (TEDIT.FORMAT.HARDCOPY TS HCFILE T NIL NIL NIL
                                           (CL:IF PDF
                                               'PDF
                                               'POSTSCRIPT)]
                      else (SETQ TS (OPENTEXTSTREAM F))
                           (TEDIT.FORMAT.HARDCOPY TS HCFILE T NIL NIL NIL (CL:IF PDF
                                                                              'PDF
                                                                              'POSTSCRIPT)]
                    then (add N 1)
                  else (PRINTOUT T "  Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T))
                (CLOSEF? TS)
                (CL:WHEN SINGLESTEP
                    (HELP (CONCAT "Just hardcopied " F " to " HCFILE)))]
          (PRINTOUT T "  Hardcopied " N " files without failure" T)
       finally (RETURN (LIST R N])

(STRESSRAND
  [LAMBDA (FILES REPS NOERROR PROBESPERFILE)                 (* ; "Edited 12-Mar-2024 09:47 by rmk")

    (* ;; "Opens, fetches random characters")

    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (CL:UNLESS PROBESPERFILE (SETQ PROBESPERFILE 100))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files with " PROBESPERFILE " probes per file" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TS inside FILES
             do (if [if NOERROR
                        then [NLSETQ (SETQ TS (OPENTEXTSTREAM F))
                                    (for I (LEN _ (TEDIT.NCHARS TS)) from 1 to PROBESPERFILE
                                       do (TEDIT.NTHCHARCODE TS (RAND 1 LEN]
                      else (SETQ TS (OPENTEXTSTREAM F))
                           (for I (LEN _ (TEDIT.NCHARS TS)) from 1 to PROBESPERFILE
                              do (TEDIT.NTHCHARCODE TS (RAND 1 LEN]
                    then (CLOSEF TS)
                         (add N 1)
                  else (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T))) finally (RETURN (LIST R N])

(STRESSPUT
  [LAMBDA (FILES REPS NOERROR CHECKEQUIV STOP)               (* ; "Edited 12-Mar-2024 09:48 by rmk")

    (* ;; "Opens, puts, reopens and tests for equivalence")

    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TS TSP inside FILES
             do (if (if NOERROR
                        then (NLSETQ (SETQ TS (OPENTEXTSTREAM F))
                                    (TEDIT.PUT TS "{CORE}FOO.TEDIT;1")
                                    (SETQ TSP (OPENTEXTSTREAM "{CORE}FOO.TEDIT;1"))
                                    (CL:WHEN (AND CHECKEQUIV (NOT (EQTEXTSTREAM TS TSP STOP)))
                                           (HELP "Get of put not equivalent" F))
                                    (CLOSEF TSP))
                      else (SETQ TS (OPENTEXTSTREAM F))
                           (TEDIT.PUT TS "{CORE}FOO.TEDIT;1")
                           (SETQ TSP (OPENTEXTSTREAM "{CORE}FOO.TEDIT;1"))
                           (CL:WHEN (AND CHECKEQUIV (NOT (EQTEXTSTREAM TS TSP STOP)))
                                  (HELP "Get of put not equivalent" F))
                           (CLOSEF TSP))
                    then (CLOSEF TS)
                         (add N 1)
                  else (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T))) finally (RETURN (LIST R N])

(STRESSOPEN
  [LAMBDA (FILES REPS NOERROR)                               (* ; "Edited 12-Mar-2024 09:48 by rmk")
                                                             (* ; "Edited 11-Mar-2024 09:15 by rmk")
    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TS inside FILES do (if (if NOERROR
                                            then (NLSETQ (SETQ TS (OPENTEXTSTREAM F)))
                                          else (SETQ TS (OPENTEXTSTREAM F)))
                                        then (CLOSEF TS)
                                             (add N 1)
                                      else (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL
                                                                             'DIRECTORY NIL
                                                                             'BODY F)
                                                  T))) finally (RETURN (LIST R N])

(STRESSREAD
  [LAMBDA (FILES REPS NOERROR)                               (* ; "Edited 12-Mar-2024 09:48 by rmk")
                                                             (* ; "Edited 11-Mar-2024 09:13 by rmk")
    (CL:UNLESS FILES
        (SETQ FILES (FILDIR '*.TEDIT;)))
    (SETQ REPS (SELECTQ REPS
                   (T MAX.SMALLP)
                   (NIL 1)
                   REPS))
    (PRINTOUT T REPS " reps of " (LENGTH FILES)
           " files" T)
    (for R (N _ 0) from 1 to REPS
       do (PRINTOUT T R " ")
          (for F TS inside FILES
             do (if (if NOERROR
                        then (NLSETQ (SETQ TS (OPENTEXTSTREAM F))
                                    (for I from 1 while (TEDIT.NTHCHARCODE TS I)))
                      else (SETQ TS (OPENTEXTSTREAM F))
                           (for I from 1 while (TEDIT.NTHCHARCODE TS I)))
                    then (CLOSEF TS)
                         (add N 1)
                  else (PRINTOUT T T "Error for " (PACKFILENAME 'HOST NIL 'DIRECTORY NIL 'BODY F)
                              T))) finally (RETURN (LIST R N])
)
(DEFINEQ

(EQTEXTSTREAM
  [LAMBDA (TS1 TS2 STOP)                                     (* ; "Edited 11-Mar-2024 16:53 by rmk")
    (AND (IEQP (TEDIT.NCHARS TS1)
               (TEDIT.NCHARS TS2))
         (OR (for I C1 C2 from 1 to (TEDIT.NCHARS TS1) eachtime (SETQ C1 (TEDIT.NTHCHARCODE TS1 I))
                                                             (SETQ C2 (TEDIT.NTHCHARCODE TS2 I))
                unless (OR (EQ C1 C2)
                           (AND (EQ C1 10)
                                (EQ C2 13))
                           (AND (EQ C1 13)
                                (EQ C2 10))
                           (AND (IMAGEOBJP C1)
                                (IMAGEOBJP C2)
                                (EQUALALL C1 C2))) do (CL:WHEN STOP
                                                          (HELP "Different characters: "
                                                                (LIST I C1 C2)))
                                                      (RETURN NIL) finally (RETURN T])

(SYSOUTRING
  [LAMBDA (NSYSOUTS SYSOUTNAME SYSOUTS)                      (* ; "Edited 12-Mar-2024 17:52 by rmk")

    (* ;; "SYSOUTS is the list of names of sysouts that currently exist.")

    (DECLARE (USEDFREE SINGLESTEP))
    (CL:WHEN (IGREATERP NSYSOUTS 0)                          (* ; 
                                                      "Keep NSYSOUT sysouts with increasing versions")
        (CL:WHEN (IGEQ (LENGTH SYSOUTS)
                       NSYSOUTS)
            (DELFILE (pop SYSOUTS)))                         (* ; 
                                                  "Drop the firstr (oldest), new one goes at the end")
        (SETQ SYSOUTNAME (SYSOUT SYSOUTNAME))
        (CL:WHEN (LISTP SYSOUTNAME)                          (* ; "Restarting")
            (SETQ SINGLESTEP T))
        (NCONC1 SYSOUTS SYSOUTNAME))])

(COPYTOCORE
  [LAMBDA (FILES NORECLAIM)                                  (* ; "Edited 12-Mar-2024 22:45 by rmk")

    (* ;; "Copy FILES to {CORE}, defaulting to TEDIT files in connected directory")

    (CL:UNLESS (LISTP FILES)
        (SETQ FILES (FILDIR (OR FILES "*.TEDIT;"))))
    (PRINTOUT T "Copying " (LENGTH FILES)
           " files to {CORE} "
           (CL:IF NORECLAIM
               "without "
               "with ")
           "reclaiming" T)
    (for F in FILES collect (COPYFILE F (PACKFILENAME 'HOST 'CORE 'DIRECTORY NIL 'BODY F))
       finally (CL:UNLESS NORECLAIM (RECLAIM])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (742 9789 (STRESSHC 752 . 4159) (STRESSRAND 4161 . 5577) (STRESSPUT 5579 . 7289) (
STRESSOPEN 7291 . 8579) (STRESSREAD 8581 . 9787)) (9790 12365 (EQTEXTSTREAM 9800 . 10851) (SYSOUTRING 
10853 . 11733) (COPYTOCORE 11735 . 12363)))))
STOP
