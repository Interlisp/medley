(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "20-May-92 11:28:47" {DSK}<project>medley2.0>library>lafitetedit.;7 12308  

      changes to%:  (FNS TEDIT.ASSURE.NO.BACKING.FILE)
                    (VARS LAFITETEDITCOMS)

      previous date%: "29-Apr-92 13:30:23" {DSK}<project>medley2.0>library>lafitetedit.;5)


(* ; "
Copyright (c) 1988, 1990, 1992 by Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT LAFITETEDITCOMS)

(RPAQQ LAFITETEDITCOMS
       (
        (* ;; "Lafite's more explicit dependencies on %"internals%" of TEDIT")

        (FNS LA.ADJUST.FORMATTING LA.SKIP.LOOKS.LIST LA.DETACH.TEDIT LA.TEDIT.INCLUDE 
             LA.WINDOW.FROM.TEXTSTREAM TEDIT.ASSURE.NO.BACKING.FILE)
        (DECLARE%: EVAL@COMPILE DONTCOPY 

               (* ;; "Need TEDIT internal declarations for LA.ADJUST.FORMATTING.  Can't just do (FILES (SOURCE) TEDITDECLS), because there is a compiled version that is already loaded that isn't enough.")

               (P (CL:UNLESS (GET 'TEDITDECLS 'FILE)
                         (FILESLOAD TEDITDECLS)))
               (FILES (SOURCE)
                      LAFITEDECLS)
               (GLOBALVARS *TEDIT-FILE-READTABLE*)
               (LOCALVARS . T))))



(* ;; "Lafite's more explicit dependencies on %"internals%" of TEDIT")

(DEFINEQ

(LA.ADJUST.FORMATTING
  [LAMBDA (FORMATSTREAM OUTSTREAM BYTE-LENGTHS)          (* ; "Edited  3-Jun-88 18:24 by bvm")

    (* ;; "Adjusts the formatting info FORMATSTREAM to account for the prepending of one or more %"paragraphs%" of default looking text, whose lengths are given by BYTE-LENGTHS (or a single number if just one piece).  It then writes the resulting formatting to OUTSTREAM.")

    (PROG ((END (GETEOFPTR FORMATSTREAM))
           NPIECES PIECEINFOCH# TYPECODE PCLEN PREFIXEND LOOKSINDEX)
          (COND
             ((<= END 8)                                     (* ; "This can't be formatting.")
              (RETURN NIL)))
          (SETFILEPTR FORMATSTREAM (- END 8))
          (SETQ PIECEINFOCH# (\DWIN FORMATSTREAM))           (* ; "Where the piece table starts relative to the whole file.  Since in our case TEXT is only the formatting, it will start at zero.")
          (SETQ NPIECES (\SMALLPIN FORMATSTREAM))            (* ; "Total number of pieces")
          (if (NEQ (\SMALLPIN FORMATSTREAM)
                       31418)
              then                                       (* ; 
                                "Not the version of TEdit formatting we understand.  Throw it out.")
                    (RETURN NIL))
          (SETFILEPTR FORMATSTREAM 0)
          [do (SETQ PCLEN (\DWIN FORMATSTREAM))
                 (SETQ TYPECODE (\SMALLPIN FORMATSTREAM))    (* ; "What kind of piece is it?")
                 (SELECTC TYPECODE
                     (\PieceDescriptorPAGEFRAME              (* ; 
                           "This is page layout info for the file, whose format is an s-expression")
                          (SKREAD FORMATSTREAM NIL *TEDIT-FILE-READTABLE*))
                     (\PieceDescriptorCHARLOOKSLIST          (* ; 
         "This is the list of CHARLOOKSs used in this document.  This is a sequence of charlooks, ")
                          (LA.SKIP.LOOKS.LIST FORMATSTREAM))
                     (\PieceDescriptorPARALOOKSLIST          (* ; 
                     "This is the list of PARALOOKSs used in this document.  Similar to CHARLOOKS.")
                          (LA.SKIP.LOOKS.LIST FORMATSTREAM))
                     (\PieceDescriptorPARA                   (* ; 
   "Start a new paragraph with different looks.  We will want to insert our new piece before this.")
                          (OR PREFIXEND (SETQ PREFIXEND (- (GETFILEPTR FORMATSTREAM)
                                                           6)))
                                                             (* ; 
                                                         "Representation is just a paralooks index")
                          (\SMALLPIN FORMATSTREAM))
                     (\PieceDescriptorLOOKS                  (* ; "Character looks for a new piece.  The piece is PCLEN bytes long, which means half that many chars if fat.")
                          (OR PREFIXEND (SETQ PREFIXEND (- (GETFILEPTR FORMATSTREAM)
                                                           6)))
                                                             (* ; 
                                      "Peek ahead to see what the charlooks of the first piece are")
                          (\BIN FORMATSTREAM)                (* ; "FLAG byte.  1=NEW; 2 = FAT")
                          (SETQ LOOKSINDEX (\SMALLPIN FORMATSTREAM))
                                                             (* ; "Charlooks index")
                          (RETURN))
                     (PROGN                                  (* ; 
                                     "Either imageobj or unknown type piece--I hope we're finished")
                            (RETURN]

     (* ;; "At this point we have read enough format info to know what to do.  Everything up to PREFIXEND is the preamble, which we can copy intact.  Then we insert our own first piece, consisting of the prepended text in a single piece.")

          [COPYBYTES FORMATSTREAM OUTSTREAM 0 (OR PREFIXEND (SETQ PREFIXEND (- (GETFILEPTR 
                                                                                      FORMATSTREAM)
                                                                               6]
          (for PIECELEN inside BYTE-LENGTHS do 

                                 (* ;; "This code is generalized to allow multiple inserted pieces, but unfortunately if the textstream already has any paragraph formatting, we can't make the pieces be different paragraphs without making them also have non-default paralooks.")

                                                          (\DWOUT OUTSTREAM PIECELEN)
                                                          (\SMALLPOUT OUTSTREAM \PieceDescriptorLOOKS
                                                                 )
                                                          (BOUT OUTSTREAM 0) 
                                                             (* ; "Flag byte")
                                                          (\SMALLPOUT OUTSTREAM (OR LOOKSINDEX 1)) 
                                                             (* ; "Char looks index--make it look like the first piece, or arbitrarily choose the first looks if the text started with an imageobj or some other ugliness")
                                                          (add PIECEINFOCH# PIECELEN)
                                                          (add NPIECES 1))
          (COPYBYTES FORMATSTREAM OUTSTREAM PREFIXEND (- END 8))
                                                             (* ; "Copy rest of piece info")
          (\DWOUT OUTSTREAM PIECEINFOCH#)                    (* ; 
                                                           "New offset of start of formatting")
          (\SMALLPOUT OUTSTREAM NPIECES)                     (* ; "More pieces now")
          (\SMALLPOUT OUTSTREAM 31418)                       (* ; "Finally, the password")
          (RETURN OUTSTREAM])

(LA.SKIP.LOOKS.LIST
  [LAMBDA (FORMATSTREAM)                                 (* ; "Edited  3-Jun-88 16:52 by bvm")

    (* ;; "Advance FORMATSTREAM past a sequence of CHAR/PARALOOKS.  Each elements starts with a word giving its byte length, so we can skip over it")

    (for I from 1 to (\SMALLPIN FORMATSTREAM) do (SETFILEPTR FORMATSTREAM
                                                                        (+ (GETFILEPTR FORMATSTREAM)
                                                                           (\SMALLPIN FORMATSTREAM])

(LA.DETACH.TEDIT
  [LAMBDA (TEXTSTREAM)                                   (* ; "Edited  3-Jun-88 17:27 by bvm")

    (* ;; "Removes the TEXTSTREAM from the window, if any, it is being edited in.")

    (* ;; "Yecch, TEdit ought to have a proper interface for this.")

    (replace (TEXTOBJ \WINDOW) of (TEXTOBJ TEXTSTREAM) with NIL])

(LA.TEDIT.INCLUDE
  [LAMBDA (TEXTSTREAM FILE CH#)                          (* ; "Edited  3-Jun-88 17:49 by bvm")

    (* ;; "Do an Include of FILE into TEXTSTREAM at (i.e., in front of) character CH#.  Returns the length of the insertion.")

    (* ;; "This code assumes that TEDIT.INCLUDE makes selection be the insertion")

    (TEDIT.SETSEL TEXTSTREAM CH# 0 'RIGHT)
    (TEDIT.INCLUDE TEXTSTREAM FILE)
    (fetch (SELECTION DCH) of (TEDIT.GETSEL TEXTSTREAM])

(LA.WINDOW.FROM.TEXTSTREAM
  [LAMBDA (TEXTSTREAM)                                   (* ; "Edited 23-Sep-87 15:36 by bvm:")
    (for W in (fetch (TEXTOBJ \WINDOW) of (TEXTOBJ TEXTSTREAM))
       when (WINDOWPROP W 'TITLE) do                 (* ; 
                                                     "Hairy loop because the window could be split")
                                            (RETURN W])

(TEDIT.ASSURE.NO.BACKING.FILE
  [LAMBDA (TEXTSTREAM)                                   (* ; "Edited 20-May-92 11:25 by rmk:")
    (LET* ((TEXTOBJ (TEXTOBJ TEXTSTREAM))
           (OFILE (FETCH (TEXTOBJ TXTFILE) OF TEXTOBJ)))
          (IF (AND (TYPE? STREAM OFILE)
                       (NEQ (FETCH (STREAM DEVICE) OF OFILE)
                            'NODIRCORE))
              THEN (LET* [(NEWFILE (OPENSTREAM '{NODIRCORE} 'BOTH))
                              (CH#S (REVERSE (CDR (TEDIT.PUT.PCTB TEXTOBJ NEWFILE]

                             (* ;; "TEDIT.PUT.PCTB has the effect of copying the whole document to NEWFILE.  There are still multiple pieces, because each looks-run is a piece.  Value gives the byte pointers within the resulting file where each real piece of text starts.  Run thru the pieces in the PCTB, pointing them to the new file and their new locations.  We do the cleanup copied from TEDIT.PUT; don't call TEDIT.PUT itself because we want it to think that we are still editing the original source.")

                             [TEDIT.MAPPIECES TEXTOBJ (FUNCTION (LAMBDA (CH# PC)
                                                                  (COND
                                                                     ((FETCH POBJ OF PC))
                                                                     (T (REPLACE PFPOS
                                                                           OF PC
                                                                           WITH (POP CH#S))
                                                                        (CLOSEF? (FETCH PFILE
                                                                                    OF PC))
                                                             (* ; 
     "If this is a piece on an open file, close it, since we're never going to read from it again.")
                                                                        (REPLACE PFILE
                                                                           OF PC WITH NEWFILE
                                                                               )
                                                                        (REPLACE PSTR
                                                                           OF PC WITH NIL]
                             (CLOSEF? OFILE)
                             (REPLACE (TEXTOBJ TXTFILE) OF TEXTOBJ WITH NIL])
)
(DECLARE%: EVAL@COMPILE DONTCOPY 

(CL:UNLESS (GET 'TEDITDECLS 'FILE)
       (FILESLOAD TEDITDECLS))


(FILESLOAD (SOURCE)
       LAFITEDECLS)

(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS *TEDIT-FILE-READTABLE*)
)

(DECLARE%: DOEVAL@COMPILE DONTCOPY

(LOCALVARS . T)
)
)
(PUTPROPS LAFITETEDIT COPYRIGHT ("Xerox Corporation" 1988 1990 1992))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1342 11940 (LA.ADJUST.FORMATTING 1352 . 7488) (LA.SKIP.LOOKS.LIST 7490 . 8064) (
LA.DETACH.TEDIT 8066 . 8431) (LA.TEDIT.INCLUDE 8433 . 8922) (LA.WINDOW.FROM.TEXTSTREAM 8924 . 9370) (
TEDIT.ASSURE.NO.BACKING.FILE 9372 . 11938)))))
STOP
