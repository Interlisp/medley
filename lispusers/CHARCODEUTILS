(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "23-Mar-2025 23:50:49" {DSK}<home>matt>Interlisp>medley>lispusers>CHARCODEUTILS.;3 5314   

      :EDIT-BY "mth"

      :CHANGES-TO (FUNCTIONS CHARCODE.ENCODE)

      :PREVIOUS-DATE "23-Mar-2025 22:25:54" 
{DSK}<home>matt>Interlisp>medley>lispusers>CHARCODEUTILS.;2)


(PRETTYCOMPRINT CHARCODEUTILSCOMS)

(RPAQQ CHARCODEUTILSCOMS ((VARIABLES CHARCODE.CTRLBITS CHARCODE.CTRLMASK CHARCODE.HASHMASK 
                                 CHARCODE.UNCTRLBIT FUNCTION.CSET META.CSET)
                          (FUNCTIONS CHARCODE.ENCODE)
                          (PROP (FILETYPE MAKEFILE-ENVIRONMENT)
                                CHARCODEUTILS)))

(CL:DEFCONSTANT CHARCODE.CTRLBITS (MASK.1'S 0 5)
                                  "Mask corresponding to bits covering control characters")

(CL:DEFCONSTANT CHARCODE.CTRLMASK (BITCLEAR 255 (LOGOR CHARCODE.CTRLBITS CHARCODE.HASHMASK))
                                  
                               "Mask corresponding to bits that aren't in hash or control characters")

(CL:DEFCONSTANT CHARCODE.HASHMASK (MASK.1'S 7 1)
                                  "Mask corresponding to bit added by # in CHARCODE")

(CL:DEFCONSTANT CHARCODE.UNCTRLBIT (MASK.1'S 6 1)
                                   
                          "The bit to set to map control char to corresponding non-control character")

(CL:DEFCONSTANT FUNCTION.CSET (CADR (CL:ASSOC "Function" (GETTOPVAL 'CHARACTERSETNAMES)
                                           :TEST
                                           #'STRING-EQUAL))
                              "The characterset for Function")

(CL:DEFCONSTANT META.CSET (CADR (CL:ASSOC "Meta" (GETTOPVAL 'CHARACTERSETNAMES)
                                       :TEST
                                       #'STRING-EQUAL))
                          "The characterset for Meta")

(CL:DEFUN CHARCODE.ENCODE (C)                                (* ; "Edited 23-Mar-2025 23:49 by mth")
   (DECLARE (GLOBALVARS CHARACTERNAMES CHARACTERSETNAMES))
   (CL:BLOCK NIL
       (CL:UNLESS (SMALLP C)
              (RETURN NIL))
       [LET ((CSET (LRSH C 8))
             (CC (LOGAND C 255))
             CTRL HASH CNAME TEMP)
            (CL:WHEN [SETQ CNAME (CAR (CL:RASSOC C CHARACTERNAMES :KEY #'CAR]

                (* ;; "An exact character match, return that name")

                (RETURN (MKSTRING CNAME)))
            (CL:FLET ((CTRLP (CX)
                             (NOT (BITTEST CX CHARCODE.CTRLMASK)))
                      (HASHP (CX)
                             (BITTEST CX CHARCODE.HASHMASK))
                      (HASHMASK.KEY.FN (ITEM)
                             (BITSET (CAR ITEM)
                                    CHARCODE.HASHMASK)))
                   (SETQ CTRL (AND (CTRLP CC)
                                   "^"))
                   (SETQ HASH (AND (HASHP CC)
                                   "#"))
                   (CL:WHEN [AND HASH (SETQ TEMP (CL:RASSOC C CHARACTERNAMES :KEY #'HASHMASK.KEY.FN))
                                 (NOT (HASHP (CADR TEMP]
                       (RETURN (CONCAT (OR HASH "")
                                      (OR CTRL "")
                                      (CAR TEMP))))
                   [COND
                      [(SETQ CNAME (CAR (CL:RASSOC CC CHARACTERNAMES :KEY #'CAR]
                      ([AND HASH (SETQ TEMP (CL:RASSOC CC CHARACTERNAMES :KEY #'HASHMASK.KEY.FN))
                            (NOT (HASHP (CADR TEMP]
                       (CL:WHEN (EQ CSET (LRSH (CADR TEMP)
                                               8))
                              (SETQ CSET 0))
                       (SETQ CNAME (CAR TEMP)))
                      (T (SETQ CNAME (COND
                                        ((EQ CC 255)
                                         (CONSTANT (OCTALSTRING 255)))
                                        (CTRL (CHARACTER (BITSET (LOGAND CC CHARCODE.CTRLBITS)
                                                                CHARCODE.UNCTRLBIT)))
                                        (HASH (CHARACTER (BITCLEAR CC CHARCODE.HASHMASK)))
                                        ((AND (CL:PLUSP CSET)
                                              (NEQ CSET META.CSET)
                                              (NEQ CSET FUNCTION.CSET))
                                         (OCTALSTRING CC))
                                        (T (CHARACTER CC]
                   [SETQ CSET (COND
                                 ((ZEROP CSET)
                                  NIL)
                                 [(CAR (CL:RASSOC CSET CHARACTERSETNAMES :KEY #'CAR]
                                 (CSET (OCTALSTRING CSET]
                   (CL:WHEN CSET
                       (SETQ CSET (CONCAT CSET ",")))
                   (RETURN (CONCAT (OR CSET "")
                                  (OR HASH "")
                                  (OR CTRL "")
                                  CNAME]))

(PUTPROPS CHARCODEUTILS FILETYPE :COMPILE-FILE)

(PUTPROPS CHARCODEUTILS MAKEFILE-ENVIRONMENT (:READTABLE "INTERLISP" :PACKAGE "INTERLISP"))
(PUTPROPS CHARCODEUTILS COPYRIGHT (NONE))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1960 5091 (CHARCODE.ENCODE 1960 . 5091)))))
STOP
