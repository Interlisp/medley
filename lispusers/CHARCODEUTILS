(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "25-Mar-2025 15:46:46" {DSK}<home>matt>Interlisp>medley>lispusers>CHARCODEUTILS.;4 6141   

      :EDIT-BY "mth"

      :CHANGES-TO (FUNCTIONS CHARCODE.ENCODE)

      :PREVIOUS-DATE "23-Mar-2025 23:50:49" 
{DSK}<home>matt>Interlisp>medley>lispusers>CHARCODEUTILS.;3)


(PRETTYCOMPRINT CHARCODEUTILSCOMS)

(RPAQQ CHARCODEUTILSCOMS ((VARIABLES CHARCODE.CTRLBITS CHARCODE.CTRLMASK CHARCODE.HASHMASK 
                                 CHARCODE.UNCTRLBIT FUNCTION.CSET META.CSET)
                          (FUNCTIONS CHARCODE.ENCODE)
                          (PROP (FILETYPE MAKEFILE-ENVIRONMENT)
                                CHARCODEUTILS)))

(CL:DEFCONSTANT CHARCODE.CTRLBITS (MASK.1'S 0 5)
                                  "Mask corresponding to bits covering control characters")

(CL:DEFCONSTANT CHARCODE.CTRLMASK (BITCLEAR 255 (LOGOR CHARCODE.CTRLBITS CHARCODE.HASHMASK))
                                  
                               "Mask corresponding to bits that aren't in hash or control characters")

(CL:DEFCONSTANT CHARCODE.HASHMASK (MASK.1'S 7 1)
                                  "Mask corresponding to bit added by # in CHARCODE")

(CL:DEFCONSTANT CHARCODE.UNCTRLBIT (MASK.1'S 6 1)
                                   
                          "The bit to set to map control char to corresponding non-control character")

(CL:DEFCONSTANT FUNCTION.CSET (CADR (CL:ASSOC "Function" (GETTOPVAL 'CHARACTERSETNAMES)
                                           :TEST
                                           #'STRING-EQUAL))
                              "The characterset for Function")

(CL:DEFCONSTANT META.CSET (CADR (CL:ASSOC "Meta" (GETTOPVAL 'CHARACTERSETNAMES)
                                       :TEST
                                       #'STRING-EQUAL))
                          "The characterset for Meta")

(CL:DEFUN CHARCODE.ENCODE (C)                                (* ; "Edited 25-Mar-2025 15:31 by mth")
                                                             (* ; "Edited 23-Mar-2025 23:49 by mth")
   (DECLARE (GLOBALVARS CHARACTERNAMES CHARACTERSETNAMES))
   (CL:BLOCK NIL
       (CL:UNLESS [OR (SMALLP C)
                      (AND (CL:CHARACTERP C)
                           (SETQ C (CL:CHAR-CODE C]
              (RETURN NIL))
       [LET ((CSET (LRSH C 8))
             (CC (LOGAND C 255))
             CTRL HASH CNAME TEMP)
            (CL:LABELS ((CTRLP (CX)
                               (NOT (BITTEST CX CHARCODE.CTRLMASK)))
                        (HASHP (CX)
                               (BITTEST CX CHARCODE.HASHMASK))
                        (KEY.FN (ITEM &AUX (CITEM (CAR ITEM)))
                               (OR (SMALLP CITEM)
                                   (CHARCODE.DECODE CITEM NIL)))
                        (HASHMASK.KEY.FN (ITEM)
                               (BITSET (KEY.FN ITEM)
                                      CHARCODE.HASHMASK)))
                   [if (EQ CC 255)
                       then (SETQ CNAME (CONSTANT (OCTALSTRING 255)))
                     else (CL:WHEN [SETQ CNAME (CAR (CL:RASSOC C CHARACTERNAMES :KEY #'KEY.FN]

                              (* ;; "An exact character match, return that name")

                              (RETURN (MKSTRING CNAME)))
                          (SETQ CTRL (AND (CTRLP CC)
                                          "^"))
                          (SETQ HASH (AND (HASHP CC)
                                          "#"))
                          (CL:WHEN [AND HASH (SETQ TEMP (CL:RASSOC C CHARACTERNAMES :KEY 
                                                               #'HASHMASK.KEY.FN))
                                        (NOT (HASHP (CADR TEMP]
                              (RETURN (CONCAT (OR HASH "")
                                             (OR CTRL "")
                                             (CAR TEMP))))
                          (COND
                             [(SETQ CNAME (CAR (CL:RASSOC CC CHARACTERNAMES :KEY #'KEY.FN]
                             ([AND HASH (SETQ TEMP (CL:RASSOC CC CHARACTERNAMES :KEY 
                                                          #'HASHMASK.KEY.FN))
                                   (NOT (HASHP (CADR TEMP]
                              (CL:WHEN (EQ CSET (LRSH (CADR TEMP)
                                                      8))
                                     (SETQ CSET 0))
                              (SETQ CNAME (CAR TEMP)))
                             (T (SETQ CNAME (COND
                                               ((AND (CL:PLUSP CSET)
                                                     (NEQ CSET META.CSET)
                                                     (NEQ CSET FUNCTION.CSET))
                                                (SETQ HASH NIL CTRL NIL)
                                                (OCTALSTRING CC))
                                               (CTRL (CHARACTER (BITSET (LOGAND CC CHARCODE.CTRLBITS)
                                                                       CHARCODE.UNCTRLBIT)))
                                               (HASH (CHARACTER (BITCLEAR CC CHARCODE.HASHMASK)))
                                               (T (CHARACTER CC]
                   [SETQ CSET (COND
                                 ((ZEROP CSET)
                                  NIL)
                                 [(CAR (CL:RASSOC CSET CHARACTERSETNAMES :KEY #'KEY.FN]
                                 (CSET (OCTALSTRING CSET]
                   (CL:WHEN CSET
                       (SETQ CSET (CONCAT CSET ",")))
                   (RETURN (CONCAT (OR CSET "")
                                  (OR HASH "")
                                  (OR CTRL "")
                                  CNAME]))

(PUTPROPS CHARCODEUTILS FILETYPE :COMPILE-FILE)

(PUTPROPS CHARCODEUTILS MAKEFILE-ENVIRONMENT (:READTABLE "INTERLISP" :PACKAGE "INTERLISP"))
(PUTPROPS CHARCODEUTILS COPYRIGHT (NONE))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1960 5918 (CHARCODE.ENCODE 1960 . 5918)))))
STOP
