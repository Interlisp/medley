(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "17-Oct-2021 12:09:55" 
{DSK}<Users>kaplan>Local>medley3.5>git-medley>lispusers>DATE.;13 19733  

      changes to%:  (FNS FINDTIME FINDYEAR DATE.GETFN DATES.MENU.APPLY FINDMONTH CURRENT.DISPLAY.FONT
                         DATE.IMAGEBOXFN DATEOBJP DATE.PUTFN DATEOBJ DATES.MENU.WHENSELECTEDFN 
                         WHICHDATE DATES.TEMPLATE DATE.BUTTONEVENTINFN)
                    (VARS DATECOMS)
                    (RECORDS DATEOBJ)

      previous date%: "17-Oct-2021 11:50:56" 
{DSK}<Users>kaplan>Local>medley3.5>git-medley>lispusers>DATE.;4)


(* ; "
Copyright (c) 1987 by Leland Stanford Junior University.
")

(PRETTYCOMPRINT DATECOMS)

(RPAQQ DATECOMS
       ((* Developed under support from NIH grant RR-00785.)
        (* Written by Frank Gilmurray and Sami Shaio.)
        (FNS DATEOBJ DATEOBJP DATE.DISPLAYFN DATE.IMAGEBOXFN CURRENT.DISPLAY.FONT DATE.PUTFN 
             DATE.GETFN DATE.BUTTONEVENTINFN DATES.TEMPLATE AMPM DATES.MENU.APPLY 
             DATES.MENU.WHENSELECTEDFN DATES.SET FINDDAY FINDHOUR FINDMONTH FINDTIME FINDYEAR NUMP 
             WHICHDATE)
        (RECORDS DATEOBJ)))



(* Developed under support from NIH grant RR-00785.)




(* Written by Frank Gilmurray and Sami Shaio.)

(DEFINEQ

(DATEOBJ
  [LAMBDA (TEMPLATE)                                         (* ; 
                                                           "Edited 17-Oct-2021 12:01 by rmk:")
                                                             (* fsg "23-Jul-86 09:53")

         (* Create an instance of a date imageobj.
       A dateobj is also defined as a record with a datestring field.
       *)

    (LET* [[TEMPLATE.TYPE (OR TEMPLATE '(M D Y F]
           (DATEANDTIME (MKSTRING (DATE)))
           (DISPLAYDATE (MKSTRING (DATES.TEMPLATE DATEANDTIME TEMPLATE.TYPE)))
           (NEWOBJ (IMAGEOBJCREATE (create DATEOBJ
                                          DATESTRING _ DATEANDTIME
                                          DISPLAY.DATE _ DISPLAYDATE
                                          TEMPLATE.DATE _ TEMPLATE.TYPE)
                          (IMAGEFNSCREATE (FUNCTION DATE.DISPLAYFN)
                                 (FUNCTION DATE.IMAGEBOXFN)
                                 (FUNCTION DATE.PUTFN)
                                 (FUNCTION DATE.GETFN)
                                 (FUNCTION NILL)
                                 (FUNCTION DATE.BUTTONEVENTINFN)
                                 (FUNCTION NILL)
                                 (FUNCTION NILL)
                                 (FUNCTION NILL)
                                 (FUNCTION NILL)
                                 (FUNCTION NILL)
                                 (FUNCTION NILL)
                                 (FUNCTION NILL]

         (* By convention, every image object will have a type property associated with 
       it that will facilitate imageobj mapping in a TEdit file.)

          (IMAGEOBJPROP NEWOBJ 'TYPE 'DATEOBJ)
          NEWOBJ])

(DATEOBJP
  [LAMBDA (IMOBJ)                                            (* ; 
                                                           "Edited 17-Oct-2021 12:02 by rmk:")
                                                             (* ss%: "24-Jun-85 16:33")

         (* Tests an imageobj to see if it is a date imageobject.
       By convention, testing functions for an imageobject will be named
       (CONCAT <type of imageobj> "P"))

    (AND IMOBJ (EQ (IMAGEOBJPROP IMOBJ 'TYPE)
                   'DATEOBJ])

(DATE.DISPLAYFN
  [LAMBDA (OBJ STREAM STREAMTYPE HOSTSTREAM)                 (* fsg "17-Feb-87 09:28")

         (* * Display function for date imageobjs.)

    (PRIN1 (fetch DISPLAY.DATE of (fetch OBJECTDATUM of OBJ))
           STREAM])

(DATE.IMAGEBOXFN
  [LAMBDA (OBJ STREAM CURRENTX RIGHTMARGIN)                  (* ; 
                                                           "Edited 17-Oct-2021 12:04 by rmk:")
                                                             (* fsg "15-Feb-87 14:05")

         (* * Return the ImageBox for the date string.
       The size is determined by the stream's current font.)

    (DSPFONT (CURRENT.DISPLAY.FONT STREAM)
           STREAM)
    (create IMAGEBOX
           XSIZE _ (STRINGWIDTH (fetch DISPLAY.DATE of (fetch OBJECTDATUM of OBJ))
                          STREAM)
           YSIZE _ (FONTPROP STREAM 'HEIGHT)
           YDESC _ (FONTPROP STREAM 'DESCENT)
           XKERN _ 0])

(CURRENT.DISPLAY.FONT
  [LAMBDA (STREAM)                                           (* ; 
                                                           "Edited 17-Oct-2021 12:04 by rmk:")
                                                             (* fsg "17-Feb-87 10:19")

         (* * Return the current font. This function is here instead of TMAX because the 
       DATE code is also used in the LetterHead code.)

    (LET [(CURRENT.FONT (fetch CLFONT of (with TEXTSTREAM
                                                        (TEXTSTREAM (CAR (fetch \WINDOW
                                                                            of TEXTOBJ)))
                                                        CURRENTLOOKS]
         (COND
            ((TYPENAMEP CURRENT.FONT 'FONTDESCRIPTOR)
             CURRENT.FONT)
            ((TYPENAMEP CURRENT.FONT 'FONTCLASS)
             (fetch DISPLAYFD of CURRENT.FONT))
            (T (SHOULDNT "Can't get current font"])

(DATE.PUTFN
  [LAMBDA (DATEOBJ STREAM)                               (* ; 
                                                           "Edited 17-Oct-2021 12:03 by rmk:")
                                                             (* fsg " 4-Feb-87 09:40")
    (PRIN2 (LIST 'Date (fetch (DATEOBJ TEMPLATE.DATE) of (fetch OBJECTDATUM
                                                                        of DATEOBJ)))
           STREAM])

(DATE.GETFN
  [LAMBDA (STREAM)                                           (* ; 
                                                           "Edited 17-Oct-2021 12:07 by rmk:")
                                                             (* fsg " 4-Feb-87 09:42")
    [OR (WINDOWPROP (PROCESSPROP (THIS.PROCESS)
                           'WINDOW)
               'IMAGEOBJ.MENUW)
        (AND (FGETD TSP.FMMENU)
             (TSP.FMMENU (TEXTSTREAM (PROCESSPROP (THIS.PROCESS)
                                            WINDOW]
    (APPLY 'DATEOBJ (CDR (READ STREAM])

(DATE.BUTTONEVENTINFN
  [LAMBDA (DATEOBJ WINDOWSTREAM SELECTION RELX RELY WINDOW HOSTSTREAM BUTTON)
                                                             (* ; 
                                                           "Edited 17-Oct-2021 11:50 by rmk:")
                                                             (* fsg "26-Jan-87 10:06")
    (AND (MOUSESTATE MIDDLE)
         (LET [(DATE.MENU (create MENU
                                 TITLE _ "Date Menu"
                                 ITEMS _ '((|Month Day, Year| (DATES.TEMPLATE DATE
                                                                     '(M D Y F))
                                                  "Insert current date as %"March 8, 1952%"")
                                           (Month/Day/Year (DATES.TEMPLATE DATE
                                                                  '(M D Y A))
                                                  "Insert current date as %"3/8/52%"")
                                           (|Day Month, Year| (DATES.TEMPLATE DATE
                                                                     '(D M Y F))
                                                  "Insert current date as %"8 March, 1952%"")
                                           (Day/Month/Year (DATES.TEMPLATE DATE
                                                                  '(D M Y A))
                                                  "Insert current date as %"8/3/52%"")
                                           (Time (DATES.TEMPLATE DATE '(T F))
                                                 "Insert current time as %"four thirty p.m.%"")
                                           (Numbered% Time (DATES.TEMPLATE DATE
                                                                  '(T A))
                                                  "Insert current time as %"4:30 p.m.%"")
                                           (Military% Time (DATES.TEMPLATE DATE
                                                                  '(T E))
                                                  "Insert current time as %"16:30%""))
                                 WHENSELECTEDFN _ (FUNCTION DATES.MENU.WHENSELECTEDFN]
              (PUTMENUPROP DATE.MENU 'IMAGEOBJ DATEOBJ)
              (MENU DATE.MENU)
              'CHANGED])

(DATES.TEMPLATE
  [LAMBDA (DATE TEMPLATE)                                    (* ; 
                                                           "Edited 17-Oct-2021 11:59 by rmk:")
                                                             (* ; 
                                                           "Edited 17-Oct-2021 11:59 by rmk:")
                                                             (* fsg "24-Jul-86 14:43")

         (* * comment)

    (COND
       [TEMPLATE (LET [[VERSION (if (EQUAL (LAST TEMPLATE)
                                               '(A))
                                    then 'ABBREV
                                  else (if (EQUAL (LAST TEMPLATE)
                                                          '(F))
                                               then 'FULL
                                             else 'EURO]
                       (FUNCLST '((D FINDDAY)
                                  (M FINDMONTH)
                                  (Y FINDYEAR]
                      (COND
                         ((EQ (CAR TEMPLATE)
                              T)
                          (FINDTIME DATE VERSION))
                         (T (LET ((CH (if (EQ VERSION 'ABBREV)
                                          then "/"
                                        else " ")))
                                 (CONCAT (APPLY (CADR (ASSOC (CAR TEMPLATE)
                                                             FUNCLST))
                                                (LIST DATE VERSION))
                                        CH
                                        (APPLY (CADR (ASSOC (CADR TEMPLATE)
                                                            FUNCLST))
                                               (LIST DATE VERSION))
                                        (if (EQUAL CH " ")
                                            then ", "
                                          else CH)
                                        (APPLY (CADR (ASSOC (CADDR TEMPLATE)
                                                            FUNCLST))
                                               (LIST DATE VERSION]
       (DATE])

(AMPM
  [LAMBDA (HOUR)
    (if (OR (LESSP (MKATOM HOUR)
                       12)
                (EQUAL (MKATOM HOUR)
                       24))
        then "a.m."
      else "p.m."])

(DATES.MENU.APPLY
  [LAMBDA (ITEM MENU)                                        (* ; 
                                                           "Edited 17-Oct-2021 12:05 by rmk:")
                                                             (* fsg "31-Jul-86 10:18")

         (* This function serves the purpose of calculating the stream and the editing 
       window from information stored on the window containing the menu.
       It then applies the appropiate function for each ITEM in the menu*)

    [SETQ ITEM (COND
                  ((ATOM ITEM)
                   ITEM)
                  (T (CAR ITEM]
    (LET* ([DATE.RECORD (fetch OBJECTDATUM of (GETMENUPROP MENU 'IMAGEOBJ]
           (DATE (fetch DATESTRING of DATE.RECORD)))
          (COND
             ((fetch ITEMS of MENU)
              (LET [(FUNCALL (CADR (ASSOC ITEM (fetch ITEMS of MENU]
                   (replace DISPLAY.DATE of DATE.RECORD with (EVAL FUNCALL))
                   (replace TEMPLATE.DATE of DATE.RECORD with (CADAR (LAST FUNCALL])

(DATES.MENU.WHENSELECTEDFN
  [LAMBDA (ITEM MENU MB)                                     (* ; 
                                                           "Edited 17-Oct-2021 12:01 by rmk:")
                                                             (* fsg "28-Jul-86 14:57")
    (COND
       ((OR (EQ MB 'LEFT)
            (EQ MB 'MIDDLE))
        (DATES.MENU.APPLY ITEM MENU])

(DATES.SET
  [LAMBDA (PROPERTY VALUE)
    (WINDOWPROP (CREATEW)
           PROPERTY VALUE)
    VALUE])

(FINDDAY
  [LAMBDA (OLDDATE VERSION)                                  (* shw%: " 1-Jul-85 11:28")
    (MKATOM (if (NUMP (SUBSTRING OLDDATE 1 2))
                then (SUBSTRING OLDDATE 1 2)
              else (SUBSTRING OLDDATE 2 2])

(FINDHOUR
  [LAMBDA (HOUR)                                             (* ss%: " 8-Feb-86 17:49")
    (COND
       ((LESSP (MKATOM HOUR)
               13)
        (COND
           [(LESSP (MKATOM HOUR)
                   10)
            (MKSTRING (CADR (UNPACK HOUR]
           (T HOUR)))
       (T (MKSTRING (SELECTQ (MKATOM HOUR)
                        (13 1)
                        (14 2)
                        (15 3)
                        (16 4)
                        (17 5)
                        (18 6)
                        (19 7)
                        (20 8)
                        (21 9)
                        (22 10)
                        (23 11)
                        (24 12)
                        NIL])

(FINDMONTH
  [LAMBDA (OLDDATE VERSION)                                  (* ; 
                                                           "Edited 17-Oct-2021 12:05 by rmk:")
                                                             (* shw%: " 1-Jul-85 11:38")
    (PROG ([DATES '((Jan 1 January)
                    (Feb 2 February)
                    (Mar 3 March)
                    (Apr 4 April)
                    (May 5 May)
                    (Jun 6 June)
                    (Jul 7 July)
                    (Aug 8 August)
                    (Sep 9 September)
                    (Oct 10 October)
                    (Nov 11 November)
                    (Dec 12 December]
           (OUTPUT NIL))
          [if (EQ VERSION 'ABBREV)
              then [SETQ OUTPUT (CAR (CDR (ASSOC (MKATOM (SUBSTRING OLDDATE 4 6))
                                                     DATES]
            else (SETQ OUTPUT (CAR (CDDR (ASSOC (MKATOM (SUBSTRING OLDDATE 4 6))
                                                    DATES]
          (RETURN OUTPUT])

(FINDTIME
  [LAMBDA (OLDDATE VERSION)                                  (* ; 
                                                           "Edited 17-Oct-2021 12:08 by rmk:")
                                                             (* shw%: "24-Jul-85 15:39")
    (LET ((HOUR (SUBSTRING OLDDATE 11 12))
          (MINUTES (SUBSTRING OLDDATE 14 15)))
         (if (EQ VERSION 'ABBREV)
             then (CONCAT (FINDHOUR HOUR)
                             ":" MINUTES " " (AMPM HOUR))
           else (if (EQ VERSION 'EURO)
                        then (SUBSTRING OLDDATE 11 15)
                      else (CONCAT (SELECTQ [if (LESSP (MKATOM MINUTES)
                                                               46)
                                                    then (MKATOM (FINDHOUR HOUR))
                                                  else (PLUS 1 (MKATOM (FINDHOUR HOUR]
                                           (1 "one")
                                           (2 "two")
                                           (3 "three")
                                           (4 "four")
                                           (5 "five")
                                           (6 "six")
                                           (7 "seven")
                                           (8 "eight")
                                           (9 "nine")
                                           (10 "ten")
                                           (11 "eleven")
                                           (12 "twelve")
                                           NIL)
                                      " "
                                      (if (AND (GREATERP (MKATOM MINUTES)
                                                          15)
                                                   (LESSP (MKATOM MINUTES)
                                                          45))
                                          then "thirty"
                                        else "o'clock")
                                      " "
                                      (if (AND (GREATERP (MKATOM MINUTES)
                                                          44)
                                                   (EQUAL (FINDHOUR HOUR)
                                                          "11"))
                                          then (if (EQUAL (AMPM HOUR)
                                                                  "a.m.")
                                                       then "p.m."
                                                     else "a.m.")
                                        else (AMPM HOUR])

(FINDYEAR
  [LAMBDA (OLDDATE VERSION)                                  (* ; 
                                                           "Edited 17-Oct-2021 12:09 by rmk:")
                                                             (* shw%: " 1-Jul-85 11:31")
    (HELP "Y2K problem")
    (if (EQ VERSION 'ABBREV)
        then (MKATOM (SUBSTRING OLDDATE 8 9))
      else (MKATOM (CONCAT "19" (SUBSTRING OLDDATE 8 9])

(NUMP
  [LAMBDA (N)                                                (* edited%: " 4-Apr-86 17:55")
                                                             (* changed)
    (NOT (NULL (NUMBERP (MKATOM N])

(WHICHDATE
  [LAMBDA (VAR1 VAR2 YEAR OLDDATE VERSION)                   (* ; 
                                                           "Edited 17-Oct-2021 12:00 by rmk:")
                                                             (* edited " 1-Jan-00 00:00")

         (* * comment)

    (PROG (DIVIDER)
          (SETQ DIVIDER (if (EQ VERSION 'ABBREV)
                            then "/"
                          else " "))
          (RETURN (MKATOM (CONCAT (APPLY VAR1 (LIST OLDDATE VERSION))
                                 DIVIDER
                                 (APPLY VAR2 (LIST OLDDATE VERSION))
                                 DIVIDER
                                 (APPLY YEAR (LIST OLDDATE VERSION])
)
(DECLARE%: EVAL@COMPILE

(RECORD DATEOBJ (DATESTRING DISPLAY.DATE TEMPLATE.DATE))
)
(PUTPROPS DATE COPYRIGHT ("Leland Stanford Junior University" 1987))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1324 19553 (DATEOBJ 1334 . 3118) (DATEOBJP 3120 . 3664) (DATE.DISPLAYFN 3666 . 3932) (
DATE.IMAGEBOXFN 3934 . 4679) (CURRENT.DISPLAY.FONT 4681 . 5701) (DATE.PUTFN 5703 . 6181) (DATE.GETFN 
6183 . 6764) (DATE.BUTTONEVENTINFN 6766 . 9151) (DATES.TEMPLATE 9153 . 11430) (AMPM 11432 . 11635) (
DATES.MENU.APPLY 11637 . 12745) (DATES.MENU.WHENSELECTEDFN 12747 . 13145) (DATES.SET 13147 . 13253) (
FINDDAY 13255 . 13512) (FINDHOUR 13514 . 14259) (FINDMONTH 14261 . 15344) (FINDTIME 15346 . 18130) (
FINDYEAR 18132 . 18577) (NUMP 18579 . 18797) (WHICHDATE 18799 . 19551)))))
STOP
