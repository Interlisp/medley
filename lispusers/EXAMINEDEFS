(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "26-Dec-2021 11:01:08" 
{DSK}<Users>kaplan>Local>medley3.5>my-medley>lispusers>EXAMINEDEFS.;15 5866   

      :CHANGES-TO (VARS EXAMINEDEFSCOMS)

      :PREVIOUS-DATE "24-Dec-2021 22:40:09" 
{DSK}<Users>kaplan>Local>medley3.5>my-medley>lispusers>EXAMINEDEFS.;14)


(PRETTYCOMPRINT EXAMINEDEFSCOMS)

(RPAQQ EXAMINEDEFSCOMS ((FNS EXAMINEDEFS)
                        (INITVARS (EXAMINEDEFS-PROCESS-LIST))))
(DEFINEQ

(EXAMINEDEFS
  [LAMBDA (NAME TYPE SOURCE1 SOURCE2 TITLE1 TITLE2 REGION)   (* ; "Edited 24-Dec-2021 22:39 by rmk")
                                                             (* ; "Edited 20-Dec-2021 11:06 by rmk")

    (* ;; "This provides for side-by-side examination of separate but presumably related expressions.  The (LISTP) expressions can be provided directly as SOURCE1 and SOURCE2 or, if NAME is given the copies of the definitions of NAME as TYPE on the two sources are examined.")

    (* ;; "")

    (* ;; "Examination is in side-by-side attached SEDIT windows if SEDIT is the EDITMODE.  You can use SEDIT operations to zoom in on the location of any changes, deleting common stuff for example.  But you are always working on a copy, so that changes are safe and ephemeral.  This is an examination, not an edit.")

    (CL:UNLESS NAME
        (CL:UNLESS (LISTP SOURCE1)
               (ERROR SOURCE1 " cannot be examined"))
        (CL:UNLESS (LISTP SOURCE2)
               (ERROR SOURCE2 " cannot be examined")))

    (* ;; "TITLE1 and TITLE2 are optional strings that will be used to construct the titles of the SEDIT windows. We would like to know where GETDEF got the definition so we can use that, but there isn't an interface that provides that information (extended WHEREIS?)")

    (LET (DEF1 DEF2)
         (SETQ DEF1 (IF (LISTP SOURCE1)
                        THEN 
                             (* ;; "Copy to simulate READONLY")

                             (SETQ DEF1 (COPY SOURCE1))
                      ELSEIF (GETDEF NAME TYPE SOURCE1)
                      ELSE (ERROR NAME " not found on " SOURCE1)))
         (SETQ DEF2 (IF (LISTP SOURCE2)
                        THEN (COPY SOURCE2)
                      ELSEIF (GETDEF NAME TYPE SOURCE2)
                      ELSE (ERROR NAME " not found on " SOURCE2)))
         (CL:UNLESS TITLE1
             (SETQ TITLE1 (OR (AND SOURCE1 (LITATOM SOURCE1))
                              "File 1")))
         (CL:UNLESS TITLE2
             (SETQ TITLE2 (OR (AND SOURCE2 (LITATOM SOURCE2))
                              "File 2")))
         (SELECTQ (EDITMODE)
             (SEDIT:SEDIT 
                          (* ;; 
                          "A kludge to eliminate dangling SEDIT processes from previous examinations")

                          [SETQ EXAMINEDEFS-PROCESS-LIST
                           (FOR PAIR IN EXAMINEDEFS-PROCESS-LIST
                              COLLECT (IF (OPENWP (CAR PAIR))
                                          THEN PAIR
                                        ELSE (DEL.PROCESS (CDR PAIR))
                                             (GO $$ITERATE]

                          (* ;; "Set it up for new side-by-side regions that are forgotten when the window is closed.  Their shape is usually not that useful for regular edits.")

                          (* ;; 
     "Crude suggestions for height, width, position.  Suggest shorter window for  smaller structures")

                          (CL:UNLESS (REGIONP REGION)
                              (SETQ REGION (GETREGION)))
                          (LET (W1 W2 HALFWIDTH)
                               (SETQ HALFWIDTH (IQUOTIENT (FETCH (REGION WIDTH) OF REGION)
                                                      2))
                               [SETQ W1
                                (SEDIT:GET-WINDOW (SEDIT:SEDIT DEF1
                                                         `(:NAME ,(CONCAT NAME " from " TITLE1)
                                                                 :REGION
                                                                 ,(CREATE REGION
                                                                     USING REGION WIDTH _ HALFWIDTH)
                                                                 :DONT-KEEP-WINDOW-REGION T]
                               [SETQ W2
                                (SEDIT:GET-WINDOW
                                 (SEDIT:SEDIT DEF2
                                        `(:NAME ,(CONCAT NAME " from " TITLE2)
                                                :REGION
                                                ,(CREATE REGION USING REGION LEFT _
                                                                      (IPLUS (FETCH (REGION LEFT)
                                                                                OF REGION)
                                                                             HALFWIDTH)
                                                                      WIDTH _ HALFWIDTH)
                                                :DONT-KEEP-WINDOW-REGION T]

                               (* ;; 
   "So we can kill the processes on the next call, if they still exist after the windows are closed.")

                               [PUSH EXAMINEDEFS-PROCESS-LIST (CONS W1 (WINDOWPROP W1 'PROCESS))
                                     (CONS W2 (WINDOWPROP W2 'PROCESS]
                               (ATTACHWINDOW W2 W1 'RIGHT 'JUSTIFY)
                               (MODERNWINDOW W2)))
             (PROGN (EDITE DEF1)
                    (EDITE DEF2])
)

(RPAQ? EXAMINEDEFS-PROCESS-LIST )
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (501 5804 (EXAMINEDEFS 511 . 5802)))))
STOP
