(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 9-Dec-2025 14:00:20" {DSK}<home>matt>Interlisp>medley>lispusers>FONTSAMPLER.;2 14236  

      :EDIT-BY "mth"

      :CHANGES-TO (FNS FontTable FontSample)

      :PREVIOUS-DATE " 8-Dec-2025 22:17:11" {DSK}<home>matt>Interlisp>medley>lispusers>FONTSAMPLER.;1
)


(PRETTYCOMPRINT FONTSAMPLERCOMS)

(RPAQQ FONTSAMPLERCOMS
       ((FNS FontSample FontSampleFaked FontTable)
        [VARS (*INTERESTING-CHARSETS* '(0 33 34 38 39 238 239 240 241]
        (DECLARE%: EVAL@COMPILE DONTCOPY (FILES (LOADCOMP)
                                                FONT))))
(DEFINEQ

(FontSample
  [LAMBDA (Fonts CharacterSets Printer StreamType Hexadecimal ColumnMajor)
                                                             (* ; "Edited  9-Dec-2025 13:48 by mth")
                                                             (* ; "Edited  5-Dec-2025 11:06 by mth")
                                                             (* ; "Edited  5-Feb-2025 17:02 by mth")
                                                             (* ; "Edited 29-Apr-87 22:03")
    (LET* [[TitleFont (FONTCREATE NIL 12 'MRR 0 (SETQ StreamType (OR StreamType (PRINTERTYPE Printer]
           (FontList (if (LISTP Fonts)
                       else (CONS Fonts)))
           [Stream (OPENIMAGESTREAM Printer StreamType (LIST 'FONTS (CONS TitleFont FontList]
           (InchesToPrinterUnits (FTIMES 72.0 (DSPSCALE NIL Stream)))
           (LastFont (CAR (LAST FontList)))
           (AllCharacterSets (CONSTANT (for CS from 0 to 255 collect CS]
          (CL:UNLESS [OR (LISTP CharacterSets)
                         (MEMB CharacterSets '(T :INCORE :ALL :INTERESTING]
              (SETQ CharacterSets (LIST (OR CharacterSets 0))))
          (DSPRIGHTMARGIN (fetch (REGION WIDTH) of (DSPCLIPPINGREGION NIL Stream))
                 Stream)
          (for Font in FontList do 
                                   (* ;; "Check for the special charset list builders")

                                   (LET (FontCharacterSets (SlugCharsetInfo (\GETCHARSETINFO Font 
                                                                                   SLUGCHARSET)))
                                        (SETQ FontCharacterSets
                                         (SELECTQ CharacterSets
                                             (:ALL 
                                                   (* ;; "Forcibly install ALL CharacterSets.")

                                                   (for CS in AllCharacterSets
                                                      when (\INSURECHARSETINFO Font CS) collect
                                                                                        CS))
                                             (:INTERESTING (for CS in *INTERESTING-CHARSETS*
                                                              when (\INSURECHARSETINFO Font CS)
                                                              collect CS))
                                             ((T :INCORE) 
                                                  (for CS in AllCharacterSets
                                                     when (\GETCHARSETINFO Font CS) collect CS))
                                             CharacterSets))

                                        (* ;; 
                                    "Exclude any CharacterSet known to reference the SlugCharsetInfo")

                                        (CL:WHEN SlugCharsetInfo

                                            (* ;; 
                         "Only if SlugCharsetInfo is non-NIL, else it won't load a requested charset")

                                            (SETQ FontCharacterSets
                                             (for CS in FontCharacterSets
                                                unless (EQ SlugCharsetInfo (\GETCHARSETINFO Font CS))
                                                collect CS)))

                                        (* ;; 
                                        "Probably ought to report charsets eliminated by the above.")

                                        (* ;; " At least report if NO charsets remain for this font.")

                                        (CL:UNLESS FontCharacterSets (printout T 
                                             "All requested character sets are empty for this font: "
                                                                            Font T))
                                        (for CharacterSet in FontCharacterSets
                                           bind (LastCharacterSet _ (CAR (LAST FontCharacterSets)))
                                           do (FontTable Font CharacterSet Stream
                                                     (OR (NEQ Font LastFont)
                                                         (NEQ CharacterSet LastCharacterSet))
                                                     TitleFont InchesToPrinterUnits Hexadecimal 
                                                     ColumnMajor))) finally (CLOSEF Stream])

(FontSampleFaked
  [LAMBDA (FontAsList Printer StreamType ColumnMajor)        (* ; "Edited  8-Dec-2025 21:19 by mth")
                                                       (* ; "Edited 27-Apr-87 18:12 by N.H.Briggs ")
    (LET* [[TitleFont (FONTCREATE NIL 12 'MRR 0 (OR StreamType (PRINTERTYPE Printer]
           (Font)
           [Stream (OPENIMAGESTREAM Printer StreamType (LIST 'FONTS (LIST TitleFont]
           (InchesToPrinterUnits (FTIMES 72.0 (DSPSCALE NIL Stream]
          [SETQ Font (NCREATE 'FONTDESCRIPTOR (DEFAULTFONT (OR StreamType (PRINTERTYPE Printer]
          (replace FONTFAMILY of Font with (CAR FontAsList))
          (replace FONTSIZE of Font with (CADR FontAsList))
          (replace FONTFACE of Font with (\FONTFACE (CADDR FontAsList)))
          (FontTable Font '(0)
                 Stream NIL TitleFont InchesToPrinterUnits NIL ColumnMajor)
          (CLOSEF Stream])

(FontTable
  [LAMBDA (Font CharacterSet Stream FormFeed TitleFont InchesToPrinterUnits Hexadecimal ColumnMajor)
                                                             (* ; "Edited  9-Dec-2025 13:23 by mth")
                                                             (* ; "Edited  5-Dec-2025 11:09 by mth")
                                                             (* ; "Edited  5-Feb-2025 17:03 by mth")
                                                             (* ; "Edited  3-Feb-2025 20:07 by mth")
                                                             (* ; "Edited 29-Apr-87 22:36")
    (LET*
     ((Family (FONTPROP Font 'FAMILY))
      (Face (FONTPROP Font 'FACE))
      (Size (FONTPROP Font 'SIZE))
      (Title (CONCAT " " Size "pt " (L-CASE Family T)
                    " "
                    (L-CASE Face T)
                    " Character set "))
      (StreamType (IMAGESTREAMTYPE Stream))
      [UseDisplayFontBitmaps (AND (EQ (FONTPROP Font 'DEVICE)
                                      'DISPLAY)
                                  (NOT (EQ StreamType 'DISPLAY]
      [RelativeDescent (FQUOTIENT (FONTPROP Font 'DESCENT)
                              (FONTPROP Font 'HEIGHT]
      (XCellSpacing (TIMES 0.45 InchesToPrinterUnits))
      (YCellSpacing (TIMES 0.5 InchesToPrinterUnits))
      ColLabelStep RowLabelStep)
     (printout T Title .I0.8 CharacterSet "Q" T)
     (RESETLST
         (RESETSAVE (RADIX (if Hexadecimal
                               then 16
                             else 8)))
         (MOVETO (FTIMES 0.75 InchesToPrinterUnits)
                (FTIMES 10 InchesToPrinterUnits)
                Stream)
         (DSPFONT TitleFont Stream)
         (if Hexadecimal
             then (printout Stream Title .I0.16 CharacterSet)
           else (printout Stream Title .I0.8 CharacterSet))
         (DSPYPOSITION (PLUS (DSPYPOSITION NIL Stream)
                             (TIMES -0.4 (FONTHEIGHT TitleFont)))
                Stream)
         (printout Stream (if Hexadecimal
                              then "16"
                            else "8"))
         (if ColumnMajor
             then (SETQ ColLabelStep 16)
                  (SETQ RowLabelStep 1)
           else (SETQ ColLabelStep 1)
                (SETQ RowLabelStep 16))
         (for XPosition from (TIMES 0.75 InchesToPrinterUnits) by XCellSpacing as Counter
            from 0 to (ITIMES ColLabelStep 15) by ColLabelStep bind (YPosition _ (TIMES 9.5 
                                                                                 InchesToPrinterUnits
                                                                                        ))
            do (MOVETO XPosition YPosition Stream)
               (PRINTNUM (if Hexadecimal
                             then '(FIX 2 16 T)
                           elseif ColumnMajor
                             then '(FIX 1 8 NIL T)
                           else '(FIX 2 8))
                      Counter Stream))
         (for YPosition from (TIMES 9 InchesToPrinterUnits) by (MINUS YCellSpacing) as Counter
            from 0 to (ITIMES RowLabelStep 15) by RowLabelStep bind (XPosition _ (TIMES 0.25 
                                                                                 InchesToPrinterUnits
                                                                                        ))
            do (MOVETO XPosition YPosition Stream)
               (PRINTNUM (if Hexadecimal
                             then '(FIX 2 16 T)
                           elseif ColumnMajor
                             then '(FIX 2 8)
                           else '(FIX 3 8))
                      Counter Stream)))
     (DRAWLINE (TIMES 0.25 InchesToPrinterUnits)
            (TIMES 9.3 InchesToPrinterUnits)
            (TIMES 8.0 InchesToPrinterUnits)
            (TIMES 9.3 InchesToPrinterUnits)
            (DSPSCALE NIL Stream)
            'PAINT Stream)
     (DRAWLINE (TIMES 0.6 InchesToPrinterUnits)
            (TIMES 9.7 InchesToPrinterUnits)
            (TIMES 0.6 InchesToPrinterUnits)
            (TIMES 1.25 InchesToPrinterUnits)
            (DSPSCALE NIL Stream)
            'PAINT Stream)
     (CL:UNLESS UseDisplayFontBitmaps (DSPFONT Font Stream))
     (for XPosition from (TIMES 0.75 InchesToPrinterUnits) by XCellSpacing as XCounter from 0
        to 15 bind [RangedCodesStreamType _ (MEMB StreamType '(DISPLAY INTERPRESS]
        do
        [for YPosition from (TIMES 9 InchesToPrinterUnits) by (MINUS YCellSpacing) as YCounter
           from 0 to 15
           do (LET* ((CharacterCode (IPLUS (ITIMES YCounter RowLabelStep)
                                           (ITIMES XCounter ColLabelStep)))
                     (CCode (IPLUS (ITIMES CharacterSet 256)
                                   CharacterCode)))
                    (MOVETO XPosition YPosition Stream)
                    (if UseDisplayFontBitmaps
                        then (LET* ((Glyph (GETCHARBITMAP CCode Font))
                                    (ImSize (BITMAPIMAGESIZE Glyph NIL Stream))
                                    (ImWidth (CAR ImSize))
                                    (ImHeight (CDR ImSize)))
                                   (BITBLT Glyph 0 0 Stream XPosition (FDIFFERENCE YPosition
                                                                             (FTIMES ImHeight 
                                                                                    RelativeDescent))
                                          ImWidth ImHeight 'INPUT 'REPLACE))
                      else (if (AND (NEQ CharacterCode (CHARCODE FF))
                                    (if RangedCodesStreamType
                                        then (OR (AND (IGREATERP CharacterCode 31)
                                                      (ILESSP CharacterCode 127))
                                                 (AND (IGREATERP CharacterCode 160)
                                                      (ILESSP CharacterCode 255)))
                                      else T))
                               then (PRINTCCODE CCode Stream]
        (printout T "."))
     (MOVETO (FTIMES 0.75 InchesToPrinterUnits)
            (FTIMES 0.75 InchesToPrinterUnits)
            Stream)
     (DSPFONT TitleFont Stream)
     (if Hexadecimal
         then (printout Stream Title .I0.16 CharacterSet)
       else (printout Stream Title .I0.8 CharacterSet))
     (DSPYPOSITION (PLUS (DSPYPOSITION NIL Stream)
                         (TIMES -0.4 (FONTHEIGHT TitleFont)))
            Stream)
     (printout Stream (if Hexadecimal
                          then "16"
                        else "8"))
     [if (EQ (FILENAMEFIELD (FULLNAME Stream)
                    'HOST)
             'LPT)
         then (MOVETO (FTIMES 0.75 InchesToPrinterUnits)
                     (FTIMES 0.5 InchesToPrinterUnits)
                     Stream)
              (printout Stream " on " (L-CASE (OR (FILENAMEFIELD (FULLNAME Stream)
                                                         'DEVICE)
                                                  (FILENAMEFIELD (FULLNAME Stream)
                                                         'NAME))
                                             T)
                     ", "
                     (GDATE NIL (DATEFORMAT NO.TIME SPACES]
     (if FormFeed
         then (DSPNEWPAGE Stream))
     (printout T " done." T])
)

(RPAQQ *INTERESTING-CHARSETS* (0 33 34 38 39 238 239 240 241))
(DECLARE%: EVAL@COMPILE DONTCOPY 

(FILESLOAD (LOADCOMP)
       FONT)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (655 14073 (FontSample 665 . 5357) (FontSampleFaked 5359 . 6317) (FontTable 6319 . 14071
)))))
STOP
