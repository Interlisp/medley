(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "24-Aug-2021 18:12:00" {DSK}<home>larry>GITSTAGEFNS.;1 8020   

      previous date%: "14-Aug-2021 10:01:24" {DSK}<tmp>GITSTAGEFNS.dat;1)


(PRETTYCOMPRINT GITSTAGEFNSCOMS)

(RPAQQ GITSTAGEFNSCOMS
       ([INITVARS (GITSTAGEDIRECTORY (CONCAT LOGINHOST/DIR "gitstage/"))
               (GITSTAGESUBDIRECTORIES '(sources library lispusers fonts unicode]
        (FNS GITSTAGE LASTSUBDIR GITUNSTAGE GITSTAGEDELETE GITSTAGEDFILES)
        (FNS PATCHONTOP)
        (COMMANDS cdgs)))

(RPAQ? GITSTAGEDIRECTORY (CONCAT LOGINHOST/DIR "gitstage/"))

(RPAQ? GITSTAGESUBDIRECTORIES '(sources library lispusers fonts unicode))
(DEFINEQ

(GITSTAGE
  [LAMBDA (FILES ONLYSYMBOLIC DONTPRESERVEVERSIONS)     (* ; "Edited 13-Aug-2021 08:34 by rmk:")
    [SETQ FILES (FOR F INSIDE FILES JOIN (IF (STRPOS "*" F)
                                                         THEN (FILDIR F)
                                                       ELSEIF (MKLIST (FINDFILE F T))
                                                       ELSE (ERROR "FILE NOT FOUND" F]
    (CL:UNLESS ONLYSYMBOLIC
        (FOR F IN FILES UNLESS (MEMB (FILENAMEFIELD F 'EXTENSION)
                                                 *COMPILED-EXTENSIONS*)
           DO (FOR EXT CF IN *COMPILED-EXTENSIONS*
                     WHEN (SETQ CF (INFILEP (PACKFILENAME 'EXTENSION EXT 'VERSION NIL
                                                       'BODY F))) DO (PUSH FILES CF))))
    (FOR F SUBDIR FDIR INSIDE FILES
       COLLECT 

             (* ;; "Flush the host, assume they match")

             (SETQ SUBDIR (LASTSUBDIR F MEDLEYDIR))
             (CL:UNLESS SUBDIR (ERROR "Medley subdirectory not recognized" F))
             (SETQ FDIR (CONCAT GITSTAGEDIRECTORY "/" (OR SUBDIR "unknown")))
             (LIST F (COPYFILE F (PACKFILENAME 'DIRECTORY FDIR 'VERSION (CL:UNLESS 
                                                                                 DONTPRESERVEVERSIONS
                                                                            (FILENAMEFIELD
                                                                             F
                                                                             'VERSION))
                                        'BODY F])

(LASTSUBDIR
  [LAMBDA (FILE HEADDIR)                                (* ; "Edited  2-Aug-2021 14:57 by rmk:")

    (* ;; "Returns sources if FILE is HEADDIR/sources")

    (LET ([FDIR (L-CASE (FILENAMEFIELD FILE 'DIRECTORY]
          FDIRPOS)
         (IF (SETQ FDIRPOS (STRPOS (FILENAMEFIELD (L-CASE HEADDIR)
                                              'DIRECTORY)
                                      FDIR 1 NIL NIL T))
             THEN (SUBSTRING FDIR (ADD1 FDIRPOS])

(GITUNSTAGE
  [LAMBDA (FILES DELETE PRESERVEVERSIONS)               (* ; "Edited  5-Aug-2021 14:51 by rmk:")
    [SETQ FILES (IF FILES
                    THEN (FOR F INSIDE FILES
                                JOIN (FOR SD SDF IN GITSTAGESUBDIRECTORIES
                                            WHEN (SETQ SDF (INFILEP (PACKFILENAME 'DIRECTORY
                                                                               (CONCAT 
                                                                                    GITSTAGEDIRECTORY
                                                                                      "/" SD)
                                                                               'BODY F)))
                                            COLLECT SDF))
                  ELSE (FOR SD IN GITSTAGESUBDIRECTORIES
                              JOIN (FILDIR (CONCAT GITSTAGEDIRECTORY "/" SD "/*.;"]
    (FOR F SD VAL IN FILES COLLECT (SETQ SD (LASTSUBDIR F GITSTAGEDIRECTORY))
                                         [SETQ VAL (LIST F (COPYFILE
                                                            F
                                                            (PACKFILENAME 'DIRECTORY (MEDLEYDIR
                                                                                      SD)
                                                                   'VERSION
                                                                   (CL:WHEN PRESERVEVERSIONS
                                                                       (FILENAMEFIELD F 'VERSION))
                                                                   'BODY F]
                                         (CL:WHEN DELETE (DELFILE F))
                                         VAL])

(GITSTAGEDELETE
  [LAMBDA (FILES ONLYSYMBOLIC ALLVERSIONS)              (* ; "Edited  2-Aug-2021 16:17 by rmk:")
    [SETQ FILES (IF FILES
                    THEN (FOR F INSIDE FILES
                                JOIN (FOR SD SDF IN GITSTAGESUBDIRECTORIES
                                            WHEN (SETQ SDF (FILDIR (PACKFILENAME
                                                                        'DIRECTORY
                                                                        (CONCAT GITSTAGEDIRECTORY "/"
                                                                               SD)
                                                                        'VERSION
                                                                        (CL:WHEN ALLVERSIONS
                                                                               '*)
                                                                        'BODY F))) JOIN SDF))
                  ELSE (FOR SD IN GITSTAGESUBDIRECTORIES
                              JOIN (FILDIR (CONCAT GITSTAGEDIRECTORY "/" SD (CL:IF ALLVERSIONS
                                                                                    "*.*;*"
                                                                                    "/*.*;")]
    (CL:UNLESS ONLYSYMBOLIC
        (FOR F IN FILES UNLESS (MEMB (FILENAMEFIELD F 'EXTENSION)
                                                 *COMPILED-EXTENSIONS*)
           DO (FOR EXT CF IN *COMPILED-EXTENSIONS*
                     WHEN (SETQ CF (INFILEP (PACKFILENAME 'EXTENSION EXT 'VERSION NIL
                                                       'BODY F))) DO (PUSH FILES CF))))
    (CL:WHEN FILES
        (PRINTOUT T "OK to delete these staged files? " 4 .PPVTL FILES T)
        (CL:WHEN (EQ 'Y (ASKUSER))
            (FOR F IN FILES DO (DELFILE F))))])

(GITSTAGEDFILES
  [LAMBDA (FILES COMPILETOO)                            (* ; "Edited  4-Aug-2021 17:42 by rmk:")
    (FOR F INSIDE (OR FILES "*.*;*")
       JOIN (FOR SD IN GITSTAGESUBDIRECTORIES
                   JOIN (FILDIR (PACKFILENAME 'DIRECTORY (CONCAT GITSTAGEDIRECTORY "/" SD "/")
                                           'EXTENSION
                                           (CL:IF COMPILETOO
                                               '*
                                               NIL)
                                           'BODY F])
)
(DEFINEQ

(PATCHONTOP
  [LAMBDA (FILES)                                       (* ; "Edited 14-Aug-2021 10:00 by rmk:")

    (* ;; "Loads all of the compiled functions for FILES to the CODE property, then makes them all active at the same time.")

    (SETQ FILES (MKLIST FILES))
    (FOR F IN FILES DO (LOAD (PACKFILENAME 'EXTENSION 'LCOM 'BODY F)
                                         'PROP))
    (FOR FN IN (FOR F IN FILES JOIN (FILEFNSLST F))
       DO (UNSAVEDEF FN 'CODE])
)

(DEFCOMMAND cdgs (SUBDIR) (/CNDIR (CONCAT LOGINHOST/DIR "/gitstage/" (OR SUBDIR ""))))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (718 7374 (GITSTAGE 728 . 2440) (LASTSUBDIR 2442 . 2939) (GITUNSTAGE 2941 . 4797) (
GITSTAGEDELETE 4799 . 6774) (GITSTAGEDFILES 6776 . 7372)) (7375 7909 (PATCHONTOP 7385 . 7907)))))
STOP
