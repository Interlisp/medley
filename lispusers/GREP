(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "18-Jun-2022 10:38:30" 
{DSK}<users>kaplan>local>medley3.5>working-medley>lispusers>GREP.;3 4351   

      :CHANGES-TO (FNS DOGREP GREP)
                  (VARS GREPCOMS)

      :PREVIOUS-DATE "14-May-86 08:04:43" 
{DSK}<users>kaplan>local>medley3.5>working-medley>lispusers>GREP.;1)


(* ; "
Copyright (c) 1984-1986 by Xerox Corporation.
")

(PRETTYCOMPRINT GREPCOMS)

(RPAQQ GREPCOMS ((FNS DOGREP GREP PHONE)
                 (INITVARS (PHONELISTFILES))))
(DEFINEQ

(DOGREP
  [LAMBDA (STR FILES OUTSTREAM)

    (* ;; "Edited 18-Jun-2022 10:38 by rmk: Search for linebreaks directly, without calling BFILEPOS or FILEPOS just for EOL character.  Also now compatible with external formats (if FFILEPOS is), and upgraded to full directory specification")
                                                             (* Newman "14-May-86 08:04")

(* ;;; "Originally coded by Larry Masinter.")

(* ;;; "No longer permanently modifies the DSPFONT when DSPFONT is not the same as the DEFAULTFONT.  -DVN '14-May-86 08:03:59'")

         (* * No longer permanently modifies the DSPFONT when DSPFONT is not the same as 
         the DEFAULTFONT. -DVN "14-May-86 08:03:59")

    (if (LISTP FILES)
        then (for FILE in FILES do (DOGREP STRS FILE))
      elseif (STRPOS "*" FILES)
        then (DOGREP STRS (DIRECTORY FILES NIL "" ""))
      elseif (DIRECTORYNAMEP FILES)
      else
      (CL:UNLESS OUTSTREAM (SETQ OUTSTREAM T))
      (RESETLST
          (RESETSAVE NIL (LIST 'DSPFONT (DSPFONT)))
          [CL:WITH-OPEN-FILE
           (STREAM FILES :DIRECTION :INPUT)
           (bind FOUND for STR inside STRS first (SETFILEINFO STREAM 'ENDOFSTREAMOP
                                                        (FUNCTION NILL))
              do (SETFILEPTR STREAM 0)
                 (bind POS while (SETQ POS (FFILEPOS STR STREAM NIL NIL NIL NIL UPPERCASEARRAY))
                    do (OR FOUND (PROGN (PRINTOUT OUTSTREAM T .FONT COMMENTFONT "(from " (FULLNAME
                                                                                          STREAM)
                                               ")" .FONT DEFAULTFONT T)
                                        (SETQ FOUND T))) 

                       (* ;; "Copying from the beginning of this line. Originally this used BFILEPOS (backwards FILEPOS?), which did repeated calls to forward FFILEPOS in what appears to be a binary set of probes.  But FFILEPOS is really SLOW-POS for a single character, and the last line-start is presumaby not that far back.  So just walk backwards.")

                       (COPYCHARS STREAM OUTSTREAM (OR [WHILE (\BACKCCODE.EOLC STREAM ANY.EOLC)
                                                          WHEN (EQ (CHARCODE EOL)
                                                                   (\PEEKCCODE STREAM T ANY.EOLC))
                                                          DO (RETURN (ADD1 (GETFILEPTR STREAM]
                                                       0)
                              POS)
                       (DSPFONT BOLDFONT)
                       (COPYCHARS STREAM OUTSTREAM POS (ADD POS (NCHARS STR)))
                       (DSPFONT DEFAULTFONT) 

                       (* ;; "Copying to the end of this line (or end of file)")

                       (BIND C DO (SELCHARQ (SETQ C (\INCCODE.EOLC STREAM ANY.EOLC))
                                       ((EOL NIL) 
                                            (RETURN))
                                       (PRINTCCODE C OUTSTREAM)))
                       (TERPRI OUTSTREAM])])

(GREP
  [LAMBDA (STRS FILES)                                       (* ; "Edited 18-Jun-2022 09:50 by rmk")
                                                             (* lmm " 1-Apr-85 15:27")
    (RESETLST
        (DOGREP STRS FILES T))])

(PHONE
  [LAMBDA (NAME)                                             (* lmm " 5-Mar-86 12:14")
    (GREP NAME (OR PHONELISTFILES (PROMPTFORWORD "Name of directory file: "])
)

(RPAQ? PHONELISTFILES )
(PUTPROPS GREP COPYRIGHT ("Xerox Corporation" 1984 1985 1986))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (554 4236 (DOGREP 564 . 3787) (GREP 3789 . 4049) (PHONE 4051 . 4234)))))
STOP
