(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "20-Jan-2024 14:14:15" {WMEDLEY}<lispusers>GREP.;23 6722   

      :EDIT-BY rmk

      :CHANGES-TO (FNS TGREP DOGREP LISPSOURCEOUTCHARFN)
                  (VARS GREPCOMS)

      :PREVIOUS-DATE "20-Jan-2024 12:47:51" {WMEDLEY}<lispusers>GREP.;17)


(PRETTYCOMPRINT GREPCOMS)

(RPAQQ GREPCOMS
       [(FNS DOGREP GREP LISPSOURCEOUTCHARFN TGREP)
        (P (MOVD? 'NILL 'TEDIT.FORMATTEDFILEP))
        (DECLARE%: EVAL@COMPILE DONTCOPY (FILES (FROM VALUEOF (MEDLEYDIR "loadups"))
                                                EXPORTS.ALL))
        (COMS (FNS PHONE)
              (INITVARS (PHONELISTFILES])
(DEFINEQ

(DOGREP
  [LAMBDA (STRS FILES OUTSTREAM)

    (* ;; "Edited 20-Jan-2024 13:12 by rmk")

    (* ;; "Edited 19-Jul-2022 22:26 by rmk")

    (* ;; "Edited 26-Jun-2022 14:36 by rmk")
                                                             (* Newman "14-May-86 08:04")
                                                             (* Newman "14-May-86 08:04")

(* ;;; "Originally coded by Larry Masinter.")

(* ;;; "No longer permanently modifies the DSPFONT when DSPFONT is not the same as the DEFAULTFONT.  -DVN '14-May-86 08:03:59'")

         (* * No longer permanently modifies the DSPFONT when DSPFONT is not the same as 
         the DEFAULTFONT. -DVN "14-May-86 08:03:59")

    (if (LISTP FILES)
        then (for FILE in FILES do (DOGREP STRS FILE OUTSTREAM))
      elseif (STRPOS "*" FILES)
        then (DOGREP STRS (DIRECTORY FILES NIL "" "")
                    OUTSTREAM)
      elseif (DIRECTORYNAMEP FILES)
      elseif (CL:WITH-OPEN-FILE
              (STREAM (OR (FINDFILE FILES T)
                          FILES)
                     :DIRECTION :INPUT)
              (bind FOUND OUTSTREAM-OUTCHARFN declare (SPECVARS OUTSTREAM-OUTCHARFN) for STR
                 inside STRS first (SETFILEINFO STREAM 'ENDOFSTREAMOP (FUNCTION NILL))
                                   (CL:WHEN (LISPSOURCEFILEP STREAM)
                                       (SETQ OUTSTREAM-OUTCHARFN (fetch (STREAM OUTCHARFN)
                                                                    of OUTSTREAM))
                                       (replace (STREAM OUTCHARFN) of OUTSTREAM
                                          with (FUNCTION LISPSOURCEOUTCHARFN)))
                 finally (replace (STREAM OUTCHARFN) of OUTSTREAM with OUTSTREAM-OUTCHARFN)
                 do (SETFILEPTR STREAM 0)
                    (bind POS while (SETQ POS (FFILEPOS STR STREAM NIL (CAR (TEDIT.FORMATTEDFILEP
                                                                             STREAM))
                                                     NIL NIL UPPERCASEARRAY))
                       do (CL:UNLESS FOUND
                              (PRINTOUT OUTSTREAM T T .FONT BOLDFONT "(from " (FULLNAME STREAM)
                                     ")" .FONT DEFAULTFONT T)
                              (SETQ FOUND T)) 

                          (* ;; "Copying from the beginning of this line. Originally this used BFILEPOS (backwards FILEPOS?), which did repeated calls to forward FFILEPOS in what appears to be a binary set of probes.  But FFILEPOS is really SLOW-POS for a single character, and the last line-start is presumaby not that far back.  So just walk backwards.")

                          (COPYCHARS STREAM OUTSTREAM (DO (SELCHARQ (\BACKCCODE.EOLC STREAM
                                                                           'ANY)
                                                               (EOL (\INCCODE.EOLC STREAM)
                                                                    (RETURN (GETFILEPTR STREAM)))
                                                               (NIL (RETURN 0))
                                                               NIL))
                                 POS)
                          (DSPFONT BOLDFONT OUTSTREAM)
                          (COPYCHARS STREAM OUTSTREAM POS (ADD POS (NCHARS STR)))
                          (DSPFONT DEFAULTFONT OUTSTREAM) 

                          (* ;; "Copying to the end of this line (or end of file)")

                          (BIND C DO (SELCHARQ (SETQ C (\INCCODE.EOLC STREAM 'ANY))
                                          ((EOL NIL) 
                                               (RETURN))
                                          (PRINTCCODE C OUTSTREAM)))
                          (TERPRI OUTSTREAM])

(GREP
  [LAMBDA (STRS FILES OUTSTREAM)

    (* ;; "Edited 14-Oct-2023 14:43 by rmk")

    (* ;; "Edited  1-Sep-2023 00:16 by rmk")

    (* ;; "Edited 23-Jul-2023 19:55 by rmk")

    (* ;; "Edited 26-Jun-2022 13:28 by rmk: added OUTSTREAM")

    (* ;; "Edited 26-Jun-2022 13:25 by rmk")
                                                             (* lmm " 1-Apr-85 15:27")
                                                             (* lmm " 1-Apr-85 15:27")
    (RESETLST
        [SELECTQ OUTSTREAM
            (NIL (SETQ OUTSTREAM T))
            (T)
            (CL:UNLESS (GETSTREAM OUTSTREAM 'OUTPUT T)
                [RESETSAVE (SETQ OUTSTREAM (OPENSTREAM OUTSTREAM 'OUTPUT 'NEW))
                       `(PROGN (CLOSEF? OLDVALUE])]
        [RESETSAVE NIL `(PROGN (DSPFONT ,(DSPFONT NIL OUTSTREAM)
                                      ,OUTSTREAM]
        [RESETSAVE (LINELENGTH T OUTSTREAM)
               `(PROGN (LINELENGTH OLDVALUE ,OUTSTREAM]
        (DOGREP STRS FILES OUTSTREAM)
        OUTSTREAM)])

(LISPSOURCEOUTCHARFN
  [LAMBDA (OUTSTREAM CODE)                                   (* ; "Edited 20-Jan-2024 13:00 by rmk")

    (* ;; "Suppress fontchange characters for Lisp source files.")

    (DECLARE (USEDFREE OUTSTREAM-OUTCHARFN))
    (CL:WHEN (IGEQ CODE (CHARCODE LF))
           (APPLY* OUTSTREAM-OUTCHARFN OUTSTREAM CODE])

(TGREP
  [LAMBDA (STRS FILES)                                       (* ; "Edited 20-Jan-2024 14:14 by rmk")
    (TEXTSTREAM (TEDIT (GREP STRS FILES (OPENTEXTSTREAM))
                       'TGREP NIL '(READONLY T])
)

(MOVD? 'NILL 'TEDIT.FORMATTEDFILEP)
(DECLARE%: EVAL@COMPILE DONTCOPY 

(FILESLOAD (FROM VALUEOF (MEDLEYDIR "loadups"))
       EXPORTS.ALL)
)
(DEFINEQ

(PHONE
  [LAMBDA (NAME)                                             (* lmm " 5-Mar-86 12:14")
    (GREP NAME (OR PHONELISTFILES (PROMPTFORWORD "Name of directory file: "])
)

(RPAQ? PHONELISTFILES )
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (703 6328 (DOGREP 713 . 4690) (GREP 4692 . 5742) (LISPSOURCEOUTCHARFN 5744 . 6094) (
TGREP 6096 . 6326)) (6475 6670 (PHONE 6485 . 6668)))))
STOP
