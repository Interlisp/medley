(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "16-Mar-2024 11:16:38" {WMEDLEY}<lispusers>GREP.;31 6115   

      :EDIT-BY rmk

      :CHANGES-TO (FNS DOGREP)

      :PREVIOUS-DATE "15-Mar-2024 16:28:09" {WMEDLEY}<lispusers>GREP.;29)


(PRETTYCOMPRINT GREPCOMS)

(RPAQQ GREPCOMS [(FNS DOGREP GREP TGREP)
                 (P (MOVD? 'NILL 'TEDIT.FORMATTEDFILEP))
                 (COMS (FNS PHONE)
                       (INITVARS (PHONELISTFILES])
(DEFINEQ

(DOGREP
  [LAMBDA (STRS FILES OUTSTREAM)

    (* ;; "Edited 16-Mar-2024 11:16 by rmk")

    (* ;; "Edited 20-Jan-2024 13:12 by rmk")

    (* ;; "Edited 19-Jul-2022 22:26 by rmk")

    (* ;; "Edited 26-Jun-2022 14:36 by rmk")
                                                             (* Newman "14-May-86 08:04")
                                                             (* Newman "14-May-86 08:04")

(* ;;; "Originally coded by Larry Masinter.")

(* ;;; "No longer permanently modifies the DSPFONT when DSPFONT is not the same as the DEFAULTFONT.  -DVN '14-May-86 08:03:59'")

         (* * No longer permanently modifies the DSPFONT when DSPFONT is not the same as 
         the DEFAULTFONT. -DVN "14-May-86 08:03:59")

    (if (LISTP FILES)
        then (for FILE in FILES do (DOGREP STRS FILE OUTSTREAM))
      elseif (STRPOS "*" FILES)
        then (DOGREP STRS (DIRECTORY FILES NIL "" "")
                    OUTSTREAM)
      elseif (DIRECTORYNAMEP FILES)
      elseif (CL:WITH-OPEN-FILE
              (STREAM (OR (FINDFILE FILES T)
                          FILES)
                     :DIRECTION :INPUT)
              (for STR FOUND FILENAME inside STRS first (SETQ FILENAME (FULLNAME STREAM))
                                                        (CL:WHEN (TEDIT.FORMATTEDFILEP STREAM)
                                                            [SETQ STREAM (OPENTEXTSTREAM
                                                                          STREAM NIL NIL NIL
                                                                          '(OBJECTBYTE 0])
                                                        (SETFILEINFO STREAM 'ENDOFSTREAMOP
                                                               (FUNCTION NILL))
                 do (SETFILEPTR STREAM 0)
                    (bind POS STARTPOS while (SETQ POS (FFILEPOS STR STREAM NIL NIL NIL NIL 
                                                              UPPERCASEARRAY))
                       do (CL:UNLESS FOUND
                              (PRINTOUT OUTSTREAM T T .FONT BOLDFONT "(from " FILENAME ")" .FONT 
                                     DEFAULTFONT T)
                              (SETQ FOUND T)) 

                          (* ;; "Copying from the beginning of this line. Originally this used BFILEPOS (backwards FILEPOS?), which did repeated calls to forward FFILEPOS in what appears to be a binary set of probes.  But FFILEPOS is really SLOW-POS for a single character, and the last line-start is presumaby not that far back.  So just walk backwards.")

                          (SETQ STARTPOS (DO (SELCHARQ (\BACKCCODE.EOLC STREAM 'ANY)
                                                  (EOL (\INCCODE.EOLC STREAM)
                                                       (RETURN (GETFILEPTR STREAM)))
                                                  (NIL (RETURN 0))
                                                  NIL)))
                          (FOR I C FROM 1 TO (IDIFFERENCE POS STARTPOS)
                             DO (SETQ C (\INCCODE.EOLC STREAM 'ANY))
                                (CL:UNLESS (ILESSP C (CHARCODE TAB))
                                       (PRINTCCODE C OUTSTREAM)))
                          (DSPFONT BOLDFONT OUTSTREAM)
                          (COPYCHARS STREAM OUTSTREAM POS (ADD POS (NCHARS STR)))
                          (DSPFONT DEFAULTFONT OUTSTREAM) 

                          (* ;; "Copying to the end of this line (or end of file)")

                          [BIND C DO (SELCHARQ (SETQ C (\INCCODE.EOLC STREAM 'ANY))
                                          ((EOL NIL) 
                                               (RETURN))
                                          (CL:UNLESS (ILESSP C (CHARCODE TAB))
                                                 (PRINTCCODE C OUTSTREAM]
                          (TERPRI OUTSTREAM])

(GREP
  [LAMBDA (STRS FILES OUTSTREAM)

    (* ;; "Edited 14-Oct-2023 14:43 by rmk")

    (* ;; "Edited  1-Sep-2023 00:16 by rmk")

    (* ;; "Edited 23-Jul-2023 19:55 by rmk")

    (* ;; "Edited 26-Jun-2022 13:28 by rmk: added OUTSTREAM")

    (* ;; "Edited 26-Jun-2022 13:25 by rmk")
                                                             (* lmm " 1-Apr-85 15:27")
                                                             (* lmm " 1-Apr-85 15:27")
    (RESETLST
        [SELECTQ OUTSTREAM
            (NIL (SETQ OUTSTREAM T))
            (T)
            (CL:UNLESS (GETSTREAM OUTSTREAM 'OUTPUT T)
                [RESETSAVE (SETQ OUTSTREAM (OPENSTREAM OUTSTREAM 'OUTPUT 'NEW))
                       `(PROGN (CLOSEF? OLDVALUE])]
        [RESETSAVE NIL `(PROGN (DSPFONT ,(DSPFONT NIL OUTSTREAM)
                                      ,OUTSTREAM]
        [RESETSAVE (LINELENGTH T OUTSTREAM)
               `(PROGN (LINELENGTH OLDVALUE ,OUTSTREAM]
        (DOGREP STRS FILES OUTSTREAM)
        OUTSTREAM)])

(TGREP
  [LAMBDA (STRS FILES)                                       (* ; "Edited 20-Jan-2024 14:14 by rmk")
    (TEXTSTREAM (TEDIT (GREP STRS FILES (OPENTEXTSTREAM))
                       'TGREP NIL '(READONLY T])
)

(MOVD? 'NILL 'TEDIT.FORMATTEDFILEP)
(DEFINEQ

(PHONE
  [LAMBDA (NAME)                                             (* lmm " 5-Mar-86 12:14")
    (GREP NAME (OR PHONELISTFILES (PROMPTFORWORD "Name of directory file: "])
)

(RPAQ? PHONELISTFILES )
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (496 5830 (DOGREP 506 . 4544) (GREP 4546 . 5596) (TGREP 5598 . 5828)) (5868 6063 (PHONE 
5878 . 6061)))))
STOP
