(DEFINE-FILE-INFO §READTABLE "INTERLISP" §PACKAGE "INTERLISP")
(FILECREATED "20-Feb-87 08:05:52" {IVY}<BLOOMBERG>LISP>MAILSHARE.;1 12250  

      previous date%: "15-Dec-86 10:01:06" {INDIGO}<GSLWS>BASICS>MAILSHARE.;3)


(* "
Copyright (c) 1986, 1987 by Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT MAILSHARECOMS)

(RPAQQ MAILSHARECOMS ((* Menu Functions)
                      (FNS MASH.TopLevel MASH.CreateFreeMenu MASH.GetMail MASH.Quit MASH.Consistent 
                           MASH.MakeDirectoryName MASH.MakeProfileName MASH.MakeIconWindow)
                      (* Icon bitmaps)
                      (BITMAPS MASH.Icon MASH.IconMask)
                      (* VARS)
                      (INITVARS (MASH.VALID-HOSTS '(IVY INDIGO PHYLUM ERIS QV CHERRY)))
                      (GLOBALVARS MASH.VALID-HOSTS)
                      (ADDVARS (BackgroundMenuCommands (Mail% Share '(MASH.TopLevel) 
                                                              "Start the Mail Share menu")))
                      (VARS (BackgroundMenu NIL))))



(* Menu Functions)

(DEFINEQ

(MASH.TopLevel
  (LAMBDA NIL                                                (* DSB " 5-Nov-86 09:55")
                                                             (* Start the Mail Share Process)
    (PROG (menuWindow)
          (SETQ menuWindow (MASH.CreateFreeMenu))
          
          (* * initialize to GV)

          (FM.CHANGESTATE (FM.ITEMFROMID menuWindow 'NETMODE)
                 menuWindow)
          
          (* * set up menu window)

          (WINDOWPROP menuWindow 'ICONFN (FUNCTION MASH.MakeIconWindow))
          (SHAPEW menuWindow '(200 200 275 82))
          (SHAPEW (GETPROMPTWINDOW menuWindow)
                 '(200 282 275 35)))))

(MASH.CreateFreeMenu
  (LAMBDA (LEFT BOTTOM)                                      (* DSB " 5-Nov-86 09:37")
                                                             (* returns a free menu window at 
                                                             specified position)
    (FM.FORMATMENU `(((TYPE TITLE LABEL Commands%: FONT (MODERN 12 BOLD))
                      (LABEL GetMail SELECTEDFN MASH.GetMail)
                      (LABEL Quit SELECTEDFN MASH.Quit))
                     ((TYPE EDITSTART LABEL Name%: FONT (MODERN 12 BOLD)
                            ITEMS
                            (NAME))
                      (TYPE EDIT ID NAME LABEL ""))
                     ((TYPE EDITSTART LABEL |Mail Directory:| FONT (MODERN 12 BOLD)
                            ITEMS
                            (DIRECTORY))
                      (TYPE EDIT ID DIRECTORY LABEL ""))
                     ((TYPE TITLE LABEL NetworkMode%: FONT (MODERN 12 BOLD))
                      (TYPE NWAY ID NETMODE LABEL GV)
                      (TYPE NWAY ID NETMODE LABEL NS))
                     (WINDOWPROPS TITLE "Mail Share" LEFT %, LEFT BOTTOM %, BOTTOM)))))

(MASH.GetMail
  (LAMBDA (ITEM WINDOW BUTTONS)                              (* DSB " 5-Nov-86 10:05")
                                                             (* Gets the mail)
    (PROG ((state (FM.READSTATE WINDOW))
           (promptW (GETPROMPTWINDOW WINDOW))
           name mailDirectory networkMode consistent mailProfile (locatedDirectory NIL))
          (CLEARW promptW)
          
          (* * check that name is specified)

          (SETQ name (U-CASE (LISTGET state 'NAME)))
          (COND
             ((EQUAL name "")
              (PRIN1 "Enter your name" promptW)
              (RETURN)))
          
          (* * check if mail directory is both entered and consistent)
          
          (* * If consistent, check if the mail directory is legitimate;
          otherwise, use mail directory host list)

          (SETQ mailDirectory (U-CASE (LISTGET state 'DIRECTORY)))
          (COND
             ((NOT (EQUAL mailDirectory ""))
              (COND
                 ((NOT (UNPACKFILENAME mailDirectory 'HOST))
                  (RETURN (PRIN1 "Include file server name (e.g., {ivy})" promptW))))
              (SETQ consistent (MASH.Consistent name mailDirectory))
              (COND
                 ((EQ consistent 'ERROR)
                  (CLEARW promptW)
                  (PRIN1 "Badly formed directory name" promptW)
                  (RETURN)))
              (COND
                 (consistent (SETQ mailProfile (MASH.MakeProfileName mailDirectory))
                        (COND
                           ((INFILEP mailProfile)
                            (SETQ locatedDirectory T))
                           (T (PRIN1 "Can't find that mail directory.  Searching..." promptW)))))))
          
          (* * look for default mail directory using mail directory host list)

          (for host in MASH.VALID-HOSTS while (NOT locatedDirectory) do (SETQ mailDirectory
                                                                         (MASH.MakeDirectoryName
                                                                          host name))
                                                                        (SETQ mailProfile
                                                                         (MASH.MakeProfileName 
                                                                                mailDirectory))
                                                                        (COND
                                                                           ((INFILEP mailProfile)
                                                                            (SETQ locatedDirectory T)
                                                                            )))
          
          (* * if unable to find valid mail directory, return)

          (COND
             ((NOT locatedDirectory)
              (CLEARW promptW)
              (PRIN1 "Please enter correct default mail directory name" promptW)
              (PRIN1 "e.g., {IVY}<your-name>MAIL>" promptW)
              (RETURN)))
          
          (* * profile exists, so do it!)

          (CLEARW promptW)
          (FM.CHANGELABEL (FM.ITEMFROMID WINDOW 'DIRECTORY)
                 WINDOW mailDirectory)
          (PRIN1 "Quitting previous Lafite..." promptW)
          (LAFITE 'OFF)
          (PRIN1 "Done" promptW)
          (TERPRI promptW)
          (LOGIN)
          (COND
             ((NOT (EQ USERNAME (MKATOM name)))
              (RETURN (PRIN1 "Mail and Login names inconsistent" promptW))))
          (CLEARW promptW)
          (PRIN1 "Mail is coming..." promptW)
          (SETQ networkMode (U-CASE (LISTGET state 'NETMODE)))
          (COND
             ((EQ networkMode 'GV)
              (LAFITEMODE 'GV))
             (T (LAFITEMODE 'NS)))
          (SETQ LAFITEDEFAULTHOST&DIR mailDirectory)
          (LAFITE 'ON))))

(MASH.Quit
  (LAMBDA (ITEM WINDOW BUTTONS)                              (* DSB " 5-Nov-86 10:11")
          
          (* * Quits LAFITE, clears menu, etc.)

    (PROG ((promptW (GETPROMPTWINDOW WINDOW)))
          (CLEARW promptW)
          (PRIN1 "Quitting Lafite..." promptW)
          (FM.CHANGELABEL (FM.ITEMFROMID WINDOW 'NAME)
                 WINDOW "")
          (FM.CHANGELABEL (FM.ITEMFROMID WINDOW 'DIRECTORY)
                 WINDOW "")
          (LAFITE 'OFF)
          (SETQ LAFITEDEFAULTHOST&DIR NIL)
          (LAFITEMODE 'GV)
          (FM.CHANGESTATE (FM.ITEMFROMID WINDOW 'NETMODE)
                 WINDOW)
          (PRIN1 "Done" promptW))))

(MASH.Consistent
  (LAMBDA (name mailDirectory)                               (* DSB " 4-Nov-86 09:21")
          
          (* * returns T if name is the root directory of mailDirectory)

    (PROG ((nameString (MKSTRING name))
           (mailDirectoryString (MKSTRING mailDirectory))
           startPosition length before after)
          (SETQ startPosition (STRPOS nameString mailDirectoryString))
          (COND
             ((NOT startPosition)
              (RETURN NIL)))
          (SETQ length (NCHARS nameString))
          (SETQ before (STRPOS "<" mailDirectoryString))
          (SETQ after (STRPOS ">" mailDirectoryString))
          (COND
             ((AND (NUMBERP before)
                   (NUMBERP after))
              (RETURN (EQP length (DIFFERENCE (DIFFERENCE after before)
                                         1))))
             (T (RETURN 'ERROR))))))

(MASH.MakeDirectoryName
  (LAMBDA (host userName)                                    (* DSB " 2-Nov-86 15:43")
    (PROG (directoryName)
          (SETQ directoryName (CONCAT userName ">MAIL>"))
          (RETURN (PACKFILENAME 'HOST host 'DIRECTORY directoryName)))))

(MASH.MakeProfileName
  (LAMBDA (directory)                                        (* DSB " 2-Nov-86 15:37")
          
          (* * returns the Lafite.Profile name)

    (PROG (profileName)
          (SETQ profileName (PACKFILENAME 'DIRECTORY directory 'NAME 'LAFITE 'EXTENSION 'PROFILE))
          (RETURN profileName))))

(MASH.MakeIconWindow
  (LAMBDA (WINDOW OLDICON)                                   (* DSB "15-Dec-86 09:58")
          
          (* * Creates a window with an icon formed by two bit maps.)

    (OR OLDICON (ICONW MASH.Icon MASH.IconMask))))
)



(* Icon bitmaps)


(RPAQQ MASH.Icon #*(90 50)@@@@@@@@@@@@@@@@@@@@@@@@COOOOOOOOOOOOOOOOOOOO@@@COOOOOOOOOOOOOOOOOOOO@@@COOOOOOOOOOOOOOOOOOOO@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@GON@@@@@@@@OOH@@G@@@CH@@GON@@@@@@@@OOH@@G@@@CHAOOOOOOOOOOOOOOOL@G@@@CHA@@@@@@@@@@@@@@@D@G@@@CHA@@@@@@@@@@@@@@@D@G@@@CHA@@@@@@@@@@@@@@@D@G@@@CHA@@@LAH@O@A@H@@@D@G@@@CHA@@@OGH@I@A@H@@@D@G@@@CHA@@@ILHAIHA@H@@@D@G@@@CHA@@@HHHA@HA@H@@@D@G@@@CHA@@@H@HCOLA@H@@@D@G@@@CHA@@@H@HB@DA@H@@@D@G@@@CHA@@@H@HF@FA@OL@@D@G@@@CHA@@@@@@@@@@@@@@@D@G@@@CHA@@@@@G@@@@N@@@@D@G@@@CHA@@@@@G@@@@N@@@@D@G@@@CHA@@@@@B@@@@D@@@@D@G@@@CHAOOOOOOOOOOOOOOOL@G@@@CH@@@@@@B@@@@D@@@@@@G@@@CH@@@@@@COOOOL@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@CH@@@@GLHHCLAOCN@@@@G@@@CH@@@@DDHHBDAABB@@@@G@@@CH@@@@D@HHFFAAB@@@@@G@@@CH@@@@CHOHDBAOCH@@@@G@@@CH@@@@@DHHOOAHB@@@@@G@@@CH@@@@DDHHHAAFBB@@@@G@@@CH@@@@GLHIHAICCN@@@@G@@@CH@@@@@@@@@@@@@@@@@@G@@@COOOOOOOOOOOOOOOOOOOO@@@COOOOOOOOOOOOOOOOOOOO@@@COOOOOOOOOOOOOOOOOOOO@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
)

(RPAQQ MASH.IconMask #*(90 50)@@@@@@@@@@@@@@@@@@@@@@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@AOOOOOOOOOOOOOOOOOOOL@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
)



(* VARS)


(RPAQ? MASH.VALID-HOSTS '(IVY INDIGO PHYLUM ERIS QV CHERRY))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS MASH.VALID-HOSTS)
)

(ADDTOVAR BackgroundMenuCommands (Mail% Share '(MASH.TopLevel) "Start the Mail Share menu"))

(RPAQQ BackgroundMenu NIL)
(PUTPROPS MAILSHARE COPYRIGHT ("Xerox Corporation" 1986 1987))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1099 9386 (MASH.TopLevel 1109 . 1795) (MASH.CreateFreeMenu 1797 . 2978) (MASH.GetMail 
2980 . 6921) (MASH.Quit 6923 . 7601) (MASH.Consistent 7603 . 8502) (MASH.MakeDirectoryName 8504 . 8783
) (MASH.MakeProfileName 8785 . 9126) (MASH.MakeIconWindow 9128 . 9384)))))
STOP
