(DEFINE-FILE-INFO READTABLE "INTERLISP" PACKAGE "INTERLISP")
(FILECREATED "14-Mar-2021 20:33:34" 
{DSK}<Users>kaplan>Local>medley3.5>git-medley>lispusers>MODERNIZE.;14 20950  

      changes to%:  (FNS MODERNWINDOW.SETUP)

      previous date%: "14-Mar-2021 18:00:34" 
{DSK}<Users>kaplan>Local>medley3.5>git-medley>lispusers>MODERNIZE.;13)


(PRETTYCOMPRINT MODERNIZECOMS)

(RPAQQ MODERNIZECOMS
       [
        (* ;; "Externals")

        (COMS (FNS MODERNWINDOW MODERNWINDOW.SETUP UNMODERNWINDOW MODERNWINDOW.UNSETUP)
              (INITVARS (MODERN-WINDOW-MARGIN 25)))
        
        (* ;; "Internals")

        [COMS (FNS MODERNWINDOW.BUTTONEVENTFN MODERNWINDOW.BUTTONEVENTFN.ANYWHERE NEARTOP 
                   NEARESTCORNER INCORNER.REGION)
              
              (* ;; "Behavior for some known window creators")

              (FNS MODERN-ADD-EXEC MODERN-SNAPW TOTOPW.MODERNIZE)
              
              (* ;; "Add some Meta commands")

              (FNS TEDIT.MODERNIZE TEDIT.SELECTALL)
              (DECLARE%: DONTEVAL@LOAD DOCOPY (P (TEDIT.MODERNIZE)
                                                 
                                                 (* ;; "Inspector")

                                                 (MODERNWINDOW.SETUP '\ITEM.WINDOW.BUTTON.HANDLER)
                                                 

                                 (* ;; "Commonlisp array inspector.  If you move the main window, the little attached window doesn't move.  But if you move the attached window, it all works.  Needs a special definition.  Shaping doesn't work either")

                                                 (* (MODERNWINDOW.SETUP 'ONEDINSPECT.BUTTONEVENTFN))
                                                 (MODERNWINDOW.SETUP 'ICMLARRAY.TITLECOMMANDFN)
                                                 
                                                 (* ;; "Freemenu")

                                                 (MODERNWINDOW.SETUP '\FM.BUTTONEVENTFN)
                                                 
                                                 (* ;; "SEDIT")

                                                 (MODERNWINDOW.SETUP 'SEDIT::BUTTONEVENTFN)
                                                 
                                                 (* ;; "Debugger")

                                                 (MODERNWINDOW.SETUP 'DBG::DEBUGGER-BUTTON-EVENT)
                                                 
                                                 (* ;; "Snap")

                                                 (MODERNWINDOW.SETUP 'SNAPW 'MODERN-SNAPW)
                                                 
                                                 (* ;; "New execs")

                                                 (MODERNWINDOW.SETUP 'ADD-EXEC 'MODERN-ADD-EXEC)
                                                 
                                                 (* ;; "Existing exec of the load")

                                                 (MODERNWINDOW (PROCESSPROP (TTY.PROCESS)
                                                                      'WINDOW))
                                                 
                                                 (* ;; "Table browser (for filebrowser)")

                                                 (MODERNWINDOW.SETUP 'TB.BUTTONEVENTFN)
                                                 
                                                 (* ;; "Grapher")

                                                 (MODERNWINDOW.SETUP 'APPLYTOSELECTEDNODE)
                                                 
                                                 (* ;; "Sketch")

                                                 (MODERNWINDOW.SETUP 'WB.BUTTON.HANDLER)
                                                 
                                                 (* ;; "Promptwindow")

                                                 (MODERNWINDOW PROMPTWINDOW T]
        (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS (ADDVARS (NLAMA)
                                                                             (NLAML)
                                                                             (LAMA MODERN-ADD-EXEC])



(* ;; "Externals")

(DEFINEQ

(MODERNWINDOW
  [LAMBDA (WINDOW ANYWHERE)                             (* ; "Edited 22-Feb-2021 16:44 by rmk:")

    (* ;; "This can be applied to windows that have been created with an unknown or unmodifiable buttoneventfn.")

    (CL:UNLESS (WINDOWPROP WINDOW 'PREMODERN-BUTTONEVENTFN)
        (WINDOWPROP WINDOW 'PREMODERN-BUTTONEVENTFN (WINDOWPROP WINDOW 'BUTTONEVENTFN))
        (WINDOWPROP WINDOW 'BUTTONEVENTFN (IF ANYWHERE
                                              THEN (FUNCTION MODERNWINDOW.BUTTONEVENTFN.ANYWHERE)
                                            ELSE (FUNCTION MODERNWINDOW.BUTTONEVENTFN))))
    WINDOW])

(MODERNWINDOW.SETUP
  [LAMBDA (ORIGFN MODERNWINDOWFN ANYWHERE)              (* ; "Edited 14-Mar-2021 20:33 by rmk:")

    (* ;; "ORIGFN is either a function that creates windows of a given type (e.g. SNAPW or ADD-EXEC) or the known BUTTONEVENTFN of a class of windows.")

    (* ;; "Moves ORIGNFN to a new name, prefixed with MODERNORIG-.")

    (* ;; "If MODERNWINDOWFN is given, then that replaces the original definition of ORIGFN, and presumably knows how to call the renamed ORIGFN under the right circumstances.  This is typically the case where ORIGFN is a window creator.")

    (* ;; "Otherwise, ORIGFN is taken to be the BUTTONEVENTFN for a class of windows, and its new definition is defaulted to one that maps left-clicks in appropriate areas into modern window operations.  If not in appropriate areas, then the renamed ORIGNFN is called to give the original button behavior.")

    (* ;; "If ANYWHERE, moving will happen for any click not in one of the shaping corners.")

    (* ;; "The renamed function has arguments in addition to WINDOW:  the new name for the original function, if MODERNWINDOFN is provided, and the value specified here for ANYWHERE.")

    (CL:WHEN (GETD ORIGFN)

        (* ;; "If ORIGFN is defined, then presumably the file containing ORIGFN (e.g. sketch) was loaded before MODERNIZE (if we are being called on our load), and we can rearrange things.  But of ORIGFN is not defined, then there is really nothing to do.  The package loader itself should call MODERNWINDOW.SETUP if we are defined when it is loaded. ")

        [LET [RENAMEDORIG (PKGNAME (CL:PACKAGE-NAME (CL:SYMBOL-PACKAGE ORIGFN]

             (* ;; "The renamed version of XCL symbols go into Interlisp, so there is less confusion about accessing it")

             (CL:WHEN (STREQUAL PKGNAME "XEROX-COMMON-LISP")
                    (SETQ PKGNAME "INTERLISP"))
             (SETQ RENAMEDORIG (CL:INTERN (CONCAT 'MODERN-ORIG- ORIGFN)
                                      PKGNAME))
             (MOVD? ORIGFN RENAMEDORIG)
             (IF MODERNWINDOWFN
                 THEN (MOVD MODERNWINDOWFN ORIGFN)
               ELSE (PUTD ORIGFN `(LAMBDA (WINDOW)
                                        (MODERNWINDOW.BUTTONEVENTFN WINDOW (FUNCTION 
                                                                                ,RENAMEDORIG)
                                               ,ANYWHERE])])

(UNMODERNWINDOW
  [LAMBDA (WINDOW)                                      (* ; "Edited 22-Feb-2021 16:44 by rmk:")

    (* ;; "Restores original window behavior")

    (CL:WHEN (WINDOWPROP WINDOW 'PREMODERN-BUTTONEVENTFN)
        (WINDOWPROP WINDOW 'BUTTONEVENTFN (WINDOWPROP WINDOW 'PREMODERN-BUTTONEVENTFN))
        (WINDOWPROP WINDOW 'PREMODERN-BUTTONEVENTFN NIL))
    WINDOW])

(MODERNWINDOW.UNSETUP
  [LAMBDA (ORIGFN)                                      (* ; "Edited 22-Feb-2021 16:45 by rmk:")
                                                            (* ; "Edited 24-Jun-2020 15:09 by rmk:")

    (* ;; "Moves the renamed original function back to its original name")

    (LET [RENAMEDORIG (PKGNAME (CL:PACKAGE-NAME (CL:SYMBOL-PACKAGE ORIGFN]

         (* ;; "The renamed version of XCL symbols go into Interlisp, so there is less confusion about accessing it")

         (CL:WHEN (STREQUAL PKGNAME "XEROX-COMMON-LISP")
                (SETQ PKGNAME "INTERLISP"))
         (SETQ RENAMEDORIG (CL:INTERN (CONCAT 'MODERN-ORIG- ORIGFN)
                                  PKGNAME))
         (CL:WHEN (GETD RENAMEDORIG)
                (MOVD RENAMEDORIG ORIGFN])
)

(RPAQ? MODERN-WINDOW-MARGIN 25)



(* ;; "Internals")

(DEFINEQ

(MODERNWINDOW.BUTTONEVENTFN
  [LAMBDA (WINDOW ORIGFUNCTION ANYWHERE)                (* ; "Edited 27-Feb-2021 17:57 by rmk:")
    (IF (AND (MOUSESTATE (ONLY LEFT))
                 (EQ LASTKEYBOARD 0))
        THEN (TOTOPW WINDOW)
              (LET [CORNER TOPMARGIN (MAINREGION (WINDOWPROP WINDOW 'REGION))
                          (ATTACHEDREGION (ATTACHEDWINDOWREGION (CENTRALWINDOW WINDOW]

                   (* ;; "If the window has a TOPMARGIN property, that tells us that it does not have a canonical title but may still have a title-like attached window just above the main window.  The TOPMARGIN should be 0 in that case.")

                   (* ;; "This is particularly the case of FILEBROWSER windows, where the the modified ATTACHEDWINDOWTOTOPFN drives the click here. ")

                   (SETQ TOPMARGIN (IF (WINDOWPROP WINDOW 'TOPMARGIN)
                                     ELSEIF (WINDOWPROP WINDOW 'TITLE)
                                       THEN (FONTPROP WindowTitleDisplayStream 'HEIGHT)
                                     ELSE MODERN-WINDOW-MARGIN))
                   (SETQ CORNER (INCORNER.REGION MAINREGION TOPMARGIN))
                   (IF CORNER
                       THEN 

                             (* ;; 
       "The upper corners may be in the title bar, near the side, so test corners before titlebar.")

                             (* ;; "We are in the corner of the main window, so we are reshaping.  But the ghost region should include all of the attached windows, and the starting cursor should be positioned at the corner closest to the selected corner of the main window.")

                             (* ;; "WINDOWREGION includes the attached windows")

                             (LET ((LEFT (FETCH LEFT OF ATTACHEDREGION))
                                   (RIGHT (FETCH RIGHT OF ATTACHEDREGION))
                                   (TOP (FETCH TOP OF ATTACHEDREGION))
                                   (BOTTOM (FETCH BOTTOM OF ATTACHEDREGION))
                                   STARTINGREGION)

                                 (* ;; "\CURSORPOSITION moves the mouse to the tracking corner of the ghost region, in screen coordinates, so that the mouse starts out at the tracking corner of the ghost region, even if there are attached windows (as in the filebrowser) that overhang the corner and the initiating click was at the corner of the mainwindow.")

                                  (CL:UNLESS (EQ 'DON'T (WINDOWPROP WINDOW 'RESHAPEFN))
                                      [SETQ STARTINGREGION
                                       (GETREGION NIL NIL NIL NIL NIL
                                              (SELECTQ CORNER
                                                  (RIGHTBOTTOM (\CURSORPOSITION RIGHT BOTTOM)
                                                               (GETMOUSESTATE)
                                                               (LIST LEFT TOP RIGHT BOTTOM))
                                                  (LEFTBOTTOM (\CURSORPOSITION LEFT BOTTOM)
                                                              (GETMOUSESTATE)
                                                              (LIST RIGHT TOP LEFT BOTTOM))
                                                  (RIGHTTOP (\CURSORPOSITION RIGHT TOP)
                                                            (GETMOUSESTATE)
                                                            (LIST LEFT BOTTOM RIGHT TOP))
                                                  (LEFTTOP (\CURSORPOSITION LEFT TOP)
                                                           (GETMOUSESTATE)
                                                           (LIST RIGHT BOTTOM LEFT TOP))
                                                  (SHOULDNT])
                                  (SHAPEW (CL:IF (MEMB 'SHAPEW (WINDOWPROP WINDOW 'PASSTOMAINCOMS))
                                              (WINDOWPROP WINDOW 'MAINWINDOW)
                                              WINDOW)
                                         STARTINGREGION))
                             T
                     ELSEIF (OR ANYWHERE (NEARTOP MAINREGION TOPMARGIN))
                       THEN (NEARESTCORNER ATTACHEDREGION)
                             (MOVEW (CL:IF (MEMB 'MOVEW (WINDOWPROP WINDOW 'PASSTOMAINCOMS))
                                        (WINDOWPROP WINDOW 'MAINWINDOW)
                                        WINDOW))
                             T
                     ELSEIF [OR ORIGFUNCTION (SETQ ORIGFUNCTION (WINDOWPROP WINDOW 
                                                                           'PREMODERN-BUTTONEVENTFN]
                       THEN (APPLY* ORIGFUNCTION WINDOW)))
      ELSEIF [OR ORIGFUNCTION (SETQ ORIGFUNCTION (WINDOWPROP WINDOW 'PREMODERN-BUTTONEVENTFN]
        THEN (APPLY* ORIGFUNCTION WINDOW])

(MODERNWINDOW.BUTTONEVENTFN.ANYWHERE
  [LAMBDA (WINDOW)                                      (* ; "Edited 22-Feb-2021 16:31 by rmk:")
                                                            (* ; "Edited 24-Jun-2020 13:24 by rmk:")

    (* ;; "Move if left-click anywhere, not just titlebar")

    (MODERNWINDOW.BUTTONEVENTFN WINDOW NIL T])

(NEARTOP
  [LAMBDA (MAINREGION TOPMARGIN)                        (* ; "Edited 12-Feb-2021 23:19 by rmk:")

    (* ;; "True if the MOUSEY is near the top of MAINREGION.  That means in the title bar for titled windows, otherwise a short distance below the top of the window.  (Could be in the border?)")

    (IGREATERP LASTMOUSEY (IDIFFERENCE (FETCH TOP OF MAINREGION)
                                 TOPMARGIN])

(NEARESTCORNER
  [LAMBDA (REGION)                                      (* ; "Edited 14-Feb-2021 21:46 by rmk:")

    (* ;; 
"Moves the cursor to the corner of REGION that is closest to the current LASTMOUSEX AND LASTMOUSEY")

    (\CURSORPOSITION (CL:IF (ILESSP (IDIFFERENCE LASTMOUSEX (FETCH LEFT OF REGION))
                                   (IDIFFERENCE (FETCH RIGHT OF REGION)
                                          LASTMOUSEX))
                         (FETCH LEFT OF REGION)
                         (FETCH RIGHT OF REGION))
           (CL:IF (ILESSP (IDIFFERENCE LASTMOUSEY (FETCH BOTTOM OF REGION))
                         (IDIFFERENCE (FETCH TOP OF REGION)
                                LASTMOUSEY))
               (FETCH BOTTOM OF REGION)
               (FETCH TOP OF REGION))])

(INCORNER.REGION
  [LAMBDA (MAINREGION TOPMARGIN)                        (* ; "Edited 22-Feb-2021 16:27 by rmk:")

    (* ;; "MAINREGION, LASTMOUSEX, LASTMOUSEY in screen coordinates.")

    (* ;; "TOPMARGIN is the height of the titlebar for titled windows, otherwise the margin at the top of the window's content that we regard as the top. ")

    (IF (ILEQ (IABS (IDIFFERENCE LASTMOUSEX (FETCH LEFT OF MAINREGION)))
                  MODERN-WINDOW-MARGIN)
        THEN (IF (NEARTOP MAINREGION TOPMARGIN)
                     THEN 'LEFTTOP
                   ELSEIF (ILEQ LASTMOUSEY (IPLUS MODERN-WINDOW-MARGIN (FETCH BOTTOM
                                                                              OF MAINREGION)))
                     THEN 'LEFTBOTTOM)
      ELSEIF (ILEQ (IABS (IDIFFERENCE LASTMOUSEX (FETCH RIGHT OF MAINREGION)))
                       MODERN-WINDOW-MARGIN)
        THEN (IF (NEARTOP MAINREGION TOPMARGIN)
                     THEN 'RIGHTTOP
                   ELSEIF (ILEQ LASTMOUSEY (IPLUS MODERN-WINDOW-MARGIN (FETCH BOTTOM
                                                                              OF MAINREGION)))
                     THEN 'RIGHTBOTTOM])
)



(* ;; "Behavior for some known window creators")

(DEFINEQ

(MODERN-ADD-EXEC
  [LAMBDA U                                             (* ; "Edited 22-Feb-2021 16:41 by rmk:")
    (LET [(PROC (APPLY (FUNCTION MODERN-ORIG-ADD-EXEC)
                       (FOR N FROM 1 TO U COLLECT (ARG U N]

         (* ;; "For some reason, the window may not be there immediately")

         (DISMISS 100)
         (MODERNWINDOW (PROCESSPROP PROC 'WINDOW))
         PROC])

(MODERN-SNAPW
  [LAMBDA NIL                                           (* ; "Edited 22-Feb-2021 16:41 by rmk:")

    (* ;; "No point in shaping a snap window, just move it.;;")

    (* ;; 
"This changes the creation function (SNAPW), since snap windows otherwise don't have a BUTTONEVENTN")

    (LET ((W (MODERN-ORIG-SNAPW)))
         [WINDOWPROP W 'BUTTONEVENTFN (FUNCTION (LAMBDA (W)
                                                  (TOTOPW W)
                                                  (MOVEW W]
         W])

(TOTOPW.MODERNIZE
  [LAMBDA (WINDOW)                                      (* ; "Edited 22-Feb-2021 16:31 by rmk:")

    (* ;; "This replaces the TOTOPW BUTTONEVENTFN on an attached window where the click is then directed to the MAINWINDOW.")

    (TOTOPW WINDOW)
    (LET ((MAIN (MAINWINDOW WINDOW T)))
         (CL:WHEN MAIN
             (MODERNWINDOW.BUTTONEVENTFN MAIN (WINDOWPROP MAIN 'BUTTONEVENTFN)))])
)



(* ;; "Add some Meta commands")

(DEFINEQ

(TEDIT.MODERNIZE
  [LAMBDA NIL                                           (* ; "Edited 22-Feb-2021 16:28 by rmk:")
    (CL:WHEN (GETD '\TEDIT.BUTTONEVENTFN)
        (MODERNWINDOW.SETUP '\TEDIT.BUTTONEVENTFN)

        (* ;; "All")

        (TEDIT.SETFUNCTION (CHARCODE "1,a")
               (FUNCTION TEDIT.SELECTALL)
               TEDIT.READTABLE)
        (TEDIT.SETFUNCTION (CHARCODE "1,A")
               (FUNCTION TEDIT.SELECTALL)
               TEDIT.READTABLE)

        (* ;; "Quit")

        (TEDIT.SETFUNCTION (CHARCODE "1,q")
               (FUNCTION TEDIT.QUIT)
               TEDIT.READTABLE)
        (TEDIT.SETFUNCTION (CHARCODE "1,Q")
               (FUNCTION TEDIT.QUIT)
               TEDIT.READTABLE))])

(TEDIT.SELECTALL
  [LAMBDA (STREAM TEXTOBJ SEL)                          (* ; "Edited  3-May-2020 17:29 by rmk:")
    (LET [(TEXTSTREAM (TEXTSTREAM (TTY.PROCESS]
         (CL:WHEN TEXTSTREAM
             (TEDIT.SETSEL TEXTSTREAM 0 (ADD1 (fetch TEXTLEN of (TEXTOBJ TEXTSTREAM)))
                    'LEFT))])
)
(DECLARE%: DONTEVAL@LOAD DOCOPY 

(TEDIT.MODERNIZE)


(* ;; "Inspector")


(MODERNWINDOW.SETUP '\ITEM.WINDOW.BUTTON.HANDLER)


(* ;; "Commonlisp array inspector.  If you move the main window, the little attached window doesn't move.  But if you move the attached window, it all works.  Needs a special definition.  Shaping doesn't work either")


                                                             (* (MODERNWINDOW.SETUP
                                                           (QUOTE ONEDINSPECT.BUTTONEVENTFN)))

(MODERNWINDOW.SETUP 'ICMLARRAY.TITLECOMMANDFN)


(* ;; "Freemenu")


(MODERNWINDOW.SETUP '\FM.BUTTONEVENTFN)


(* ;; "SEDIT")


(MODERNWINDOW.SETUP 'SEDIT::BUTTONEVENTFN)


(* ;; "Debugger")


(MODERNWINDOW.SETUP 'DBG::DEBUGGER-BUTTON-EVENT)


(* ;; "Snap")


(MODERNWINDOW.SETUP 'SNAPW 'MODERN-SNAPW)


(* ;; "New execs")


(MODERNWINDOW.SETUP 'ADD-EXEC 'MODERN-ADD-EXEC)


(* ;; "Existing exec of the load")


(MODERNWINDOW (PROCESSPROP (TTY.PROCESS)
                         'WINDOW))


(* ;; "Table browser (for filebrowser)")


(MODERNWINDOW.SETUP 'TB.BUTTONEVENTFN)


(* ;; "Grapher")


(MODERNWINDOW.SETUP 'APPLYTOSELECTEDNODE)


(* ;; "Sketch")


(MODERNWINDOW.SETUP 'WB.BUTTON.HANDLER)


(* ;; "Promptwindow")


(MODERNWINDOW PROMPTWINDOW T)
)
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 

(ADDTOVAR NLAMA )

(ADDTOVAR NLAML )

(ADDTOVAR LAMA MODERN-ADD-EXEC)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (4369 8713 (MODERNWINDOW 4379 . 5037) (MODERNWINDOW.SETUP 5039 . 7501) (UNMODERNWINDOW 
7503 . 7897) (MODERNWINDOW.UNSETUP 7899 . 8711)) (8778 16791 (MODERNWINDOW.BUTTONEVENTFN 8788 . 13799)
 (MODERNWINDOW.BUTTONEVENTFN.ANYWHERE 13801 . 14172) (NEARTOP 14174 . 14610) (NEARESTCORNER 14612 . 
15491) (INCORNER.REGION 15493 . 16789)) (16849 18267 (MODERN-ADD-EXEC 16859 . 17290) (MODERN-SNAPW 
17292 . 17835) (TOTOPW.MODERNIZE 17837 . 18265)) (18308 19391 (TEDIT.MODERNIZE 18318 . 19060) (
TEDIT.SELECTALL 19062 . 19389)))))
STOP
