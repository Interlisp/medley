(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 6-Aug-2022 12:31:49" {DSK}<home>larry>medley>lispusers>PICK.;2 2405   

      :CHANGES-TO (FNS PICK)

      :PREVIOUS-DATE " 4-Aug-2022 09:48:12" {DSK}<home>larry>medley>lispusers>PICK.;1)


(PRETTYCOMPRINT PICKCOMS)

(RPAQQ PICKCOMS ((COMMANDS "pick")
                 (FNS PICK)))

(DEFCOMMAND "pick" (FIRST . REST)
   (PICK FIRST REST))
(DEFINEQ

(PICK
  [LAMBDA (TYPE CHOICES)                                     (* ; "Edited  6-Aug-2022 12:31 by lmm")
                                                           (* ; "Edited 25-Jun-2022 16:58 by larry")
    (SELECTQ (MKATOM (U-CASE (MKSTRING TYPE)))
        (NIL 
             (* ;; "pick -- choose an issue, a file, a project")

             [PICK (PICK 'ONEOF '(FILE ISSUE PROJECT])
        (ISSUE 
               (* ;; "pick issue [number] -- display an issue; if none given, choose one at random")

               [LET ([ISSUE (PICK 'ONEOF (OR CHOICES (GIT-COMMAND 
                               "gh issue list -L 5000 -R interlisp/medley | sed 's/\([0-9]*\).*/\1/'"
                                                            ]
                     (STR (OPENTEXTSTREAM)))
                    (for S in (GIT-COMMAND (CL:FORMAT NIL "gh issue view -R interlisp/medley ~a" 
                                                  ISSUE)) do (CL:FORMAT STR "~a~&" S)
                       finally (TEDIT STR NIL NIL `(READONLY T TITLE ,(CL:FORMAT NIL "Issue #~a" 
                                                                             ISSUE])
        (DIR 
             (* ;; "pick a directory to choose files from")

             (PICK 'ONEOF '(LISPUSERS LIBRARY DOCTOOLS SOURCES INTERNAL)))
        (FILE 
              (* ;; " pick a file from a (randomly chosen) directory")

              [PICK 'ONEOF (DIRECTORY (MEDLEYDIR (PICK 'DIR])
        (PROJECT 
                 (* ;; "pick PROJECT will choose some repo to work on")

                 (PICK 'ONEOF '(CLOS ROOMS LOOPS NOTECARDS ONLINE WEBSITE TEST COMMUNITY ENVOS)))
        (ONEOF 
               (* ;; "PICK ONEOF A1 A2 A3 ... - just choose from choices listed")

               [CAR (NTH CHOICES (RAND 1 (LENGTH CHOICES])
        (HELP TYPE "Unknown type"])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (450 2382 (PICK 460 . 2380)))))
STOP
