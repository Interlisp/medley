(DEFINE-FILE-INFO READTABLE "INTERLISP" PACKAGE "INTERLISP")
(FILECREATED "15-Jan-98 09:49:00" {DSK}<project>medley2.0>lispusers>PLAINTEXTSTREAM.;48 16624  

      changes to%:  (FNS WRITEPLAINTEXTPAGE PLAINTEXTOUTCHARFN OPENPLAINTEXTSTREAM CLEARPLAINTEXTPAGE
                         MAKEPLAINTEXTPAGE)
                    (MACROS PLAINTEXTPARAM)
                    (VARS PLAINTEXTSTREAMCOMS)
                    (RECORDS PLAINTEXTIMAGEDATA)

      previous date%: "11-Jan-98 23:04:10" {DSK}<project>medley2.0>lispusers>PLAINTEXTSTREAM.;29)


(* ; "
Copyright (c) 1998 by Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT PLAINTEXTSTREAMCOMS)

(RPAQQ PLAINTEXTSTREAMCOMS
       [(ADDVARS (DEFAULTFILETYPELIST (PLAINTEXT . TEXT)
                        (PT . TEXT)))
        (FNS OPENPLAINTEXTSTREAM PLAINTEXTOUTCHARFN PLAINTEXT.TEDIT PLAINTEXT.TEXT)
        (FNS WRITEPLAINTEXTPAGE)
        (MACROS PLAINTEXTPARAM)
        (RECORDS PLAINTEXTIMAGEDATA)
        [ADDVARS [PRINTFILETYPES (PLAINTEXT (EXTENSION (PT PLAINTEXT]
               (IMAGESTREAMTYPES (PLAINTEXT (OPENSTREAM OPENPLAINTEXTSTREAM)
                                        (FONTCREATE \CREATEDISPLAYFONT)
                                        (FONTSAVAILABLE \SEARCHDISPLAYFONTFILES)
                                        (CREATECHARSET \CREATECHARSET.DISPLAY]
        (DECLARE%: EVAL@COMPILE DONTCOPY (P [OR (RECLOOK 'STREAM)
                                                (EVAL (SYSRECLOOK1 'STREAM]
                                            (OR (RECLOOK 'IMAGEOPS)
                                                (EVAL (SYSRECLOOK1 'IMAGEOPS])

(ADDTOVAR DEFAULTFILETYPELIST (PLAINTEXT . TEXT)
                                  (PT . TEXT))
(DEFINEQ

(OPENPLAINTEXTSTREAM
  [LAMBDA (FILE OPTIONS)                                 (* ; "Edited 15-Jan-98 00:04 by rmk:")

    (* ;; "Assert that scale is one, so that display fonts etc. can be used.")

    (LET [(STREAM (OPENSTREAM FILE 'OUTPUT NIL '((SEQUENTIAL T]
         (REPLACE (STREAM OUTCHARFN) OF STREAM WITH (FUNCTION PLAINTEXTOUTCHARFN))
         [REPLACE (STREAM IMAGEDATA) OF STREAM
            WITH (CREATE PLAINTEXTIMAGEDATA
                            PTPAGE _ (CL:MAKE-ARRAY (ADD1 (TIMES 72 11))
                                            :INITIAL-ELEMENT NIL)
                            PTXPOSITION _ 0
                            PTYPOSITION _ (TIMES 72 11)
                            PTRIGHTMARGIN _ (FIX (TIMES 8.5 72))
                            PTLEFTMARGIN _ 0
                            PTCLIPPINGREGION _ (CREATE REGION
                                                      LEFT _ 0
                                                      BOTTOM _ 0
                                                      WIDTH _ (FIX (TIMES 8.5 72))
                                                      HEIGHT _ (TIMES 72 11]
         [REPLACE (STREAM IMAGEOPS) OF STREAM
            WITH (CREATE IMAGEOPS USING (FETCH (STREAM IMAGEOPS) OF STREAM)
                                                IMAGETYPE _ 'PLAINTEXT IMFONT _
                                                [FUNCTION (LAMBDA (STREAM FONT)
                                                            (CL:WHEN FONT
                                                                [PLAINTEXTPARAM
                                                                 PTLINEFEED
                                                                 (IMINUS (FONTPROP FONT 'HEIGHT])
                                                            (PLAINTEXTPARAM PTFONT FONT]
                                                IMCLIPPINGREGION _
                                                [FUNCTION (LAMBDA (STREAM REGION)
                                                            (CL:WHEN (AND REGION
                                                                          (NOT (TYPE? REGION 
                                                                                      REGION)))
                                                                   (\ILLEGAL.ARG REGION))
                                                            (PLAINTEXTPARAM PTCLIPPINGREGION REGION]
                                                IMXPOSITION _ [FUNCTION (LAMBDA (STREAM POS)
                                                                          (PLAINTEXTPARAM PTXPOSITION
                                                                                 POS T]
                                                IMYPOSITION _ [FUNCTION (LAMBDA (STREAM POS)
                                                                          (PLAINTEXTPARAM PTYPOSITION
                                                                                 POS T]
                                                IMMOVETO _ [FUNCTION (LAMBDA (STREAM X Y)
                                                                       (PLAINTEXTPARAM PTXPOSITION X
                                                                              T)
                                                                       (PLAINTEXTPARAM PTYPOSITION Y
                                                                              T]
                                                IMLEFTMARGIN _ [FUNCTION (LAMBDA (STREAM M)
                                                                           (PLAINTEXTPARAM 
                                                                                  PTLEFTMARGIN M T]
                                                IMRIGHTMARGIN _ [FUNCTION (LAMBDA (STREAM M)
                                                                            (PLAINTEXTPARAM 
                                                                                   PTRIGHTMARGIN M T]
                                                IMLINEFEED _ [FUNCTION (LAMBDA (STREAM DY)
                                                                         (PLAINTEXTPARAM PTLINEFEED 
                                                                                DY T]
                                                IMSPACEFACTOR _ [FUNCTION (LAMBDA NIL 1]
                                                IMFONTCREATE _ 'DISPLAY IMSTRINGWIDTH _
                                                [FUNCTION (LAMBDA (STREAM STR RDTBL)
                                                            (STRINGWIDTH STR
                                                                   (FETCH PTFONT
                                                                      OF (FETCH (STREAM
                                                                                         IMAGEDATA)
                                                                                OF STREAM))
                                                                   RDTBL RDTBL]
                                                IMCHARWIDTH _ [FUNCTION (LAMBDA (STREAM CHARCODE)
                                                                          (CHARWIDTH
                                                                           CHARCODE
                                                                           (FETCH PTFONT
                                                                              OF
                                                                              (FETCH (STREAM
                                                                                          IMAGEDATA)
                                                                                 OF STREAM]
                                                IMCLOSEFN _ (FUNCTION WRITEPLAINTEXTPAGE)
                                                IMCHARSET _ [FUNCTION (LAMBDA (STREAM CHARSET)

                                 (* ;; "If we had another illegal character set value, then we could simply fix it so that the character set didn't match anything, which would cause the character set shift to be put out on the next character")

                                                                        (COND
                                                                           ((\IOMODEP STREAM
                                                                                   'OUTPUT T)
                                                                            (\BOUT STREAM 
                                                                                   NSCHARSETSHIFT)
                                                                            (COND
                                                                               ((EQ CHARSET T)
                                                                                (\BOUT STREAM 
                                                                                       NSCHARSETSHIFT
                                                                                       )
                                                                                (\BOUT STREAM 0))
                                                                               (T (\BOUT STREAM 
                                                                                         CHARSET]
                                                IMDRAWPOLYGON _ (FUNCTION NILL)
                                                IMDRAWPOINT _ (FUNCTION NILL)
                                                IMSCALE _ (FUNCTION (LAMBDA NIL 1]
         (DSPFONT '(GACHA 10)
                STREAM)
         STREAM])

(PLAINTEXTOUTCHARFN
  [LAMBDA (STREAM CHARCODE)                              (* ; "Edited 15-Jan-98 00:06 by rmk:")

    (* ;; "Put character data in PAGE entry indexed by current yposition")

    (LET ((IMDATA (FETCH IMAGEDATA OF STREAM)))
         (SELCHARQ CHARCODE
              (CR 
                  (* ;; "Set parameters but don't output--that means we can do lots of moving around, up and down, and still come out OK.")

                  (REPLACE PTXPOSITION OF IMDATA WITH 0)
                  (ADD (FETCH PTYPOSITION OF IMDATA)
                         (FETCH PTLINEFEED OF IMDATA)))
              (FORM (WRITEPLAINTEXTPAGE STREAM)
                    (BOUT STREAM (CHARCODE FORM))
                    (REPLACE PTXPOSITION OF IMDATA WITH 0)
                    (REPLACE PTYPOSITION OF IMDATA WITH (TIMES 72 11)))
              (LF (ADD (FETCH PTYPOSITION OF IMDATA)
                         (FETCH PTLINEFEED OF IMDATA)))
              (CL:PUSH [LIST (FETCH PTXPOSITION OF IMDATA)
                             CHARCODE
                             (ADD (FETCH PTXPOSITION OF IMDATA)
                                    (CHARWIDTH CHARCODE (FETCH PTFONT OF IMDATA]
                     (CL:SVREF (FETCH PTPAGE OF IMDATA)
                            (FETCH PTYPOSITION OF IMDATA])

(PLAINTEXT.TEDIT
  [LAMBDA (FILE PTFILE)                                  (* ; "Edited  8-Jan-98 06:17 by rmk:")
                                                             (* ; "Edited 18-Sep-91 18:16 by jds")

    (* ;; "Make a plaintext file from a TEdit document.  If FILE is a string, make it into a symbol for the file-name.  If it's a STREAM, use that stream.")

    [COND
       ((STRINGP FILE)
        (SETQ FILE (MKATOM FILE]
    (SETQ FILE (OPENTEXTSTREAM FILE))
    (TEDIT.FORMAT.HARDCOPY FILE PTFILE T NIL NIL NIL 'PLAINTEXT)
    PTFILE])

(PLAINTEXT.TEXT
  [LAMBDA (FILE PTFILE FONTS HEADING TABS)               (* ; "Edited  8-Jan-98 06:20 by rmk:")

    (* ;; 
"The effect of this should be to throw away font change characters and coerce characters to ISO8859")

    (TEXTTOIMAGEFILE FILE PTFILE 'PLAINTEXT FONTS HEADING TABS])
)
(DEFINEQ

(WRITEPLAINTEXTPAGE
  [LAMBDA (STREAM)                                       (* ; "Edited 15-Jan-98 09:48 by rmk:")
    (LET [(PAGE (FETCH PTPAGE OF (FETCH IMAGEDATA OF STREAM]

         (* ;; 
   "Have to run through y-positions indexed backwards, since have to print higher positions first.")

         (FOR YPOS LINE LASTYPOS DIFF (DLF _ (FONTPROP DEFAULTFONT 'HEIGHT))
                (DSP _ (CHARWIDTH (CHARCODE SPACE)
                              DEFAULTFONT)) FROM (SUB1 (CL:ARRAY-DIMENSION PAGE 0)) TO 0
            BY -1 FIRST (SETQ LASTYPOS YPOS) WHEN (SETQ LINE (CL:SVREF PAGE YPOS))
            DO (SETQ DIFF (- LASTYPOS YPOS))
                  (CL:WHEN (IGREATERP DIFF DLF)              (* ; "Distance is more than a line")

                      (* ;; 
                    "Start at 2 because one was already put out at the end of the previous line")

                      (FOR I FROM 2 TO (IQUOTIENT DIFF DLF)
                         DO (BOUT STREAM (CHARCODE CR))))
                  (SORT LINE T)                              (* ; "To print from left to right")
                  (FOR C (LASTX _ 0) IN LINE
                     DO (SETQ DIFF (- (POP C)
                                          LASTX))
                           (CL:WHEN (IGREATERP DIFF DSP)     (* ; "Distance is more than a space")
                               (FOR I FROM 1 TO (IQUOTIENT DIFF DLF)
                                  DO (BOUT STREAM (CHARCODE SPACE))))
                           [IF (ILEQ (CAR C)
                                         127)
                               THEN (BOUT STREAM (CAR C))
                             ELSE 

                                   (* ;; "Should coerce to ISO8859.  If get something below 256, use it.  Otherwise, try to print charactername")

                                   (LET (STRING)
                                        (SETQ STRING (SELCHARQ (CAR C)
                                                          (phi "phi")
                                                          (MEMBEROF "memb")
                                                          (UC-SIGMA "Sigma")
                                                          (46,123 "Pi")
                                                          (357,147 "o")
                                                          NIL))
                                        (IF STRING
                                            THEN (BOUT STREAM (CHARCODE \))
                                                  (FOR I C FROM 1
                                                     WHILE (SETQ C (NTHCHARCODE STRING I))
                                                     DO (BOUT STREAM C))
                                                  (BOUT STREAM (CHARCODE \))
                                          ELSE (BOUT STREAM (CHARCODE ~]
                           (SETQ LASTX (CADR C)))
                  (\FILEOUTCHARFN STREAM (CHARCODE CR))
                  (SETQ LASTYPOS YPOS) 

                  (* ;; "Now clear the entry")

                  (CL:SETF (CL:SVREF PAGE YPOS)
                         NIL])
)
(DECLARE%: EVAL@COMPILE 

(PUTPROPS PLAINTEXTPARAM  MACRO
   [(PNAME PVAL NUMBERPFLAG)
    (PROG1 (FETCH PNAME OF (FETCH (STREAM IMAGEDATA) OF STREAM))
        [LET ((PV PVAL))
             (CL:WHEN PV
                 (REPLACE PNAME OF (FETCH (STREAM IMAGEDATA) OF STREAM)
                    WITH (COND
                                ('NUMBERPFLAG (OR (NUMBERP PV)
                                                  (\ILLEGAL.ARG PV)))
                                (T PV))))])])
)
(DECLARE%: EVAL@COMPILE

(RECORD PLAINTEXTIMAGEDATA (PTPAGE PTXPOSITION PTYPOSITION PTFONT PTLINEFEED PTRIGHTMARGIN 
                                      PTLEFTMARGIN PTCLIPPINGREGION))
)

(ADDTOVAR PRINTFILETYPES (PLAINTEXT (EXTENSION (PT PLAINTEXT))))

(ADDTOVAR IMAGESTREAMTYPES (PLAINTEXT (OPENSTREAM OPENPLAINTEXTSTREAM)
                                      (FONTCREATE \CREATEDISPLAYFONT)
                                      (FONTSAVAILABLE \SEARCHDISPLAYFONTFILES)
                                      (CREATECHARSET \CREATECHARSET.DISPLAY)))
(DECLARE%: EVAL@COMPILE DONTCOPY 

[OR (RECLOOK 'STREAM)
    (EVAL (SYSRECLOOK1 'STREAM]

[OR (RECLOOK 'IMAGEOPS)
    (EVAL (SYSRECLOOK1 'IMAGEOPS]
)
(PUTPROPS PLAINTEXTSTREAM COPYRIGHT ("Xerox Corporation" 1998))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1746 11976 (OPENPLAINTEXTSTREAM 1756 . 9644) (PLAINTEXTOUTCHARFN 9646 . 11087) (
PLAINTEXT.TEDIT 11089 . 11661) (PLAINTEXT.TEXT 11663 . 11974)) (11977 15294 (WRITEPLAINTEXTPAGE 11987
 . 15292)))))
STOP
