(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "18-Jan-2022 15:38:56" 
{DSK}<Users>kaplan>Local>medley3.5>my-medley>lispusers>PSEUDOHOSTS.;93 15350  

      :CHANGES-TO (FNS CONTRACT.PH EXPAND.PH)

      :PREVIOUS-DATE "18-Jan-2022 13:09:58" 
{DSK}<Users>kaplan>Local>medley3.5>my-medley>lispusers>PSEUDOHOSTS.;91)


(PRETTYCOMPRINT PSEUDOHOSTSCOMS)

(RPAQQ PSEUDOHOSTSCOMS
       [(FNS PSEUDOHOST PSEUDOHOSTP PSEUDOHOSTS)
        (FNS EXPAND.PH CONTRACT.PH)
        (FNS OPENFILE.PH GETFILENAME.PH DIRECTORYNAMEP.PH CLOSEFILE.PH REOPENFILE.PH DELETEFILE.PH 
             OPENP.PH UNREGISTERFILE.PH REGISTERFILE.PH GENERATEFILES.PH GETFILEINFO.PH 
             SETFILEINFO.PH NEXTFILEFN.PH FILEINFOFN.PH RENAMEFILE.PH)
        (DECLARE%: EVAL@COMPILE DONTCOPY (RECORDS PHDEVICE PHGENFILESTATE)
               (MACROS PSEUDOHOST.OP PSEUDOHOST.TARGETVAL)
               (P (CL:UNLESS (GETP 'EXPORTS.ALL 'FILE)
                      (LOAD 'EXPORTS.ALL))])
(DEFINEQ

(PSEUDOHOST
  [LAMBDA (HOST PREFIX)                                      (* ; "Edited 18-Jan-2022 13:08 by rmk")
    (CL:WHEN (AND (LISTP HOST)
                  (NULL PREFIX))
        (SETQ PREFIX (CADR HOST))
        (SETQ HOST (CAR HOST)))
    (SETQ HOST (U-CASE (MKATOM HOST)))
    (IF PREFIX
        THEN [LET* [(TARGETHOST (FILENAMEFIELD PREFIX 'HOST))
                    (TARGETDEVICE (OR (\GETDEVICEFROMHOSTNAME TARGETHOST)
                                      (ERROR "UNKNOWN TARGET HOST" TARGETHOST]
                   (CL:UNLESS (MEMB (NTHCHARCODE PREFIX -1)
                                    (CHARCODE (> / <)))
                       (SETQ PREFIX (CONCAT PREFIX (IF (STRPOS "/" PREFIX)
                                                       THEN "/"
                                                     ELSE ">"))))

                   (* ;; "Save the last directory marker to pack on if needed.")

                   (\DEFINEDEVICE HOST (CREATE FDEV
                                          USING TARGETDEVICE DEVICENAME _ HOST FDEV1 _ TARGETDEVICE 
                                                FDEV2 _ (CONS PREFIX (CL:IF (EQ (CHARCODE /)
                                                                                (NTHCHARCODE PREFIX 
                                                                                       -1))
                                                                         '/
                                                                         '<))
                                                OPENFILE _ (FUNCTION OPENFILE.PH)
                                                GETFILENAME _ (FUNCTION GETFILENAME.PH)
                                                DIRECTORYNAMEP _ (FUNCTION DIRECTORYNAMEP.PH)
                                                CLOSEFILE _ (FUNCTION CLOSEFILE.PH)
                                                REOPENFILE _ (FUNCTION REOPENFILE.PH)
                                                DELETEFILE _ (FUNCTION DELETEFILE.PH)
                                                OPENP _ (FUNCTION OPENP.PH)
                                                UNREGISTERFILE _ (FUNCTION UNREGISTERFILE.PH)
                                                REGISTERFILE _ (FUNCTION REGISTERFILE.PH)
                                                GENERATEFILES _ (FUNCTION GENERATEFILES.PH)
                                                GETFILEINFO _ (FUNCTION GETFILEINFO.PH)
                                                SETFILEINFO _ (FUNCTION SETFILEINFO.PH)
                                                RENAMEFILE _ (FUNCTION RENAMEFILE.PH]
      ELSEIF (PSEUDOHOSTP HOST)
        THEN (\DEFINEDEVICE HOST NIL)
      ELSE (ERROR HOST "is not a pseudohost"))
    HOST])

(PSEUDOHOSTP
  [LAMBDA (HOST)                                             (* ; "Edited 18-Jan-2022 11:29 by rmk")
    (LET ((DEV (\GETDEVICEFROMNAME HOST T T)))
         (CL:WHEN (AND DEV (TYPE? FDEV (FETCH (PHDEVICE TARGETDEV) OF DEV)))
             (LIST HOST (FETCH (PHDEVICE PREFIX)
                               DEV)))])

(PSEUDOHOSTS
  [LAMBDA NIL                                                (* ; "Edited 17-Jan-2022 18:15 by rmk")
    (FOR DEV IN \FILEDEVICES WHEN (TYPE? FDEV (FETCH (PHDEVICE TARGETDEV) OF DEV))
       COLLECT (LIST (FETCH (FDEV DEVICENAME) OF DEV)
                     (FETCH (PHDEVICE PREFIX) OF DEV])
)
(DEFINEQ

(EXPAND.PH
  [LAMBDA (FILENAME PHDEV)                                   (* ; "Edited 18-Jan-2022 15:34 by rmk")
                                                             (* ; "Edited 16-Jan-2022 14:41 by rmk")
                                                             (* ; "Edited 14-Jan-2022 00:02 by rmk")

    (* ;; "Assumes that FILENAME contains }, because HOST was identified in it.")

    (CL:UNLESS (TYPE? FDEV PHDEV)
        (SETQ PHDEV (\GETDEVICEFROMNAME PHDEV)))
    (LET (SUFFIX SUFFIXPOS)
         (CL:WHEN (SETQ SUFFIXPOS (STRPOS "}" FILENAME))
             (SETQ SUFFIX (SUBSTRING FILENAME (ADD1 SUFFIXPOS)))
             (CL:WHEN (FMEMB (CHCON1 SUFFIX)
                             (CHARCODE (< > /)))
                 (SETQ SUFFIX (SUBSTRING SUFFIX 2)))
             (CONCAT (FETCH (PHDEVICE PREFIX) OF PHDEV)
                    SUFFIX))])

(CONTRACT.PH
  [LAMBDA (NAME PHDEV)                                       (* ; "Edited 18-Jan-2022 15:33 by rmk")
                                                             (* ; "Edited 16-Jan-2022 19:57 by rmk")
                                                             (* ; "Edited 14-Jan-2022 00:03 by rmk")
    (CL:UNLESS (TYPE? FDEV PHDEV)
        (SETQ PHDEV (\GETDEVICEFROMNAME PHDEV)))
    (CL:WHEN NAME
        (LET* [(INFO (FETCH (FDEV FDEV2) OF PHDEV))
               (PREFIX (CAR INFO))
               (CONNECTOR (CDR INFO))
               (SUFFIX (SUBSTRING NAME (ADD1 (NCHARS (CAR INFO]
              (IF (EQ 1 (STRPOS PREFIX NAME 1 NIL T NIL FILEDIRCASEARRAY))
                  THEN (CL:WHEN (STRPOS ">" SUFFIX 1 NIL NIL NIL FILEDIRCASEARRAY)

                           (* ;; "Must be a subdirectory.  (CDR INFO) tells us whether to pack on / or > depending on what the prefix has")

                           (SETQ SUFFIX (CONCAT CONNECTOR SUFFIX)))
                       (PACK* '{ (FETCH (FDEV DEVICENAME) OF PHDEV)
                              "}"
                              (OR SUFFIX ""))
                ELSE 
                     (* ;; "If the target's NAME didn't begin with the prefix, then the caller must have jumped outside the pseudo root.  So just return the NAME")

                     NAME)))])
)
(DEFINEQ

(OPENFILE.PH
  [LAMBDA (FILE ACCESS RECOG OTHERINFO FDEV OLDSTRING)       (* ; "Edited 18-Jan-2022 10:29 by rmk")
    (LET ((STREAM (PSEUDOHOST.TARGETVAL OPENFILE (FILE ACCESS RECOG OTHERINFO FDEV OLDSTRING)
                         FDEV)))
         (CHANGE (FETCH (STREAM FULLFILENAME) OF STREAM)
                (CONTRACT.PH DATUM FDEV))
         STREAM])

(GETFILENAME.PH
  [LAMBDA (NAME RECOG FDEV)                                  (* ; "Edited 16-Jan-2022 20:27 by rmk")
    (PSEUDOHOST.OP GETFILENAME (NAME RECOG FDEV])

(DIRECTORYNAMEP.PH
  [LAMBDA (DIRSPEC DEV CREATE?)                              (* ; "Edited 18-Jan-2022 11:32 by rmk")

    (* ;; "{FOO} by itself is always a legitimate directory--you should be able to connect to it when you are starting up")
                                                             (* ; "Edited 16-Jan-2022 20:35 by rmk")
    (OR (EQ (CHARCODE })
            (NTHCHARCODE DIRSPEC -1))
        (PSEUDOHOST.OP DIRECTORYNAMEP (DIRSPEC DEV CREATE?)
               DEV])

(CLOSEFILE.PH
  [LAMBDA (STREAM ABORTFLG)                                  (* ; "Edited 16-Jan-2022 15:38 by rmk")
    (APPLY* (FETCH (FDEV CLOSEFILE) OF (FETCH (PHDEVICE TARGETDEV) OF (FETCH (STREAM DEVICE)
                                                                         OF STREAM)))
           STREAM ABORTFLG])

(REOPENFILE.PH
  [LAMBDA (FILE ACCESS RECOG OTHERINFO FDEV OLDSTREAM)       (* ; "Edited 18-Jan-2022 11:41 by rmk")
    (PSEUDOHOST.TARGETVAL REOPENFILE (FILE ACCESS RECOG OTHERINFO FDEV OLDSTREAM)
           FDEV)
    (LET ((STREAM (PSEUDOHOST.TARGETVAL REOPENFILE (FILE ACCESS RECOG OTHERINFO FDEV OLDSTREAM)
                         FDEV)))
         (CHANGE (FETCH (STREAM FULLFILENAME) OF STREAM)
                (CONTRACT.PH DATUM FDEV))
         STREAM])

(DELETEFILE.PH
  [LAMBDA (FILENAME DEV)                                     (* ; "Edited 18-Jan-2022 10:23 by rmk")
    (PSEUDOHOST.OP DELETEFILE (FILENAME DEV])

(OPENP.PH
  [LAMBDA (FILENAME ACCESS DEVICE)                           (* ; "Edited 18-Jan-2022 10:29 by rmk")
    (PSEUDOHOST.TARGETVAL OPENP (FILENAME ACCESS DEVICE])

(UNREGISTERFILE.PH
  [LAMBDA (DEVICE STREAM)                                    (* ; "Edited 16-Jan-2022 16:47 by rmk")
    (APPLY* (FETCH (FDEV UNREGISTERFILE) OF (FETCH (PHDEVICE TARGETDEV) OF DEVICE))
           (FETCH (PHDEVICE TARGETDEV) OF DEVICE)
           STREAM])

(REGISTERFILE.PH
  [LAMBDA (DEVICE STREAM)                                    (* ; "Edited 16-Jan-2022 16:46 by rmk")
    (APPLY* (FETCH (FDEV REGISTERFILE) OF (FETCH (PHDEVICE TARGETDEV) OF DEVICE))
           (FETCH (PHDEVICE TARGETDEV) OF DEVICE)
           STREAM])

(GENERATEFILES.PH
  [LAMBDA (FDEV PATTERN DESIREDPROPS OPTIONS)                (* ; "Edited 17-Jan-2022 20:46 by rmk")

    (* ;; "FDEV is the pseudohost. We will generate from the target directory using its GENFILESTATE, but fiddle the output so that it looks like it is coming from the pseudo host.")

    (LET ((TARGETGENOBJ (APPLY* (FETCH (FDEV GENERATEFILES) OF (FETCH (PHDEVICE TARGETDEV)
                                                                  OF FDEV))
                               (FETCH (PHDEVICE TARGETDEV) OF FDEV)
                               (EXPAND.PH PATTERN FDEV)
                               DESIREDPROPS OPTIONS)))

         (* ;; "The TARGETGENOBJ contains the targets functions as well as its GENFILESTATE.  We need the ph FDEV to contract the generated names")

         (CREATE FILEGENOBJ
                NEXTFILEFN _ (FUNCTION NEXTFILEFN.PH)
                FILEINFOFN _ (FUNCTION FILEINFOFN.PH)
                GENFILESTATE _ (LIST FDEV TARGETGENOBJ])

(GETFILEINFO.PH
  [LAMBDA (STREAM ATTRIBUTE DEVICE)                          (* ; "Edited 17-Jan-2022 18:21 by rmk")
    (APPLY* (FETCH (FDEV GETFILEINFO) OF (FETCH (PHDEVICE TARGETDEV) OF DEVICE))
           (OR (STREAMP STREAM)
               (EXPAND.PH STREAM DEVICE))
           ATTRIBUTE
           (FETCH (PHDEVICE TARGETDEV) OF DEVICE])

(SETFILEINFO.PH
  [LAMBDA (STREAM ATTRIBUTE VALUE DEVICE)
    (PSEUDOHOST.OP SETFILEINFO (STREAM ATTRIBUTE VALUE DEVICE])

(NEXTFILEFN.PH
  [LAMBDA (GENFILESTATE NAMEONLY)                            (* ; "Edited 17-Jan-2022 21:27 by rmk")
    (LET* ((TARGETGENOBJ (CADR GENFILESTATE))
           (TARGETGENFILESTATE (FETCH GENFILESTATE OF TARGETGENOBJ))
           (FILENAME (APPLY* (FETCH NEXTFILEFN OF TARGETGENOBJ)
                            TARGETGENFILESTATE NAMEONLY)))
          (CL:WHEN FILENAME
              (CL:UNLESS NAMEONLY
                  (SETQ FILENAME (CONTRACT.PH FILENAME (CAR GENFILESTATE)))))
          FILENAME])

(FILEINFOFN.PH
  [LAMBDA (GENFILESTATE ATTRIBUTE)                           (* ; "Edited 17-Jan-2022 20:52 by rmk")
    (APPLY* (FETCH FILEINFOFN OF (CADR GENFILESTATE))
           (FETCH GENFILESTATE OF (CADR GENFILESTATE))
           ATTRIBUTE])

(RENAMEFILE.PH
  [LAMBDA (OLD-DEVICE OLD-NAME NEW-DEVICE NEW-NAME)          (* ; "Edited 18-Jan-2022 09:52 by rmk")
    (LET ((OLDTARGETDEV (FETCH (PHDEVICE TARGETDEV) OF OLD-DEVICE))
          (NEWTARGETDEV (FETCH (PHDEVICE TARGETDEV) OF NEW-DEVICE))
          (NEWTARGETNAME NEW-NAME)
          RESULT)
         (CL:WHEN (TYPE? FDEV NEWTARGETDEV)                  (* ; "NEW-DEVICE is a pseudo host")
             (SETQ NEWTARGETNAME (EXPAND.PH NEW-NAME NEW-DEVICE)))
         (SETQ RESULT (APPLY* (FETCH (FDEV RENAMEFILE) OF OLDTARGETDEV)
                             OLDTARGETDEV
                             (EXPAND.PH OLD-NAME OLD-DEVICE)
                             (OR NEWTARGETDEV NEW-DEVICE)
                             NEWTARGETNAME))
         (CL:WHEN (AND RESULT (NEQ NEWTARGETDEV NEW-DEVICE))
             (SETQ RESULT (CONTRACT.PH RESULT NEW-DEVICE)))
         RESULT])
)
(DECLARE%: EVAL@COMPILE DONTCOPY 
(DECLARE%: EVAL@COMPILE

(ACCESSFNS PHDEVICE ((PREFIX (CAR (FETCH (FDEV FDEV2) OF DATUM)))
                     (TARGETDEV (FETCH (FDEV FDEV1) OF DATUM)
                            (REPLACE (FDEV FDEV1) OF DATUM WITH NEWVALUE))))

(RECORD PHGENFILESTATE (PHDEVICE . TARGETGENFILESTATE))
)

(DECLARE%: EVAL@COMPILE 

(PUTPROPS PSEUDOHOST.OP MACRO
          [TAIL (LET [(OPNAME (CAR TAIL))
                      (ARGS (CADR TAIL))
                      (DEV (OR (CADDR TAIL)
                               (CAR (LAST (CADR TAIL]

                     (* ;; 
    "Assumes that the name is (CAR ARGS), the device is the last or args if not specified separately")

                     `(CONTRACT.PH [APPLY* (FETCH (FDEV ,OPNAME) OF (FETCH (PHDEVICE TARGETDEV)
                                                                       OF ,DEV))
                                          (EXPAND.PH ,(CAR ARGS)
                                                 ,DEV)
                                          ,@(SUBST `(FETCH (PHDEVICE TARGETDEV) OF ,DEV)
                                                   DEV
                                                   (CDR ARGS]
                             ,DEV])

(PUTPROPS PSEUDOHOST.TARGETVAL MACRO
          [TAIL (LET [(OPNAME (CAR TAIL))
                      (ARGS (CADR TAIL))
                      (DEV (OR (CADDR TAIL)
                               (CAR (LAST (CADR TAIL]

                     (* ;; "Assumes that the name is (CAR ARGS), the device is the last or args if not specified separately.  Unlike PSEUDOHOST.OP, this returns the target value, doesn't assume it is a name to be contracted.")

                     `(APPLY* (FETCH (FDEV ,OPNAME) OF (FETCH (PHDEVICE TARGETDEV)
                                                          OF ,DEV))
                             (EXPAND.PH ,(CAR ARGS)
                                    ,DEV)
                             ,@(SUBST `(FETCH (PHDEVICE TARGETDEV) OF ,DEV)
                                      DEV
                                      (CDR ARGS])
)


(CL:UNLESS (GETP 'EXPORTS.ALL 'FILE)
    (LOAD 'EXPORTS.ALL))
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1002 4551 (PSEUDOHOST 1012 . 3838) (PSEUDOHOSTP 3840 . 4190) (PSEUDOHOSTS 4192 . 4549))
 (4552 6857 (EXPAND.PH 4562 . 5458) (CONTRACT.PH 5460 . 6855)) (6858 13041 (OPENFILE.PH 6868 . 7249) (
GETFILENAME.PH 7251 . 7425) (DIRECTORYNAMEP.PH 7427 . 7932) (CLOSEFILE.PH 7934 . 8288) (REOPENFILE.PH 
8290 . 8774) (DELETEFILE.PH 8776 . 8945) (OPENP.PH 8947 . 9123) (UNREGISTERFILE.PH 9125 . 9430) (
REGISTERFILE.PH 9432 . 9733) (GENERATEFILES.PH 9735 . 10775) (GETFILEINFO.PH 10777 . 11156) (
SETFILEINFO.PH 11158 . 11283) (NEXTFILEFN.PH 11285 . 11827) (FILEINFOFN.PH 11829 . 12100) (
RENAMEFILE.PH 12102 . 13039)))))
STOP
