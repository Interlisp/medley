(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "31-Oct-2024 17:27:44" {WMEDLEY}<lispusers>TEDIT-PF-SEE.;124 10208  

      :EDIT-BY rmk

      :CHANGES-TO (VARS TEDIT-PF-SEECOMS)

      :PREVIOUS-DATE "31-Oct-2024 17:25:56" {WMEDLEY}<lispusers>TEDIT-PF-SEE.;123)


(PRETTYCOMPRINT TEDIT-PF-SEECOMS)

(RPAQQ TEDIT-PF-SEECOMS [(FNS PF-TEDIT PF-TEDIT-FROM-TEXT)
                         (COMMANDS ts tf)
                         (FILES (SYSLOAD)
                                REGIONMANAGER)
                         (P (MOVD? 'PFCOPYBYTES 'PFI.MAYBE.PP.DEFINITION)
                            (MOVD? 'NILL (FUNCTION TEDIT.SETFUNCTION))
                            (TEDIT.SETFUNCTION "Meta,T" (FUNCTION PF-TEDIT-FROM-TEXT))
                            (TEDIT.SETFUNCTION "Meta,t" (FUNCTION PF-TEDIT-FROM-TEXT)))
                         (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS (ADDVARS
                                                                                        (NLAMA)
                                                                                        (NLAML)
                                                                                        (LAMA])
(DEFINEQ

(PF-TEDIT
  [LAMBDA (FN IFILES REPRINT)                                (* ; "Edited 27-Aug-2024 13:03 by rmk")
                                                             (* ; "Edited 27-Mar-2024 23:45 by rmk")
                                                             (* ; "Edited 25-Dec-2023 12:24 by rmk")
                                                             (* ; "Edited  5-Dec-2023 23:50 by rmk")
                                                             (* ; "Edited 12-Oct-2023 00:19 by rmk")
                                                             (* ; "Edited 14-Sep-2023 22:33 by rmk")
                                                             (* ; "Edited 14-Jul-2023 00:14 by rmk")
                                                             (* ; "Edited  5-May-2022 23:11 by rmk")
                                                             (* ; "Edited 12-Jan-2022 13:15 by rmk")
                                                             (* ; "Edited 30-Dec-2021 23:17 by rmk")

    (* ;; "PF* to a read-only TEDIT window.  First argument is the function name, second if given is the input file.")

    (* ;; "This uses PFCOPYBYTES so we see what it looks like on the file.  But some functions were not prettyprinted, so they appear as useless garbage.")

    (* ;; "In that case, calling again with REPRINT=T will read and reprint.  And, invoking tf again with no arguments at all will also reprint the last function in the same window")

    (SETQ IFILES (MKLIST IFILES))
    (CL:WHEN (LISTP FN)
        (SETQ FN (CAR FN)))
    (SELECTQ FN
        ((t T NIL) 
             (SETQ REPRINT T)
             (SETQ FN LASTWORD))
        (SETQ LASTWORD FN))
    (CL:UNLESS FN (ERROR "No function to print"))
    (CL:WHEN (INTERSECTION '(T t)
                    IFILES)
        (SETQ REPRINT T)
        [SETQ IFILES (LDIFFERENCE IFILES '(t T])
    (IF [OR IFILES (SETQ IFILES (APPEND (WHEREIS FN 'FNS T)
                                       (WHEREIS FN 'FUNCTIONS T]
        THEN                                                 (* ; "skip compiled files")

        (* ;; "Since we are creating readonly Tedits, try to keep the TTY where it is.")

        (FOR IFILE LOC TSTREAM ENV EXPR TFPROP WINDOW INSIDE IFILES
           UNLESS (MEMB (FILENAMEFIELD IFILE 'EXTENSION)
                        *COMPILED-EXTENSIONS*)
           DO (SETQ LOC (FINDFNDEF FN IFILE))
              (IF (LISTP LOC)
                  THEN (SETQ TFPROP (LIST FN (CAR LOC)))
                       [SETQ WINDOW (FIND W IN (OPENWINDOWS)
                                       SUCHTHAT (AND (EQUAL TFPROP (WINDOWPROP W 'TF))
                                                     (fetch (TEXTWINDOW WTEXTSTREAM) of W]
                       (IF (AND WINDOW (NOT REPRINT))
                           THEN 
                                (* ;; 
               "If already an open PF window on this function in this file, just raise it to the top")

                                (TOTOPW WINDOW)
                                (RETURN)
                         ELSE (CL:WITH-OPEN-FILE (ISTREAM (POP LOC)
                                                        :DIRECTION :INPUT)
                                     (SETQ ENV (LISPSOURCEFILEP ISTREAM))
                                     (SETFILEINFO ISTREAM 'FORMAT ENV)
                                     (SETQ TSTREAM (OPENTEXTSTREAM))
                                     (DSPFONT DEFAULTFONT TSTREAM)
                                     (PRINTOUT TSTREAM 5 "[From " (FULLNAME ISTREAM)
                                            "]" T)
                                     (PRINT-READER-ENVIRONMENT ENV TSTREAM)
                                     (IF REPRINT
                                         THEN (SETFILEPTR ISTREAM (POP LOC))
                                              (SETQ EXPR (WITH-READER-ENVIRONMENT ENV (READ ISTREAM))
                                               )
                                              (WITH-READER-ENVIRONMENT ENV
                                                  (IF (EQ FN (CAR EXPR))
                                                      THEN (DSPFONT BOLDFONT TSTREAM)
                                                           (PRINT FN TSTREAM)
                                                           (DSPFONT DEFAULTFONT TSTREAM)
                                                           (SETQ EXPR (CADR EXPR))
                                                           (PRINTDEF EXPR 3 T NIL NIL TSTREAM)
                                                    ELSEIF (EQ FN (CADR EXPR))
                                                      THEN 
                                                           (* ;; 
                                              "Presumably a DEFUN.  Print the CAR, boldface the cadr")

                                                           (PRINTOUT TSTREAM "(" .P2 (CAR EXPR)
                                                                  " " .FONT BOLDFONT .P2 (CADR EXPR)
                                                                  .FONT DEFAULTFONT " " .P2
                                                                  (CADDR EXPR)
                                                                  T 3)
                                                           (PRINTDEF (CDDDR EXPR)
                                                                  3 T T NIL TSTREAM)
                                                           (PRIN3 ")" TSTREAM)
                                                    ELSE (PRINTDEF EXPR 3 NIL NIL NIL TSTREAM)))
                                       ELSE (PFI.MAYBE.PP.DEFINITION ISTREAM TSTREAM (POP LOC)
                                                   (POP LOC)))
                                     (TERPRI TSTREAM)
                                     [TEDIT TSTREAM (OR WINDOW 'TF)
                                            NIL
                                            `(READONLY T LEAVETTY T TITLE ,(CONCAT FN "  from "
                                                                                  (FULLNAME ISTREAM]
                                     (TEXTPROP TSTREAM 'ITEM-NAME FN)
                                     (TEXTPROP TSTREAM 'BOUNDTABLE (TEDIT.ATOMBOUND.READTABLE 
                                                                          *READTABLE*))

                                 (* ;; "The windowprop allows for reprinting as a window action, or reprinting from a command that can find and reuse the previous (presumably unprettied) window.")

                                     (WINDOWPROP (WFROMDS TSTREAM)
                                            'TF TFPROP)))
                ELSEIF (EQ LOC 'FILE.NOT.FOUND)
                  THEN (printout T "file " IFILE " not found." T)
                ELSE (printout T FN " not found on " LOC "." T)))
        (SETQ *LAST-DF* FN)
      ELSE (PRINTOUT T FN " has no function definition" T])

(PF-TEDIT-FROM-TEXT
  [LAMBDA (TSTREAM TEXTOBJ SEL)                              (* ; "Edited 26-Aug-2024 23:13 by rmk")

    (* ;; "The function key for the meta,T and meta,t keys.  This shows in a separate Tedit window the definition in TSTREAM of the function named by the selection SEL.")

    (SETQ TSTREAM (TEXTSTREAM TSTREAM))
    (CL:UNLESS SEL
        (SETQ SEL (TEDIT.GETSEL TSTREAM)))
    (LET [[FILENAME (OR (TEXTPROP TSTREAM 'FILENAME)
                        (AND (\TEDIT.PRIMARYPANE TSTREAM)
                             (CADR (WINDOWPROP (\TEDIT.PRIMARYPANE TSTREAM TSTREAM)
                                          'TF]
          (FN (MKATOM (TEDIT.SEL.AS.STRING TSTREAM SEL]
         (if (EQ 0 (NCHARS FN))
             then (TEDIT.PROMPTPRINT TSTREAM "Please select a function to display" T)
           elseif FILENAME
             then (PF-TEDIT FN FILENAME)
           else (TEDIT.PROMPTPRINT TSTREAM (CONCAT FN " not found")
                       T])
)

(DEFCOMMAND ts (FILE WINDOW FORMAT) 
   (TEDIT-SEE (OR (FINDFILE-WITH-EXTENSIONS FILE NIL '(NIL TEDIT TED TXT TEXT TEX))
                  (ERROR "FILE NOT FOUND" FILE))
          (OR WINDOW 'SEE)
          FORMAT))

(DEFCOMMAND tf (FN . IFILES) (PF-TEDIT FN IFILES))

(FILESLOAD (SYSLOAD)
       REGIONMANAGER)

(MOVD? 'PFCOPYBYTES 'PFI.MAYBE.PP.DEFINITION)

(MOVD? 'NILL (FUNCTION TEDIT.SETFUNCTION))

(TEDIT.SETFUNCTION "Meta,T" (FUNCTION PF-TEDIT-FROM-TEXT))

(TEDIT.SETFUNCTION "Meta,t" (FUNCTION PF-TEDIT-FROM-TEXT))
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 

(ADDTOVAR NLAMA )

(ADDTOVAR NLAML )

(ADDTOVAR LAMA )
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1243 9524 (PF-TEDIT 1253 . 8506) (PF-TEDIT-FROM-TEXT 8508 . 9522)))))
STOP
