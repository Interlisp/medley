(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "XCL" BASE 10)
(FILECREATED " 6-May-2000 10:37:14" |{DSK}<project>medley3.5>lispusers>TMAX-NUMBER.;4| 32399  

      |changes| |to:|  (FNS NUMBEROBJ.TEDIT-TO-TEX-FN NUMBEROBJ)
                       (VARS TMAX-NUMBERCOMS)

      |previous| |date:| "18-May-99 22:54:30" |{DSK}<project>medley3.5>lispusers>TMAX-NUMBER.;2|)


; Copyright (c) 1987, 1999, 2000 by Xerox Corporation.  All rights reserved.

(PRETTYCOMPRINT TMAX-NUMBERCOMS)

(RPAQQ TMAX-NUMBERCOMS
       ((* |Developed| |under| |support| |from| NIH |grant| RR-00785.)
        (* |Written| |by| |Frank| |Gilmurray| |and| |Sami| |Shaio.|)
        (* * TMAX-NUMBERNIL |ImageObject| |functions|)
        (FNS NUMBEROBJ NUMBEROBJP NGROUPP NUMBER.DISPLAYFN NUMBER.PREPRINTFN NUMBER.IMAGEBOXFN 
             NUMBER.PUTFN NUMBER.GETFN NUMBER.COPYFN NUMBER.BUTTONEVENTINFN NUMBEROBJ.TEDIT-TO-TEX-FN
             )
        (FNS COPY.NGROUP.BRANCH DUMP.NGROUP.GRAPH NGROUP.BUTTONEVENTINFN NGROUP.DEFINE.TAG 
             NUMBER.DELETE.TAG NGROUP.SHOW.TAG CHANGE.INSERTED.NGROUP.FORMAT 
             CHANGE.NGROUP.FORMAT.#TEXT SHOW.INSERTED.NGROUP.FORMAT)
        (* * |Variable| |and| |Record| |definitions|)
        (VARS NGROUP.GRAPH.MENU.ITEMS NGROUP.INSERTED.MENU.ITEMS NGROUP.INSERTED.NOTAG.ITEMS 
              NGROUP.INSERTED.TAG.ITEMS)
        (RECORDS NGCOUNTER NGTEMPLATE NUMBEROBJ)))



(* |Developed| |under| |support| |from| NIH |grant| RR-00785.)




(* |Written| |by| |Frank| |Gilmurray| |and| |Sami| |Shaio.|)

(* * TMAX-NUMBERNIL |ImageObject| |functions|)

(DEFINEQ

(NUMBEROBJ
  (LAMBDA (USE TEMPLATE NUMSTRING REF.TYPE FONT MOTHER ABBREV.LEVEL)
                                                             (* |fsg| "26-Aug-87 14:29")
    (LET ((NEWOBJ (IMAGEOBJCREATE (|create| NUMBEROBJ
                                         REF.TYPE _ REF.TYPE
                                         NUMSTRING _ (OR NUMSTRING "Note#")
                                         USE _ USE
                                         NGROUP.MOTHER _ MOTHER
                                         TEMPLATE _ TEMPLATE
                                         UPDATED.OBJ _ NIL
                                         TEXT.AFTER# _ NIL
                                         PAGE.NUMBER _ NIL
                                         FONT _ FONT
                                         TEXT.BEFORE# _ NIL
                                         ABBREV-VAL _ ABBREV.LEVEL)
                         \\NUMBEROBJ.IMAGEFNS)))
         (IMAGEOBJPROP NEWOBJ 'TYPE 'NUMBEROBJ)
         (IMAGEOBJPROP NEWOBJ 'TEDIT-TO-TEX-FN (FUNCTION NUMBEROBJ.TEDIT-TO-TEX-FN))
         NEWOBJ)))

(numberobjp
  (lambda (imobj)                                            (* |ss:| "27-Jun-87 16:21")
          (* * |Tests| |an| |imageobj| |to| |see| |if| |it| |is| \a |number| 
          |imageobject.| |The| |only| |number| |imageobjects| |so| |far| |are| |NGroups| 
          |and| |Endnotes.| b\y |convention,| |testing| |functions| |for| |an| 
          |imageobject| |will| |be| |named| (concat |<type| |of| |imageobj>| "P"))

    (and imobj (eq (imageobjprop imobj 'type)
                   'numberobj))))

(ngroupp
  (lambda (imobj)                                            (* |ss:| "27-Jun-87 16:15")
          (* * |Like| numberobjp |but| |also| |checks| |for| |NGroup| |ImageObject.|)

    (and (numberobjp imobj)
         (eq (|fetch| (numberobj use) |of| (|fetch| objectdatum |of| imobj))
             'ngroup))))

(number.displayfn
  (lambda (image.obj stream)                                 (* |fsg| "24-Sep-87 14:56")
          (* |Display| |function| |for| |numberobjs.|
          |Allows| |different| |formats| |for| |display| |according| |to| |the| |use| 
          |to| |which| |the| |numberobj| |is| |being| |put.|
          i\f |no| |specific| |action| |is| |specified,| |displaying| |defaults| |to| 
          |printing| |out| |as| \a |plain| |number.*|)

    (|with| numberobj (|fetch| objectdatum |of| image.obj)
           (let* ((main.window (|with| textobj textobj (car \\window)))
                  (image.tag (imageobjprop image.obj 'tag))
                  (old.font (dspfont nil stream))
                  (nbr.font (selectq use
                                (note (|fetch| (endnotefonts number.font) |of| (get.endnote.fonts
                                                                                main.window)))
                                (ngroup font)
                                (error "Undefined USE field" use))))
                 (and image.tag (or (tsp.getcodeval image.tag main.window)
                                    (tsp.putcode image.tag image.obj main.window)))
                 (and (fontp nbr.font)
                      (dspfont (fontcreate (fontprop nbr.font 'family)
                                      (fontprop nbr.font 'size)
                                      (fontprop nbr.font 'face))
                             stream))
                 (or (imagestreamtypep stream 'display)
                     (setq page.number (car formattingstate)))
                 (tmax.shadeobj image.obj stream)
                 (selectq use
                     (ngroup (prin1 (concat (or text.before# "")
                                           (mkstring numstring)
                                           (or text.after# ""))
                                    stream)
                             (or (imagestreamtypep stream 'display)
                                 (update.ngroup.manindex template ref.type main.window)))
                     (note (let ((current.ypos (dspyposition nil stream))
                                 (imagebox (listget (|fetch| imageobjplist |of| image.obj)
                                                  'boundbox)))
                                (dspyposition (iplus current.ypos (idifference (|fetch| ysize
                                                                                  |of| imagebox)
                                                                         (fontprop stream
                                                                                'height)))
                                       stream)
                                (prin1 (mkstring numstring)
                                       stream)
                                (dspyposition current.ypos stream)))
                     nil)
                 (dspfont old.font stream)))))

(NUMBER.PREPRINTFN
  (LAMBDA (IMAGE.OBJ)                                        (* \; "Edited 18-May-99 22:51 by rmk:")
                                                             (* |fsg| "24-Sep-87 14:56")

    (* |;;| "Returns string that represents the number object, for plaintext put.  If no specific action is specified, displaying defaults to printing out as a plain number.*")

    (WITH NUMBEROBJ (FETCH OBJECTDATUM OF IMAGE.OBJ)
           (LET* ((MAIN.WINDOW (WITH TEXTOBJ TEXTOBJ (CAR \\WINDOW)))
                  (IMAGE.TAG (IMAGEOBJPROP IMAGE.OBJ 'TAG)))
                 (AND IMAGE.TAG (OR (TSP.GETCODEVAL IMAGE.TAG MAIN.WINDOW)
                                    (TSP.PUTCODE IMAGE.TAG IMAGE.OBJ MAIN.WINDOW)))
                 (SELECTQ USE
                     (NGROUP (CONCAT (OR TEXT.BEFORE# "")
                                    (MKSTRING NUMSTRING)
                                    (OR TEXT.AFTER# "")))
                     (NOTE (MKSTRING NUMSTRING))
                     NIL)))))

(number.imageboxfn
  (lambda (obj stream currentx rightmargin)                  (* |fsg| " 4-Aug-87 14:56")
          (* * |For| |Endnote| |numbers,| |the| |YSize| |is| |the| |current| |font| 
          |height| |plus| 0.25 |times| |the| |Endnote| |number| |font| |height.|
          w\e |do| |this| |so| |the| |the| |Endnote| |number| |will| |be| |superscripted| 
          |but| |not| |too| |much.|)

          (* * |The| |YSize| |is| |computed| |as| |the| |current| |font| |height| |plus| 
          |half| |of| |the| note |or| |NGroup| |font.|
          |The| |reason| |is| |weird.| |Ask| |Sami| |for| |more| |details.|)

    (|with| numberobj (|fetch| objectdatum |of| obj)
           (let* ((main.window (|with| textobj textobj (car \\window)))
                  (imobj.string (mkstring numstring))
                  (nbr.font (selectq use
                                (note (|fetch| (endnotefonts number.font) |of| (get.endnote.fonts
                                                                                main.window)))
                                (ngroup font)
                                (error "Undefined USE field" use))))
                 (and (eq use 'ngroup)
                      (progn (and (stringp text.before#)
                                  (setq imobj.string (concat text.before# imobj.string)))
                             (and (stringp text.after#)
                                  (setq imobj.string (concat imobj.string text.after#)))))
                 (and (fontp nbr.font)
                      (dspfont (fontcreate (fontprop nbr.font 'family)
                                      (fontprop nbr.font 'size)
                                      (fontprop nbr.font 'face))
                             stream))
                 (|create| imagebox
                        xsize _ (stringwidth imobj.string stream)
                        ysize _ (selectq use
                                    (note (fix (plus (times (dspscale nil stream)
                                                            (fontprop (current.display.font stream)
                                                                   'height))
                                                     (times 0.25 (fontprop stream 'height)))))
                                    (fontprop stream 'height))
                        ydesc _ (fontprop stream 'descent)
                        xkern _ 0)))))

(number.putfn
  (lambda (obj stream)                                       (* |fsg| " 5-Aug-87 08:24")
    (let ((window (|with| textobj textobj (car \\window)))
          (use (|with| numberobj (|fetch| objectdatum |of| obj)
                      use))
          (old.font (|with| numberobj (|fetch| objectdatum |of| obj)
                           font)))
         (selectq use
             (note (note.putfn obj stream window))
             (ngroup (let ((ngroup.rec (copy (|fetch| objectdatum |of| obj))))
                          (|with| numberobj ngroup.rec (setq font (list.font.props font))
                                 (prin4 (list '|NGroup| (and (windowprop window 'dumpngroupgraph)
                                                             (dump.ngroup.graph window))
                                              (imageobjprop obj 'tag)
                                              ngroup.rec)
                                        stream))))
             (error "Unknown NUMBER ImageObject type" use)))))

(number.getfn
  (lambda (stream copy.object)                               (* |fsg| " 3-Sep-87 15:17")
          (* * i\f copy.object |is| |non-NIL| |then| |we| |are| |COPYing| |it| |to| 
          |this| |window.|)

    (let ((nbrobj.datum (or copy.object (cdr (read stream))))
          (newobj (numberobj))
          (window (|with| textobj textobj (car \\window))))
         (tsp.setup.fmmenu window)
         (and (ilessp (length nbrobj.datum)
                     3)
              (setq nbrobj.datum (cons nil nbrobj.datum)))
         (and (car nbrobj.datum)
              (not (and (boundp 'tmax.prune.ngraph)
                        tmax.prune.ngraph))
              (copy.ngroup.branch (car nbrobj.datum)
                     window))
         (and (cadr nbrobj.datum)
              (not (gethash (cadr nbrobj.datum)
                          (windowprop window 'tsp.code.array)))
              (progn (tsp.putcode (cadr nbrobj.datum)
                            newobj window)
                     (imageobjprop newobj 'tag (cadr nbrobj.datum))))
         (|with| numberobj (setq nbrobj.datum (ngroup.fixup.records (caddr nbrobj.datum)
                                                     copy.object))
                (selectq use
                    (note (note.getfn newobj nbrobj.datum window))
                    (ngroup (and (listp font)
                                 (setq font (fontcreate font)))
                            (create.ngroup.node ref.type ngroup.mother nbrobj.datum window)
                            (create.ngroup.node ngroup.mother nil nil window)
                            (add.ngroup.to.mother.node ref.type ngroup.mother window)
                            (windowprop window 'rebuild.graphflg t)
                            (and (|fetch| (ngtemplate ng.manualindex) |of| template)
                                 (windowaddprop window 'manualgroups ref.type))
                            (|replace| objectdatum |of| newobj |with| nbrobj.datum))
                    (error "Unknown USE type in NUMBER.GETFN" use)))
         newobj)))

(number.copyfn
  (lambda (image.obj source.stream target.stream)            (* |fsg| " 4-Aug-87 09:46")
          (* * |Here| |to| copy \a |Number| |Image| |Object.|
          i\f |we| |are| |copying| |to| |our| |own| |window,| |we| |delete| |the| tag 
          |if| |any| |so| |we| |don't| |get| |two| |ImageObjs| |with| |the| |same| tag 
          |name.|)

    (selectq (imagestreamtype target.stream)
        (text (let ((source.window (|with| textobj textobj (car \\window)))
                    (textobj (textobj target.stream)))
                   (apply* (imageobjprop image.obj 'getfn)
                          target.stream
                          (list (|with| numberobj (|fetch| objectdatum |of| image.obj)
                                       (and (eq use 'ngroup)
                                            (neq source.stream target.stream)
                                            (|for| parent |in| (append (list.ancestors ref.type nil 
                                                                              source.window)
                                                                      (list ref.type))
                                               |collect| (car (gethash parent (tsp.get.ngroup.array
                                                                               source.window))))))
                                (and (neq source.stream target.stream)
                                     (imageobjprop image.obj 'tag))
                                (|fetch| objectdatum |of| image.obj)))))
        (error "Unknown TARGET stream type" (imagestreamtype target.stream)))))

(number.buttoneventinfn
  (lambda (obj stream sel relx rely window hoststream button)(* |fsg| " 2-Sep-87 11:09")
          (* * |Here| |when| \a |NumberOBJ| |is| |left| |or| |middle| |buttoned.|
          |Left| |just| |dislays| |the| |Tag| |if| |any| |in| |the| |prompt| |window.|
          |Middle| |pops| |up| \a |menu| |allowing| |this| |user| |to| |do| |various| 
          |things.|)

    (and (mousestate left)
         (cond
            ((imageobjprop obj 'tag)
             (|with| numberobj (|fetch| objectdatum |of| obj)
                    (tedit.promptprint stream (concat "Tag for this " (selectq use
                                                                          (note "Endnote")
                                                                          (ngroup ref.type)
                                                                          (error "Undefined USE code" 
                                                                                 use))
                                                     " is \""
                                                     (imageobjprop obj 'tag)
                                                     "\"")
                           t)))
            (t (tedit.promptprint stream "" t))))
    (and (mousestate middle)
         (let* ((datum (|fetch| objectdatum |of| obj))
                (use (|fetch| (numberobj use) |of| datum))
                (ref.type (|fetch| (numberobj ref.type) |of| datum)))
               (and (selectq use
                        (note (note.buttoneventinfn obj stream window))
                        (ngroup (ngroup.buttoneventinfn ref.type obj stream window))
                        (error "Undefined USE code" use))
                    (progn (tedit.promptprint stream "" t)
                           'changed))))))

(NUMBEROBJ.TEDIT-TO-TEX-FN
  (LAMBDA (OBJ STREAM)
    (PRIN3 "\\ex{" STREAM)
    (LET ((DATUM (IMAGEOBJPROP OBJ 'OBJECTDATUM)))
         (PRIN3 (CAR DATUM)
                STREAM)
         (PRIN3 (CADR DATUM)
                STREAM))
    (PRIN3 "}" STREAM)
    T))
)
(DEFINEQ

(copy.ngroup.branch
  (lambda (ngroup.parents window)                            (* |fsg| "11-Aug-87 09:36")
          (* * |Build| |the| |NGroup| |database| |for| |the| |parents| |of| \a |copied| 
          |NGroup| |or| |the| |entire| |NGroup| |database| |on| \a get.)

    (|for| parent |in| ngroup.parents |do| (and parent (|with| numberobj parent (and (listp font)
                                                                                     (setq font
                                                                                      (fontcreate
                                                                                       font)))
                                                              (or ngroup.mother (setq ngroup.mother
                                                                                 'new.ngroup))
                                                              (create.ngroup.node ref.type 
                                                                     ngroup.mother parent window)
                                                              (create.ngroup.node ngroup.mother nil 
                                                                     nil window)
                                                              (add.ngroup.to.mother.node ref.type 
                                                                     ngroup.mother window))))))

(dump.ngroup.graph
  (lambda (window)                                           (* |fsg| " 3-Aug-87 16:03")
          (* * |Return| \a |list| |of| |the| |NGroup| |graph| |data| |that| |is| put 
          |along| |with| |the| |NGroup| |Imageobject.|
          w\e |can| |then| |rebuild| |the| |entire| |NGroup| |graph| |on| \a get.)

    (let ((graph.list (tconc nil)))
         (maphash (tsp.get.ngroup.array window)
                (function (lambda (val key)
                            (and (neq key 'new.ngroup)
                                 (let ((ngroup.rec (copy (car val))))
                                      (|with| numberobj ngroup.rec (setq font (list.font.props font))
                                             (tconc graph.list ngroup.rec)))))))
         (windowprop window 'dumpngroupgraph nil)
         (cdar graph.list))))

(ngroup.buttoneventinfn
  (lambda (ref.type ngroup.obj stream window)                (* |fsg| " 5-Aug-87 08:31")
          (* * |Here| |when| |an| |inserted| |NGroup| |is| |middle| |buttoned.|)

    (let ((tag (imageobjprop ngroup.obj 'tag))
          (graphw (windowprop window 'imageobj.menuw)))
         (menu (|create| menu
                      title _ (concat ref.type " Menu")
                      items _ (append (cond
                                         (tag ngroup.inserted.tag.items)
                                         (t ngroup.inserted.notag.items))
                                     ngroup.inserted.menu.items)
                      centerflg _ t)))))

(ngroup.define.tag
  (lambda (ref.type window ngroup.obj)                       (* |fsg| " 5-Aug-87 09:26")
          (* * |Define| \a tag |for| |this| |NGroup| |or| |Change| |the| tag |if| |it| 
          |already| |exists.|)

    (let ((old.tag (imageobjprop ngroup.obj 'tag))
          (new.tag (tsp.get.incode (textstream window))))
         (and new.tag (neq new.tag old.tag)
              (progn (and old.tag (number.delete.tag window ngroup.obj))
                     (tsp.putcode new.tag ngroup.obj window)
                     (imageobjprop ngroup.obj 'tag new.tag))))))

(number.delete.tag
  (lambda (window ngroup.obj)                                (* |fsg| " 5-Aug-87 09:27")
          (* * |Delete| |this| |Imageobj's| tag.)

    (tsp.putcode (imageobjprop ngroup.obj 'tag nil)
           nil window)
    nil))

(ngroup.show.tag
  (lambda (ref.type window ngroup.obj)                       (* |fsg| " 5-Aug-87 08:43")
          (* * |Show| |this| |NGroup's| tag.)

    (tedit.promptprint (textstream window)
           (concat ref.type ":  Tag=\"" (imageobjprop ngroup.obj 'tag)
                  "\"")
           t)))

(change.inserted.ngroup.format
  (lambda (ref.type ngroup.obj stream window format.field)   (* |fsg| " 1-Sep-87 15:33")
          (* * |Change| |an| |inserted| |NGroup's| |entire| |format| |or| \a |selected| 
          |field.|)

    (let ((graphw (windowprop window 'imageobj.menuw))
          (new.format (|for| field |in| (cond
                                           (format.field (list format.field))
                                           (t '(txtbefore display txtafter abbrevval before#txt 
                                                      after#txt)))
                         |collect| (selectq field
                                       (txtbefore (change.ngroup.format.txtbefore ref.type graphw 
                                                         ngroup.obj))
                                       (display (change.ngroup.format.display ref.type graphw 
                                                       ngroup.obj))
                                       (txtafter (change.ngroup.format.txtafter ref.type graphw 
                                                        ngroup.obj))
                                       (abbrevval (change.ngroup.format.abbrev ref.type graphw 
                                                         ngroup.obj))
                                       (before#txt (change.ngroup.format.#text ref.type window 
                                                          ngroup.obj 'before))
                                       (after#txt (change.ngroup.format.#text ref.type window 
                                                         ngroup.obj 'after))
                                       (error "Unknown NGroup Format field" field)))))
         (apply 'or new.format))))

(change.ngroup.format.#text
  (lambda (ref.type window ngroup.obj flavor)                (* |fsg| "25-Aug-87 14:48")
          (* * |Change| |the| |text| |before| |or| |after| |an| |inserted| |NGroup| 
          |regardless| |of| |the| |Text| |Before| |or| |Text| |After| |toggle| 
          |settings.|)

    (|with| numberobj (|fetch| objectdatum |of| ngroup.obj)
           (let ((old.string (selectq flavor
                                 (before text.before#)
                                 text.after#))
                 (new.string (tedit.getinput (textstream window)
                                    (concat (selectq flavor
                                                (before "Text before ")
                                                "Text after ")
                                           ref.type ":"))))
                (and new.string (setq new.string (concat (selectq flavor
                                                             (before "")
                                                             " ")
                                                        (convert.tabs.to.spaces new.string)
                                                        (selectq flavor
                                                            (before " ")
                                                            ""))))
                (selectq flavor
                    (before (setq text.before# new.string))
                    (setq text.after# new.string))
                (not (strequal old.string new.string))))))

(show.inserted.ngroup.format
  (lambda (ref.type ngroup.obj stream window)                (* |fsg| "26-Aug-87 12:05")
          (* * |Show| |the| |format| |of| |an| |inserted| |NGroup.|)

    (|with| numberobj (|fetch| objectdatum |of| ngroup.obj)
           (|with| ngtemplate template (tedit.promptprint stream
                                              (concat ref.type ": Display="
                                                     (concat (cond
                                                                (ng.text-before (concat "\"" 
                                                                                       ng.text-before 
                                                                                       "\""))
                                                                (t "\"\""))
                                                            ng.chartype
                                                            (cond
                                                               (ng.text-after (concat "\"" 
                                                                                     ng.text-after 
                                                                                     "\""))
                                                               (t "\"\"")))
                                                     " Abbrev="
                                                     (or abbrev-val "None"))
                                              t)))))
)
(* * |Variable| |and| |Record| |definitions|)


(RPAQQ NGROUP.GRAPH.MENU.ITEMS
       `((|Change Font| (CHANGE.NGROUP.FONT LABEL GRAPHW)
                "Change this NGroup's entire FONT."
                (SUBITEMS (|Family| (CHANGE.NGROUP.FONT LABEL GRAPHW 'FAMILY)
                                 "Change this NGroup's font family.")
                       (|Size| (CHANGE.NGROUP.FONT LABEL GRAPHW 'SIZE)
                              "Change this NGroup's font size.")
                       (|Face| (CHANGE.NGROUP.FONT LABEL GRAPHW 'FACE)
                              "Change this NGroup's font face.")))
         (|Show Font| (SHOW.NGROUP.FONT LABEL GRAPHW)
                "Show this NGroup's FONT.")
         (|Change Format| (CHANGE.NGROUP.FORMAT LABEL GRAPHW)
                "Change this NGroup's entire FORMAT."
                (SUBITEMS (|Delimiter Before| (CHANGE.NGROUP.FORMAT LABEL GRAPHW 'TXTBEFORE)
                                 "Change the delimiter preceding this NGroup.")
                       (|Display Type| (CHANGE.NGROUP.FORMAT LABEL GRAPHW 'DISPLAY)
                              "Change how this NGroup is displayed.")
                       (|Delimiter After| (CHANGE.NGROUP.FORMAT LABEL GRAPHW 'TXTAFTER)
                              "Change the delimiter following this NGroup.")
                       (|Abbreviate Level| (CHANGE.NGROUP.FORMAT LABEL GRAPHW 'ABBREVVAL)
                              "Specify the starting level of this NGroup value.")
                       (|Starting Value| (CHANGE.NGROUP.FORMAT LABEL GRAPHW 'START)
                              "Change this NGroup's starting value.")
                       (|Table-Of-Contents| (CHANGE.NGROUP.FORMAT LABEL GRAPHW 'TOC)
                              "Include this NGroup in the Table-Of-Contents.")
                       \,@
                       (AND (MANUALINDEX.ENABLED? (WINDOWPROP GRAPHW 'TWINDOW))
                            (LIST (LIST '|Manual Index| (FUNCTION (CHANGE.NGROUP.FORMAT LABEL GRAPHW
                                                                         'MANINDEX))
                                        "Include this NGroup in the Manual Index page numbers.")))))
         (|Show Format| (SHOW.NGROUP.FORMAT LABEL GRAPHW)
                "Show this NGroup's FORMAT.")))

(RPAQQ NGROUP.INSERTED.MENU.ITEMS
       ((|Change Font| (CHANGE.NGROUP.FONT REF.TYPE GRAPHW NIL NGROUP.OBJ)
               "Change this NGroup's entire FONT."
               (SUBITEMS (|Family| (CHANGE.NGROUP.FONT REF.TYPE GRAPHW 'FAMILY NGROUP.OBJ)
                                "Change this NGroup's font family.")
                      (|Size| (CHANGE.NGROUP.FONT REF.TYPE GRAPHW 'SIZE NGROUP.OBJ)
                             "Change this NGroup's font size.")
                      (|Face| (CHANGE.NGROUP.FONT REF.TYPE GRAPHW 'FACE NGROUP.OBJ)
                             "Change this NGroup's font face.")))
        (|Show Font| (SHOW.NGROUP.FONT REF.TYPE GRAPHW NGROUP.OBJ)
               "Show this NGroup's FONT.")
        (|Change Format| (CHANGE.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ STREAM WINDOW)
               "Change this NGroup's entire FORMAT."
               (SUBITEMS (|Delimiter Before| (CHANGE.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ 
                                                    STREAM WINDOW 'TXTBEFORE)
                                "Change the delimiter preceding this NGroup.")
                      (|Display Type| (CHANGE.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ STREAM 
                                             WINDOW 'DISPLAY)
                             "Change how this NGroup is displayed.")
                      (|Delimiter After| (CHANGE.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ STREAM 
                                                WINDOW 'TXTAFTER)
                             "Change the delimiter following this NGroup.")
                      (|Abbreviate Level| (CHANGE.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ STREAM 
                                                 WINDOW 'ABBREVVAL)
                             "Specify the starting level of this NGroup value.")
                      (|Text Before| (CHANGE.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ STREAM WINDOW
                                            'BEFORE#TXT)
                             "Change the text preceding this NGroup.")
                      (|Text After| (CHANGE.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ STREAM WINDOW
                                           'AFTER#TXT)
                             "Change the text following this NGroup.")))
        (|Show Format| (SHOW.INSERTED.NGROUP.FORMAT REF.TYPE NGROUP.OBJ STREAM WINDOW)
               "Show this NGroup's FORMAT.")))

(RPAQQ NGROUP.INSERTED.NOTAG.ITEMS ((|Define Tag| (NGROUP.DEFINE.TAG REF.TYPE WINDOW NGROUP.OBJ)
                                               "Define a TAG for this NGroup.")))

(RPAQQ NGROUP.INSERTED.TAG.ITEMS ((|Change Tag| (NGROUP.DEFINE.TAG REF.TYPE WINDOW NGROUP.OBJ)
                                             "Change this NGroup's TAG.")
                                      (|Delete Tag| (NUMBER.DELETE.TAG WINDOW NGROUP.OBJ)
                                             "Delete this NGroup's TAG.")
                                      (|Show Tag| (NGROUP.SHOW.TAG REF.TYPE WINDOW NGROUP.OBJ)
                                             "Show this NGroup's TAG.")))
(DECLARE\: EVAL@COMPILE

(RECORD NGCOUNTER (NCOUNT . ANCESTRY))

(RECORD NGTEMPLATE (NG.CHARTYPE NG.TEXT-AFTER NG.START NG.ADDTOTOC NG.CURRENTVAL NG.MANUALINDEX 
                              NG.TEXT-BEFORE))

(RECORD NUMBEROBJ (REF.TYPE NUMSTRING USE NGROUP.MOTHER TEMPLATE UPDATED.OBJ TEXT.AFTER# 
                             PAGE.NUMBER FONT TEXT.BEFORE# ABBREV-VAL))
)
(PUTPROPS TMAX-NUMBER COPYRIGHT ("Xerox Corporation" 1987 1999 2000))
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (1599 17297 (NUMBEROBJ 1609 . 2715) (NUMBEROBJP 2717 . 3257) (NGROUPP 3259 . 3613) (
NUMBER.DISPLAYFN 3615 . 6654) (NUMBER.PREPRINTFN 6656 . 7700) (NUMBER.IMAGEBOXFN 7702 . 10195) (
NUMBER.PUTFN 10197 . 11281) (NUMBER.GETFN 11283 . 13419) (NUMBER.COPYFN 13421 . 15118) (
NUMBER.BUTTONEVENTINFN 15120 . 17025) (NUMBEROBJ.TEDIT-TO-TEX-FN 17027 . 17295)) (17298 26469 (
COPY.NGROUP.BRANCH 17308 . 18764) (DUMP.NGROUP.GRAPH 18766 . 19642) (NGROUP.BUTTONEVENTINFN 19644 . 
20344) (NGROUP.DEFINE.TAG 20346 . 20949) (NUMBER.DELETE.TAG 20951 . 21210) (NGROUP.SHOW.TAG 21212 . 
21534) (CHANGE.INSERTED.NGROUP.FORMAT 21536 . 23331) (CHANGE.NGROUP.FORMAT.#TEXT 23333 . 24919) (
SHOW.INSERTED.NGROUP.FORMAT 24921 . 26467)))))
STOP
