(DEFINE-FILE-INFO READTABLE "INTERLISP" PACKAGE "INTERLISP")
(FILECREATED " 8-May-2021 14:49:36" 
{DSK}<Users>kaplan>Local>medley3.5>git-medley2>lispusers>WHEELSCROLL.;3 7560   

      changes to%:  (FNS ENABLEWHEELSCROLL)

      previous date%: " 8-May-2021 14:33:04" 
{DSK}<Users>kaplan>Local>medley3.5>git-medley2>lispusers>WHEELSCROLL.;2)


(PRETTYCOMPRINT WHEELSCROLLCOMS)

(RPAQQ WHEELSCROLLCOMS
       [(FNS ENABLEWHEELSCROLL WHEELSCROLL WHEELSCROLL.DOIT INSTALL-WHEELSCROLL 
             LISPINTERRUPTS.WHEELSCROLL)
        [INITVARS (WHEELSCROLLINTERRUPTS '((520 (WHEELSCROLL 'VERTICAL WHEELSCROLLDELTA)
                                                T)
                                           (521 (WHEELSCROLL 'VERTICAL (IMINUS WHEELSCROLLDELTA))
                                                T)
                                           (522 (WHEELSCROLL 'HORIZONTAL (IMINUS WHEELSCROLLDELTA)
                                                       T))
                                           (523 (WHEELSCROLL 'HORIZONTAL WHEELSCROLLDELTA T]
        (GLOBALVARS WHEELSCROLLDELTA WHEELSCROLLSETTLETIME \WHEELSCROLLINPROGRESS)
        (INITVARS (WHEELSCROLLDELTA 20)
               (WHEELSCROLLSETTLETIME 50)
               (\WHEELSCROLLINPROGRESS NIL))
        (DECLARE%: DONTEVAL@LOAD DOCOPY (P (INSTALL-WHEELSCROLL)
                                           (ENABLEWHEELSCROLL T])
(DEFINEQ

(ENABLEWHEELSCROLL
  [LAMBDA (ON)                                      (* ; "Edited  8-May-2021 14:48 by rmk:")

    (* ;; "So we can toggle this scrolling, for experimentation.")

    (IF ON
        THEN [KEYACTION 'PAD1 '((520 520) . IGNORE]
              [KEYACTION 'PAD2 '((521 521) . IGNORE]         (* (KEYACTION (QUOTE PAD4)
                                                           (QUOTE ((522 522) . IGNORE)))
                                                           (KEYACTION (QUOTE PAD5)
                                                           (QUOTE ((523 523) . IGNORE))))
              (KEYACTION 'PAD4 '(IGNORE . IGNORE))
              (KEYACTION 'PAD5 '(IGNORE . IGNORE))
      ELSE (KEYACTION 'PAD1 '(IGNORE . IGNORE))
            (KEYACTION 'PAD2 '(IGNORE . IGNORE))
            (KEYACTION 'PAD4 '(IGNORE . IGNORE))
            (KEYACTION 'PAD5 '(IGNORE . IGNORE])

(WHEELSCROLL
  [LAMBDA (DIRECTION DELTA)                             (* ; "Edited 21-Feb-2021 09:38 by rmk:")

    (* ;; "The wheel may accidentally turn (giving the interrupt) when the users intention is simply to push the middle button.  And there may be another accidental turn (also giving an interrupt) when the user is releasing the middle button.  We don't yet have a good solution to this problem.  (This is not an issue with a trackpad)")

    (* ;; "")

    (CL:WHEN (MOUSESTATE UP)                                 (* ; 
                                                           "Ignore interrupt if a button is down")
        [LET ((W (WHICHW)))

         (* Unsuccessful a ttempt to suppress scroll if middlebutton comes down within 
       the setetle time (NOT (UNTILMOUSESTATE (ONLY MIDDLE) WHEELSCROLLSETTLETIME)))

             (CL:WHEN W

                 (* ;; "We scroll only if the window has a scrollfn.  Our behavior is thus different from a direct call to SCROLLW, which defaults to SCROLLBYREPAINTFN in that case, but conforms to what happens with IN/SCROLL/BAR? and SCROLL.HANDLER in WINDOWSCROLL.  Menus and scrollbars typically do not have scrollfns, so this suppresses otherwise funky behavior. ")

                 (IF (WINDOWPROP W 'SCROLLFN)
                     THEN [PROCESS.EVAL (FIND.PROCESS 'MOUSE)
                                     (CL:IF (EQ DIRECTION 'VERTICAL)
                                         `(WHEELSCROLL.DOIT ,(KWOTE W)
                                                 0
                                                 ,DELTA)
                                         `(WHEELSCROLL.DOIT ,(KWOTE W)
                                                 ,DELTA 0))]
                   ELSEIF (EQ DIRECTION 'VERTICAL)
                     THEN 

                           (* ;; "We are in a pop-up scrollbar.  This moves the cursor there, the user has to click to scroll the main window.")

                           (CL:WHEN (WINDOWPROP W 'VERTICALSCROLLBARFOR)
                               (\CURSORPOSITION LASTMOUSEX (IPLUS LASTMOUSEY DELTA))
                               (GETMOUSESTATE))
                   ELSEIF (EQ DIRECTION 'HORIZONTAL)
                     THEN (CL:WHEN (WINDOWPROP W 'HORIZONTALSCROLLBARFOR)
                                  (\CURSORPOSITION (IPLUS DELTA LASTMOUSEX)
                                         LASTMOUSEY)
                                  (GETMOUSESTATE))))])])

(WHEELSCROLL.DOIT
  [LAMBDA (WINDOW DX DY)                                (* ; "Edited 20-Feb-2021 17:34 by rmk:")

    (* ;; "This does the actual wheel scrolling, runing in the mouse process.")

    (* ;; "There have been instances where the window gets garbled as the wheel moves.  The hypothesis is that this is because the wheel moves so fast that another scroll starts before a previous one completes.")

    (* ;; "The global variable \WHEELSCROLLINPROGRESS is set to prevent that interference.")

    (CL:UNLESS \WHEELSCROLLINPROGRESS
        (RESETVAR \WHEELSCROLLINPROGRESS T (SCROLLW WINDOW DX DY)))])

(INSTALL-WHEELSCROLL
  [LAMBDA NIL                                           (* ; "Edited 17-Feb-2021 11:53 by rmk:")
    (CL:WHEN (GETD 'LISPINTERRUPTS.WHEELSCROLL)
        (MOVD? 'LISPINTERRUPTS 'LISPINTERRUPTS.WSORIG)
        (MOVD 'LISPINTERRUPTS.WHEELSCROLL 'LISPINTERRUPTS))
    (FOR I IN WHEELSCROLLINTERRUPTS DO (INTERRUPTCHAR (CAR I)
                                                          (CADR I)
                                                          (CADDR I))
                                                  (CL:WHEN (BOUNDP 'TEDIT.READTABLE)

                                                (* ;; "These actions are invoked when the caret is in the Tedit window, because TEDIT disables the interrupts")

                                                      (TEDIT.SETFUNCTION (CAR I)
                                                             `[LAMBDA NIL
                                                                ,(CADR I]
                                                             TEDIT.READTABLE))])

(LISPINTERRUPTS.WHEELSCROLL
  [LAMBDA NIL                                           (* ; "Edited 17-Feb-2021 11:09 by rmk:")

    (* ;; "So wheelscroll interrupts will be installed in every process")

    (APPEND WHEELSCROLLINTERRUPTS (LISPINTERRUPTS.WSORIG])
)

(RPAQ? WHEELSCROLLINTERRUPTS
       '((520 (WHEELSCROLL 'VERTICAL WHEELSCROLLDELTA)
              T)
         (521 (WHEELSCROLL 'VERTICAL (IMINUS WHEELSCROLLDELTA))
              T)
         (522 (WHEELSCROLL 'HORIZONTAL (IMINUS WHEELSCROLLDELTA)
                     T))
         (523 (WHEELSCROLL 'HORIZONTAL WHEELSCROLLDELTA T))))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS WHEELSCROLLDELTA WHEELSCROLLSETTLETIME \WHEELSCROLLINPROGRESS)
)

(RPAQ? WHEELSCROLLDELTA 20)

(RPAQ? WHEELSCROLLSETTLETIME 50)

(RPAQ? \WHEELSCROLLINPROGRESS NIL)
(DECLARE%: DONTEVAL@LOAD DOCOPY 

(INSTALL-WHEELSCROLL)

(ENABLEWHEELSCROLL T)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1417 6885 (ENABLEWHEELSCROLL 1427 . 2365) (WHEELSCROLL 2367 . 4903) (WHEELSCROLL.DOIT 
4905 . 5541) (INSTALL-WHEELSCROLL 5543 . 6606) (LISPINTERRUPTS.WHEELSCROLL 6608 . 6883)))))
STOP
