(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "10-Jan-2025 11:19:15" {WMEDLEY}<lispusers>document.;18 18780  

      :EDIT-BY rmk

      :CHANGES-TO (FNS Document.Create)

      :PREVIOUS-DATE " 9-Jan-2025 17:00:59" {WMEDLEY}<lispusers>document.;16)


(PRETTYCOMPRINT DOCUMENTCOMS)

(RPAQQ DOCUMENTCOMS (

(* ;;; "This program creates documentation for any Lisp package in the style of the Lisp Users documentation.  It requires that the code for the package be loaded.  It does not completely format the document, but it minimizes the amount of work that the documenter must do.  To make most effective use of these functions, each function in the package being documented must have a comment as the first expression in the function after the timestamp.  Similarly, the COMS variable of the file should contain a comment (like this one) as the first item in the list.  This package was documented using itself.")

                     (FNS Document.Create Document.Begin Document.FileComments Document.Functions 
                          Document.Variables Document.Things Document.Finish Document.RunningHead 
                          Document.Title Document.Information Document.SectionHead Document.Format 
                          Document.FunctionCommentedP Document.Insert.Comment)))



(* ;;; 
"This program creates documentation for any Lisp package in the style of the Lisp Users documentation.  It requires that the code for the package be loaded.  It does not completely format the document, but it minimizes the amount of work that the documenter must do.  To make most effective use of these functions, each function in the package being documented must have a comment as the first expression in the function after the timestamp.  Similarly, the COMS variable of the file should contain a comment (like this one) as the first item in the list.  This package was documented using itself."
)

(DEFINEQ

(Document.Create
  [LAMBDA (FileName)                                         (* ; "Edited 10-Jan-2025 11:19 by rmk")
                                                             (* Newman "29-Aug-86 10:07")

(* ;;; "This function builds a document for a loaded file.  The document is in the style of the Lisp Library package documentation.  The function collects comments from the COMS variable of the file and from the functions in the file.  It also collects some information from the Interlisp-D file package.  This is the top-level function in the DOCUMENT package.")

    (if (MEMBER FileName FILELST)
        then (RESETLST
                 [LET ([Stream (OPENTEXTSTREAM NIL NIL '(HISTORY OFF FONT (MODERN 10 MRR]
                       Pointer)
                      (Document.Begin FileName Stream)
                      (SETQ Pointer (ADD1 (GETEOFPTR Stream)))
                      (Document.FileComments FileName Stream)
                      (Document.Functions FileName Stream)
                      (Document.Variables FileName Stream)
                      (Document.Things FileName Stream 'MACROS 'Macro)
                      (Document.Things FileName Stream 'RECORDS 'Record)
                      (Document.Finish Stream Pointer)
                      (TEDIT Stream NIL NIL '(HISTORY ON])
      else (ERROR FileName " not a loaded file."])

(Document.Begin
  [LAMBDA (FileName Stream)                                  (* Newman "29-Aug-86 10:23")

         (* * This function initializes the begining of the document stream.)

    (Document.RunningHead FileName Stream)
    (Document.Title FileName Stream (ADD1 (GETEOFPTR Stream)))
    (Document.Information FileName Stream (ADD1 (GETEOFPTR Stream])

(Document.FileComments
  [LAMBDA (FileName Stream)                                  (* ; "Edited  8-Jan-2025 23:49 by rmk")
                                                             (* Newman "29-Aug-86 10:17")

         (* * This function places all comments found in the COMS variable of FileName 
         into Stream. It is intended for use in documenting a Lisp code file.
         Ideally, these commends would be distributed through the documentation as they 
         are distributed through the COMS variable.)

    (Document.SectionHead Stream "INTRODUCTION")
    (PROMPTPRINT "Collecting File Comments ...")
    (for Comment in (FILECOMSLST FileName '*) do (Document.Insert.Comment Stream Comment])

(Document.Functions
  [LAMBDA (FileName Stream)                                  (* ; "Edited  8-Jan-2025 14:16 by rmk")
                                                             (* ; "Edited  6-Jan-2025 12:15 by rmk")
                                                             (* Newman "29-Aug-86 10:25")

         (* * This function documents the Functions on FileName into Stream.
         It does this by obtaining the function names from the file package, using the 
         ARGLIST function to obtain the argument list, and obtainin the initial comment in 
         the function if it exists.)

    (Document.SectionHead Stream "FUNCTIONS")
    (PROMPTPRINT "Collecting Function Comments ... ")
    (for Function in (FILEFNSLST FileName) do (TEDIT.INSERT Stream (CONCAT "(" Function " ")
                                                     (ADD1 (GETEOFPTR Stream))
                                                     (FONTCREATE 'MODERN 10 'MRR))
                                              (TEDIT.INSERT Stream (SUBSTRING (MKSTRING (SMARTARGLIST
                                                                                         Function T))
                                                                          2 -2)
                                                     (ADD1 (GETEOFPTR Stream))
                                                     (FONTCREATE 'MODERN 10 'ITALIC))
                                              (TEDIT.INSERT Stream (CONCAT ")	[Function]"
                                                                          (CHARACTER 13))
                                                     (ADD1 (GETEOFPTR Stream))
                                                     (FONTCREATE 'MODERN 10 'MRR)) 

         (* * This SETFILEPTR is here because someone leaves the FILEPTR in the wrong 
         place.)

                                              (SETFILEPTR Stream (GETEOFPTR Stream))
                                              (Document.Insert.Comment Stream (
                                                                          Document.FunctionCommentedP
                                                                               Function])

(Document.Variables
  [LAMBDA (FileName Stream)                                  (* ; "Edited  9-Jan-2025 11:49 by rmk")
                                                             (* Newman "29-Aug-86 10:28")

         (* This function is intended for use while documenting Lisp code.
         It collects a list of the variables from FileName, and places them in Stream in 
         the format of the Lisp Library Documentation.)

    (Document.SectionHead Stream "VARIABLES")
    (PROMPTPRINT "Collecting Variables ...")
    (DSPFONT '(MODERN 10 MRR)
           Stream)
    (for Variable in (FILECOMSLST FileName 'VARS)
       do (if (MEMBER Variable (FILECOMSLST FileName 'GLOBALVARS))
              then (printout Stream Variable %, " 	[GlobalVar]" T)
            elseif (MEMBER Variable (FILECOMSLST FileName 'SPECVARS))
              then (printout Stream Variable %, " 	[SpecialVar]" T)
            else (printout Stream Variable %, " 	[Variable]" T])

(Document.Things
  [LAMBDA (FileName Stream FilePkgType TypeName)             (* ; "Edited  9-Jan-2025 11:50 by rmk")
                                                             (* Newman "29-Aug-86 10:32")

         (* This function is intended for use while documenting Lisp code.
         It collects a list of items of type TypeName, that are written using the 
         FilePkgType file package command, from FileName, and places them in Stream in the 
         format of the Lisp Library Documentation.)

    (if (AND FilePkgType (MEMBER FilePkgType FILEPKGTYPES))
        then (if (FILECOMSLST FileName FilePkgType)
                 then (Document.SectionHead Stream (CONCAT (U-CASE (SETQ TypeName (OR TypeName 
                                                                                      FilePkgType)))
                                                          "S"))
                      (PROMPTPRINT (CONCAT "Collecting " TypeName " ..."))
                      (DSPFONT '(MODERN 10 MRR)
                             Stream)
                      (for Thing in (FILECOMSLST FileName FilePkgType)
                         do (printout Stream Thing %, (CONCAT (CHARACTER 9)
                                                             "[" TypeName "]")
                                   T)))
      else (ERROR FilePkgType "Bad file package type: "])

(Document.Finish
  [LAMBDA (Stream Pointer)                                   (* ; "Edited  9-Jan-2025 17:00 by rmk")
                                                             (* Newman "28-Aug-86 15:22")

(* ;;; "This function performs formatting and the like to make the document pretty, and to make the format correct as specified.")

(* ;;; "Set the selection to cover the non-header portions of the file.")

    (TEDIT.SETSEL Stream Pointer (DIFFERENCE (GETEOFPTR Stream)
                                        Pointer))

(* ;;; "Set the page, paragraph, and character looks of the non-header portions of the document.")

    (Document.Format Stream)

(* ;;; "TEdit the stream so the user can finish the job by hand.")

    (TEDIT.SETSEL Stream 1 0 'LEFT)
    (TEDIT Stream])

(Document.RunningHead
  [LAMBDA (FileName Stream)                                  (* ; "Edited  9-Jan-2025 16:58 by rmk")
                                                             (* Newman "28-Aug-86 15:57")

         (* * This function creates the running header for the document.)

    (TEDIT.INSERT Stream "Medley" 0 (FONTCREATE 'TIMESROMAN 36 'BOLD))
    (TEDIT.INSERT Stream (CONCAT " " (CHARACTER 9)
                                FileName
                                (CHARACTER 13)
                                (CHARACTER 13))
           NIL
           (FONTCREATE 'MODERN 10))
    (TEDIT.INSERT.OBJECT (HRULE.CREATE '2)
           Stream
           (GETEOFPTR Stream))
    (TEDIT.PARALOOKS (TEXTOBJ Stream)
           '(TYPE PAGEHEADING SUBTYPE RUNNINGHEAD RIGHTMARGIN 456 LEFTMARGIN 0 TABS
                  (NIL (456 . RIGHT))
                  QUAD JUSTIFIED)
           0
           (ADD1 (GETEOFPTR Stream)))

         (* * we set the file ptr to be kind to others, as TEDIT.PARALOOKS moves it from 
         the end of the file.)

    (SETFILEPTR Stream (GETFILEPTR Stream])

(Document.Title
  [LAMBDA (FileName Stream Pointer)                          (* Newman "29-Aug-86 11:35")

         (* * This function creates the title area of the document consisting of the name 
         of the package being documented in between two lines.)

    (TERPRI Stream)

         (* This TERPRI is here because otherwise the first HRULE in the title becomes a 
         part of the running header. This happens for no apparent reason, and is a mystery 
         to me.)

    (TEDIT.INSERT.OBJECT (HRULE.CREATE '(5 4 1))
           Stream
           (ADD1 (GETEOFPTR Stream)))
    (TEDIT.INSERT Stream (CONCAT (CHARACTER 13)
                                FileName
                                (CHARACTER 13)
                                (CHARACTER 13))
           (ADD1 (GETEOFPTR Stream))
           (FONTCREATE 'MODERN 12 'BRR))
    (TEDIT.INSERT.OBJECT (HRULE.CREATE '(1 4 5))
           Stream
           (GETEOFPTR Stream))
    (TEDIT.PARALOOKS (TEXTOBJ Stream)
           '(TYPE NIL PARALEADING 6 LINELEADING 4 RIGHTMARGIN 312 LEFTMARGIN 138 1STLEFTMARGIN 138 
                  QUAD CENTERED)
           Pointer
           (DIFFERENCE (ADD1 (GETEOFPTR Stream))
                  Pointer))

         (* * This call to SETFILEPTR is here because I believe TEDIT.PARALOOKS does not 
         leave the file pointer at the end of the stream as the printout function 
         requires. I believe that if I eliminate all calls to printout, I can eliminate 
         all calls to SETFILEPTR. I also believe that PRINTOUT can always be replaced by 
         TEDIT.INSERT.)

    (SETFILEPTR Stream (GETEOFPTR Stream])

(Document.Information
  [LAMBDA (FileName Stream Pointer)                          (* Newman "29-Aug-86 10:37")

         (* * This function creates the information at the top of the document, including 
         the form for the author's name and a list of other packages necessary to run this 
         package.)

    (TEDIT.INSERT Stream (CONCAT " By: >>Author's Name<< (>>Net Address<<)" (CHARACTER 13)
                                " "
                                (SUBSTRING (DATE)
                                       1 9)
                                (CHARACTER 13))
           (ADD1 (GETEOFPTR Stream))
           (FONTCREATE 'MODERN 10)
           T)
    (if (FILECOMSLST FileName 'FILES)
        then (TEDIT.INSERT Stream (CONCAT "The following packages are loaded by " FileName ": "
                                         (SUBSTRING (MKSTRING (FILECOMSLST FileName 'FILES))
                                                2 -2)
                                         (CHARACTER 13)
                                         " ")
                    (ADD1 (GETEOFPTR Stream))
                    (FONTCREATE 'MODERN 10)
                    T))
    (TEDIT.PARALOOKS (TEXTOBJ Stream)
           '(RIGHTMARGIN 456 LEFTMARGIN 0 1STLEFTMARGIN 0 QUAD CENTERED PARALEADING 17)
           Pointer
           (ADD1 (GETEOFPTR Stream)))

         (* The following SETFILEPTR exists because TEDIT.PARALOOKS does not leave the 
         file pointer at the end of the file.)

    (SETFILEPTR Stream (GETEOFPTR Stream])

(Document.SectionHead
  [LAMBDA (Stream String)                                    (* Newman "29-Aug-86 10:39")

         (* * This function is intended to create the bold section headings in Stream that 
         are needed for the Lisp Users document style.)

    (TEDIT.INSERT Stream (CONCAT String (CHARACTER 13))
           (ADD1 (GETEOFPTR Stream))
           (FONTCREATE 'MODERN 10 'BRR))
    (SETFILEPTR Stream (GETEOFPTR Stream))

         (* * The following TEDIT.CARETLOOKS prevents the bolding from continuing past the 
         section heading in some cases.)

    (TEDIT.CARETLOOKS Stream (FONTCREATE 'MODERN 10 'MRR])

(Document.Format
  [LAMBDA (Stream)                                           (* ; "Edited  9-Jan-2025 16:57 by rmk")
                                                             (* Newman "29-Aug-86 10:54")

         (* * This function formats the document. It sets the page layout, font looks, and 
         paragraph formatting for all selected text.)

    (TEDIT.SETSEL Stream Pointer (DIFFERENCE (GETEOFPTR Stream)
                                        Pointer))
    (TEDIT.PARALOOKS Stream '(TABS (NIL (0 . RIGHT)
                                        (456 . RIGHT))
                                   LINELEADING 4 PARALEADING 11 RIGHTMARGIN 456 LEFTMARGIN 0 
                                   1STLEFTMARGIN 0 QUAD JUSTIFIED)
           (TEDIT.GETSEL Stream))
    (TEDIT.PAGEFORMAT Stream (TEDIT.COMPOUND.PAGEFORMAT (TEDIT.SINGLE.PAGEFORMAT
                                                         T 26.5 3.0 '(MODERN 10)
                                                         'CENTERED 7.0 6.0 8.0 8.0 1 38.0 0
                                                         '((RUNNINGHEAD 7.0 62.0))
                                                         'PICAS
                                                         '(STARTINGPAGE# 1)
                                                         'LETTER)
                                    (TEDIT.SINGLE.PAGEFORMAT T 26.5 3.5 '(MODERN 10)
                                           'CENTERED 7.0 6.0 8.0 8.0 1 38.0 0
                                           '((RUNNINGHEAD 7.0 62.0))
                                           'PICAS
                                           '(STARTINGPAGE# 1)
                                           'LETTER)
                                    (TEDIT.SINGLE.PAGEFORMAT T 26.5 3.0 '(MODERN 10)
                                           'CENTERED 7.0 6.0 8.0 8.0 1 38.0 0
                                           '((RUNNINGHEAD 7.0 62.0))
                                           'PICAS
                                           '(STARTINGPAGE# 1)
                                           'LETTER])

(Document.FunctionCommentedP
  [LAMBDA (Function)                                         (* ; "Edited  8-Jan-2025 21:06 by rmk")
                                                             (* Newman "29-Aug-86 10:42")

    (* ;; "Hack for old format dates, like Newman here.")

    (for X in [CDDR (GETDEF Function NIL NIL '(NOERROR]
       unless [OR (EDITDATE? X)
                  (AND (EQ '* (CAR X))
                       (IDATE (CADDR X] do (RETURN (CL:IF (EQ '* (CAR X))
                                                          X])

(Document.Insert.Comment
  [LAMBDA (Stream Comment)                                   (* ; "Edited  9-Jan-2025 16:56 by rmk")
    (CL:WHEN (EQ '* (CAR (LISTP Comment)))
        (DSPFONT '(MODERN 10 MRR)
               Stream)
        [if (AND (MEMB (CADR Comment)
                       '(; ;; ;;;))
                 (STRINGP (CADDR Comment)))
            then (PRIN3 (CADDR Comment)
                        Stream)
          else [SETQ Comment (find TAIL on Comment suchthat (NEQ (CAR TAIL)
                                                                 '*]
               (for TAIL on Comment do (PRIN3 (CAR TAIL)
                                              Stream)
                                       (CL:WHEN (CDR TAIL)
                                              (PRIN3 " " Stream]
        (TERPRI Stream))])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1966 18757 (Document.Create 1976 . 3395) (Document.Begin 3397 . 3780) (
Document.FileComments 3782 . 4546) (Document.Functions 4548 . 6841) (Document.Variables 6843 . 7867) (
Document.Things 7869 . 9305) (Document.Finish 9307 . 10127) (Document.RunningHead 10129 . 11258) (
Document.Title 11260 . 12944) (Document.Information 12946 . 14512) (Document.SectionHead 14514 . 15170
) (Document.Format 15172 . 17303) (Document.FunctionCommentedP 17305 . 17883) (Document.Insert.Comment
 17885 . 18755)))))
STOP
