(DEFINE-FILE-INFO READTABLE "XCL" PACKAGE "INTERLISP")
(FILECREATED "15-Jun-90 11:42:23" |{DSK}<usr>local>lde>lispcore>internal>library>BIGBITMAPS.;2| 9850   

      |changes| |to:|  (VARS BIGBITMAPSCOMS)

      |previous| |date:| "27-Feb-89 11:47:33" 
|{DSK}<usr>local>lde>lispcore>internal>library>BIGBITMAPS.;1|)


; Copyright (c) 1989, 1990 by Venue & Xerox Corporation.  All rights reserved.

(PRETTYCOMPRINT BIGBITMAPSCOMS)

(RPAQQ BIGBITMAPSCOMS ((RECORDS BIGBM)
                           
                           (* |;;| "Big bitmaps provide light duty bitmap capabilities for applications which need large bitmaps (e.g. color).  Currently the only functions provided are BITBLT and BLTSHADE")

                           (FNS BIGBMCREATE BIGBMTEST BITBLT.BIGBM BLTSHADE.BIGBM)))
(DECLARE\: EVAL@COMPILE

(DATATYPE BIGBM (BIGBMWIDTH BIGBMHEIGHT BIGBMLIST))
)

(/DECLAREDATATYPE 'BIGBM '(POINTER POINTER POINTER)
       '((BIGBM 0 POINTER)
         (BIGBM 2 POINTER)
         (BIGBM 4 POINTER))
       '6)



(* |;;| 
"Big bitmaps provide light duty bitmap capabilities for applications which need large bitmaps (e.g. color).  Currently the only functions provided are BITBLT and BLTSHADE"
)

(DEFINEQ

(BIGBMCREATE
  (LAMBDA (WIDTH HEIGHT BITSPERPIXEL)                    (* \; "Edited 23-Feb-89 16:47 by shih")

    (* |;;| "Purely experimental for now")

    (LET (H HLEFT BM BIGBM)
         (SETQ H 16)                                         (* \; 
                              "slice should be a multiple of 16 so that textures tesselate nicely.")
         (SETQ HLEFT HEIGHT)
         (SETQ BIGBM (CREATE BIGBM))
         (FREPLACE (BIGBM BIGBMWIDTH) OF BIGBM WITH WIDTH)
         (FREPLACE (BIGBM BIGBMHEIGHT) OF BIGBM WITH HEIGHT)
         (FREPLACE (BIGBM BIGBMLIST) OF BIGBM WITH (WHILE (IGREATERP HLEFT 0)
                                                                  COLLECT (SETQ BM
                                                                               (BITMAPCREATE
                                                                                WIDTH
                                                                                (MIN H HLEFT)
                                                                                BITSPERPIXEL))
                                                                        (SETQ HLEFT (- HLEFT H))
                                                                        BM))
         BIGBM)))

(BIGBMTEST
  (LAMBDA (SWITCH ITERATIONS)                            (* \; "Edited 23-Feb-89 17:30 by shih")
    (LET NIL 

         (* |;;| "Initialization")

         (IF (NOT (WINDOWP SRCEW))
             THEN (PROMPTPRINT "Select a window for the source")
                   (SETQ SRCEW (WHICHW (GETPOSITION))))
         (IF (NOT (WINDOWP DESTW))
             THEN (PROMPTPRINT "Select a window for the source")
                   (SETQ DESTW (WHICHW (GETPOSITION))))

         (* |;;| "Real stuff below")

         (|if| (EQ SWITCH T)
             |then| (|for| I |from| 1 |to| ITERATIONS
                           |do| (CLEARW DESTW)
                                 (BITBLT SRCEW 50 50 DESTW 50 50 1000 600))
           |else| (BITBLT.BIGBM SRCEW 50 50 BIGBM 70 70 1000 600)
                 (|for| I |from| 1 |to| ITERATIONS
                    |do| (CLEARW DESTW)
                          (BITBLT.BIGBM BIGBM 70 70 DESTW 50 50 1000 600))))))

(BITBLT.BIGBM
  (LAMBDA (SRCE SRCELEFT SRCEBOTTOM DEST DESTLEFT DESTBOTTOM WIDTH HEIGHT SRCETYPE OPERATION TEXTURE
                CLIPPINGREGION)                          (* \; "Edited 23-Feb-89 17:21 by shih")

    (* |;;| "Just like bitblt, but do the right thing if dest is a big bitmap.  Sufficient?")

    (* |;;| "Clippingregion is handled incorrectly (at least in the Y direction).")

    (LET (BMLIST H SLITHEIGHT)
         (|if| (|type?| BIGBM SRCE)
             |then| (SETQ BMLIST (|fetch| (BIGBM BIGBMLIST) |of| SRCE))
                   (|for| BM |in| BMLIST
                      |do| 

                            (* |;;| 
                          "Run through bitmaps until we find the one that intersects sourcebottom")

                            (SETQ SLITHEIGHT (BITMAPHEIGHT BM)) 
                                                             (* \; 
                     "This could be faster (if known a bitmap, and if all slits are the same size.")
                            (|if| (NOT (AND (<= 0 SRCEBOTTOM)
                                                (<= SRCEBOTTOM SLITHEIGHT)))
                                |then| (SETQ SRCEBOTTOM (- SRCEBOTTOM SLITHEIGHT))
                              |else| 

                                    (* |;;| 
                              "Now handle normal bitmaps.  srcebottom is between 0 and sliceheight")

                                    (BITBLT.BIGBM BM SRCELEFT SRCEBOTTOM DEST DESTLEFT DESTBOTTOM
                                           WIDTH HEIGHT SRCETYPE OPERATION TEXTURE CLIPPINGREGION)
                                    (SETQ H (- SLITHEIGHT SRCEBOTTOM)) 
                                                             (* \; "Amount bltted")
                                    (SETQ HEIGHT (- HEIGHT H)) 
                                                             (* \; 
                                                           "Decrement height by amount bltted")
                                    (SETQ DESTBOTTOM (+ DESTBOTTOM H)) 
                                                             (* \; 
                                                           "Increment desty by amount bltted")
                                    (SETQ SRCEBOTTOM 0)))
           |elseif| (|type?| BIGBM DEST)
             |then| (SETQ BMLIST (|fetch| (BIGBM BIGBMLIST) |of| DEST))
                   (|for| BM |in| BMLIST
                      |do| 

                            (* |;;| 
                          "Run through bitmaps until we find the one that intersects destbottom")

                            (SETQ SLITHEIGHT (BITMAPHEIGHT BM)) 
                                                             (* \; 
                     "This could be faster (if known a bitmap, and if all slits are the same size.")
                            (|if| (NOT (AND (<= 0 DESTBOTTOM)
                                                (<= DESTBOTTOM SLITHEIGHT)))
                                |then| (SETQ DESTBOTTOM (- DESTBOTTOM SLITHEIGHT))
                              |else| (BITBLT.BIGBM SRCE SRCELEFT SRCEBOTTOM BM DESTLEFT 
                                                DESTBOTTOM WIDTH HEIGHT SRCETYPE OPERATION TEXTURE 
                                                CLIPPINGREGION)
                                    (SETQ H (- SLITHEIGHT DESTBOTTOM)) 
                                                             (* \; "Amount bltted")
                                    (SETQ HEIGHT (- HEIGHT H)) 
                                                             (* \; 
                                                           "Decrement height by amount bltted")
                                    (SETQ SRCEBOTTOM (+ SRCEBOTTOM H)) 
                                                             (* \; 
                                                           "Increment srcey by amount bltted")
                                    (SETQ DESTBOTTOM 0)))
           |else| 

                 (* |;;| "Try the default")

                 (BITBLT SRCE SRCELEFT SRCEBOTTOM DEST DESTLEFT DESTBOTTOM WIDTH HEIGHT SRCETYPE 
                        OPERATION TEXTURE CLIPPINGREGION)))))

(BLTSHADE.BIGBM
  (LAMBDA (TEXTURE DESTINATION DESTLEFT DESTBOTTOM WIDTH HEIGHT OPERATION CLIPPINGREGION)
                                                             (* \; "Edited 23-Feb-89 17:40 by shih")
    (LET (H SLITHEIGHT)

         (* |;;| "Clippingregion is handled incorrectly (at least in the Y direction).")

         (IF (NOT (|type?| BIGBM DESTINATION))
             THEN (BLTSHADE TEXTURE DESTINATION DESTLEFT DESTBOTTOM WIDTH HEIGHT OPERATION 
                             CLIPPINGREGION)
           ELSE (|for| BM |in| (|fetch| (BIGBM BIGBMLIST) |of| DESTINATION)
                       |do| (SETQ SLITHEIGHT (BITMAPHEIGHT BM)) 
                                                             (* \; 
                     "This could be faster (if known a bitmap, and if all slits are the same size.")
                             (|if| (NOT (AND (<= 0 DESTBOTTOM)
                                                 (<= DESTBOTTOM SLITHEIGHT)))
                                 |then| (SETQ DESTBOTTOM (- DESTBOTTOM SLITHEIGHT))
                               |else| (BLTSHADE.BIGBM TEXTURE BM DESTLEFT DESTBOTTOM WIDTH 
                                                 HEIGHT OPERATION CLIPPINGREGION)
                                     (SETQ H (- SLITHEIGHT DESTBOTTOM)) 
                                                             (* \; "Amount bltted")
                                     (SETQ HEIGHT (- HEIGHT H)) 
                                                             (* \; 
                                                           "Decrement height by amount bltted")
                                     (SETQ DESTBOTTOM 0)))))))
)
(PUTPROPS BIGBITMAPS COPYRIGHT ("Venue & Xerox Corporation" 1989 1990))
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (1228 9755 (BIGBMCREATE 1238 . 2568) (BIGBMTEST 2570 . 3609) (BITBLT.BIGBM 3611 . 8000) 
(BLTSHADE.BIGBM 8002 . 9753)))))
STOP
