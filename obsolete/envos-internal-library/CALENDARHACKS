(DEFINE-FILE-INFO READTABLE "XCL" PACKAGE "INTERLISP")
(FILECREATED "15-Jun-90 11:46:01" |{DSK}<usr>local>lde>lispcore>internal>library>CALENDARHACKS.;2| 10979  

      |changes| |to:|  (VARS CALENDARHACKSCOMS)

      |previous| |date:| "20-Oct-87 17:27:11" 
|{DSK}<usr>local>lde>lispcore>internal>library>CALENDARHACKS.;1|)


; Copyright (c) 1987, 1990 by Venue & Xerox Corporation.  All rights reserved.

(PRETTYCOMPRINT CALENDARHACKSCOMS)

(RPAQQ CALENDARHACKSCOMS
       (
        (* |;;| "Hacks for making reminder-book pages for calendars.")

        (FILES CALENDAR)
        (COMS 
              (* |;;| "User level functions")

              (FNS PRINT-LAND-MONTH PRINT-LAND-YEAR PRINT-NOTEBOOK-MONTH PRINT-NOTEBOOK-YEAR 
                   PRINT-SUMMARY-YEAR PRINT-NARROW-MONTH))
        (COMS 
              (* |;;| "Internal functions and macros")

              (FNS PRINT-SCALED-MONTH PRINTMONTHIMAGE)
              (FUNCTIONS CAL-X CAL-Y))))



(* |;;| "Hacks for making reminder-book pages for calendars.")


(FILESLOAD CALENDAR)



(* |;;| "User level functions")

(DEFINEQ

(PRINT-LAND-MONTH
  (LAMBDA (MONTH YEAR STREAM)                                (* \; "Edited 17-Oct-87 17:45 by jds")
          
          (* |;;| "Print a single month's calendar landscape on letter paper.")

    (LET ((PRINTSTREAM (OR STREAM (OPENIMAGESTREAM "{LPT}" 'INTERPRESS '(LANDSCAPE T)))))
         (PRINT-SCALED-MONTH MONTH YEAR 635 635 1.0 1.0 PRINTSTREAM 12 18 6)
         (CLOSEF PRINTSTREAM))))

(PRINT-LAND-YEAR
  (LAMBDA (YEAR STREAM)                                      (* \; "Edited 17-Oct-87 17:49 by jds")
          
          (* |;;| "Print a single month's calendar landscape on letter paper.")

    (LET ((PRINTSTREAM (OR STREAM (OPENIMAGESTREAM "{LPT}" 'INTERPRESS '(LANDSCAPE T)))))
         (|for| MONTH |from| 1 |to| 12
            |do| (PRINT-SCALED-MONTH MONTH YEAR 635 635 1.0 1.0 PRINTSTREAM 12 18 6)
                 (DSPNEWPAGE PRINTSTREAM))
         (CLOSEF PRINTSTREAM))))

(PRINT-NOTEBOOK-MONTH
  (LAMBDA (MONTH YEAR STREAM)                                (* \; "Edited 17-Sep-87 21:55 by jds")
          
          (* |;;| "Print a single month's calendar on a half-sheet, suitable for punching and putting in a Time-Design notebook or a 5 1/2 x 8 1/2\" reminder book.")
          
          (* |;;| "If you leave STREAM NIL, you'll get one page on the printer.")

    (PRINT-SCALED-MONTH MONTH YEAR 0 0 0.75 0.6 STREAM)))

(PRINT-NOTEBOOK-YEAR
  (LAMBDA (YEAR STREAM)                                      (* \; "Edited 17-Sep-87 21:57 by jds")
          
          (* |;;| "Print a year's worth of month-calendar pages in half-sheet size.")

    (LET ((PRINTSTREAM (OR STREAM (OPENIMAGESTREAM "{LPT}" 'INTERPRESS))))
         (|for| MONTH |from| 1 |to| 12 |do| (PRINT-SCALED-MONTH MONTH YEAR 0 (COND
                                                                                ((EVENP MONTH 2)
                                                                                 13970)
                                                                                (T 0))
                                                   0.75 0.6 PRINTSTREAM)
                                            (COND
                                               ((EVENP MONTH 2)
                                                (DSPNEWPAGE PRINTSTREAM))))
         (CLOSEF PRINTSTREAM))))

(PRINT-SUMMARY-YEAR
  (LAMBDA (YEAR STREAM)                                      (* \; "Edited 17-Sep-87 22:25 by jds")
          
          (* |;;| "Print a year's worth of small months on 1 sheet of paper that will fit into a 8.25 x 10.5 format (for Time-Design books).")

    (LET ((PRINTSTREAM (OR STREAM (OPENIMAGESTREAM "{LPT}" 'INTERPRESS '(LANDSCAPE T)))))
         (|for| MONTH |from| 1 |to| 4 |as| YOFFSET |from| 15716 |by| -5241
            |do| (PRINT-SCALED-MONTH MONTH YEAR 80 YOFFSET 0.33 0.23 PRINTSTREAM 6 8 6))
         (|for| MONTH |from| 5 |to| 8 |as| YOFFSET |from| 15716 |by| -5241
            |do| (PRINT-SCALED-MONTH MONTH YEAR 8970 YOFFSET 0.33 0.23 PRINTSTREAM 6 8 6))
         (|for| MONTH |from| 9 |to| 12 |as| YOFFSET |from| 15716 |by| -5241
            |do| (PRINT-SCALED-MONTH MONTH YEAR 17860 YOFFSET 0.33 0.23 PRINTSTREAM 6 8 6))
         (CLOSEF PRINTSTREAM))))

(PRINT-NARROW-MONTH
  (LAMBDA (MONTH YEAR STREAM)                                (* \; "Edited 17-Sep-87 22:32 by jds")

    (PRINT-SCALED-MONTH MONTH YEAR 0 0 0.45 0.95 STREAM NIL NIL NIL '(LANDSCAPE T))))
)



(* |;;| "Internal functions and macros")

(DEFINEQ

(PRINT-SCALED-MONTH
  (LAMBDA (MONTH YEAR X-OFFSET Y-OFFSET X-SCALE Y-SCALE STREAM DAYSIZE DATESIZE TINYSIZE OPTIONS)
                                                             (* \; "Edited 17-Sep-87 22:08 by jds")
          
          (* |;;| 
        "Print a month's calendar on STREAM. MONTH is a number 1-12, Year is the year, 19-- and all.")

    (PROG ((STREAM-EXISTED STREAM)
           PBIGFONT PCALFONT PLITTLEFONT)
          (SETCURSOR WAITINGCURSOR)
          (PRINTOUT PROMPTWINDOW T "Formatting for print...")
          (SETQ STREAM (OR STREAM (OPENIMAGESTREAM "{LPT}" 'INTERPRESS OPTIONS)))
          (SETQ PBIGFONT (FONTCREATE 'MODERN (OR DAYSIZE 8)
                                NIL 0 'INTERPRESS))
          (SETQ PCALFONT (FONTCREATE 'CLASSIC (OR DATESIZE 12)
                                NIL 0 'INTERPRESS))
          (SETQ PLITTLEFONT (FONTCREATE 'MODERN (OR TINYSIZE 6)
                                   NIL 0 'INTERPRESS))
          (PRINTMONTHIMAGE MONTH YEAR STREAM X-OFFSET Y-OFFSET X-SCALE (OR Y-SCALE X-SCALE)
                 PBIGFONT PCALFONT PLITTLEFONT)              (* \; "Print horizontal lines")

          (OR STREAM-EXISTED (CLOSEF STREAM))
          (PRINTOUT PROMPTWINDOW "done." T)
          (CURSOR T))))

(PRINTMONTHIMAGE
  (LAMBDA (MONTH YEAR STREAM XOFFSET YOFFSET X-SCALE Y-SCALE DAYFONT DATEFONT TINYDATEFONT)
                                                             (* \; "Edited 17-Sep-87 22:18 by jds")
          
          (* |;;| 
        "Print a month's calendar on STREAM. MONTH is a number 1-12, Year is the year, 19-- and all.")
          
          (* |;;| 
          " X-SCALE & XOFFSET,  and Y-SCALE & YOFFSET are used in the CAL-X and CAL-Y macros, resp.")
          
          (* |;;| 
          "DAYFONT and DATEFONT are used for printing the day names and dates/month title resp.")

    (DSPRESET STREAM)
    (DSPRIGHTMARGIN 65535 STREAM)
    (LET ((TITLESTRING (CONCAT (MONTHNAME MONTH)
                              "   " YEAR)))
         (MOVETO (- (CAL-X 13250)
                    (IQUOTIENT (STRINGWIDTH TITLESTRING DATEFONT)
                           2))
                (CAL-Y 20400)
                STREAM))
    (DSPFONT DATEFONT STREAM)
    (PRINTOUT STREAM (MONTHNAME MONTH)
           "   " YEAR)
    (LET ((DAYLABELS (APPEND (|for| N |from| 1 |to| (DAYOF MONTH 1 YEAR) |collect| '\ )
                            (|for| N |from| 1 |to| (DAYSIN MONTH YEAR) |collect| N)))
          (X 550)
          (Y 16700)
          (CT 0))
         (|for| I |in| DAYLABELS |do| 
          
          (* |;;| "Print blanks up to the first day of the month (to allow for not starting on Sunday), then print the dates.")

                                      (MOVETO (CAL-X X)
                                             (CAL-Y Y)
                                             STREAM)
                                      (PRIN1 I STREAM)
                                      (|add| X 3750)
                                      (|add| CT 1)
                                      (COND
                                         ((EQ (IREMAINDER CT 7)
                                              0)
                                          (SETQ X 600)
                                          (|add| Y -3166)))))
    (|for| X |from| 300 |to| 26800 |by| 3750 |do| 
          
          (* |;;| "Print vertical lines")

                                                  (DRAWLINE (CAL-X X)
                                                         (CAL-Y 600)
                                                         (CAL-X X)
                                                         (CAL-Y 19600)
                                                         40
                                                         'PAINT STREAM))
    (|for| Y |from| 600 |to| 19600 |by| 3166 |do| 
          
          (* |;;| "Print horizontal lines")

                                                  (DRAWLINE (CAL-X 300)
                                                         (CAL-Y Y)
                                                         (CAL-X 26550)
                                                         (CAL-Y Y)
                                                         40
                                                         'PAINT STREAM))
    (DSPFONT DAYFONT STREAM)
    (|for| X |from| 800 |to| 25600 |by| 3750 |as| D |from| 0 |to| 6 |do| 
          
          (* |;;| "Print day names")

                                                                         (MOVETO (CAL-X X)
                                                                                (CAL-Y 19800)
                                                                                STREAM)
                                                                         (PRIN1 (DAYNAME D)
                                                                                STREAM))
    (COND
       ((>= X-SCALE 0.7)
        (DSPFONT PLITTLEFONT STREAM)
        (SHOWMONTHSMALL (MONTHPLUS MONTH -1)
               (MONTHYEARPLUS MONTH YEAR -1)
               (CAL-X 19300)
               (CAL-Y 950)
               (FTIMES X-SCALE 28.0)
               STREAM)
        (SHOWMONTHSMALL (MONTHPLUS MONTH 1)
               (MONTHYEARPLUS MONTH YEAR 1)
               (CAL-X 23100)
               (CAL-Y 950)
               (FTIMES X-SCALE 28.0)
               STREAM)))                                     (* (SHOWMOON MONTH YEAR 32.0 STREAM)
                                                             (SHOWREMSINMONTH MONTH YEAR 31.0 
                                                             STREAM))
    STREAM))
)

(DEFMACRO CAL-X (VALUE)
   `(+ XOFFSET (FIXR (FTIMES ,VALUE X-SCALE))))

(DEFMACRO CAL-Y (VALUE)
   `(+ YOFFSET (FIXR (FTIMES ,VALUE Y-SCALE))))
(PUTPROPS CALENDARHACKS COPYRIGHT ("Venue & Xerox Corporation" 1987 1990))
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (1113 4781 (PRINT-LAND-MONTH 1123 . 1552) (PRINT-LAND-YEAR 1554 . 2088) (
PRINT-NOTEBOOK-MONTH 2090 . 2564) (PRINT-NOTEBOOK-YEAR 2566 . 3546) (PRINT-SUMMARY-YEAR 3548 . 4555) (
PRINT-NARROW-MONTH 4557 . 4779)) (4831 10727 (PRINT-SCALED-MONTH 4841 . 6123) (PRINTMONTHIMAGE 6125 . 
10725)))))
STOP
