(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "18-Feb-91 12:10:05" {DSK}<users>gadener>medley>work>PS>UNIXPRINT.;4 8632   

      changes to%:  (VARS UNIXPRINTCOMS)
                    (FNS InstallUnixPrinter UnixPrint UnixShellQuote UnixTempFile)

      previous date%: "14-Feb-91 18:10:52" {DSK}<users>gadener>medley>work>PS>UNIXPRINT.;2)


(* ; "
Copyright (c) 1990, 1991 by Venue.  All rights reserved.
")

(PRETTYCOMPRINT UNIXPRINTCOMS)

(RPAQQ UNIXPRINTCOMS
       [(FNS InstallUnixPrinter UnixPrint UnixShellQuote UnixTempFile)
        (FUNCTIONS ShellCommand)
        (INITVARS (UnixPrinterName NIL))
        (P (InstallUnixPrinter))
        (PROP FILETYPE UNIXPRINT)
        (DECLARE%: EVAL@COMPILE DOCOPY (FILES UNIXCOMM))
        (DECLARE%: EVAL@COMPILE DONTCOPY (GLOBALVARS UnixPrinterName))
        (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS (ADDVARS (NLAMA)
                                                                             (NLAML)
                                                                             (LAMA])
(DEFINEQ

(InstallUnixPrinter
  [LAMBDA (PrinterTypes)                               (* ; "Edited 14-Feb-91 15:45 by gadener")

    (* ;; "Set up any printers in PrinterTypes (or just Postscript by default) so that they'll be printed using the unix LPR command.")

    (DECLARE (GLOBAL PRINTERTYPES))
    (for type inside (OR PrinterTypes '(POSTSCRIPT))
       do (for x in PRINTERTYPES when (EQMEMB type (CAR x))
                 do (LET ((PRINTERTYPE type))
                             (PUTASSOC 'SEND (LIST 'UnixPrint)
                                    (CDR x])

(UnixPrint
  [LAMBDA (HOST FILE PRINTOPTIONS)                     (* ; "Edited 14-Feb-91 18:03 by gadener")

    (* ;; "Given a print FILE, use the Unix %"lpr%" command to spool it to a printer.")

    (* ;; "The printer is named by HOST or UnixPrinterName, a Global variable.")

    [LET* ((PRINTER (OR UnixPrinterName HOST))
           (COPIES (LISTGET PRINTOPTIONS '%#COPIES))
           (NAME (LISTGET PRINTOPTIONS 'DOCUMENT.NAME))
           (TYPE (PRINTERTYPE PRINTER)))
          [COND
             ((NULL TYPE)
              (ERROR (CONCAT "Printertype unknown for " PRINTER)))
             ((NOT (EQL (U-CASE TYPE)
                        'POSTSCRIPT))
              (ERROR (CONCAT "Printertype  for " PRINTER " is not Postscript"]
          [COND
             ((OR (NULL NAME)
                  (STRPOS "{LPT}" NAME 1 NIL T))
              (SETQ NAME "Medley Output"))
             ((EQ (CHCON1 NAME)
                  (CHARCODE {))
              (SETQ NAME (UNPACKFILENAME.STRING NAME 'NAME))
              (COND
                 ((EQ (NCHARS NAME)
                      0)
                  (SETQ NAME "Medley Output"]
          (CL:MULTIPLE-VALUE-BIND (tmpstream tmpname)
                 (UnixTempFile NIL NIL)
                 (COND
                    (tmpstream 

                           (* ;; "First, copy the lisp file to /tmp so lpr can find it.")

                           (CL:WITH-OPEN-STREAM (out tmpstream)
                                  (CL:WITH-OPEN-STREAM (in (OPENSTREAM FILE 'INPUT))
                                         (printout PROMPTWINDOW .TAB0 0 
                                                "Spooling output to Unix printer"
                                                (COND
                                                   (PRINTER (CONCAT " '" PRINTER "'"))
                                                   (T ""))
                                                "...")
                                         (COPYCHARS in out)))

                           (* ;; "Now make Unix print the /tmp file.")

                           (ShellCommand (CONCAT "/usr/ucb/lpr " (COND
                                                                        (PRINTER (CONCAT "-P"
                                                                                        (
                                                                                     UnixShellQuote
                                                                                         PRINTER)
                                                                                        " "))
                                                                        (T ""))
                                                    (COND
                                                       ((AND (FIXP COPIES)
                                                             (NEQ COPIES 1))
                                                        (CONCAT "-#" COPIES " "))
                                                       (T ""))
                                                    " -J"
                                                    (UnixShellQuote NAME)
                                                    " -r -s " tmpname)
                                  PROMPTWINDOW)
                           (printout PROMPTWINDOW "done" T))
                    (T (ERROR "Couldn't create unix temp file"]
    T])

(UnixShellQuote
  [LAMBDA (STRING)
    (DECLARE (LOCALVARS . T))                        (* ; "Edited 19-Apr-89 21:14 by TAL")
    (LET* ((X (CHCON STRING))
           (CT X)
           C FLG)
          [while (LISTP CT) do (SETQ C (CAR CT))
                                      (COND
                                         ([OR (<= (CHARCODE a)
                                                  C
                                                  (CHARCODE z))
                                              (<= (CHARCODE A)
                                                  C
                                                  (CHARCODE Z))
                                              (<= (CHARCODE 0)
                                                  C
                                                  (CHARCODE 9))
                                              (FMEMB C (CHARCODE (- /]
                                          (SETQ CT (CDR CT)))
                                         (T (SETQ FLG T)
                                            (RPLNODE CT (CHARCODE \)
                                                   (CONS (COND
                                                            ((FMEMB C (CHARCODE (CR LF)))
                                                             (CHARCODE SPACE))
                                                            (T C))
                                                         (SETQ CT (CDR CT]
          (COND
             (FLG (CONCATCODES X))
             (T STRING])

(UnixTempFile
  [LAMBDA (Prefix DontOpen)                              (* ; "Edited 12-Jan-89 19:07 by TAL")
    (LET* ([host (AND (BOUNDP 'FISTempDir)
                      (UNPACKFILENAME.STRING FISTempDir 'HOST]
           (dir (OR [COND
                       ((OR (STRING-EQUAL host "UNIX")
                            (STRING-EQUAL host "DSK"))
                        (UNPACKFILENAME.STRING FISTempDir 'DIRECTORY]
                    "tmp"))
           (str (CONCAT (OR Prefix "")
                       (IDATE)))
           file unix)
          (COND
             ([for i from 1 to 100
                 thereis (NOT (INFILEP (SETQ file (CONCAT "{UNIX}"
                                                             (SETQ unix
                                                              (CONCAT "/" dir "/" str i]
              (CL:VALUES [COND
                            (DontOpen file)
                            (T (OPENSTREAM file 'OUTPUT]
                     unix])
)

(CL:DEFUN ShellCommand (Cmd &OPTIONAL (Output T))
   (CL:WITH-OPEN-STREAM (s (CREATE-PROCESS-STREAM Cmd))
          (CL:TAGBODY [SETFILEINFO s 'ENDOFSTREAMOP #'(CL:LAMBDA (s)
                                                             (GO OUT]
                 (CL:LOOP (PRINTCCODE (READCCODE s)
                                 Output))
                 OUT))
   NIL)

(RPAQ? UnixPrinterName NIL)

(InstallUnixPrinter)

(PUTPROPS UNIXPRINT FILETYPE :COMPILE-FILE)
(DECLARE%: EVAL@COMPILE DOCOPY 

(FILESLOAD UNIXCOMM)
)
(DECLARE%: EVAL@COMPILE DONTCOPY 
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS UnixPrinterName)
)
)
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 

(ADDTOVAR NLAMA )

(ADDTOVAR NLAML )

(ADDTOVAR LAMA )
)
(PUTPROPS UNIXPRINT COPYRIGHT ("Venue" 1990 1991))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1110 7779 (InstallUnixPrinter 1120 . 1725) (UnixPrint 1727 . 5210) (UnixShellQuote 5212
 . 6766) (UnixTempFile 6768 . 7777)))))
STOP
