(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "10-May-88 00:46:44" {ERINYES}<LISPUSERS>MEDLEY>IRISNET.;1 15482  

      previous date%: " 4-Feb-87 19:47:55" {ERINYES}<LISPUSERS>LYRIC>IRISNET.;1)


(* "
Copyright (c) 1988 by Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT IRISNETCOMS)

(RPAQQ IRISNETCOMS ([DECLARE%: EVAL@LOAD DONTCOPY (P (LOADCOMP 'LLETHER)
                                                         (LOADCOMP 'ETHERRECORDS]
                        (FNS IRIS.RESET IRIS.TRACE IRISBOOTPROCESS SENDIRISPACKET IRISFILTER 
                             OPEN.IRISCONN IRISBOOTSERVER)
                        (VARS \IRIS.VERBOSE (PRINTSPPDATAFLG T))
                        (VARS (IRIS.LOCK (CREATE.MONITORLOCK "iris boot lock")))
                        (GLOBALVARS IRISNSHOSTNUMBER)
                        [INITVARS (IRISNET 146)
                               (IRISBOOTDIRECTORIES '({CORE} {ERIS}<IRIS>gl2>boot>]
                        (CONSTANTS (IRISSOCKET 37)
                               (IRIS.PACKETTYPE 32790)
                               (IRIS.BOOT.STREAM.NAME '|IRIS boot SPP|))
                        [DECLARE%: EVAL@LOAD DONTCOPY (FILES ETHERRECORDS)
                               (P (LOADCOMP 'LLETHER]
                        [P (ACCESSFNS IRISENCAPSULATION [(IRISBASE (LOCF (FETCH (ETHERPACKET 
                                                                                      EPENCAPSULATION
                                                                                       )
                                                                                OF DATUM]
                                  [BLOCKRECORD IRISBASE ((IRISLENGTH WORD)
                                                         (IRISDESTHOSTO 3 WORD)
                                                         (IRISSOURCEHOSTO 3 WORD)
                                                         (IRISTYPE WORD)
                                                         (IRISEXCHID WORD)
                                                         (INFOCHAR1 BYTE)
                                                         (INFOCHAR2 BYTE))
                                         [ACCESSFNS IRISDESTHOSTO ((IRISDESTHOST (\LOADNSHOSTNUMBER
                                                                                  (LOCF DATUM))
                                                                          (\STORENSHOSTNUMBER
                                                                           (LOCF DATUM)
                                                                           NEWVALUE))
                                                                   (IRISPACKETBASE (LOCF DATUM))
                                                                   (IRISDESTHOSTBASE (LOCF DATUM]
                                         (ACCESSFNS IRISSOURCEHOSTO ((IRISSOURCEHOST
                                                                      (\LOADNSHOSTNUMBER (LOCF DATUM)
                                                                             )
                                                                      (\STORENSHOSTNUMBER
                                                                       (LOCF DATUM)
                                                                       NEWVALUE))
                                                                     (IRISSOURCEHOSTBASE (LOCF DATUM]
                                  (TYPE? (type? ETHERPACKET DATUM]
                        (MACROS BROADCASTP)))
(DECLARE%: EVAL@LOAD DONTCOPY 
(LOADCOMP 'LLETHER)
(LOADCOMP 'ETHERRECORDS)
)
(DEFINEQ

(IRIS.RESET
  [LAMBDA NIL                                            (* gbn "24-Jun-85 01:31")
    (PROG (PROC)
          (CLOSEF? '{DSK}IRISBOOTFILE)
          (if (SETQ PROC (FIND.PROCESS IRIS.BOOT.STREAM.NAME))
              then (DEL.PROCESS PROC))
          (if (SETQ PROC (FIND.PROCESS '|Iris Terminal SPP|))
              then (DEL.PROCESS PROC))
          (if (SETQ PROC (FIND.PROCESS 'IRISBOOTPROCESS))
              then (DEL.PROCESS PROC))
          (PROCESS.STATUS.WINDOW (CREATEPOSITION 5 5])

(IRIS.TRACE
  [LAMBDA NIL                                            (* gbn "25-Feb-86 12:24")
    (SETQ PRINTSPPDATAFLG NIL)
    (SETQ XIPIGNORETYPES '(1 TRANS))
    (XIPTRACE T])

(IRISBOOTPROCESS
  [LAMBDA (IRISPACKET)                                   (* gbn "12-Nov-85 23:16")
    (DECLARE (GLOBALVARS IRISBOOTDIRECTORIES IRISNSADDRESS IRIS.LOCK IRISNET IRISNSHOSTNUMBER))
    (COND
       ((OBTAIN.MONITORLOCK IRIS.LOCK T T)
        [PROG (DH CHAR NET IRISBOOTFILE INBOOTSTREAM OUTBOOTSTREAM IRISBOOTFILENAME BOOTFILENAME TEMP
                  )
              (SETQ DH (fetch (IRISENCAPSULATION IRISSOURCEHOST) of IRISPACKET))
              (SETQ CHAR (fetch (IRISENCAPSULATION INFOCHAR1) of IRISPACKET))
              (replace (IRISENCAPSULATION IRISDESTHOST) of IRISPACKET
                 with (SETQ IRISNSHOSTNUMBER (fetch (IRISENCAPSULATION IRISSOURCEHOST)
                                                    of IRISPACKET)))
              (replace (IRISENCAPSULATION IRISSOURCEHOST) of IRISPACKET with 
                                                                                     \MY.NSHOSTNUMBER
                     )
              [COND
                 (\IRIS.VERBOSE 
          (* inform the user that a boot attempt is being made)
                        (FLASHWINDOW PROMPTWINDOW)
                        (PROMPTPRINT (CONCAT "IRIS boot initiated to: " DH]
          (* "E" for "reply" to booting and "H" for "hostname" for spp connection)
              (SETQ NET (fetch (XIP XIPSOURCENET) of IRISPACKET))
              (SETQ IRISNSADDRESS (create NSADDRESS
                                         NSNET _ IRISNET
                                         NSHOSTNUMBER _ IRISNSHOSTNUMBER))
          (* this should be using the net from the iris, but it can't be trusted.
        SGI doesn't handle turning around packets properly)
              (COND
                 ((EQ CHAR (CHARCODE C))                     (* replace EPSOCKET of IRISPACKET 
                                                           with 41)
                  (replace (IRISENCAPSULATION INFOCHAR1) of IRISPACKET with (CHARCODE
                                                                                         E))
                  (SENDIRISPACKET IRISPACKET)
          (* just sends back an "E" packet with our host socket
        (41) filled in)
                  (SETQ INBOOTSTREAM (SPP.OPEN NIL 41 NIL IRIS.BOOT.STREAM.NAME))
                  (SETQ OUTBOOTSTREAM (SPPOUTPUTSTREAM INBOOTSTREAM))
                  (SPP.DSTYPE INBOOTSTREAM 108)              (* returns a connection which is not 
                                                           yet established)
                  (BIN INBOOTSTREAM)
                  [SETQ TEMP (CONCAT (PACKC (while (SPP.READP INBOOTSTREAM)
                                               collect (BIN INBOOTSTREAM]
                  [SETQ BOOTFILENAME (L-CASE (SUBSTRING TEMP (STRPOS ":*:" TEMP 1 NIL NIL T)
                                                    (SUB1 (STRPOS (CONCAT (CHARACTER 0))
                                                                 TEMP]
                  [COND
                     ((STREQUAL BOOTFILENAME (CONSTANT "defaultboot"))
                      (SETQ BOOTFILENAME (CONSTANT "iris"]   (* This is a packet specifying the 
                                                           boot file name,)
                  [SETQ IRISBOOTFILE (OPENSTREAM (SETQ IRISBOOTFILENAME (FINDFILE BOOTFILENAME NIL 
                                                                               IRISBOOTDIRECTORIES))
                                            'INPUT NIL '((TYPE BINARY]
                  [COND
                     (\IRIS.VERBOSE 
          (* inform the user that a boot attempt is being made)
                            (PROMPTPRINT (CONCAT "Booting IRIS from: " IRISBOOTFILENAME]
                  (COPYBYTES IRISBOOTFILE OUTBOOTSTREAM NIL NIL)
                  (FORCEOUTPUT OUTBOOTSTREAM)
                  (CLOSEF IRISBOOTFILE)
                  (PROMPTPRINT "IRIS boot server complete, closing boot file")
                  (CLOSEF INBOOTSTREAM))
                 ((EQ CHAR (CHARCODE A))                     (* replace EPSOCKET of IRISPACKET 
                                                           with 41)
                  (replace (IRISENCAPSULATION INFOCHAR1) of IRISPACKET with (CHARCODE
                                                                                         E))
                  (SENDIRISPACKET IRISPACKET)
          (* just sends back an "E" packet with our host socket
        (41) filled in)
                  (SETQ INBOOTSTREAM (SPP.OPEN NIL 41 NIL IRIS.BOOT.STREAM.NAME))
                  (SETQ OUTBOOTSTREAM (SPPOUTPUTSTREAM INBOOTSTREAM))
                  (SPP.DSTYPE INBOOTSTREAM 108)              (* returns a connection which is not 
                                                           yet established)
                  (BIN INBOOTSTREAM)
                  (while (SPP.READP INBOOTSTREAM) collect (BIN INBOOTSTREAM))
                  (SETQ BOOTFILENAME (CONSTANT "iris"))      (* This is a packet specifying the 
                                                           boot file name,)
                  [SETQ IRISBOOTFILE (OPENSTREAM (SETQ IRISBOOTFILENAME (FINDFILE BOOTFILENAME NIL 
                                                                               IRISBOOTDIRECTORIES))
                                            'INPUT NIL '((TYPE BINARY]
                  [COND
                     (\IRIS.VERBOSE 
          (* inform the user that a boot attempt is being made)
                            (PROMPTPRINT (CONCAT "Booting IRIS from: " IRISBOOTFILENAME]
                  (COPYBYTES IRISBOOTFILE OUTBOOTSTREAM NIL NIL)
                  (FORCEOUTPUT OUTBOOTSTREAM)
                  (CLOSEF IRISBOOTFILE)
                  (PROMPTPRINT "IRIS boot server complete, closing boot file")
                  (CLOSEF INBOOTSTREAM))
                 ((EQ CHAR (CHARCODE H))                     (* serv-hostname in SGIspeak)
                  (PRINT "workstation server connection")    (* replace EPSOCKET of IRISPACKET 
                                                           with 41)
                  (replace (IRISENCAPSULATION INFOCHAR1) of IRISPACKET with (CHARCODE
                                                                                         H))
                  (SETQ INBOOTSTREAM (SPP.OPEN NIL 37))
                  (SENDIRISPACKET IRISPACKET))
                 (T (PRINT "Iris connection")                (* replace EPSOCKET of IRISPACKET 
                                                           with 41)
                    (PRINTOUT PROMPTWINDOW CHAR "RECEIVED")
                    (replace (IRISENCAPSULATION INFOCHAR1) of IRISPACKET with
                                                                                 (CHARCODE H))
                    (SETQ INBOOTSTREAM (SPP.OPEN NIL 37))
                    (SENDIRISPACKET IRISPACKET]
        (RELEASE.MONITORLOCK IRIS.LOCK])

(SENDIRISPACKET
  [LAMBDA (IRISPACKET)                                   (* gbn "10-Jun-85 16:05")
          (* * Sends a raw seething IRIS packet)
    (COND
       ((fetch (ETHERPACKET EPTRANSMITTING) of IRISPACKET)
        'AlreadyQueued)
       (T                                                    (* (\RCLK (LOCF (fetch
                                                           (ETHERPACKET EPTIMESTAMP) of 
                                                           IRISPACKET))))
          (TRANSMIT.ETHERPACKET (fetch (ETHERPACKET EPNETWORK) of IRISPACKET)
                 IRISPACKET])

(IRISFILTER
  [LAMBDA (IRISPACKET)                                   (* gbn " 3-Jun-85 22:49")
    (if (AND (EQ (fetch (ETHERPACKET EPTYPE) of IRISPACKET)
                     IRIS.PACKETTYPE)
                 (BROADCASTP IRISPACKET))
        then (ADD.PROCESS `(IRISBOOTPROCESS %, IRISPACKET))
              T
      else                                               (* not an iris packet)
            NIL])

(OPEN.IRISCONN
  [LAMBDA (NSADDRESS)                                    (* gbn " 7-Jul-85 14:42")
    (SETQ IRISCONN (SPP.OPEN (OR NSADDRESS IRISNSADDRESS)
                          IRISSOCKET T '|Iris Terminal SPP|])

(IRISBOOTSERVER
  [LAMBDA (ON?)                                          (* gbn " 7-Jul-85 14:54")
    (if ON?
        then (PROMPTPRINT "Enabling IRIS boot server")
              (\ADD.PACKET.FILTER (FUNCTION IRISFILTER))
      else (PROMPTPRINT "Disabling IRIS boot server")
            (\DEL.PACKET.FILTER (FUNCTION IRISFILTER])
)

(RPAQQ \IRIS.VERBOSE T)

(RPAQQ PRINTSPPDATAFLG T)

(RPAQ IRIS.LOCK (CREATE.MONITORLOCK "iris boot lock"))
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS IRISNSHOSTNUMBER)
)

(RPAQ? IRISNET 146)

(RPAQ? IRISBOOTDIRECTORIES '({CORE} {ERIS}<IRIS>gl2>boot>))
(DECLARE%: EVAL@COMPILE 

(RPAQQ IRISSOCKET 37)

(RPAQQ IRIS.PACKETTYPE 32790)

(RPAQQ IRIS.BOOT.STREAM.NAME |IRIS boot SPP|)

(CONSTANTS (IRISSOCKET 37)
       (IRIS.PACKETTYPE 32790)
       (IRIS.BOOT.STREAM.NAME '|IRIS boot SPP|))
)
(DECLARE%: EVAL@LOAD DONTCOPY 
(FILESLOAD ETHERRECORDS)

(LOADCOMP 'LLETHER)
)
(ACCESSFNS IRISENCAPSULATION [(IRISBASE (LOCF (FETCH (ETHERPACKET EPENCAPSULATION)
                                                     OF DATUM]
       [BLOCKRECORD IRISBASE ((IRISLENGTH WORD)
                              (IRISDESTHOSTO 3 WORD)
                              (IRISSOURCEHOSTO 3 WORD)
                              (IRISTYPE WORD)
                              (IRISEXCHID WORD)
                              (INFOCHAR1 BYTE)
                              (INFOCHAR2 BYTE))
              [ACCESSFNS IRISDESTHOSTO ((IRISDESTHOST (\LOADNSHOSTNUMBER (LOCF DATUM))
                                               (\STORENSHOSTNUMBER (LOCF DATUM)
                                                      NEWVALUE))
                                        (IRISPACKETBASE (LOCF DATUM))
                                        (IRISDESTHOSTBASE (LOCF DATUM]
              (ACCESSFNS IRISSOURCEHOSTO ((IRISSOURCEHOST (\LOADNSHOSTNUMBER (LOCF DATUM))
                                                 (\STORENSHOSTNUMBER (LOCF DATUM)
                                                        NEWVALUE))
                                          (IRISSOURCEHOSTBASE (LOCF DATUM]
       (TYPE? (type? ETHERPACKET DATUM)))
(DECLARE%: EVAL@COMPILE 
[PUTPROPS BROADCASTP MACRO ((PACKET)
                            ([LAMBDA (NDB)
                                    (AND NDB (APPLY* (fetch NDBBROADCASTP of NDB)
                                                    PACKET NDB]
                             (fetch EPNETWORK of PACKET]
)
(PUTPROPS IRISNET COPYRIGHT ("Xerox Corporation" 1988))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (3679 13267 (IRIS.RESET 3689 . 4230) (IRIS.TRACE 4232 . 4424) (IRISBOOTPROCESS 4426 . 
11586) (SENDIRISPACKET 11588 . 12227) (IRISFILTER 12229 . 12677) (OPEN.IRISCONN 12679 . 12908) (
IRISBOOTSERVER 12910 . 13265)))))
STOP
