(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "XCL" BASE 10)

(FILECREATED " 4-Oct-2022 16:31:38" |{DSK}<home>larry>medley>sources>MAIKOETHER.;2| 9453   

      :CHANGES-TO (FNS \\NS.SETTIME \\10MB.RESTART.ETHER \\10MB.STARTDRIVER \\10MB.TURNOFFETHER 
                       \\10MB.TURNONETHER \\10MBSENDPACKET \\10MBWATCHER \\MAIKO.10MBTURNONETHER 
                       \\CHECKSUM \\INPUT.INTERRUPT \\ETHER-INTERRUPT \\CONSOLE-LOG-PRINT 
                       \\IO-INTERRUPT)
                  (VARS MAIKOETHERCOMS)

      :PREVIOUS-DATE "25-Oct-2021 15:12:33" |{DSK}<home>larry>medley>sources>MAIKOETHER.;1|)


; Copyright (c) 1988-1991, 2021 by Venue & Xerox Corporation.

(PRETTYCOMPRINT MAIKOETHERCOMS)

(RPAQQ MAIKOETHERCOMS
       ((FNS \\INPUT.INTERRUPT)
        (INITVARS (\\MAIKO.INPUT.PACKET)
               (|\\ETHERtopMonitor| (CREATE.MONITORLOCK "ETHERTopMonitor")))
        (DECLARE\: EVAL@COMPILE DONTCOPY (FILES (LOADCOMP)
                                                10MBDRIVER)
               (GLOBALVARS \\MAIKO.INPUT.PACKET |\\ETHERtopMonitor|)
               
               (* |;;| "The NDB for Maiko's 10MB connection; used by \\MAIKO.ETHER-INTERRUPT:")

               (GLOBALVARS \\MAIKO.10MB.NDB)
               (GLOBALVARS \\MAIKO.IO-INTERRUPT-FLAGS \\MAIKO.IO-INTERRUPT-VECTOR))
        (COMS                                                (* \; "MAIKO handler for new interrupt-driven incoming ethernet communication, rather than polling for it.")
              (FNS \\ETHER-INTERRUPT))
        (COMS                                                (* \; "MAIKO Log & Console message handling.  Interrupt-driven message printing, instead of polled printing.")
              (FNS \\CONSOLE-LOG-PRINT))
        (COMS 
              (* |;;| "Asynchronous I/O handling")

              (FNS \\IO-INTERRUPT)
              (VARS \\EPT.3TO10 (\\MAIKO.IO-INTERRUPT-FLAGS (\\CREATECELL \\FIXP))
                    (\\MAIKO.IO-INTERRUPT-VECTOR NIL)))))
(DEFINEQ

(\\INPUT.INTERRUPT
  (LAMBDA (NDB)                                          (* \; "Edited 11-May-88 16:05 by MASINTER")

    (* |;;| "This routine gets called when 10MB input signals an interrupt.  See if the \\MAIKO.INPUT.PACKET has indeed been processed, and if so, take care of it")

    (PROG (LENGTH (PACKET \\MAIKO.INPUT.PACKET))
          (COND
             ((NEQ (SETQ LENGTH (|fetch| DLFIRSTICB |of| (|fetch| NDBCSB |of| NDB)))
                   \\ES.PENDING)
              (|replace| 10MBLENGTH |of| PACKET |with| LENGTH)
              (\\RCLK (LOCF (|fetch| EPTIMESTAMP |of| PACKET)))
              (|replace| EPNETWORK |of| PACKET |with| NDB)
              (|replace| EPTYPE |of| PACKET |with| (|for| PAIR |in| \\10MBTYPE.TRANSLATIONS
                                                      |bind| (TYPE _ (|fetch| 10MBTYPE |of| PACKET))
                                                      |when| (EQ TYPE (CAR PAIR))
                                                      |do| (RETURN (CDR PAIR))
                                                      |finally| (RETURN TYPE)))
              (COND
                 (\\RAWTRACING (\\MAYBEPRINTPACKET PACKET 'RAWGET)))
              (RETURN (PROG1 PACKET
                          (SETQ \\MAIKO.INPUT.PACKET (\\ALLOCATE.ETHERPACKET))
                          (|replace| DLFIRSTICB |of| (|fetch| NDBCSB |of| NDB) |with| \\ES.PENDING)
                          (SUBRCALL ETHER-GET \\10MBPACKETLENGTH (|fetch| 10MBPACKETBASE |of| 
                                                                                 \\MAIKO.INPUT.PACKET
                                                                        )))))
             (T (RETURN NIL))))))
)

(RPAQ? \\MAIKO.INPUT.PACKET )

(RPAQ? |\\ETHERtopMonitor| (CREATE.MONITORLOCK "ETHERTopMonitor"))
(DECLARE\: EVAL@COMPILE DONTCOPY 

(FILESLOAD (LOADCOMP)
       10MBDRIVER)

(DECLARE\: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS \\MAIKO.INPUT.PACKET |\\ETHERtopMonitor|)
)

(DECLARE\: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS \\MAIKO.10MB.NDB)
)

(DECLARE\: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS \\MAIKO.IO-INTERRUPT-FLAGS \\MAIKO.IO-INTERRUPT-VECTOR)
)
)



(* \; 
"MAIKO handler for new interrupt-driven incoming ethernet communication, rather than polling for it.")

(DEFINEQ

(\\ETHER-INTERRUPT
  (LAMBDA NIL                                                (* \; "Edited  4-May-91 13:46 by jds")

    (* |;;| "This routine gets called when 10MB input signals an interrupt.  See if the \\MAIKO.INPUT.PACKET has indeed been processed, and if so, take care of it")

    (PROG ((NDB \\MAIKO.10MB.NDB)
           LENGTH)

     (* |;;| "First, turn off the interrupt flag:")

          (REPLACE (INTERRUPTSTATE ETHERINTERRUPT) OF \\INTERRUPTSTATE WITH NIL)

     (* |;;| "Now handle it:")

          (UNINTERRUPTABLY
              (WITH.MONITOR |\\ETHERtopMonitor|
                  (PROG ((PACKET \\MAIKO.INPUT.PACKET))

                   (* |;;| "We come back here if there's more than one packet ready to be read, so we process as many as possible in one swell foop.")

                    READ-MORE-LOOP
                        (COND
                           ((NEQ (SETQ LENGTH (|fetch| DLFIRSTICB |of| (|fetch| NDBCSB |of| NDB)))
                                 \\ES.PENDING)
                            (|replace| 10MBLENGTH |of| PACKET |with| LENGTH)
                            (\\RCLK (LOCF (|fetch| EPTIMESTAMP |of| PACKET)))
                            (|replace| EPNETWORK |of| PACKET |with| NDB)
                            (|replace| EPTYPE |of| PACKET
                               |with| (|for| PAIR |in| \\10MBTYPE.TRANSLATIONS
                                         |bind| (TYPE _ (|fetch| 10MBTYPE |of| PACKET))
                                         |when| (EQ TYPE (CAR PAIR)) |do| (RETURN (CDR PAIR))
                                         |finally| (RETURN TYPE)))
                            (COND
                               (\\RAWTRACING (\\MAYBEPRINTPACKET PACKET 'RAWGET)))
                            (\\HANDLE.RAW.PACKET PACKET)
                            (SETQ \\MAIKO.INPUT.PACKET (\\ALLOCATE.ETHERPACKET))
                            (|replace| DLFIRSTICB |of| (|fetch| NDBCSB |of| NDB) |with| \\ES.PENDING)
                            (COND
                               ((SUBRCALL ETHER-GET \\10MBPACKETLENGTH (|fetch| 10MBPACKETBASE
                                                                          |of| \\MAIKO.INPUT.PACKET))

                                (* |;;| 
                                "Returned T, so there's another packet waiting already.  Process it.")

                                (SETQ PACKET \\MAIKO.INPUT.PACKET)
                                (GO READ-MORE-LOOP)))))))))))
)



(* \; 
"MAIKO Log & Console message handling.  Interrupt-driven message printing, instead of polled printing."
)

(DEFINEQ

(\\CONSOLE-LOG-PRINT
  (LAMBDA NIL                                                (* \; "Edited 18-Dec-89 12:16 by jds")

    (* |;;| "Read any pending Console or Log messages, and print them in the prompt window.")

    (* |;;| 
   "Called from INTERRUPTED when the Maiko emulator sets the LogMsgPending flag in \\INTERRUPTSTATE.")

    (PROG (MESSAGE-BUFFER MESSAGE-LENGTH)
          (|replace| (INTERRUPTSTATE LOGMSGSPENDING) |of| \\INTERRUPTSTATE |with| NIL)
          (|while| (SUBRCALL MESSAGE-READP)
             |do| (FRESHLINE PROMPTWINDOW)
                  (PRIN1 (|if| (SETQ MESSAGE-LENGTH (SUBRCALL MESSAGE-READ (OR MESSAGE-BUFFER
                                                                               (SETQ MESSAGE-BUFFER
                                                                                (ALLOCSTRING 1024)))
                                                           1024))
                             |then| (SUBSTRING MESSAGE-BUFFER 1 MESSAGE-LENGTH)
                           |else| "?? system message: polling failed")
                         PROMPTWINDOW)))))
)



(* |;;| "Asynchronous I/O handling")

(DEFINEQ

(\\IO-INTERRUPT
  (LAMBDA NIL                                                (* \; "Edited 18-Dec-89 13:09 by jds")

    (* |;;| "Handle I/O pending on an asynchronous file descriptor.")

    (* |;;| 
    "Called from INTERRUPTED when the Maiko emulator sets theIOINTERRUPT flag in \\INTERRUPTSTATE.")

    (PROG NIL
          (|replace| (INTERRUPTSTATE IOINTERRUPT) |of| \\INTERRUPTSTATE |with| NIL)
          (FOR INFO IN \\MAIKO.IO-INTERRUPT-VECTOR WHEN (NOT (ZEROP (LOGAND (CAR INFO)
                                                                           \\MAIKO.IO-INTERRUPT-FLAGS
                                                                           )))
             DO (CL:FUNCALL (CADR INFO))))))
)

(RPAQQ \\EPT.3TO10 513)

(RPAQ \\MAIKO.IO-INTERRUPT-FLAGS (\\CREATECELL \\FIXP))

(RPAQQ \\MAIKO.IO-INTERRUPT-VECTOR NIL)
(PUTPROPS MAIKOETHER COPYRIGHT ("Venue & Xerox Corporation" 1988 1989 1990 1991 2021))
(DECLARE\: DONTCOPY
  (FILEMAP (NIL (2010 3860 (\\INPUT.INTERRUPT 2020 . 3858)) (4433 7091 (\\ETHER-INTERRUPT 4443 . 7089)) 
(7213 8385 (\\CONSOLE-LOG-PRINT 7223 . 8383)) (8431 9208 (\\IO-INTERRUPT 8441 . 9206)))))
STOP
