(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "13-Jul-2025 13:36:59" {WMEDLEY}<sources>MCCSFONTS.;68 10853  

      :EDIT-BY rmk

      :CHANGES-TO (FNS FILECOMPLETED)

      :PREVIOUS-DATE "11-Jul-2025 11:08:25" {WMEDLEY}<sources>MCCSFONTS.;67)


(PRETTYCOMPRINT MCCSFONTSCOMS)

(RPAQQ MCCSFONTSCOMS [(FNS FONTSINCORE.TO.MCCS FONT.TO.MCCS)
                      (FNS FILECOMPLETED FILEFONTS)
                      (VARS (LOWLINE (CHARACTER (CHARCODE Lowline)))
                            (CARET (CHARACTER (CHARCODE Caret])
(DEFINEQ

(FONTSINCORE.TO.MCCS
  [LAMBDA (DEVICE)                                           (* ; "Edited  2-May-2025 23:30 by rmk")
    (CL:UNLESS DEVICE
        (SETQ DEVICE 'DISPLAY))
    (for FONT in (\LOOKUPFONTSINCORE '* '* '* '* DEVICE) do (FONT.TO.MCCS FONT DEVICE])

(FONT.TO.MCCS
  [LAMBDA (FONT DEVICE VIRGIN)                               (* ; "Edited 27-May-2025 23:02 by rmk")
                                                             (* ; "Edited 24-May-2025 20:53 by rmk")
                                                             (* ; "Edited 22-May-2025 10:38 by rmk")
                                                             (* ; "Edited 20-May-2025 17:22 by rmk")
                                                             (* ; "Edited 16-May-2025 14:38 by rmk")
                                                             (* ; "Edited 13-May-2025 09:32 by rmk")
                                                             (* ; "Edited  2-May-2025 23:29 by rmk")

    (* ;; 
  "Move the character information for FONT's current charencoding to the corresponding MCCS codes.  ")

    (* ;; "If VIRGIN, this starts fresh from source fonts, ignoring already constructed fonts in core and ignoring all previously created medleyformat fonts.  But it does do coercions.")

    (RESETLST
        (CL:WHEN VIRGIN
            (RESETSAVE \FONTSINCORE NIL)
            (RESETSAVE DISPLAYFONTEXTENSIONS '(DISPLAYFONT)))
        (SETQ FONT (FONTCREATE FONT NIL NIL NIL DEVICE))
        (LET ((FILE (MEDLEYFONT.FILENAME FONT)))
             (CL:UNLESS [AND (COMPLETEFONTP FONT)
                             (EQ 'MCCS (FONTPROP FONT 'CHARENCODING]
                 [LET [(MAPPING (MCCSCODEMAP (FONTPROP FONT 'CHARENCODING]
                      (CL:WHEN MAPPING
                          (printout T "Converting " (FONTPROP FONT 'SPEC)
                                 " from "
                                 (FONTPROP FONT 'CHARENCODING)
                                 " to :MCCS" T)
                          (SETQ FONT (COMPLETEFONT FONT))
                          (MOVEFONTCHARS MAPPING DEVICE FONT)
                          (SETFONTCHARENCODING FONT :MCCS)
                          (SETQ FILE (MEDLEYFONT.WRITE.FONT FONT FILE)))])
             (LIST FONT FILE)))])
)
(DEFINEQ

(FILECOMPLETED
  [LAMBDA (FONTS DONTFILE DEVICE INCORETOO)                  (* ; "Edited 13-Jul-2025 13:36 by rmk")
                                                             (* ; "Edited 11-Jul-2025 11:04 by rmk")
                                                             (* ; "Edited 21-Jun-2025 23:46 by rmk")
                                                             (* ; "Edited 19-Jun-2025 15:38 by rmk")
                                                             (* ; "Edited 12-Jun-2025 22:48 by rmk")
                                                             (* ; "Edited 11-Jun-2025 18:16 by rmk")
                                                             (* ; "Edited 10-Jun-2025 14:09 by rmk")

    (* ;; "Get the faces cached in a useful order")

    (CL:UNLESS DEVICE
        (SETQ DEVICE 'DISPLAY))
    (RESETLST
        (CL:UNLESS INCORETOO
            (RESETSAVE \FONTSINCORE NIL)
            (RESETSAVE \FONTEXISTS?-CACHE NIL))

        (* ;; "There might be other face combinations")

        (if (AND (LISTP FONTS)
                 (LITATOM (CAR FONTS))
                 (FIXP (CADR FONTS)))
            then (SETQ FONTS (CONS FONTS))
          else 
               (* ;; "Get all the C0 fonts for DEVICE, throw out those with unrequested families, then order smaller first and then MRR BIR and MIR before other faces, to anticipate coercions")

               (SETQ FONTS (for A FAMILIES in (FONTSAVAILABLE '* '* '* 0 DEVICE (CL:IF INCORETOO
                                                                                    T
                                                                                    'ONLY))
                              first (SETQ FAMILIES (for FAM inside FONTS
                                                      collect (CL:UNLESS (LITATOM FAM)
                                                                     (ERROR "Can't interpret FONTS" 
                                                                            FAM))
                                                            FAM)) when (MEMB (CAR A)
                                                                             FAMILIES) collect A)))
        [SETQ FONTS
         (SORT FONTS (FUNCTION (LAMBDA (F1 F2)
                                 (SELECTQ (ALPHORDER (CAR F1)
                                                 (CAR F2))
                                     (LESSP T)
                                     (EQUAL [OR (ILESSP (CADR F1)
                                                       (CADR F2))
                                                (AND (EQ (CADR F1)
                                                         (CADR F2))
                                                     (MEMBER (CADDR F2)
                                                            (CDR (MEMBER (CADDR F1)
                                                                        '((MEDIUM REGULAR REGULAR)
                                                                          (BOLD REGULAR REGULAR)
                                                                          (MEDIUM ITALIC REGULAR)
                                                                          (BOLD ITALIC REGULAR)
                                                                          (LIGHT REGULAR REGULAR)
                                                                          (MEDIUM REGULAR COMPRESSED)
                                                                          (MEDIUM REGULAR EXPANDED])
                                     NIL]
        (CL:WHEN (CDR FONTS)
               (TDRIBBLE))
        (PRINTOUT T "Processing " (LENGTH FONTS)
               " fonts" T)
        (for F FONT FONTSTART TOTALTIME MFILE (TOTALSIZE _ 0) in FONTS first (SETQ TOTALTIME
                                                                              (CLOCK 0))
           collect (BLOCK)
                 (PRINTOUT T F)
                 (SETQ FONTSTART (CLOCK 0))
                 (SETQ FONT (COMPLETE.FONT F T))
                 (CL:UNLESS DONTFILE
                     (SETQ MFILE (MEDLEYFONT.WRITE.FONT FONT)))
                 (PRINTOUT T 55 .I3 (FIXR (FQUOTIENT (IDIFFERENCE (CLOCK 0)
                                                            FONTSTART)
                                                 1000)))
                 (CL:WHEN MFILE
                     (PRINTOUT T -3 .I6 (GETFILEINFO MFILE 'LENGTH))
                     (add TOTALSIZE (GETFILEINFO MFILE 'LENGTH)))
                 (TERPRI T)
                 FONT finally (SETQ TOTALTIME (FIXR (FQUOTIENT (IDIFFERENCE (CLOCK 0)
                                                                      TOTALTIME)
                                                           60000)))
                            (PRINTOUT T 4 (LENGTH $$VAL)
                                   " fonts completed ")
                            (if DONTFILE
                                then (PRINTOUT T "(but not written) in " TOTALTIME " minutes" T)
                              else (PRINTOUT T "and written in " TOTALTIME " minutes" -3 TOTALSIZE 
                                          " total bytes" T T)) 

                            (* ;; 
                    "Keep everything, but don't obscure what was on the screen with the cache values")

                            (if (AND FONTS (LITATOM FONTS))
                                then (SET FONTS (LIST $$VAL \FONTSINCORE \FONTEXISTS?-CACHE))
                              else (SETQ COMPLETEDVALUE (LIST $$VAL \FONTSINCORE \FONTEXISTS?-CACHE))
                                )
                            (CL:WHEN (CDR FONTS)
                                   (DRIBBLE))))])

(FILEFONTS
  [LAMBDA (FONTS DEVICE INCORETOO)                           (* ; "Edited 19-Jun-2025 16:18 by rmk")
                                                             (* ; "Edited 12-Jun-2025 22:48 by rmk")
                                                             (* ; "Edited 11-Jun-2025 18:16 by rmk")
                                                             (* ; "Edited 10-Jun-2025 14:09 by rmk")

    (* ;; "Get the faces cached in a useful order.  This is for HIPPO etc., doesn't complete")

    (CL:UNLESS DEVICE
        (SETQ DEVICE 'DISPLAY))
    (RESETLST
        (CL:UNLESS INCORETOO
            (RESETSAVE \FONTSINCORE NIL)
            (RESETSAVE \FONTEXISTS?-CACHE NIL))
        (for F FONT TOTALSTART in (CL:IF (LITATOM FONTS)
                                      (APPEND (FONTSAVAILABLE FONTS '* 'MRR '* DEVICE T)
                                             (FONTSAVAILABLE FONTS '* 'BRR '* DEVICE T)
                                             (FONTSAVAILABLE FONTS '* 'MIR '* DEVICE T)
                                             (FONTSAVAILABLE FONTS '* 'BIR '* DEVICE T))
                                      (CONS FONTS)) first (SETQ TOTALSTART (CLOCK 0))
           collect (PRINTOUT T F)
                 (SETQ FONT (FONTCREATE F))
                 (MEDLEYFONT.WRITE.FONT FONT)
                 FONT finally (PRINTOUT T 5 (LENGTH $$VAL)
                                     " fonts completed and written in "
                                     (FIXR (FQUOTIENT (IDIFFERENCE (CLOCK 0)
                                                             TOTALSTART)
                                                  1000))
                                     " seconds" T)
                            (RETURN (LIST $$VAL \FONTSINCORE \FONTEXISTS?-CACHE))))])
)

(RPAQ LOWLINE (CHARACTER (CHARCODE Lowline)))

(RPAQ CARET (CHARACTER (CHARCODE Caret)))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (576 2972 (FONTSINCORE.TO.MCCS 586 . 877) (FONT.TO.MCCS 879 . 2970)) (2973 10732 (
FILECOMPLETED 2983 . 8875) (FILEFONTS 8877 . 10730)))))
STOP
