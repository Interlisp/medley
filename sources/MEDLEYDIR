(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "17-Oct-2022 20:03:47" {DSK}<home>larry>medley>sources>MEDLEYDIR.;11 10386  

      :CHANGES-TO (VARS MEDLEYDIRCOMS)
                  (FNS MEDLEYSUBSTDIR MEDLEY-INIT-VARS MEDLEYDIR.AROUNDEXITFN)

      :PREVIOUS-DATE "17-Oct-2022 18:53:31" {DSK}<home>larry>medley>sources>MEDLEYDIR.;6)


(PRETTYCOMPRINT MEDLEYDIRCOMS)

(RPAQQ MEDLEYDIRCOMS
       [
        (* ;; "set up initialization for file paths relative to where Medley is installed. This assumes that the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path) and all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)")

        (FNS MEDLEY-INIT-VARS MEDLEYDIR MEDLEYSUBSTDIR)
        (INITVARS (MEDLEYDIR))
        (ADDVARS (AROUNDEXITFNS MEDLEY-INIT-VARS))
        
        (* ;; 
 "NOTE:  Do not use backquote in the variable definitions.  These get evaluated early in the loadup.")

        [VARS (MEDLEY-INIT-VARS '([LISPUSERSDIRECTORIES (MEDLEYDIR '("library" "lispusers" "internal"
                                                                           "greetfiles" "doctools"]
                                  [LISPSOURCEDIRECTORIES (MEDLEYDIR '("sources"]
                                  (LISPSOURCEDIRECTORY (CAR LISPSOURCEDIRECTORIES))
                                  (IRM.HOST&DIR (MEDLEYDIR '"docs/dinfo"))
                                  (IRM.DINFOGRAPH)
                                  (DIRECTORIES (APPEND LISPUSERSDIRECTORIES LISPSOURCEDIRECTORIES))
                                  [LOGINHOST/DIR (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                                                    (UNIX-GETENV "HOME"]
                                  [USERGREETFILES (LIST (CONS LOGINHOST/DIR '("INIT" COM))
                                                        (CONS LOGINHOST/DIR '("INIT"]
                                  (DISPLAYFONTDIRECTORIES (MEDLEYDIR '("fonts/displayfonts" 
                                                                             "fonts/altofonts" 
                                                                             "fonts/adobe" 
                                                                             "fonts/big" 
                                                                             "fonts/other")
                                                                 NIL NIL T))
                                  (POSTSCRIPTFONTDIRECTORIES (MEDLEYDIR '("fonts/postscriptfonts")
                                                                    NIL NIL T))
                                  (INTERPRESSFONTDIRECTORIES (MEDLEYDIR '("fonts/ipfonts")
                                                                    NIL NIL T))
                                  (UNICODEDIRECTORIES (MEDLEYDIR '("unicode/xerox")
                                                             NIL NIL T))
                                  (XCL::*WHERE-IS-CASH-FILES*]
        (DECLARE%: EVAL@COMPILE DOCOPY (ADDVARS (GLOBALVARS MEDLEYDIR MEDLEY-INIT-VARS 
                                                       \SAVE.MEDLEYDIR DIRECTORIES])



(* ;; 
"set up initialization for file paths relative to where Medley is installed. This assumes that the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path) and all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)"
)

(DEFINEQ

(MEDLEY-INIT-VARS
  [LAMBDA (EVENT)                                            (* ; "Edited 17-Oct-2022 18:36 by lmm")
    (SELECTQ EVENT
        ((BEFORESYSOUT BEFOREMAKESYS BEFORELOGOUT BEFORESAVEVM T) 
             (SETTOPVAL '\SAVE.MEDLEYDIR MEDLEYDIR)
             [SETTOPVAL 'MEDLEY-INIT-VARS (FOR X IN MEDLEY-INIT-VARS
                                             COLLECT (LIST (CAR X)
                                                           (CADR X)
                                                           (GETTOPVAL (CAR X])
        ((AFTERSYSOUT AFTERMAKESYS AFTERLOGOUT AFTERSAVEVM RESTART INIT NIL) 

                                                 (* ;; "MEDLEY-INIT-VARS has variables that might need to get reset BECAUSE their values depend on MEDLEYDIR")

             (LET* ((OM (U-CASE \SAVE.MEDLEYDIR))
                    (NM (MEDLEYDIR)))
                   [IF (STREQUAL OM (U-CASE NM))
                       THEN 
                            (* ;; "no changed -- assume everything is OK. Should do some checking?")

                     ELSE 
                          (* ;; " different -- substitue")

                          (for X in MEDLEY-INIT-VARS
                             do (SETTOPVAL (CAR X)
                                       (IF (NULL (CDDR X))
                                           then (EVAL (CADR X))
                                         ELSE (MEDLEYSUBSTDIR OM NM (CADDR X]

                   (* ;; "this is a temporary hack (10/17/22 LMM) to get around the fact that  we are doing something odd, and WHEREIS doesn't follow conventions")

                   [LET [(NEWSYS (STRPOS "tmp/" (UNIX-GETENV "LDESRCESYSOUT"]
                        (if NEWSYS
                            then (push DIRECTORIES (MEDLEYDIR "tmp")))
                        (CL:WHEN (GETD 'XCL::ADD-WHERE-IS-DATABASE)
                            (SETQ XCL::*WHERE-IS-CASH-FILES* NIL)
                            (NLSETQ (XCL::ADD-WHERE-IS-DATABASE (MEDLEYDIR (if NEWSYS
                                                                               then "tmp"
                                                                             else "loadups")
                                                                       "WHEREIS.HASH"))))]
                   NIL))
        (PROGN                                               (* ; "no changes")
               NIL])

(MEDLEYDIR
  [LAMBDA (DIRNAME FILENAME OUTPUT NOERROR)                (* ; "Edited  5-Mar-2022 12:43 by larry")
                                                          (* ; "Edited  2-Dec-2021 20:23 by kaplan")
    (DECLARE (GLOBALVARS MEDLEYDIR))
    (DECLARE (GLOBALVARS MEDLEYDIR))
    (if (NULL DIRNAME)
        then (if (OR (NOT (BOUNDP 'MEDLEYDIR))
                     (NOT MEDLEYDIR))
                 then (OR (SETQ MEDLEYDIR (DIRECTORYNAME (OR (UNIX-GETENV "MEDLEYDIR")
                                                             T)))
                          (DIRECTORYNAME T))
               elseif (STRPOS "/" MEDLEYDIR)
                 then (SETQ MEDLEYDIR (DIRECTORYNAME MEDLEYDIR))
               else MEDLEYDIR)
      elseif (LISTP DIRNAME)
        then (for X Y in DIRNAME when (SETQ Y (MEDLEYDIR X FILENAME OUTPUT NOERROR)) collect Y)
      elseif FILENAME
        then [if (NULL (SETQ DIRNAME (MEDLEYDIR DIRNAME NIL OUTPUT NOERROR)))
                 then (OR NOERROR (SHOULDNT))
                      NIL
               else (SETQ FILENAME (CONCAT DIRNAME FILENAME))
                    (if OUTPUT
                        then FILENAME
                      else (OR (INFILEP FILENAME)
                               (if NOERROR
                                   then NIL
                                 else (ERROR "No such medley file" FILENAME]
      else (OR (DIRECTORYNAME (CONCAT (MEDLEYDIR)
                                     DIRNAME ">")
                      NIL OUTPUT)
               (if NOERROR
                   then NIL
                 else (ERROR "No such medley directory" DIRNAME])

(MEDLEYSUBSTDIR
  [LAMBDA (OLD NEW BODY)                                     (* ; "Edited 17-Oct-2022 19:35 by lmm")
    (IF (NULL BODY)
        THEN NIL
      ELSEIF (LISTP BODY)
        THEN (LET [(A (MEDLEYSUBSTDIR OLD NEW (CAR BODY)))
                   (D (MEDLEYSUBSTDIR OLD NEW (CDR BODY]
                  (IF (AND (EQ A (CAR BODY))
                           (EQ D (CDR BODY)))
                      THEN BODY
                    ELSE (CONS A D)))
      ELSEIF (STRINGP BODY)
        THEN (IF (EQ 1 (STRPOS OLD (U-CASE BODY)
                              1))
                 THEN (CONCAT NEW (SUBSTRING BODY (NCHARS OLD)))
               ELSE BODY)
      ELSEIF [AND (LITATOM BODY)
                  (EQ 1 (STRPOS OLD (U-CASE (MKSTRING BODY]
        THEN (PACK* NEW (SUBSTRING BODY (NCHARS OLD)))
      ELSE BODY])
)

(RPAQ? MEDLEYDIR )

(ADDTOVAR AROUNDEXITFNS MEDLEY-INIT-VARS)



(* ;; 
"NOTE:  Do not use backquote in the variable definitions.  These get evaluated early in the loadup.")


(RPAQQ MEDLEY-INIT-VARS
       ([LISPUSERSDIRECTORIES (MEDLEYDIR '("library" "lispusers" "internal" "greetfiles" "doctools"]
        [LISPSOURCEDIRECTORIES (MEDLEYDIR '("sources"]
        (LISPSOURCEDIRECTORY (CAR LISPSOURCEDIRECTORIES))
        (IRM.HOST&DIR (MEDLEYDIR '"docs/dinfo"))
        (IRM.DINFOGRAPH)
        (DIRECTORIES (APPEND LISPUSERSDIRECTORIES LISPSOURCEDIRECTORIES))
        [LOGINHOST/DIR (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                          (UNIX-GETENV "HOME"]
        [USERGREETFILES (LIST (CONS LOGINHOST/DIR '("INIT" COM))
                              (CONS LOGINHOST/DIR '("INIT"]
        (DISPLAYFONTDIRECTORIES (MEDLEYDIR '("fonts/displayfonts" "fonts/altofonts" "fonts/adobe" 
                                                   "fonts/big" "fonts/other")
                                       NIL NIL T))
        (POSTSCRIPTFONTDIRECTORIES (MEDLEYDIR '("fonts/postscriptfonts")
                                          NIL NIL T))
        (INTERPRESSFONTDIRECTORIES (MEDLEYDIR '("fonts/ipfonts")
                                          NIL NIL T))
        (UNICODEDIRECTORIES (MEDLEYDIR '("unicode/xerox")
                                   NIL NIL T))
        (XCL::*WHERE-IS-CASH-FILES*)))
(DECLARE%: EVAL@COMPILE DOCOPY 

(ADDTOVAR GLOBALVARS MEDLEYDIR MEDLEY-INIT-VARS \SAVE.MEDLEYDIR DIRECTORIES)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (3561 8795 (MEDLEY-INIT-VARS 3571 . 6099) (MEDLEYDIR 6101 . 7883) (MEDLEYSUBSTDIR 7885
 . 8793)))))
STOP
