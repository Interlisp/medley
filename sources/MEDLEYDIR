(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "27-Oct-2022 10:25:15" {DSK}<home>larry>ilisp>medley>sources>MEDLEYDIR.;7 10795  

      :CHANGES-TO (FNS MEDLEY-INIT-VARS)
                  (VARS MEDLEYDIRCOMS MEDLEY-INIT-VARS)

      :PREVIOUS-DATE "26-Oct-2022 15:34:12" {DSK}<home>larry>ilisp>medley>sources>MEDLEYDIR.;5)


(PRETTYCOMPRINT MEDLEYDIRCOMS)

(RPAQQ MEDLEYDIRCOMS
       (
        (* ;; "set up initialization for file paths relative to where Medley is installed, in cases where the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path); all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)")

        (FNS MEDLEY-INIT-VARS MEDLEYDIR MEDLEYSUBSTDIR)
        (INITVARS (MEDLEYDIR)
               (\SAVE.MEDLEYDIR))
        (ADDVARS (AROUNDEXITFNS MEDLEY-INIT-VARS))
        
        (* ;; "**WARNING**  The EVALed expressions get run early in the loadup.")

        (VARS MEDLEY-INIT-VARS)
        (DECLARE%: EVAL@COMPILE DONTCOPY (GLOBALVARS MEDLEYDIR MEDLEY-INIT-VARS \SAVE.MEDLEYDIR 
                                                DIRECTORIES))))



(* ;; 
"set up initialization for file paths relative to where Medley is installed, in cases where the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path); all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)"
)

(DEFINEQ

(MEDLEY-INIT-VARS
  [LAMBDA (EVENT)                                            (* ; "Edited 27-Oct-2022 10:10 by lmm")
                                                             (* ; "Edited 26-Oct-2022 08:48 by lmm")

    (* ;; "Called on events including before & after loadup")

    (SELECTQ EVENT
        ((BEFOREMAKESYS T)                                   (* ; "Clear old values")
             (FOR X IN MEDLEY-INIT-VARS DO (IF (CDDR X)
                                               THEN (SETTOPVAL (CAR X)
                                                           NIL)))
             (SETQ \SAVE.MEDLEYDIR NIL))
        ((BEFORESYSOUT BEFORELOGOUT BEFORESAVEVM)            (* ; "save old values")
             [SETQ \SAVE.MEDLEYDIR (CONS MEDLEYDIR (FOR X IN MEDLEY-INIT-VARS
                                                      COLLECT (CONS (CAR X)
                                                                    (GETTOPVAL (CAR X])
        ((AFTERSYSOUT AFTERMAKESYS AFTERLOGOUT AFTERSAVEVM RESTART INIT NIL) 
                                                             (* ; 
                                       "Any old values, restore them, substituting the new MEDLEYDIR")
             (PROG (OLDMD NEWMD SAME TMP OLDVALUES)
                   (SETQ MEDLEYDIR)                          (* ; 
                              "MEDLEYDIR resets from GETENV when it is NIL -- still might return NIL")
                   (SETQ NEWMD (MEDLEYDIR NIL NIL NIL T))
                   [COND
                      ((NULL NEWMD)                          (* ; "no MEDLEYDIR set -- warn user? If you're not using MEDLEYDIR then need another way of turning this off")
                       )
                      ((EQ \SAVE.MEDLEYDIR T)
                       (SETQ SAME T)                         (* ; " Already restored")
                                                             (* ; " Just do the RESET")
                       )
                      ((NULL \SAVE.MEDLEYDIR)                (* ; "clean slate")
                       )
                      (T (SETQ OLDVALUES (CDR \SAVE.MEDLEYDIR))
                         (SETQ OLDMD (U-CASE (CAR \SAVE.MEDLEYDIR)))
                         (SETQ SAME (STRING-EQUAL OLDMD NEWMD]
                   [for X in MEDLEY-INIT-VARS
                      do (/SETTOPVAL (CAR X)
                                (IF (NULL NEWMD)
                                    THEN (EVAL (CL:THIRD X))
                                  ELSEIF [OR (EQ (CL:FOURTH X)
                                                 'RESET)
                                             (NOT (SETQ TMP (ASSOC (CL%:FIRST X)
                                                                   OLDVALUES]
                                    THEN 
                                         (* ;; "either RESET or no saved value")

                                         (EVAL (CL:SECOND X))
                                  ELSEIF SAME
                                    THEN (CDR TMP)
                                  ELSE (MEDLEYSUBSTDIR OLDMD NEWMD (CDR TMP]
                   (SETQ \SAVE.MEDLEYDIR T)                  (* ; "only use once")
               ))
        (PROGN                                               (* ; "no changes")
               NIL])

(MEDLEYDIR
  [LAMBDA (DIRNAME FILENAME OUTPUT NOERROR)                  (* ; "Edited 18-Oct-2022 17:49 by lmm")
                                                           (* ; "Edited  5-Mar-2022 12:43 by larry")
                                                          (* ; "Edited  2-Dec-2021 20:23 by kaplan")
    (COND
       ((NULL DIRNAME)
        (if (OR (NOT (BOUNDP 'MEDLEYDIR))
                (NOT MEDLEYDIR))
            then (OR (SETQ MEDLEYDIR (DIRECTORYNAME (OR (UNIX-GETENV "MEDLEYDIR")
                                                        T)))
                     (DIRECTORYNAME T))
          elseif (STRPOS "/" MEDLEYDIR)
            then (SETQ MEDLEYDIR (DIRECTORYNAME MEDLEYDIR))
          else MEDLEYDIR))
       [(EQUAL DIRNAME "login")                              (* ; "special case for login dir")
        (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                           (UNIX-GETENV "HOME"]
       ((LISTP DIRNAME)
        (for X Y in DIRNAME when (SETQ Y (MEDLEYDIR X FILENAME OUTPUT NOERROR)) collect Y))
       [FILENAME (if (NULL (SETQ DIRNAME (MEDLEYDIR DIRNAME NIL OUTPUT NOERROR)))
                     then (OR NOERROR (SHOULDNT))
                          NIL
                   else (SETQ FILENAME (CONCAT DIRNAME FILENAME))
                        (if OUTPUT
                            then FILENAME
                          else (OR (INFILEP FILENAME)
                                   (if NOERROR
                                       then NIL
                                     else (ERROR "No such medley file" FILENAME]
       (T (OR (DIRECTORYNAME (CONCAT (MEDLEYDIR)
                                    DIRNAME ">")
                     NIL OUTPUT)
              (if NOERROR
                  then NIL
                else (ERROR "No such medley directory" DIRNAME])

(MEDLEYSUBSTDIR
  [LAMBDA (OLD NEW BODY)                          (* ; 
                                         "Edited 18-Oct-2022 18:06 by lmm: assumes OLD is upper case")
    (IF (NULL BODY)
        THEN NIL
      ELSEIF (LISTP BODY)
        THEN (LET [(A (MEDLEYSUBSTDIR OLD NEW (CAR BODY)))
                   (D (MEDLEYSUBSTDIR OLD NEW (CDR BODY]
                  (IF (AND (EQ A (CAR BODY))
                           (EQ D (CDR BODY)))
                      THEN BODY
                    ELSE (CONS A D)))
      ELSEIF (STRINGP BODY)
        THEN (IF (EQ 1 (STRPOS OLD (U-CASE BODY)
                              1))
                 THEN [CONCAT NEW (SUBSTRING BODY (ADD1 (NCHARS OLD]
               ELSE BODY)
      ELSEIF [AND (LITATOM BODY)
                  (EQ 1 (STRPOS OLD (U-CASE (MKSTRING BODY]
        THEN [PACK* NEW (SUBSTRING BODY (ADD1 (NCHARS OLD]
      ELSE BODY])
)

(RPAQ? MEDLEYDIR )

(RPAQ? \SAVE.MEDLEYDIR )

(ADDTOVAR AROUNDEXITFNS MEDLEY-INIT-VARS)



(* ;; "**WARNING**  The EVALed expressions get run early in the loadup.")


(RPAQQ MEDLEY-INIT-VARS
       ((LISPUSERSDIRECTORIES (MEDLEYDIR '("library" "lispusers" "internal" "greetfiles" "doctools")
                                     NIL NIL T)
               '("{DSK}"))
        (LISPSOURCEDIRECTORIES (MEDLEYDIR '("sources")
                                      NIL NIL T)
               '("{DSK}"))
        (LISPSOURCEDIRECTORY (CAR LISPSOURCEDIRECTORIES)
               "{DSK}")
        (IRM.HOST&DIR (MEDLEYDIR '"docs/dinfo" NIL NIL T)
               NIL)
        (IRM.DINFOGRAPH NIL NIL RESET)
        (DIRECTORIES (APPEND (MEDLEYDIR '("sources" "tmp")
                                    NIL NIL T)
                            LISPUSERSDIRECTORIES)
               '("{DSK}"))
        (LOGINHOST/DIR (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                          (UNIX-GETENV "HOME")
                                          "{DSK}"))
               (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                  (UNIX-GETENV "HOME")
                                  "{DSK}")))
        (DISPLAYFONTDIRECTORIES (MEDLEYDIR '("fonts/displayfonts" "fonts/altofonts" "fonts/adobe" 
                                                   "fonts/big" "fonts/other")
                                       NIL NIL T)
               '("{DSK}"))
        (POSTSCRIPTFONTDIRECTORIES (MEDLEYDIR '("fonts/postscriptfonts")
                                          NIL NIL T)
               '("{DSK}"))
        (INTERPRESSFONTDIRECTORIES (MEDLEYDIR '("fonts/ipfonts")
                                          NIL NIL T)
               '("{DSK}"))
        (UNICODEDIRECTORIES (MEDLEYDIR '("unicode/xerox")
                                   NIL NIL T)
               '("{DSK}"))
        (LOGINHOST/DIR (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                          (UNIX-GETENV "HOME")))
               (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                  (UNIX-GETENV "HOME")))
               RESET)
        (XCL::*WHERE-IS-CASH-FILES* (COND ((GETD 'XCL::ADD-WHERE-IS-DATABASE)
                                           (SETQ XCL::*WHERE-IS-CASH-FILES* NIL)
                                           (NLSETQ (XCL::ADD-WHERE-IS-DATABASE (MEDLEYDIR "tmp" 
                                                                                      "WHEREIS.HASH"
                                                                                      NIL T)))
                                           XCL::*WHERE-IS-CASH-FILES*))
               NIL RESET)))
(DECLARE%: EVAL@COMPILE DONTCOPY 
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS MEDLEYDIR MEDLEY-INIT-VARS \SAVE.MEDLEYDIR DIRECTORIES)
)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1490 7884 (MEDLEY-INIT-VARS 1500 . 4945) (MEDLEYDIR 4947 . 6902) (MEDLEYSUBSTDIR 6904
 . 7882)))))
STOP
