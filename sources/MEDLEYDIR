(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "18-Oct-2022 08:45:52" {DSK}<home>larry>medley-moved>sources>MEDLEYDIR.;2 10445  

      changes to%:  (VARS MEDLEYDIRCOMS)

      previous date%: "17-Oct-2022 23:27:07" {DSK}<home>larry>medley-moved>sources>MEDLEYDIR.;1)


(PRETTYCOMPRINT MEDLEYDIRCOMS)

(RPAQQ MEDLEYDIRCOMS [
                          (* ;; "set up initialization for file paths relative to where Medley is installed. This assumes that the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path) and all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)")

                          (FNS MEDLEY-INIT-VARS MEDLEYDIR MEDLEYSUBSTDIR)
                          (INITVARS (MEDLEYDIR)
                                 (\SAVE.MEDLEYDIR))
                          (ADDVARS (AROUNDEXITFNS MEDLEY-INIT-VARS))
                          
                          (* ;; 
"NOTE:  Do not use backquote in the variable definitions.  These get evaluated early in the loadup.")

                          (VARS MEDLEY-INIT-VARS)
                          (DECLARE%: EVAL@COMPILE DOCOPY (ADDVARS (GLOBALVARS MEDLEYDIR 
                                                                         MEDLEY-INIT-VARS 
                                                                         \SAVE.MEDLEYDIR DIRECTORIES])



(* ;; 
"set up initialization for file paths relative to where Medley is installed. This assumes that the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path) and all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)"
)

(DEFINEQ

(MEDLEY-INIT-VARS
  [LAMBDA (EVENT)                                            (* ; "Edited 17-Oct-2022 23:26 by lmm")

    (* ;; "Called on events including before & after loadup")

    (SELECTQ EVENT
        ((T BEFOREMAKESYS) 
                           (* ;; "Clear old values")

             (FOR X IN MEDLEY-INIT-VARS DO (IF (CDDR X)
                                               THEN (SETTOPVAL (CAR X)
                                                           NIL)))
             (SETQ \SAVE.MEDLEYDIR NIL))
        ((BEFORESYSOUT BEFORELOGOUT BEFORESAVEVM) 
                                                  (* ;; "save old values")

             [SETQ \SAVE.MEDLEYDIR (CONS MEDLEYDIR (FOR X IN MEDLEY-INIT-VARS
                                                      COLLECT (CONS (CAR X)
                                                                    (GETTOPVAL (CAR X])
        ((AFTERSYSOUT AFTERMAKESYS AFTERLOGOUT AFTERSAVEVM RESTART INIT NIL) 
                                                                             (* ;; 
                                                         "Restore old values, subtituting medleydirs")

             (LET* [[OLDMD (AND \SAVE.MEDLEYDIR (U-CASE (POP \SAVE.MEDLEYDIR]
                    (NEWMD (PROGN (SETQ MEDLEYDIR)
                                  (MEDLEYDIR)))
                    (SAME (AND OLDMD (STREQUAL OLDMD (U-CASE NEWMD]
                   [for X TMP in MEDLEY-INIT-VARS
                      do (/SETTOPVAL (CAR X)
                                (IF [OR (EQ (CADDR X)
                                            'RESET)
                                        (NOT (SETQ TMP (ASSOC (CAR X)
                                                              \SAVE.MEDLEYDIR]
                                    THEN (EVAL (CADR X))
                                  ELSEIF SAME
                                    THEN (CDR TMP)
                                  ELSE (MEDLEYSUBSTDIR OLDMD NEWMD (CDR TMP]

                   (* ;; "now some variables that are reset hard-coded")

                   [LET [(NEWSYS (STRPOS "tmp/" (UNIX-GETENV "LDESRCESYSOUT"]
                        (if NEWSYS
                            then (push DIRECTORIES (MEDLEYDIR "tmp")))
                        (CL:WHEN (GETD 'XCL::ADD-WHERE-IS-DATABASE)
                            (SETQ XCL::*WHERE-IS-CASH-FILES* NIL)
                            (NLSETQ (XCL::ADD-WHERE-IS-DATABASE (MEDLEYDIR (if NEWSYS
                                                                               then "tmp"
                                                                             else "loadups")
                                                                       "WHEREIS.HASH"))))]
                   NIL))
        (PROGN                                               (* ; "no changes")
               NIL])

(MEDLEYDIR
  [LAMBDA (DIRNAME FILENAME OUTPUT NOERROR)                  (* ; "Edited 17-Oct-2022 21:46 by lmm")
                                                           (* ; "Edited  5-Mar-2022 12:43 by larry")
                                                          (* ; "Edited  2-Dec-2021 20:23 by kaplan")
    (DECLARE (GLOBALVARS MEDLEYDIR))
    (DECLARE (GLOBALVARS MEDLEYDIR))
    (COND
       ((NULL DIRNAME)
        (if (OR (NOT (BOUNDP 'MEDLEYDIR))
                (NOT MEDLEYDIR))
            then (OR (SETQ MEDLEYDIR (DIRECTORYNAME (OR (UNIX-GETENV "MEDLEYDIR")
                                                        T)))
                     (DIRECTORYNAME T))
          elseif (STRPOS "/" MEDLEYDIR)
            then (SETQ MEDLEYDIR (DIRECTORYNAME MEDLEYDIR))
          else MEDLEYDIR))
       [(EQUAL DIRNAME "login")                              (* ; "special case for login dir")
        (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                           (UNIX-GETENV "HOME"]
       ((LISTP DIRNAME)
        (for X Y in DIRNAME when (SETQ Y (MEDLEYDIR X FILENAME OUTPUT NOERROR)) collect Y))
       [FILENAME (if (NULL (SETQ DIRNAME (MEDLEYDIR DIRNAME NIL OUTPUT NOERROR)))
                     then (OR NOERROR (SHOULDNT))
                          NIL
                   else (SETQ FILENAME (CONCAT DIRNAME FILENAME))
                        (if OUTPUT
                            then FILENAME
                          else (OR (INFILEP FILENAME)
                                   (if NOERROR
                                       then NIL
                                     else (ERROR "No such medley file" FILENAME]
       (T (OR (DIRECTORYNAME (CONCAT (MEDLEYDIR)
                                    DIRNAME ">")
                     NIL OUTPUT)
              (if NOERROR
                  then NIL
                else (ERROR "No such medley directory" DIRNAME])

(MEDLEYSUBSTDIR
  [LAMBDA (OLD NEW BODY)                                     (* ; "Edited 17-Oct-2022 19:35 by lmm")
    (IF (NULL BODY)
        THEN NIL
      ELSEIF (LISTP BODY)
        THEN (LET [(A (MEDLEYSUBSTDIR OLD NEW (CAR BODY)))
                   (D (MEDLEYSUBSTDIR OLD NEW (CDR BODY]
                  (IF (AND (EQ A (CAR BODY))
                           (EQ D (CDR BODY)))
                      THEN BODY
                    ELSE (CONS A D)))
      ELSEIF (STRINGP BODY)
        THEN (IF (EQ 1 (STRPOS OLD (U-CASE BODY)
                              1))
                 THEN (CONCAT NEW (SUBSTRING BODY (NCHARS OLD)))
               ELSE BODY)
      ELSEIF [AND (LITATOM BODY)
                  (EQ 1 (STRPOS OLD (U-CASE (MKSTRING BODY]
        THEN (PACK* NEW (SUBSTRING BODY (NCHARS OLD)))
      ELSE BODY])
)

(RPAQ? MEDLEYDIR )

(RPAQ? \SAVE.MEDLEYDIR )

(ADDTOVAR AROUNDEXITFNS MEDLEY-INIT-VARS)



(* ;; 
"NOTE:  Do not use backquote in the variable definitions.  These get evaluated early in the loadup.")


(RPAQQ MEDLEY-INIT-VARS ([LISPUSERSDIRECTORIES (MEDLEYDIR '("library" "lispusers" "internal" 
                                                                      "greetfiles" "doctools"]
                             [LISPSOURCEDIRECTORIES (MEDLEYDIR '("sources"]
                             (LISPSOURCEDIRECTORY (CAR LISPSOURCEDIRECTORIES))
                             (IRM.HOST&DIR (MEDLEYDIR '"docs/dinfo"))
                             (IRM.DINFOGRAPH)
                             (DIRECTORIES (APPEND LISPUSERSDIRECTORIES LISPSOURCEDIRECTORIES))
                             [LOGINHOST/DIR (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                                               (UNIX-GETENV "HOME"]
                             [USERGREETFILES (LIST (CONS LOGINHOST/DIR '("INIT" COM))
                                                   (CONS LOGINHOST/DIR '("INIT"]
                             (DISPLAYFONTDIRECTORIES (MEDLEYDIR '("fonts/displayfonts" 
                                                                        "fonts/altofonts" 
                                                                        "fonts/adobe" "fonts/big" 
                                                                        "fonts/other")
                                                            NIL NIL T))
                             (POSTSCRIPTFONTDIRECTORIES (MEDLEYDIR '("fonts/postscriptfonts")
                                                               NIL NIL T))
                             (INTERPRESSFONTDIRECTORIES (MEDLEYDIR '("fonts/ipfonts")
                                                               NIL NIL T))
                             (UNICODEDIRECTORIES (MEDLEYDIR '("unicode/xerox")
                                                        NIL NIL T))
                             (XCL::*WHERE-IS-CASH-FILES* NIL RESET)
                             (LOGINHOST/DIR (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                                               (UNIX-GETENV "HOME")))
                                    RESET)
                             (USERGREETFILES [LIST (CONS LOGINHOST/DIR '("INIT" COM))
                                                   (CONS LOGINHOST/DIR '("INIT"]
                                    RESET)
                             (XCL::*WHERE-IS-CASH-FILES* NIL RESET)))
(DECLARE%: EVAL@COMPILE DOCOPY 

(ADDTOVAR GLOBALVARS MEDLEYDIR MEDLEY-INIT-VARS \SAVE.MEDLEYDIR DIRECTORIES)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1744 7710 (MEDLEY-INIT-VARS 1754 . 4759) (MEDLEYDIR 4761 . 6798) (MEDLEYSUBSTDIR 6800
 . 7708)))))
STOP
