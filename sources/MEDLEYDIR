(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 5-Mar-2022 12:08:34" {DSK}<Users>kaplan>Local>medley3.5>my-medley>sources>MEDLEYDIR.;5 7172   

      :CHANGES-TO (FNS MEDLEYDIR)

      :PREVIOUS-DATE " 5-Mar-2022 12:02:48" 
{DSK}<Users>kaplan>Local>medley3.5>my-medley>sources>MEDLEYDIR.;4)


(PRETTYCOMPRINT MEDLEYDIRCOMS)

(RPAQQ MEDLEYDIRCOMS
       [
        (* ;; "set up initialization for file paths relative to where Medley is installed. This assumes that the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path) and all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)")

        (FNS MEDLEY-INIT-VARS MEDLEYDIR)
        (INITVARS (MEDLEYDIR))
        (ADDVARS (BEFORESYSOUTFORMS (SETQ MEDLEYDIR))
               (BEFOREMAKESYSFORMS (SETQ MEDLEYDIR))
               (AFTERSYSOUTFORMS (MEDLEY-INIT-VARS))
               (AFTERMAKESYSFORMS (MEDLEY-INIT-VARS)))
        (VARS MEDLEY-INIT-VARS)
        (DECLARE%: EVAL@COMPILE DOCOPY (ADDVARS (GLOBALVARS MEDLEYDIR MEDLEY-INIT-VARS])



(* ;; 
"set up initialization for file paths relative to where Medley is installed. This assumes that the environment variable MEDLEYDIR is set (usually by the ./run-medley script) to the (unix path) and all of the other directories variables are set relative to that (by MEDLEY-INIT-VARS)"
)

(DEFINEQ

(MEDLEY-INIT-VARS
  [LAMBDA (CLEAR)                                            (* ; 
                                                           "Edited 21-Aug-2021 18:23 by larry")

    (* ;; "MEDLEY-INIT-VARS has variables that might need to get reset. ")

    (if CLEAR
        then (SETQ MEDLEYDIR NIL)
              (SETQ XCL::*WHERE-IS-CASH-FILES* NIL)
              (for X in MEDLEY-INIT-VARS do (SET (CAR X)))
      elseif [OR (NOT (BOUNDP 'MEDLEYDIR))
                     (AND (NULL MEDLEYDIR)
                          (NULL (MEDLEYDIR]
        then (PRINTOUT T "WARNING: MEDLEYDIR not set correctly" 
                        " set it and call (MEDLEY-INIT-VARS) again" T)
      else [for X in MEDLEY-INIT-VARS do (SET (CAR X)
                                                              (EVAL (CADR X] 

            (* ;; "WHEREIS doesn't follow conventions")

            [LET [(NEWSYS (STRPOS "/tmp/" (UNIX-GETENV "LDESRCESYSOUT"]
                 (if NEWSYS
                     then (push DIRECTORIES (MEDLEYDIR "tmp")))
                 (CL:WHEN (GETD 'XCL::ADD-WHERE-IS-DATABASE)
                     (SETQ XCL::*WHERE-IS-CASH-FILES* NIL)
                     (NLSETQ (XCL::ADD-WHERE-IS-DATABASE (MEDLEYDIR (if NEWSYS
                                                                            then "tmp"
                                                                          else "loadups")
                                                                "WHEREIS.HASH"))))]
            NIL])

(MEDLEYDIR
  [LAMBDA (DIRNAME FILENAME OUTPUT NOERROR)                  (* ; "Edited  5-Mar-2022 12:08 by rmk")
                                                          (* ; "Edited  2-Dec-2021 20:23 by kaplan")
    (DECLARE (GLOBALVARS MEDLEYDIR))

    (* ;; "OUTPUT is true if we want to create FILENAME in DIRNAME, otherwise the file must exist (unless NOERROR).")

    [if (AND (BOUNDP 'MEDLEYDIR)
             MEDLEYDIR)
        then (CL:UNLESS (STRPOS "/" MEDLEYDIR)
                 (SETQ MEDLEYDIR (DIRECTORYNAME MEDLEYDIR T)))
      else (SETQ MEDLEYDIR (DIRECTORYNAME (OR (UNIX-GETENV "MEDLEYDIR")
                                              T]

    (* ;; "Check all the directories first, even if we are looking for a particular file. Unless NOERROR, stop on the first one that doesn't exist, maybe causing an error. Should we just throw out the bad ones instead of stopping? ")

    (* ;; "Errors happen at the top-level call (unless NOERROR).")

    (LET (DIRS)
         (if (SETQ DIRS (if DIRNAME
                            then (for D inside DIRNAME
                                    collect (if (DIRECTORYNAME (CONCAT MEDLEYDIR D ">"))
                                              elseif NOERROR
                                                then 
                                                     (* ;; 
                    "Not sure about this:  should we keep looking for good guys? Just GO $$ITERATE ?")

                                                     (RETURN NIL)
                                              else (ERROR "No such medley directory" D)))
                          else (CONS MEDLEYDIR)))
             then (if FILENAME
                      then 
                           (* ;; "Return the first file that exists (or can be created)")

                           (if OUTPUT
                               then (CONCAT (CAR DIRS)
                                           FILENAME)
                             elseif (FINDFILE FILENAME T DIRS)
                             elseif (NOT NOERROR)
                               then (ERROR "No such medley file" FILENAME))
                    elseif (LISTP DIRNAME)
                      then 
                           (* ;; "Presumably, if caller gave a singleton, they want a singleton")

                           DIRS
                    else (CAR DIRS))
           elseif (AND FILENAME (NOT NOERROR))
             then (ERROR "No such medley file" FILENAME])
)

(RPAQ? MEDLEYDIR )

(ADDTOVAR BEFORESYSOUTFORMS (SETQ MEDLEYDIR))

(ADDTOVAR BEFOREMAKESYSFORMS (SETQ MEDLEYDIR))

(ADDTOVAR AFTERSYSOUTFORMS (MEDLEY-INIT-VARS))

(ADDTOVAR AFTERMAKESYSFORMS (MEDLEY-INIT-VARS))

(RPAQQ MEDLEY-INIT-VARS
       ([LISPUSERSDIRECTORIES (MEDLEYDIR '("library" "lispusers" "internal" "greetfiles" "doctools"]
        [LISPSOURCEDIRECTORIES (MEDLEYDIR '("sources"]
        (LISPSOURCEDIRECTORY (CAR LISPSOURCEDIRECTORIES))
        (IRM.HOST&DIR (MEDLEYDIR '"docs/dinfo"))
        (IRM.DINFOGRAPH)
        (DIRECTORIES (APPEND LISPUSERSDIRECTORIES LISPSOURCEDIRECTORIES))
        [LOGINHOST/DIR (DIRECTORYNAME (OR (UNIX-GETENV "LOGINDIR")
                                          (UNIX-GETENV "HOME"]
        [USERGREETFILES `((,LOGINHOST/DIR "INIT" COM)
                          (,LOGINHOST/DIR "INIT"]
        (DISPLAYFONTDIRECTORIES (MEDLEYDIR '("fonts/displayfonts" "fonts/altofonts" "fonts/adobe" 
                                                   "fonts/xerox" "fonts/big" "fonts/other")
                                       NIL NIL T))
        (POSTSCRIPTFONTDIRECTORIES (MEDLEYDIR '("fonts/postscriptfonts")
                                          NIL NIL T))
        (INTERPRESSFONTDIRECTORIES (MEDLEYDIR '("fonts/ipfonts")
                                          NIL NIL T))
        (XCL::*WHERE-IS-CASH-FILES*)))
(DECLARE%: EVAL@COMPILE DOCOPY 

(ADDTOVAR GLOBALVARS MEDLEYDIR MEDLEY-INIT-VARS)
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1418 5677 (MEDLEY-INIT-VARS 1428 . 3042) (MEDLEYDIR 3044 . 5675)))))
STOP
