(FILECREATED "13-Sep-85 14:27:55" {ERINYES}<TEST>TOOLS>FILEBANGER.;2 12693  

      changes to:  (FNS FILEBANGER)

      previous date: "14-AUG-83 13:56:54" {ERINYES}<TEST>TOOLS>FILEBANGER.;1)


(* Copyright (c) 1983, 1985 by Xerox Corporation. All rights reserved.)

(PRETTYCOMPRINT FILEBANGERCOMS)

(RPAQQ FILEBANGERCOMS ((FNS DOFILEBANGER DOMAKEFILEBANGER DOZEROBANGER FILEBANGER 
			    FBCOPYBYTES FBMAKETESTFILE MAKEBANGERWINDOW MAKEFILEBANGER 
			    ZEROBANGER SUSPEND.FILEBANGER WATCHDISKPAGES)
		       (FNS BINCOM)
		       (FNS CHECKFORZEROS)
		       (INITVARS (FBREPEATCOUNT 4)
				 (FILEBANGERS))))
(DEFINEQ

(DOFILEBANGER
  [LAMBDA (DESTINATION LENGTH NOBREAK)                (* bvm: "10-AUG-83 17:37")
    (push FILEBANGERS (ADD.PROCESS (BQUOTE (FILEBANGER (QUOTE , LENGTH)
						       (QUOTE , DESTINATION)
						       T
						       (QUOTE , NOBREAK])

(DOMAKEFILEBANGER
  [LAMBDA (SOURCE)                                    (* bvm: "14-AUG-83 13:53")
    (push FILEBANGERS (ADD.PROCESS (BQUOTE (MAKEFILEBANGER (QUOTE , SOURCE])

(DOZEROBANGER
  [LAMBDA (TESTFILE1 TESTFILE2 TMPFILENAME)           (* bvm: "14-AUG-83 13:54")
    (push FILEBANGERS (ADD.PROCESS (BQUOTE (ZEROBANGER (QUOTE , TESTFILE1)
						       (QUOTE , TESTFILE2)
						       (QUOTE , TMPFILENAME])

(FILEBANGER
  [LAMBDA (TESTFILE DESTINATION MAKEWINDOW NOBREAK INPARMS OUTPARMS)
                                                      (* mbb "13-Sep-85 14:26")
    (DECLARE (SPECVARS ERRCNT LOOPCNT))
    (RESETLST (PROG ((ERRCNT 0)
		     (LOOPCNT 0)
		     (OPTION (AND (NOT NOBREAK)
				  (QUOTE BREAK)))
		     MYFILE NEWFILE LASTFILE TMPFILENAME OUTPUTSTREAM)
		    [COND
		      [(OR (NULL TESTFILE)
			   (FIXP TESTFILE))
			(SETQ TESTFILE (SETQ MYFILE (FBMAKETESTFILE
			      TESTFILE
			      (PACKFILENAME (QUOTE EXTENSION)
					    (QUOTE SOURCE)
					    (QUOTE BODY)
					    (OR DESTINATION (QUOTE FILEBANGER]
		      (T (CLOSEF (SETQ TESTFILE (OPENFILE (OR TESTFILE (RETURN 
							       "No TESTFILE supplied"))
							  (QUOTE INPUT]
		    [COND
		      [MAKEWINDOW (SETQ OUTPUTSTREAM (GETSTREAM (MAKEBANGERWINDOW 
									     TESTFILE 
									"File Banger")
								(QUOTE OUTPUT]
		      (T (SETQ OUTPUTSTREAM (GETSTREAM T (QUOTE OUTPUT]
		    (COND
		      ((NOT MYFILE)
			(SETQ MYFILE (COPYFILE TESTFILE (PACKFILENAME (QUOTE EXTENSION)
								      (QUOTE FBTESTER)
								      (QUOTE VERSION)
								      NIL
								      (QUOTE BODY)
								      TESTFILE)))
			(BINCOM MYFILE TESTFILE OPTION OUTPUTSTREAM)))
		    [SETQ TMPFILENAME (OR DESTINATION (PACKFILENAME (QUOTE EXTENSION)
								    (QUOTE FBTEMP)
								    (QUOTE VERSION)
								    NIL
								    (QUOTE BODY)
								    (OR MYFILE
									(QUOTE FILEBANGER]
		LP  (PRIN1 (add LOOPCNT 1)
			   OUTPUTSTREAM)
		    (RESETLST [RESETSAVE (SETQ NEWFILE (OPENFILE TMPFILENAME
								 (QUOTE OUTPUT)
								 NIL NIL OUTPARMS))
					 (QUOTE (PROGN (CLOSEF OLDVALUE]
			      [RESETSAVE (OPENFILE MYFILE (QUOTE INPUT)
						   NIL NIL INPARMS)
					 (QUOTE (PROGN (CLOSEF OLDVALUE]
			      (COPYBYTES MYFILE NEWFILE))
		    (AND LASTFILE (DELFILE LASTFILE))
		    [COND
		      ((NULL (MEMB MYFILE (DIRECTORY DESTINATION)))
			(add ERRCNT 1)
			(COND
			  ((NEQ OPTION (QUOTE NOMSG))
			    (printout OUTPUTSTREAM T MYFILE 
				      " not found in directory enumeration.")
			    (TERPRI T)))
			(COND
			  ((EQ OPTION (QUOTE BREAK))
			    (HELP MYFILE]
		    [RPTQ FBREPEATCOUNT (PROGN (PRIN1 (QUOTE %.)
						      OUTPUTSTREAM)
					       (COND
						 ((NEQ (BINCOM MYFILE NEWFILE OPTION 
							       OUTPUTSTREAM)
						       T)
						   (add ERRCNT 1]
		    (SETQ LASTFILE NEWFILE)
		    (GO LP])

(FBCOPYBYTES
  [LAMBDA (INSTREAM ECHOSTREAM START)                 (* bvm: "24-JUN-83 19:00")
    (SETFILEPTR INSTREAM START)
    (RPTQ 40 (\OUTCHAR ECHOSTREAM (\BIN INSTREAM])

(FBMAKETESTFILE
  [LAMBDA (LENGTH NAME)                               (* bvm: "10-AUG-83 17:47")
    (RESETLST (PROG [(FILE (OPENFILE (OR NAME (QUOTE FILEBANGER.TMP))
				     (QUOTE OUTPUT)
				     (QUOTE NEW]
		    (RESETSAVE NIL (LIST (QUOTE CLOSEF)
					 FILE))
		    (for I from 1 to (OR LENGTH 1000) bind (STREAM _(GETSTREAM
								     FILE
								     (QUOTE OUTPUT)))
		       do (\OUTCHAR STREAM (RAND 32 127)))
		    (RETURN FILE])

(MAKEBANGERWINDOW
  [LAMBDA (FILE TYPE)                                 (* bvm: "12-AUG-83 13:06")
    (PROG (W)
          [RESETSAVE (TTYDISPLAYSTREAM (SETQ W (CREATEW NIL (CONCAT TYPE " for " FILE]
          (DSPFONT (QUOTE (GACHA 8))
		   W)
          [WINDOWPROP W (QUOTE CLOSEFN)
		      (FUNCTION (LAMBDA (W P)
			  (AND [PROCESSP (SETQ P (WINDOWPROP W (QUOTE PROCESS]
			       (PROCESS.EVAL P (QUOTE (ERROR!]
          (WINDOWPROP W (QUOTE PAGEFULLFN)
		      (FUNCTION NILL))
          (RETURN W])

(MAKEFILEBANGER
  [LAMBDA (TESTFILE)                                  (* bvm: "14-AUG-83 13:56")
    (DECLARE (SPECVARS ERRCNT LOOPCNT))
    (RESETLST (PROG ((LOOPCNT 0)
		     NEWFILE LASTFILE)
		    [SETQ TESTFILE (LOADFROM (OR TESTFILE (RETURN "No TESTFILE supplied"]
		    (MAKEBANGERWINDOW TESTFILE "MAKEFILE Banger")
		    (SETQ TESTFILE (NAMEFIELD TESTFILE T))
		LP  (SETQ NEWFILE (MAKEFILE TESTFILE))
		    (AND (CHECKFORZEROS NEWFILE)
			 (HELP "Zeros found"))
		    [COND
		      (LASTFILE (DELFILE LASTFILE)
				(REMPROP LASTFILE (QUOTE PAGES]
		    (SETQ LASTFILE NEWFILE)
		    (GO LP])

(ZEROBANGER
  [LAMBDA (TESTFILE1 TESTFILE2 TMPFILENAME N NOBREAK OUTPUTSTREAM)
                                                      (* bvm: "12-AUG-83 13:07")
    (DECLARE (SPECVARS ERRCNT LOOPCNT))
    (RESETLST (PROG ((ERRCNT 0)
		     (LOOPCNT 0)
		     (OPTION (AND (NOT NOBREAK)
				  (QUOTE BREAK)))
		     THISFILE NEWFILE LASTFILE)
		    [SETQ THISFILE (CLOSEF (SETQ TESTFILE1 (OPENFILE (OR TESTFILE1
									 (RETURN 
							       "No TESTFILE supplied"))
								     (QUOTE INPUT]
		    (RESETSAVE NIL (LIST (QUOTE CLOSEF?)
					 TESTFILE1))
		    [CLOSEF (SETQ TESTFILE2 (OPENFILE (OR TESTFILE2 (RETURN 
							       "No TESTFILE supplied"))
						      (QUOTE INPUT]
		    (RESETSAVE NIL (LIST (QUOTE CLOSEF?)
					 TESTFILE2))
		    [CLOSEF (SETQ TMPFILENAME (OPENFILE (OR TMPFILENAME (QUOTE 
								       ZEROBANGER.TMP))
							(QUOTE OUTPUT]
		    (RESETSAVE NIL (LIST (QUOTE CLOSEF?)
					 TMPFILENAME))
		    (SETQ OUTPUTSTREAM (GETSTREAM (OR OUTPUTSTREAM (MAKEBANGERWINDOW
							THISFILE "Zero Banger"))
						  (QUOTE OUTPUT)))
		LP  (COND
		      ((AND N (ILESSP (add N -1)
				      0))
			(RETURN ERRCNT)))
		    (printout OUTPUTSTREAM (add LOOPCNT 1)
			      ,)
		    (OPENFILE TMPFILENAME (QUOTE BOTH)
			      (QUOTE OLD))
		    (OPENFILE THISFILE (QUOTE INPUT))
		    (COPYBYTES THISFILE TMPFILENAME 0 -1)
		    (CLOSEF THISFILE)
		    (SETFILEINFO TMPFILENAME (QUOTE LENGTH)
				 (GETFILEPTR TMPFILENAME))
		    (CLOSEF TMPFILENAME)              (* (AND LASTFILE (DELFILE LASTFILE)))
		    (COND
		      ((NEQ (BINCOM THISFILE TMPFILENAME OPTION OUTPUTSTREAM)
			    T)
			(add ERRCNT 1)))              (* (SETQ LASTFILE NEWFILE))
		    (SETQ THISFILE (COND
			((EQ THISFILE TESTFILE1)
			  TESTFILE2)
			(T TESTFILE1)))
		    (GO LP])

(SUSPEND.FILEBANGER
  [LAMBDA NIL                                         (* bvm: "10-AUG-83 17:39")
    (for PROC in FILEBANGERS when (AND (PROCESSP PROC)
				       (NEQ PROC (THIS.PROCESS)))
       do (SUSPEND.PROCESS PROC))
    (CLOSEF (PROG1 PUPTRACEFILE (SETQ PUPTRACEFILE (PUPTRACE PUPTRACEFLG
							     (QUOTE (832 416 190 336])

(WATCHDISKPAGES
  [LAMBDA (THRESHOLD)                                 (* bvm: "10-AUG-83 17:11")
    (OR THRESHOLD (SETQ THRESHOLD 2000))
    (while T bind (MARGIN _ THRESHOLD)
		  LASTFILE
       do (COND
	    ((ILESSP (DISKFREEPAGES)
		     (IPLUS THRESHOLD MARGIN))
	      (COND
		(LASTFILE (DELFILE LASTFILE)))
	      (SETQ LASTFILE (CLOSEF PUPTRACEFILE))
	      (SETQ PUPTRACEFILE (OPENFILE (QUOTE {DSK}PUPTRACE.TMP)
					   (QUOTE OUTPUT)
					   (QUOTE NEW)))
	      (SETQ MARGIN 0)))
	  (BLOCK 60000])
)
(DEFINEQ

(BINCOM
  [LAMBDA (FILE1 FILE2 OPTION OUTPUTSTREAM)           (* bvm: "24-JUN-83 18:45")
    (RESETLST (PROG ((STRM1 (OPENSTREAM FILE1 (QUOTE INPUT)
					(QUOTE OLD)))
		     (STRM2 (OPENSTREAM FILE2 (QUOTE INPUT)
					(QUOTE OLD)))
		     HERE B1 B2)
		    (RESETSAVE NIL (LIST (QUOTE CLOSEF)
					 STRM1))
		    (RESETSAVE NIL (LIST (QUOTE CLOSEF)
					 STRM2))
		    (SETQ OUTPUTSTREAM (GETSTREAM (OR OUTPUTSTREAM T)
						  (QUOTE OUTPUT)))
		    (RETURN (COND
			      ((IEQP (GETEOFPTR STRM1)
				     (GETEOFPTR STRM2))
				(for I from 1 to (GETEOFPTR STRM1)
				   do (COND
					((NEQ (SETQ B1 (\BIN STRM1))
					      (SETQ B2 (\BIN STRM2)))
					  (COND
					    ((NEQ OPTION (QUOTE NOMSG))
					      (printout OUTPUTSTREAM T (FULLNAME STRM1)
							" and "
							(FULLNAME STRM2)
							" differ at byte " .P2
							(SETQ HERE (SUB1 (GETFILEPTR
									   STRM1)))
							" (page " .P2
							(fetch (BYTEPTR PAGE)
							   of HERE)
							", byte " .P2
							(fetch (BYTEPTR OFFSET)
							   of HERE)
							"): ")
					      (\OUTCHAR OUTPUTSTREAM B1)
					      (printout OUTPUTSTREAM "[" .P2 B1 "] vs. ")
					      (\OUTCHAR OUTPUTSTREAM B2)
					      (printout OUTPUTSTREAM "[" .P2 B2 "]" T
							(FULLNAME STRM1)
							" reads:" T)
					      (FBCOPYBYTES STRM1 OUTPUTSTREAM HERE)
					      (printout OUTPUTSTREAM T (FULLNAME STRM2)
							" reads:" T)
					      (FBCOPYBYTES STRM2 OUTPUTSTREAM HERE)
					      (TERPRI T)))
					  (COND
					    ((EQ OPTION (QUOTE BREAK))
					      (HELP STRM1 STRM2)))
					  (RETURN I)))
				   finally (RETURN T)))
			      (T (COND
				   ((NEQ OPTION (QUOTE NOMSG))
				     (printout OUTPUTSTREAM T (FULLNAME STRM1)
					       " has length " .P2 (GETEOFPTR STRM1)
					       ", but "
					       (FULLNAME STRM2)
					       " has length " .P2 (GETEOFPTR STRM2)
					       T)))
				 (COND
				   ((EQ OPTION (QUOTE BREAK))
				     (HELP STRM1 STRM2)))
				 (LIST (GETEOFPTR STRM1)
				       (GETEOFPTR STRM2])
)
(DEFINEQ

(CHECKFORZEROS
  [LAMBDA (FILE MINZEROS)                             (* bvm: " 9-AUG-83 16:14")
    (RESETLST (PROG ((STREAM (OPENSTREAM FILE (QUOTE INPUT)))
		     (#FAILURES 0)
		     N)
		    (RESETSAVE NIL (LIST (QUOTE CLOSEF)
					 STREAM))
		    (OR MINZEROS (SETQ MINZEROS 20))
		    (replace ENDOFSTREAMOP of STREAM with (FUNCTION NILL))
		    (printout T (FULLNAME STREAM)
			      ": " T)
		    (do (SELECTQ (BIN STREAM)
				 (NIL (RETURN))
				 [0 (SETQ N 1)
				    (while (ZEROP (BIN STREAM)) do (add N 1))
				    (COND
				      ((IGREATERP N MINZEROS)
					(printout T .P2 N " zeros starting at byte " .P2
						  (SUB1 (IDIFFERENCE (GETFILEPTR STREAM)
								     N))
						  T)
					(add #FAILURES 1]
				 NIL))
		    (RETURN (AND (NOT (ZEROP #FAILURES))
				 #FAILURES])
)

(RPAQ? FBREPEATCOUNT 4)

(RPAQ? FILEBANGERS )
(PUTPROPS FILEBANGER COPYRIGHT ("Xerox Corporation" 1983 1985))
(DECLARE: DONTCOPY
  (FILEMAP (NIL (624 9302 (DOFILEBANGER 634 . 918) (DOMAKEFILEBANGER 920 . 1123) (
DOZEROBANGER 1125 . 1399) (FILEBANGER 1401 . 4197) (FBCOPYBYTES 4199 . 4399) (
FBMAKETESTFILE 4401 . 4942) (MAKEBANGERWINDOW 4944 . 5550) (MAKEFILEBANGER 5552 . 6247) (
ZEROBANGER 6249 . 8297) (SUSPEND.FILEBANGER 8299 . 8701) (WATCHDISKPAGES 8703 . 9300)) (
9303 11605 (BINCOM 9313 . 11603)) (11606 12552 (CHECKFORZEROS 11616 . 12550)))))
STOP
